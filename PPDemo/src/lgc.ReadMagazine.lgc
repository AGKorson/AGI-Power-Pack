[*********************************************************************
[ 
[ lgc.ReadMagazine
[ 
[ The magazine is the Autumn 1989 issue of Sierra News Magazine 
[ (Volume 2, Issue 2). Several pages are included, with articles that
[ are based on real ones found in the actual magazine. A hint to the
[ four digit access code for the control room door is on the last
[ page.
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
#define pageNum     v250
#define pageImg     v251
#define PAGES         5

[***************************************
[ MAIN CODE BLOCK 
[***************************************

[ pause game clock while reading
block(PAUSE.CLOCK, PAUSE_CLOCK, 0, 0); [ pause.clock(MODE)

[ no script entries for this sequence
set(noScript);
[ no regular input either
prevent.input();
status.line.off();

[ save colors
set.text.attribute(PUSH.ATTRIBUTES, 0); [ push.attributes()

load.view(vw.MagazineArt);
[ ! IMPORTANT! since this is a called logic,
[ anything loaded MUST be manually discarded 
[ before returning!!

[ start with cover, which is page 1
pageImg = pic.MagazineCover;
pageNum = 1;

[ fade out until done loading cover
block(FADE.OUT, BYNUM, 5, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
[ use default mouse cursor
cursoricon = C_DEFAULT;
set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)

[ add keyboard instructions
set.text.attribute(DK_GRAY, BLACK);
display(23, 35, "ESC = EXIT");
display(23, 67, "RIGHT = >");

[ hide objects while showing magazine
block(HIDE.OBJS, HIDE_OBJS, 0, 0); [ hide.objs(MODE)
load.pic(pageImg);
draw.pic(pageImg);
show.pic();
discard.pic(pageImg);
[ fade back in
block(FADE.IN, BYNUM, 5, BLACK); [ fade.in(FN_MODE, DELAY, COLOR)

[ main input loop; wait until a key is pressed
[ (mouse-clicks are converted to return/escape 
[ by the have.key command)
:waitforit
  lastChar = 0;
if (!have.key()) {
  goto(waitforit);
}
[ ESC or right-click to exit
if (lastChar == 27) {
  goto(closemag);
}
[ '<' for left, '>' for right
if (lastChar == 60 && pageNum > 1) {
  goto(pageleft);
}
if (lastChar == 62 && pageNum < 5) {
  goto(pageright);
}

[ left button down (can't check for controller
[ because normal AGI cycle is skipped)
if (mouseBtn == 1) {
  block(GET.PIXEL, &mouseX, &mouseY, &work2); [ get.pixel(vX, vY, vRESULT)
  work2 /= 16;
  if (work2 == 7) {
    [ done
    goto(closemag);
  }
  if ((work2 == 6 || work2 == 5)) {
    if (work2 == 6) {
      :pageright
      block(FADE.OUT, BYVAR, 1, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
      ++pageNum;
      if (pageNum == 5) {
        pageImg = pic.MagazineEnd;
        [ hide right help text
        set.text.attribute(WHITE, BLACK);
        display(23, 67, "         ");
      } else {
        pageImg = pic.MagazinePages;
        if (pageNum == 2) {
          [ show left help text
          set.text.attribute(DK_GRAY, BLACK);
          display(23, 3, "< = LEFT");
        }
      }
    } else {
      :pageleft
      block(FADE.OUT, BYNUM, 1, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
      --pageNum;
      if (pageNum == 1) {
        pageImg = pic.MagazineCover;
        [ hide left text
        set.text.attribute(WHITE, BLACK);
        display(23, 3, "        ");
      } else {
        pageImg = pic.MagazinePages;
        if (pageNum == 4) {
          [ show right help tex
          set.text.attribute(DK_GRAY, BLACK);
          display(23, 67, "RIGHT = >");
        }
      }
    }
    load.pic(pageImg);
    draw.pic(pageImg);
    show.pic();
    if (pageNum > 1) {
      [ add text/images for current page
      if (pageNum == 2) {
        display( 3,  9, "      Table of Contents:");
        set.text.attribute(BLUE, WHITE);
        display( 6,  9, "  Sierra's Newest Games     3");
        display( 8,  9, "  President's Corner        4");
        display(10,  9, "  Interview with Roberta    6");
        display(12,  9, "  What exactly is a Modem?  7");
        display(14,  9, "  Heard it in the Hallway   8");
        set.text.attribute(LT_BLUE, WHITE);
        display( 3, 41, "   SIERRA'S NEWEST GAMES" );
        set.text.attribute(BLACK, WHITE);
        display( 4, 41, "This year's crop of Sierra ");
        display( 5, 41, "games is the biggest, best,");
        display( 6, 41, "and most varied yet. From sub-");
        display( 7, 41, "marine voyages to murder mys-");
        display( 8, 41, "teries, you'll be sure to find");
        display( 9, 41, "something that interests you.");
        display(11, 41, "Scheduled for release in 1989:");
        set.text.attribute(BLACK, LT_RED);
        display(12, 42,  " - Codename: Iceman         ");
        display(13, 42,  " - The Colonel's Bequest    ");
        display(14, 42,  " - Hero's Quest I           ");
        display(15, 42,  " - Manhunter: San Francisco ");
        display(16, 42,  " - Hoyle's Book of Games    ");
        display(17, 42,  " - Leisure Suit Larry III   ");
        display(18, 42,  " - Sorcerian                ");
        display(19, 42,  " - Conquests of Camelot     ");
        set.text.attribute(BLACK, WHITE);
        display(20, 23, "2");
        display(20, 56, "3");
      }
      if (pageNum == 3) {
        [ add Ken's photo
        add.to.pic(vw.MagazineArt, 0, 0, 48, 58, 4, 4);
        set.text.attribute(LT_BLUE, WHITE);
        display( 3,  9, "President's");
        display( 4,  9, "Corner");
        display( 5,  9, "อออออออออออ   ");
        set.text.attribute(BLACK, WHITE);
        display( 6,  9, "When Roberta");
        display( 7,  9, "and I started");
        display( 8,  9, "our company in");
        display( 9,  9, "1980, we were the only employ-");
        display(10,  9, "ees. (She was the first!) From");
        display(11,  9, "that humble beginning, it is");
        display(12,  9, "still amazing that we grew so");
        display(13,  9, "quickly into one of the great-");
        display(14,  9, "est software companies in the");
        display(15,  9, "world. I think part of our");
        display(16,  9, "success is that I have always");
        display(17,  9, "focused on pushing new tech-");
        display(18,  9, "nologies to the limit. Multi-");
        display(19,  9, "media, as it applies to per-");
        display( 4, 41, "sonal computers is a great");
        display( 5, 41, "example of this. Phillips");
        display( 6, 41, "Corporation is introducing a");
        display( 7, 41, "new electronic device soon");
        display( 8, 41, "that will represent a mile-");
        display( 9, 41, "stone for personal consumer");
        display(10, 41, "electronics. The Compact Disk");
        display(11, 41, "Interactive (CDI) is expected");
        display(12, 41, "to be a game changer in the");
        display(13, 41, "multimedia industry. Here at");
        display(14, 41, "Sierra, we intend to make sim-");
        display(15, 41, "ilar investments in new tech-");
        display(16, 41, "nology, pushing the limits of");
        display(17, 41, "computer gaming by incorpor-");
        display(18, 41, "ating cutting edge multimedia");
        display(19, 41, "technology into future games.");
        display(20, 23, "4");
        display(20, 56, "5");
      }
      if (pageNum == 4) {
        set.text.attribute(LT_BLUE, WHITE);
        display( 3,  9, "   INTERVIEW WITH ROBERTA");
        set.text.attribute(CYAN, WHITE);
        display( 4,  9, "Q: Your current project- The ");
        display( 5,  9, "Colonel's Bequest- is a murder");
        display( 6,  9, "mystery. Why did you pick a ");
        display( 7,  9, "mystery story line? ");
        set.text.attribute(BLACK, WHITE);
        display( 8,  9, "A: I thought it was time. ");
        display( 9,  9, "Games are now very fantasy ");
        display(10,  9, "oriented, even a lot of the ");
        display(11,  9, "science fiction. This game is ");
        display(12,  9, "more of a suspense story, more");
        display(13,  9, "in tune with Agatha Christie ");
        display(14,  9, "or even a little bit of Alfred");
        display(15,  9, "Hitchcock. Nobody has some-");
        display(16,  9, "thing like this. It is more ");
        display(17,  9, "like a story than a quest, you");
        display(18,  9, "want to get to the end, and ");
        display(19,  9, "find out the truth.");
        set.text.attribute(LT_BLUE, WHITE);
        display( 3, 41, " WHAT EXACTY IS A MODEM?");
        set.text.attribute(BLACK, WHITE);
        display( 4, 41, "Your computer and your tele-");
        display( 5, 41, "phone don't speak the same");
        display( 6, 41, "language. A telephone carries");
        display( 7, 41, "signals in analog form. Com-");
        display( 8, 41, "puter signals are digital. A");
        display( 9, 41, "modem is needed to translate");
        display(10, 41, "between them. Modem is short");
        display(11, 41, "for 'modulator/demodulator'.");
        display(12, 41, "It works by changing the tele-");
        display(13, 41, "phone's analog signal to a");
        display(14, 41, "digital signal. This lets your");
        display(15, 41, "computer contact other com-");
        display(16, 41, "puters directly and transfer");
        display(17, 41, "information from one computer");
        display(18, 41, "to another through online");
        display(19, 41, "services or bulletin boards.");
        
        display(20, 23, "6");
        display(20, 56, "7");
      }
      if (pageNum == 5) {
        set.text.attribute(LT_BLUE, WHITE);
        display( 3, 25, "   HEARD IT IN THE HALLWAY");
        set.text.attribute(BLACK, WHITE);
        display( 5, 25, "Now that SCI is here to stay, ");
        display( 6, 25, "AGI is going to be officially ");
        display( 7, 25, "retired. But rumors persist");
        display( 8, 25, "that a major upgrade, with the");
        display( 9, 25, "code name 'POWERPACK', had");
        display(10, 25, "been developed. It added many");
        display(11, 25, "new features to the engine.");
        display(12, 25, "Although the development team");
        display(13, 25, "spent over ---- hours on this");
        set.text.attribute(RED, WHITE);
        display(13, 36, "2437"); [ code to control room door 
        set.text.attribute(BLACK, WHITE);
        display(14, 25, "upgrade, Sierra management ");
        display(15, 25, "ultimately decided to go in a");
        display(16, 25, "new direction with brand new");
        display(17, 25, "game engine, which we now know");
        display(18, 25, "as 'SCI', the Sierra Creative");
        display(19, 25, "Interpreter.");
        
        display(20, 39, "8");
      }
    }
    block(FADE.IN, BYNUM, 1, BLACK); [ fade.in(FN_MODE, DELAY, COLOR)
    discard.pic(pageImg);
  }
}
[ after page change, wait for next input
goto(waitforit);

:closemag
[ score
if (!scoreMagazine) {
  set(scoreMagazine);
  work1 = 2;
  call(lgc.ScoreHandler);
}
[ fade out during the switch
block(FADE.OUT, BYNUM, 5, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)

[ ! All resources must be manually discarded before returning
discard.view(vw.MagazineArt);
return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "%g30"
#message 2 "%g31"
#message 3 "%g32"
#message 4 "%g33"
#message 5 "%g34"
#message 6 "%g35"
#message 7 "%g36"
#message 8 "%g37"
