[ ********************************************************************
[
[ rm.SecretRoom
[
[ 
[
[ ********************************************************************
[ TODO: more said statements for secret room

#define fDone           f220
#define tmpFlag         f221

#define crouch          f227
#define uncrouch        f228

if (newRoom) {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(0);
  
  [ add additional room initialization here
  
  [ add toolbar buttons
  load.view(vw.ButtonsDown);
  call(lgc.InitToolbar);
  
  load.view(vw.ClosetArt);

  egoDir = STOPPED;
  if (previousRoom == rm.Closet) {
    position(ego, 73, 137);
    set.view(ego, vw.ClosetArt);
    set.loop(ego, 3);
    set.cel(ego, 4);
    program.control();
    prevent.input();
    [ turn off auto-cycle to allow cycle-at-rest
    work1 = 0;
    set.upper.left(&ego, &work1);
    reverse.loop(ego, uncrouch);
  } else {
    [if (previousRoom == rm.Stairway) {
    position(ego, 29, 166);
    set.loop(ego, 3);
  }
  
  [ add ego to the room
  draw(ego);
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, FADECOUNT, BLACK); [ fade.in(BYVAR, DELAY, COLOR)
  
  if (previousRoom == rm.Stairway) {
    [ restore cursor
    cursoricon = cursormode;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  }
  return();
}

[ check for uncrouching
if (uncrouch) {
  [ restore player control
  reset(uncrouch);
  set.view(ego, vw.Ego);
  set.loop(ego, 3);
  normal.cycle(ego);
  player.control();
  accept.input();
  [ restore autocycle
  work1 = 1;
  set.upper.left(&ego, &work1);
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
}

[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ place said tests here
  if (said("look")) {
    print("You see nothing of interest.");
  }
}

[*****
:noInput           [  all non-input related code goes here 
[*****

[ if cursor is look/talk/use and left-click and NOT 
[ clicking on the actual toolbar, check for clicks
[ on in-game items
if (cursoricon > C_WALK && cursoricon < C_WAIT && 
    controller(cLeftClick) &&
    (mouseX < 14 || mouseX > 145 || mouseY > 17)) {
  [ ignore borders
  if ((mouseX < 16 || mouseX > 112 || mouseY < 66 || mouseY > 167)) {
    goto(doneclick);
  }
  [ look
  if (cursormode == C_LOOK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You see you.");
      goto(doneclick);
    }
    
    [ interior of hidden room
    reset(tmpFlag);
    if (mouseX > 47 && mouseX < 84 && mouseY > 82 && mouseY < 136) {
      set(tmpFlag);
    }
    if (mouseX > 32 && mouseX < 44 && mouseY > 108 && mouseY < 139) {
      set(tmpFlag);
    }
    if (tmpFlag) {
      print("This dark, dusty room is empty. There is a "
            "stairway leading down.");
      goto(doneclick);
    }

    [ stairs
    if (mouseX > 24 && mouseX < 44 && mouseY > 138 && mouseY < 168) {
      print("Flickering light illuminates stairs that "
            "lead down into the basement.");
      goto(doneclick);
    }
    
    [anything else is the closet
    print("You can see the closet through the hole in the wall.");
    goto(doneclick);
  }
  
  [ talk
  if (cursormode == C_TALK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You have nothing to say.");
    } else {
      print("There is no one here to talk to.");
    }
    goto(doneclick);
  }
  
  [ use
  if (cursormode == C_USE) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("Please don't touch yourself. Not even when you're alone.");
      goto(doneclick);
    }
  }
  
  :doneclick
  set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
}

[ add other room specific statements here


[ **************************************
[ CHECK FOR AN EXIT CONDITION
[ **************************************
:exit

[ add exit checks here (or use layout editor)

if (edgeEgoHit == BOTTOM_EDGE) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  new.room(rm.Stairway);  [ ##LE002##
}

if (egoHitSpecial && !crouch) {
  egoDir = STOPPED;
  set(crouch);
  set.view(ego, vw.ClosetArt);
  set.loop(ego, 4);
  set.cel(ego, 4);
  program.control();
  prevent.input();
  [ turn off auto-cycle to allow cycle-at-rest
  work1 = 0;
  set.upper.left(&ego, &work1);
  reverse.loop(ego, fDone);
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
}
if (crouch && fDone) {
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  new.room(rm.Closet);  [ ##LE001##
}

return();

[ **************************************
[ DECLARED MESSAGES
[ **************************************
#message 1 "%g20"
#message 2 "%g21"
#message 3 "%g22"
#message 4 "%g23"
#message 5 "%g24"
#message 6 "%g25"
#message 7 "%g26"
#message 8 "%g27"
 