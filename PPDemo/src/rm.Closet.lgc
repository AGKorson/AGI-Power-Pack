[ ********************************************************************
[
[ rm.Closet
[
[ 
[
[ ********************************************************************

#define wall        o1
#define broom       o2
#define drip        o3
#define door        o4

#define fDone           f220
#define fFalling        f221
#define clickbroom      f222
#define clickmops       f223
#define dripping        f224
#define dripdone        f225
#define cantrip         f226
#define crouch          f227
#define uncrouch        f228
#define opendoor        f229
#define closedoor       f230
#define fDoorDone       f231
#define fEgoDead        f232


#define fallSeq      v220
#define    NOFALL        0
#define    STARTFALL     1
#define    MOVE2WALL1    2
#define    MOVE2WALL2    3
#define    FALLDOWN      4
#define    BREAKWALL     5
#define    SITUP         6
#define    FALLDONE      7
#define    STANDUP       8

#define movebroomstatus v221
#define    NOMOVE        0
#define    MOVE2CORNER   1
#define    PLACEBROOM    2
#define driptimerM      v222
#define driptimerS      v223
#define triptimer       v224
#define    TRIPDELAY     80
#define tmpLoop         v225


if (newRoom) {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ do basic room initialization
  call(lgc.RoomInit);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(0);
  
  [ add toolbar buttons
  load.view(vw.Toolbar);
  call(lgc.InitToolbar);
  
  load.view(vw.ClosetArt);
  load.view(vw.Tripping);
  [ closet door
  animate.obj(door);
  set.view(door, vw.ClosetArt);
  set.loop(door, 5);
  if (closetopen) { 
    set.cel(door, 3);
  } else {
    set.cel(door, 0);
  }
  position(door, 107, 148);
  draw(door);
  stop.update(door);
  
  [ wall animation
  animate.obj(wall);
  set.view(wall, vw.ClosetArt);
  set.loop(wall, 1);
  position(wall, 69, 142);
  set.priority(wall, 4);
  [ if wall not broken yet, cover up the hole
  if (!wallbreak) {
    set.cel(wall, 0);
  } else {
    if (trapped) {
      set.loop(wall, 0);
      set.cel(wall, 2);
    } else {
      set.cel(wall, 12);
      ignore.objs(wall);
    }
  }
  draw(wall);
  stop.cycling(wall);
  
  [ add the broom
  animate.obj(broom);
  set.view(broom, vw.ClosetArt);
  set.loop(broom, 0);
  ignore.objs(broom);
  ignore.blocks(broom);
  [ if moved, put in corner
  if (broommoved) {
    position(broom, 44, 153);
    set.cel(broom, 1);
    block(43, 148, 50, 154);
  } else {
    [ if not moved, it's still on floor
    set.cel(broom, 0);
    set.priority(broom, 11);
    position(broom, 59, 152);
    if (wallbreak) {
      triptimer = TRIPDELAY;
    } else {
      triptimer = 0;
    }
  }
  draw(broom);
  stop.update(broom);
  
  [ drip
  if (!faucetfixed) {
    animate.obj(drip);
    position(drip, 63, 129);
    set.view(drip, vw.ClosetArt);
    set.loop(drip, 2);
    set.cel(drip, 3);
    ignore.objs(drip);
    ignore.blocks(drip);
    draw(drip);
    stop.update(drip);
    
    [ setup a timer; every few seconds, show another drip
    [ until faucet gets tightened
    driptimerM = 0;
    driptimerS = 3;
    block(START.TIMER, &driptimerM, &driptimerS, &dripdone); [ set.timer(vMIN, vSEC, fDONE);
  }
  
  [ add additional room initialization here
  egoDir = STOPPED;
  if (previousRoom == rm.SecretRoom) {
    if (trapped) {
      set.view(ego, vw.ClosetArt);
      set.loop(ego, 4);
      set.cel(ego, 0);
      set(fEgoDead);
      deathType = 2;
    } else {
      position(ego, 72, 141);
      [ uncrouch
      set.view(ego, vw.ClosetArt);
      set.loop(ego, 4);
      set.cel(ego, 0);
      program.control();
      prevent.input();
      [ turn off auto-cycle to allow cycle-at-rest
      work1 = 0;
      set.upper.left(&ego, &work1);
      end.of.loop(ego, uncrouch);
    }
  } else {
    [if (previousRoom == rm.Hallway) {
    position(ego, 100, 147);
    set.loop(ego, 1);
  }
  [ add ego to the room, unless trapped
  if (!trapped) {
    draw(ego);
  }
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, FADECOUNT, BLACK); [ fade.in(BYVAR, DELAY, COLOR)
  [ only restore cursor for Hallway entrance, or if trapped
  [ (coming from secret room has extra standup sequence)
  if ((previousRoom == rm.Hallway || fEgoDead)) {
    [ restore cursor
    cursoricon = cursormode;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  }
  return();
}

[ check drip timer if faucet isn't fixed yet
if (dripdone && !faucetfixed) {
  if (dripping) {
    stop.update(drip);
    driptimerM = 0;
    driptimerS = 3;
    block(START.TIMER, &driptimerM, &driptimerS, &dripdone); [ set.timer(vMIN, vSEC, fDONE);
  } else {
    set.cel(drip, 0);
    end.of.loop(drip, dripdone);
    start.update(drip);
  }
  toggle(dripping);    
}

[ if dead, skip player input
if (fEgoDead) {
  return();
}

[ check for uncrouching
if (uncrouch) {
  [ restore player control
  reset(uncrouch);
  set.view(ego, vw.Ego);
  set.loop(ego, 2);
  normal.cycle(ego);
  player.control();
  accept.input();
  [ restore autocycle
  work1 = 1;
  set.upper.left(&ego, &work1);
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
}

[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ place said tests here
  if (said("look")) {
    print("This is an old janitor closet.");
  }
  
}

[ if NOT clicking on the actual toolbar, check for
[ clicks on ingame items (unless wait cursor is active)
if (mouseX > 40 && mouseX < 114 && mouseY > 69 && mouseY < 167 && 
    (cursoricon == C_TALK || cursoricon == C_LOOK || cursoricon == C_USE) && 
    controller(cLeftClick)) {

  [ if still sitting (after falling)
  if (fallSeq == FALLDONE) {
    print("You should stand up first.");
    goto(doneclick);
  }
  
  [ always stop motion
  egoDir = STOPPED;
  
  [ if ego isn't facing the location clicked, change loop
  [ current loop
  current.loop(ego, tmpLoop);
  [ get left/right pos of ego
  get.posn(ego, work1, work2);
  work3 = work1;
  work3 += 6;
  if (mouseX < work1) {
    [looking left
    if (tmpLoop != 1) {
      set.loop(ego, 1);
      force.update(ego);
    }
  } else {
    if (mouseX > work3) {
      [ looking right
      if (tmpLoop != 0) {
        set.loop(ego, 0);
        force.update(ego);
      }
    } else {
      [ looking up or down
      work2 += 8; [ adjust for pic offset
      work3 = work2; 
      work3 -= 33;
      if (mouseY < work3) {
        [ looking up
        if (tmpLoop != 3) {
          set.loop(ego, 3);
          force.update(ego);
        }
      } else {
        if (mouseY > work2) {
          [ looking down
          if (tmpLoop != 2) {
            set.loop(ego, 2);
            force.update(ego);
          }
        }
      }
    }
  }
  
  [ look
  if (cursormode == C_LOOK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You see you.");
      goto(doneclick);
    }
    
    [ closet door
    if (posn(door, ON.OBJ, 0, &mouseX, &mouseY)) {
      if (closetopen) {
        print("The closet door is open.");
      } else {
        print("The closet door is closed.");
      }
      goto(doneclick);
    }
    
    [ broom
    reset(clickbroom);
    if (broommoved) {
      [ broom is in corner
      if (mouseX > 40 && mouseX < 48 && mouseY > 138 && mouseY < 163) {
        print("After tripping over that stupid broom, you're glad you moved it out of the way.");
        goto(doneclick);
      }
    } else {
      [ broom is on floor
      if (mouseX > 57 && mouseX < 65 && mouseY > 154 && mouseY < 162) {
        set(clickbroom);
      }
      if (mouseX > 62 && mouseX < 67 && mouseY > 151 && mouseY < 157) {
        set(clickbroom);
      }
      if (mouseX > 65 && mouseX < 70 && mouseY > 149 && mouseY < 155) {
        set(clickbroom);
      }
      if (mouseX > 68 && mouseX < 73 && mouseY > 147 && mouseY < 154) {
        set(clickbroom);
      }
    }
    if (clickbroom) {
      if (!wallbreak) {
        print("That broom looks like a serious tripping hazard!");
      } else {
        print("It's the broom you tripped over.");
      }
      goto(doneclick);
    }
    
    [ ladder (55, 93, 76, 105)
    if (mouseX > 54 && mouseX < 77 && mouseY > 100 && mouseY < 114) {
      print("There is a large ladder hanging on the wall.");
      goto(doneclick);
    }
    
    [ faucet (62, 117, 66, 123)
    if (mouseX > 61 && mouseX < 67 && mouseY > 124 && mouseY < 134) {
      if (faucetfixed) {
        print("The faucet is no longer dripping.");
      } else {
        print("The faucet has a slight drip.");
      }
      goto(doneclick);
    }
    
    [ sink (59, 123, 68, 134)
    if (mouseX > 58 && mouseX < 69 && mouseY > 130 && mouseY < 145) {
      print("It's a typical utility closet sink. Great for rinsing mops.");
      goto(doneclick);
    }
    
    [ mop/bucket/items on wall (41, 106, 48, 148 || 49, 117, 52, 142 || 53, 126, 58, 142)
    reset(clickmops);
    if (mouseX > 40 && mouseX < 49 && mouseY > 113 && mouseY < 157) {
      set(clickmops);
    }
    if (mouseX > 48 && mouseX < 53 && mouseY > 124 && mouseY < 151) {
      set(clickmops);
    }
    if (mouseX > 52 && mouseX < 59 && mouseY > 133 && mouseY < 151) {
      set(clickmops);
    }
    if (clickmops) {
      print("There mop and bucket in the corner and several mops and brooms hanging on the wall.");
      goto(doneclick);
    }
    
    [ plunger (80, 129, 84, 141)
    if (mouseX > 79 && mouseX < 85 && mouseY > 136 && mouseY < 151) {
      print("This old plunger looks like it needs to be rinsed clean.");
      goto(doneclick);
    }
    [ boxes on top shelf (86, 92, 102, 102)
    if (mouseX > 85 && mouseX < 103 && mouseY > 99 && mouseY < 111) {
      print("There are several boxes on the top shelf.");
      goto(doneclick);
    }
    
    [ boxes on ground (90, 139, 112, 154)
    if (mouseX > 89 && mouseX < 113 && mouseY > 146 && mouseY < 163) {
      print("The label on the boxes says 'Toilet Paper - 100 rolls'. "
            "That's a lot of toilet paper.");
      goto(doneclick);
    }
    
    [ shelf1 supplies (89, 107, 102, 113)
    if (mouseX > 88 && mouseX < 103 && mouseY > 114 && mouseY < 122) {
      print("There are an assortment of typical cleaning supplies on this shelf.");
      goto(doneclick);
    }
    
    [ shelf2 supplies (89, 118, 102, 126)
    if (mouseX > 88 && mouseX < 103 && mouseY > 125 && mouseY < 135) {
      print("There are an assortment of typical cleaning supplies on this shelf.");
      goto(doneclick);
    }
    
    [ shelf (84, 103, 105, 141)
    if (mouseX > 83 && mouseX < 106 && mouseY > 110 && mouseY < 150) {
      print("This is a study metal shelf unit.");
      goto(doneclick);
    }
    
    [ floor drain (74, 142, 81, 158)
    if (mouseX > 74 && mouseX < 81 && mouseY > 150 && mouseY < 166) {
      print("The stainless steel drain keeps the floor dry.");
      goto(doneclick);
    }
    
    [ steam pipe (44, 77, 98, 83)
    if (mouseX > 43 && mouseX < 99 && mouseY > 84 && mouseY < 94) {
      print("It looks like a drain pipe from the second floor.");
      goto(doneclick);
    }
    
    if (posn(wall, ON.OBJ, 1, &mouseX, &mouseY)) { [ on.obj(MODE, vX, vY);
      if (wallbreak) {
        print("There is a large hole in the wall where you fell.");
      } else {
        [ if wall not broken yet
        print("The wall looks severely cracked and worn here.");
      }
      goto(doneclick);
    }
     
    [ nothing specific - check general areas
    [ ceiling - 41, 62, 113, 73
    if (mouseX > 40 && mouseX < 114 && mouseY > 69 && mouseY < 82) {
      print("You see a drain pipe hanging from the ceiling.");
      goto(doneclick);
    }
    [ walls - 41, 74, 113, 139
    if (mouseX > 40 && mouseX < 114 && mouseY > 81 && mouseY < 148) {
      print("The walls are stained and worn. They could be use a new coat of paint.");
      goto(doneclick);
    }
    [ floor - 41, 140, 113, 158
    if (mouseX > 40 && mouseX < 114 && mouseY > 147 && mouseY < 167) {
      print("The floor is scuffed and worn, but clean. There is a drain in the center.");
      goto(doneclick);
    }
  }
  
  [ talk
  if (cursormode == C_TALK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You have nothing to say.");
    } else {
      print("There is no one here to talk to.");
    }
    goto(doneclick);
  }

  [ grab/use
  if (cursormode == C_USE) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("Please don't touch yourself. Not even in a closet.");
      goto(doneclick);
    }
    
    [ closet door
    if (posn(door, ON.OBJ, 0, &mouseX, &mouseY)) {
      if (closetopen) {
        set(closedoor);
      } else {
        set(opendoor);
      }
      goto(doneclick);
    }
    
    [ broom
    reset(clickbroom);
    if (broommoved) {
      [ broom is in corner
      if (mouseX > 40 && mouseX < 48 && mouseY > 138 && mouseY < 163) {
        if (obj.in.box(ego, 47, 149, 58, 155)) {
          print("You decide to leave the broom where it is.");
        } else {
          print("You can't reach that from here.");
        }
        goto(doneclick);
      }
    } else {
      [ broom is on floor
      if (mouseX > 57 && mouseX < 65 && mouseY > 154 && mouseY < 162) {
        set(clickbroom);
      }
      if (mouseX > 62 && mouseX < 67 && mouseY > 151 && mouseY < 157) {
        set(clickbroom);
      }
      if (mouseX > 65 && mouseX < 70 && mouseY > 149 && mouseY < 155) {
        set(clickbroom);
      }
      if (mouseX > 68 && mouseX < 73 && mouseY > 147 && mouseY < 154) {
        set(clickbroom);
      }
    }
    
    if (clickbroom) {
      if ((obj.in.box(ego, 47, 146, 77, 156) ||
           obj.in.box(ego, 61, 140, 85, 147))) {
        if (!wallbreak) {
          print("As you bend over to pick up the broom, you trip and fall!");
          fallSeq = STARTFALL;
        } else {
          print("You decide to move the broom out of the way so you won't trip on it anymore.");
          erase(broom);
          move.obj(ego, 50, 153, 1, fDone);
          movebroomstatus = MOVE2CORNER;
        }
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ ladder (55, 93, 76, 105)
    if (mouseX > 54 && mouseX < 77 && mouseY > 100 && mouseY < 114) {
      print("You have not taken ladder safety training recently, so you decide to leave it on the wall.");
      goto(doneclick);
    }
    
    [ faucet (62, 117, 66, 123)
    if (mouseX > 61 && mouseX < 67 && mouseY > 124 && mouseY < 134) {
      if (posn(ego, 57, 140, 70, 146)) {
        if (faucetfixed) {
          print("You already tightened the faucet handle.");
        }else {
          print("After tightening the faucet handle, the drip stops.");
          set(faucetfixed);
        }
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ sink (59, 123, 68, 134)
    if (mouseX > 58 && mouseX < 69 && mouseY > 130 && mouseY < 145) {
      if (posn(ego, 57, 140, 70, 150)) {
        if (!handsWashed) {
          set(handsWashed);
          if (handsDirty) {
            print("You wash the sap from your hands. Now they aren't so sticky.");
            currentScore += 3;
          } else {
            [ TODO: need a hand washing scene
            print("Might as well wash your hands");
            currentScore += 1;
          }
        } else {
          print("You already washed your hands.");
        }
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ mop/bucket/items on wall (41, 106, 48, 148 || 49, 117, 52, 142 || 53, 126, 58, 142)
    reset(clickmops);
    if (mouseX > 40 && mouseX < 49 && mouseY > 113 && mouseY < 157) {
      set(clickmops);
    }
    if (mouseX > 48 && mouseX < 53 && mouseY > 124 && mouseY < 151) {
      set(clickmops);
    }
    if (mouseX > 52 && mouseX < 59 && mouseY > 133 && mouseY < 151) {
      set(clickmops);
    }
    if (clickmops) {
      if ((posn(ego, 52, 138, 61, 149) || posn(ego, 41, 148, 51, 155))) {
        print("As much as you've always wanted to become a janitor, perhaps you "
              " should just leave the mops and brooms alone.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ plunger (80, 129, 84, 141)
    if (mouseX > 79 && mouseX < 85 && mouseY > 136 && mouseY < 151) {
      if (obj.in.box(ego, 72, 138, 92, 147)) {
        print("You decide to leave the plunger alone. It smells bad.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    [ boxes on top shelf (86, 92, 102, 102)
    if (mouseX > 85 && mouseX < 103 && mouseY > 99 && mouseY < 111) {
      if (obj.in.box(ego, 83, 140, 108, 146)) {
        print("The boxes are up too high for you to reach.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ boxes on ground (90, 139, 112, 154)
    if (mouseX > 89 && mouseX < 113 && mouseY > 146 && mouseY < 163) {
      if (obj.in.box(ego, 83, 144, 108, 156)) {
        print("The boxes are sealed up. You shouldn't open them.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ shelf1 supplies (89, 107, 102, 113)
    if (mouseX > 88 && mouseX < 103 && mouseY > 114 && mouseY < 122) {
      if (obj.in.box(ego, 85, 140, 108, 148)) {
        print("You have no need for cleaning supplies.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ shelf2 supplies (89, 118, 102, 126)
    if (mouseX > 88 && mouseX < 103 && mouseY > 125 && mouseY < 135) {
      if (obj.in.box(ego, 85, 140, 108, 148)) {
        print("You have no need for cleaning supplies.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ shelf (84, 103, 105, 141)
    if (mouseX > 83 && mouseX < 106 && mouseY > 110 && mouseY < 150) {
      if (obj.in.box(ego, 74, 140, 108, 148)) {
        print("There is nothing you can do with the shelf.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ floor drain (74, 142, 81, 158)
    if (mouseX > 74 && mouseX < 81 && mouseY > 150 && mouseY < 162) {
      if (obj.in.box(ego, 66, 142, 89, 156)) {
        egodir = STOPPED;
        
        print("The drain is firmly attached to the floor. You can't pull it up.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ steam pipe (44, 77, 98, 83)
    if (mouseX > 43 && mouseX < 99 && mouseY > 84 && mouseY < 94) {
        egodir = STOPPED;
      print("You can't reach the pipe. Not even if you jumped.");
    }
  }
  
  :doneclick
  set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
}



[*****
:noInput           [  all non-input related code goes here 
[*****

[ add other room specific statements here

[ trip and fall into wall
if (egoHitSpecial && !wallbreak && fallSeq == NOFALL) {
  [ fall, and break wall
  print("Look out for that broom!");
  fallSeq = STARTFALL;
}
[ startfall is a separate state so that it can be reached
[ by trying to pick up the broom, or by moving over it
if (fallSeq == STARTFALL) {
  fallSeq = MOVE2WALL1;
  set.view(ego, vw.Tripping);
  set.loop(ego, 0);
  fix.loop(ego);
  move.obj(ego, 72, 145, 1, fDone);
  prevent.input();
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ disable click2move if in walk mode
  if (cursormode == C_WALK) {
    reset(click2move);
  }
    
}
if (fDone && fallSeq == MOVE2WALL1) {
  fallSeq = MOVE2WALL2;
  move.obj(ego, 72, 146, 1, fDone);
}
if (fDone && fallSeq == MOVE2WALL2) {
  fallSeq = FALLDOWN;
  [ turn off auto-cycle to allow cycle-at-rest
  work1 = 0;
  set.upper.left(&ego, &work1);
  set.loop(ego, 2);
  set.cel(ego, 0);
  end.of.loop(ego, fDone);
}
if (fDone && fallSeq == FALLDOWN) {
  fallSeq = BREAKWALL;
  start.update(wall);
  end.of.loop(wall, fDone);
}
if (fDone && fallSeq == BREAKWALL) {
  reset(fDone);
  fallSeq = SITUP;
  print("As you fall against the wall, it caves in, revealing a large hole.");
  set(wallbreak);
  stop.update(wall);
  ignore.objs(wall);
  set.loop(ego, 3);
  set.cel(ego, 0);
  end.of.loop(ego, fDONE);
}
if (fDone && fallSeq ==  SITUP) {
  reset(fDone);
  player.control();
  accept.input();
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  fallSeq = FALLDONE;
}
if (egoDir != STOPPED && fallSeq == FALLDONE) {
  [ get up first
  egoDir = STOPPED;
  fallSeq = STANDUP;
  set.loop(ego, 4);
  end.of.loop(ego, fDone);
}
if (fDone && fallSeq == STANDUP) {
  reset(fDone);
  fallSeq = NOFALL;
  set.view(ego, vw.Ego);
  set.loop(ego, 0);
  start.cycling(ego);
  release.loop(ego);
  [ reenable click2move if in walk mode
  if (cursormode == C_WALK) {
    set(click2move);
  }
  [ restore auto-cycle
  work1 = 1;
  set.upper.left(&ego, &work1);
  triptimer = TRIPDELAY;
}
[ trip over broom even after first fall, unless it's been moved
if (egoHitSpecial && !broommoved && cantrip && egodir != STOPPED && movebroomstatus == NOMOVE) {
  print("You trip over the broom again!");
  reset(cantrip);
  triptimer = TRIPDELAY;
  triptimer = 90;
  set.view(ego, vw.Tripping);
  current.loop(ego, work1);
  if (work1 > 1) {
    work1 -= 2;
  }
  set.loop.v(ego, work1);
  fix.loop(ego);
  wander(ego);
  prevent.input();
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  prevent.input();
}
[ check for end of stumbling sequence
if (triptimer > TRIPDELAY) {
  --triptimer;
  if (triptimer == TRIPDELAY) {
    [ restore ego movement
    set.view(ego, vw.Ego);
    release.loop(ego);
    egoDir = STOPPED;
    player.control();
    accept.input();
    cursoricon = cursormode;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  }
} else {
  [ not stumbling - if not on the broom, run the countdown
  if (!egoHitSpecial && triptimer > 0 && !broommoved) {
    --triptimer;
    if (triptimer == 0) {
      set(cantrip);
    }
  }
}

[ move broom to corner
if (fDone && movebroomstatus == MOVE2CORNER) {
  erase(broom);
  set.cel(broom, 1);
  position(broom, 44, 153);
  draw(broom);
  stop.update(broom);
  movebroomstatus = NOMOVE;
  block(43, 148, 50, 154);
  set(broommoved);
}

if (opendoor) {
  reset(opendoor);
  if (closetopen) {
    print("The closet door is already open.");
  } else {
    if (posn(ego, 97, 142, 106, 147)) {
      end.of.loop(door, fDoorDone);
    } else {
      print("You are not close enough.");
    }
  }
}
if (closedoor) {
  reset(closedoor);
  if (closetopen) {
    if (posn(ego, 97, 142, 106, 156)) {
      reverse.loop(door, fDoorDone);
    } else {
      print("You are not close enough.");
    }
  } else {
    print("The closet door is already closed.");
  }
}

if (fDoorDone) {
  reset(fDoorDone);
  if (closetopen) {
    reset(closetopen);
  } else {
    set(closetopen);
  }
}
[*****
:exit              [  test for leaving the room
[*****
if (wallbreak && !crouch && posn(ego, 70, 139, 80, 139)) {
  egoDir = STOPPED;
  set(crouch);
  set.view(ego, vw.ClosetArt);
  set.loop(ego, 3);
  set.cel(ego, 0);
  program.control();
  prevent.input();
  [ turn off auto-cycle to allow cycle-at-rest
  work1 = 0;
  set.upper.left(&ego, &work1);
  end.of.loop(ego, fDone);
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
}
if (crouch && fDone) {
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  new.room(rm.SecretRoom);  [ ##LE002##
}

if ((posn(ego, 100, 143, 100, 143) || posn(ego, 101, 144, 101, 144) ||
     posn(ego, 101, 145, 101, 145) || posn(ego, 102, 146, 102, 146) ||
     posn(ego, 102, 147, 102, 147))) {
  if (closetopen) { 
    [ show wait cursor
    cursoricon = C_WAIT;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
    [ fade out, then change room
    block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
    new.room(rm.Hallway);  [ ##LE001##
  } else {
    if (egoDir > 0 && egoDir < 5) {
      egoDir = STOPPED;
    }
  }
}
return();


[ **************************************
[ DECLARED MESSAGES
[ **************************************
#message 1 "%l90|40"
#message 2 "%l90|41"
#message 3 "%l90|42"
#message 4 "%l90|43"
#message 5 "%l90|44"
#message 6 "%l90|45"
#message 7 "This is an old janitor closet."
#message 8 "You see you."
#message 9 "After tripping over that stupid broom, you're glad you moved it out of the way."
#message 10 "That broom looks like a serious tripping hazard!"
#message 11 "It's the broom you tripped over."
#message 12 "The faucet is no longer dripping."
#message 13 "The faucet has a slight drip."
#message 14 "It's a typical utility closet sink. Great for rinsing mops."
#message 15 "There are several mops and brooms hanging on the wall."
#message 16 "This old plunger looks like it needs to be rinsed clean."
#message 17 "There are several boxes on the top shelf."
#message 18 "The label on the boxes says 'Toilet Paper - 100 rolls'. That's a lot of toilet paper."
#message 19 "There are an assortment of typical cleaning supplies on this shelf."
#message 20 "This is a study metal shelf unit."
#message 21 "The stainless steel drain keeps the floor dry."
#message 22 "It looks like a drain pipe from the second floor."
#message 23 "There is a large hole in the wall where you fell."
#message 24 "The wall looks severely cracked and worn here."
#message 25 "You see a drain pipe hanging from the ceiling."
#message 26 "The walls are stained and worn. They could be use a new coat of paint."
#message 27 "The floor is scuffed and worn, but clean. There is a drain in the center."
#message 28 "You have nothing to say."
#message 29 "There is no one here to talk to."
#message 30 "Please don't touch yourself. Not even in a closet."
#message 31 "You decide to leave the broom where it is."
#message 32 "You can't reach that from here."
#message 33 "As you bend over to pick up the broom, you trip and fall!"
#message 34 "You decide to move the broom out of the way so you won't trip on it anymore."
#message 35 "You already tightened the faucet handle."
#message 36 "After tightening the faucet handle, the drip stops."
#message 37 "Might as well wash your hands"
#message 38 "As much as you've always wanted to become a janitor, perhaps you  should just leave the mops and brooms alone."
#message 39 "You decide to leave the plunger alone. It smells bad."
#message 40 "The boxes are up too high for you to reach."
#message 41 "The boxes are sealed up. You shouldn't open them."
#message 42 "You have no need for cleaning supplies."
#message 43 "There is nothing you can do with the shelf."
#message 44 "The drain is firmly attached to the floor. You can't pull it up."
#message 45 "You can't reach the pipe. Not even if you jumped."
#message 46 "Look out for that broom!"
#message 47 "As you fall against the wall, it caves in, revealing a large hole."
#message 48 "You trip over the broom again!"