[ ********************************************************************
[
[ rm.Closet
[
[ 
[
[ ********************************************************************
[ TODO: more said statements for closet

[ **************************************
[ LOCAL DEFINES
[ **************************************
#define wall        o1
#define broom       o2
#define drip        o3
#define door        o4

#define fDone           f220
#define fFalling        f221
#define clickbroom      f222
#define clickmops       f223
#define dripping        f224
#define dripdone        f225
#define cantrip         f226
#define crouch          f227
#define uncrouch        f228
#define opendoor        f229
#define closedoor       f230
#define fDoorDone       f231
#define fEgoDead        f232
#define fDoneWash       f233

#define fallSeq         v220
#define    NOFALL        0
#define    STARTFALL     1
#define    MOVE2WALL1    2
#define    MOVE2WALL2    3
#define    FALLDOWN      4
#define    BREAKWALL     5
#define    SITUP         6
#define    FALLDONE      7
#define    STANDUP       8
#define movebroomstatus v221
#define    NOMOVE        0
#define    MOVE2CORNER   1
#define    PLACEBROOM    2
#define driptimerM      v222
#define driptimerS      v223
#define triptimer       v224
#define    TRIPDELAY     80
#define tmpLoop         v225
#define washSeq         v226
#define    MOVESINK1     1
#define    MOVESINK2     2
#define    WASHING       3

if (newRoom) {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(0);
  
  [ add toolbar buttons
  load.view(vw.ButtonsDown);
  call(lgc.InitToolbar);
  
  load.view(vw.ClosetArt);
  load.view(vw.Tripping);
  [ closet door
  animate.obj(door);
  set.view(door, vw.ClosetArt);
  set.loop(door, 5);
  if (closetopen) { 
    set.cel(door, 3);
  } else {
    set.cel(door, 0);
  }
  position(door, 107, 148);
  draw(door);
  stop.update(door);
  
  [ wall animation
  animate.obj(wall);
  set.view(wall, vw.ClosetArt);
  set.loop(wall, 1);
  position(wall, 69, 142);
  set.priority(wall, 4);
  [ if wall not broken yet, cover up the hole
  if (!wallbreak) {
    set.cel(wall, 0);
  } else {
    if (trapped) {
      set.loop(wall, 0);
      set.cel(wall, 2);
    } else {
      set.cel(wall, 12);
      ignore.objs(wall);
    }
  }
  draw(wall);
  stop.cycling(wall);
  
  [ add the broom
  animate.obj(broom);
  set.view(broom, vw.ClosetArt);
  set.loop(broom, 0);
  ignore.objs(broom);
  ignore.blocks(broom);
  [ if moved, put in corner
  if (broommoved) {
    position(broom, 44, 153);
    set.cel(broom, 1);
    block(43, 148, 50, 154);
  } else {
    [ if not moved, it's still on floor
    set.cel(broom, 0);
    set.priority(broom, 11);
    position(broom, 59, 152);
    if (wallbreak) {
      triptimer = TRIPDELAY;
    } else {
      triptimer = 0;
    }
  }
  draw(broom);
  stop.update(broom);
  
  [ drip
  if (!faucetfixed) {
    animate.obj(drip);
    position(drip, 63, 129);
    set.view(drip, vw.ClosetArt);
    set.loop(drip, 2);
    set.cel(drip, 3);
    ignore.objs(drip);
    ignore.blocks(drip);
    draw(drip);
    stop.update(drip);
    
    [ setup a timer; every few seconds, show another drip
    [ until faucet gets tightened
    driptimerM = 0;
    driptimerS = 3;
    block(START.TIMER, &driptimerM, &driptimerS, &dripdone); [ set.timer(vMIN, vSEC, fDONE);
    load.sound(s.Drip);
  }
  
  if (!wallbreak) {
    [ breaking wall sounds
    load.sound(s.Crumble1);
    load.sound(s.Crumble2);
  }
  load.sound(s.DoorOpen);
  load.sound(s.DoorClose);
  
  [ add additional room initialization here
  egoDir = STOPPED;
  if (previousRoom == rm.SecretRoom) {
    if (trapped) {
      set.view(ego, vw.ClosetArt);
      set.loop(ego, 4);
      set.cel(ego, 0);
      set(fEgoDead);
      deathType = DT_TRAPPED;
    } else {
      position(ego, 72, 141);
      [ uncrouch
      set.view(ego, vw.ClosetArt);
      set.loop(ego, 4);
      set.cel(ego, 0);
      program.control();
      prevent.input();
      [ turn off auto-cycle to allow cycle-at-rest
      work1 = 0;
      set.upper.left(&ego, &work1);
      end.of.loop(ego, uncrouch);
    }
  } else {
    [if (previousRoom == rm.Hallway) {
    position(ego, 100, 147);
    set.loop(ego, 1);
  }
  [ add ego to the room, unless trapped
  if (!trapped) {
    draw(ego);
  }
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, FADECOUNT, BLACK); [ fade.in(BYVAR, DELAY, COLOR)
  [ only restore cursor for Hallway entrance, or if trapped
  [ (coming from secret room has extra standup sequence)
  if ((previousRoom == rm.Hallway || fEgoDead)) {
    [ restore cursor
    cursoricon = cursormode;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  }
  return();
}

[ check drip timer if faucet isn't fixed yet
if (dripdone && !faucetfixed) {
  if (dripping) {
    stop.update(drip);
    [ ok to use timer here; the closet door timer is never
    [ active in this room
    driptimerM = 0;
    driptimerS = 3;
    block(START.TIMER, &driptimerM, &driptimerS, &dripdone); [ set.timer(vMIN, vSEC, fDONE);
  } else {
    set.cel(drip, 0);
    end.of.loop(drip, dripdone);
    start.update(drip);
    [ only play sound if walkman is not in use, and not crashing through wall
    if ((currentTrack == 0 || currentTrack > 128)  && (fallSeq == NOFALL || fallSeq >= SITUP)) {
      [ play drip sound, force 4-channel FM
      set(4channelsound);
      set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      [ use correct instrument
      work1 = 0;
      work2 = 115;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT);
      sound(s.Drip, soundDone);
      set(playSound);
    }
  }
  toggle(dripping);    
}

[ if dead, skip player input
if (fEgoDead) {
  return();
}

[ check for uncrouching
if (uncrouch) {
  [ restore player control
  reset(uncrouch);
  set.view(ego, vw.Ego);
  set.loop(ego, 2);
  normal.cycle(ego);
  player.control();
  accept.input();
  [ restore autocycle
  work1 = 1;
  set.upper.left(&ego, &work1);
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
}

[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ place said tests here
  if (said("look")) {
    print("This is an old janitor closet.");
  }
  
}

[ if cursor is look/talk/use and left-click and NOT 
[ clicking on the actual toolbar, check for clicks
[ on in-game items
if (cursoricon > C_WALK && cursoricon < C_WAIT && 
    controller(cLeftClick) &&
    (mouseX < 14 || mouseX > 145 || mouseY > 17)) {
  [ ignore borders
  if ((mouseX < 41 || mouseX > 113 || mouseY < 62 || mouseY > 155)) {
    goto(doneclick);
  }
  
  [ if still sitting (after falling)
  if (fallSeq == FALLDONE) {
    print("You should stand up first.");
    goto(doneclick);
  }
  
  [ always stop motion
  egoDir = STOPPED;
  
  [ if ego isn't facing the location clicked, change loop
  [ current loop
  current.loop(ego, tmpLoop);
  [ get left/right pos of ego
  get.posn(ego, work1, work2);
  work3 = work1;
  work3 += 6;
  if (mouseX < work1) {
    [looking left
    if (tmpLoop != 1) {
      set.loop(ego, 1);
      force.update(ego);
    }
  } else {
    if (mouseX > work3) {
      [ looking right
      if (tmpLoop != 0) {
        set.loop(ego, 0);
        force.update(ego);
      }
    } else {
      [ looking up or down
      work3 = work2; 
      work3 -= 33;
      if (mouseY < work3) {
        [ looking up
        if (tmpLoop != 3) {
          set.loop(ego, 3);
          force.update(ego);
        }
      } else {
        if (mouseY > work2) {
          [ looking down
          if (tmpLoop != 2) {
            set.loop(ego, 2);
            force.update(ego);
          }
        }
      }
    }
  }
  
  [ look
  if (cursormode == C_LOOK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You see you.");
      goto(doneclick);
    }
    
    [ closet door
    if (posn(door, ON.OBJ, 0, &mouseX, &mouseY)) {
      if (closetopen) {
        print("The closet door is open.");
      } else {
        print("The closet door is closed.");
      }
      goto(doneclick);
    }
    
    [ broom
    reset(clickbroom);
    if (broommoved) {
      [ broom is in corner
      if (mouseX > 40 && mouseX < 48 && mouseY > 130 && mouseY < 155) {
        print("After tripping over that stupid broom, you're glad you "
              "moved it out of the way.");
        goto(doneclick);
      }
    } else {
      [ broom is on floor
      if (mouseX > 57 && mouseX < 65 && mouseY > 146 && mouseY < 154) {
        set(clickbroom);
      }
      if (mouseX > 62 && mouseX < 67 && mouseY > 143 && mouseY < 149) {
        set(clickbroom);
      }
      if (mouseX > 65 && mouseX < 70 && mouseY > 141 && mouseY < 147) {
        set(clickbroom);
      }
      if (mouseX > 68 && mouseX < 73 && mouseY > 139 && mouseY < 146) {
        set(clickbroom);
      }
    }
    if (clickbroom) {
      if (!wallbreak) {
        print("That broom looks like a serious tripping hazard!");
      } else {
        print("It's the broom you tripped over.");
      }
      goto(doneclick);
    }
    
    [ ladder (55, 93, 76, 105)
    if (mouseX > 54 && mouseX < 77 && mouseY > 92 && mouseY < 106) {
      print("There is a large ladder hanging on the wall.");
      goto(doneclick);
    }
    
    [ faucet (62, 117, 66, 125)
    if (mouseX > 61 && mouseX < 67 && mouseY > 116 && mouseY < 126) {
      if (faucetfixed) {
        print("The faucet is no longer dripping.");
      } else {
        print("The faucet has a slight drip.");
      }
      goto(doneclick);
    }
    
    [ sink (59, 123, 68, 136)
    if (mouseX > 58 && mouseX < 69 && mouseY > 122 && mouseY < 137) {
      print("It's a typical utility closet sink. Great for rinsing mops.");
      goto(doneclick);
    }
    
    [ mop/bucket/items on wall (41, 106, 48, 148 || 49, 117, 52, 142 || 53, 126, 58, 142)
    reset(clickmops);
    if (mouseX > 40 && mouseX < 49 && mouseY > 105 && mouseY < 149) {
      set(clickmops);
    }
    if (mouseX > 48 && mouseX < 53 && mouseY > 116 && mouseY < 143) {
      set(clickmops);
    }
    if (mouseX > 52 && mouseX < 59 && mouseY > 125 && mouseY < 143) {
      set(clickmops);
    }
    if (clickmops) {
      print("There mop and bucket in the corner and several mops and "
            "brooms hanging on the wall.");
      goto(doneclick);
    }
    
    [ plunger (80, 129, 84, 142)
    if (mouseX > 79 && mouseX < 85 && mouseY > 128 && mouseY < 143) {
      print("This old plunger looks like it needs to be rinsed clean.");
      goto(doneclick);
    }
    [ boxes on top shelf (86, 92, 102, 102)
    if (mouseX > 85 && mouseX < 103 && mouseY > 91 && mouseY < 103) {
      print("There are several boxes on the top shelf.");
      goto(doneclick);
    }
    
    [ boxes on ground (90, 139, 112, 154)
    if (mouseX > 89 && mouseX < 113 && mouseY > 138 && mouseY < 155) {
      print("The label on the boxes says 'Toilet Paper - 100 rolls'. "
            "That's a lot of toilet paper.");
      goto(doneclick);
    }
    
    [ shelf1 supplies (89, 107, 102, 113)
    if (mouseX > 88 && mouseX < 103 && mouseY > 106 && mouseY < 114) {
      print("There are an assortment of typical cleaning supplies on this shelf.");
      goto(doneclick);
    }
    
    [ shelf2 supplies (89, 118, 102, 126)
    if (mouseX > 88 && mouseX < 103 && mouseY > 117 && mouseY < 127) {
      print("There are an assortment of typical cleaning supplies on this shelf.");
      goto(doneclick);
    }
    
    [ shelf (84, 103, 105, 141)
    if (mouseX > 83 && mouseX < 106 && mouseY > 102 && mouseY < 142) {
      print("This is a study metal shelf unit.");
      goto(doneclick);
    }
    
    [ floor drain (74, 142, 81, 158)
    if (mouseX > 74 && mouseX < 81 && mouseY > 142 && mouseY < 158) {
      print("The stainless steel drain keeps the floor dry.");
      goto(doneclick);
    }
    
    [ steam pipe (44, 77, 98, 85)
    if (mouseX > 43 && mouseX < 99 && mouseY > 76 && mouseY < 86) {
      print("It looks like a drain pipe from the second floor.");
      goto(doneclick);
    }
    
    if (posn(wall, ON.OBJ, 1, &mouseX, &mouseY)) { [ on.obj(MODE, vX, vY);
      if (wallbreak) {
        print("There is a large hole in the wall where you fell.");
      } else {
        [ if wall not broken yet
        print("The wall looks severely cracked and worn here.");
      }
      goto(doneclick);
    }
     
    [ nothing specific - check general areas
    [ ceiling - 41, 62, 113, 73
    if (mouseX > 40 && mouseX < 114 && mouseY > 61 && mouseY < 74) {
      print("You see a drain pipe hanging from the ceiling.");
      goto(doneclick);
    }
    [ walls - 41, 74, 113, 139
    if (mouseX > 40 && mouseX < 114 && mouseY > 73 && mouseY < 140) {
      print("The walls are stained and worn. They could be use a new coat of paint.");
      goto(doneclick);
    }
    [ floor - 41, 140, 113, 158
    if (mouseX > 40 && mouseX < 114 && mouseY > 139 && mouseY < 159) {
      print("The floor is scuffed and worn, but clean. There is a drain in the center.");
      goto(doneclick);
    }
  }
  
  [ talk
  if (cursormode == C_TALK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You have nothing to say.");
    } else {
      print("There is no one here to talk to.");
    }
    goto(doneclick);
  }

  [ grab/use
  if (cursormode == C_USE) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("Please don't touch yourself. Not even in a closet.");
      goto(doneclick);
    }
    
    [ closet door
    if (posn(door, ON.OBJ, 0, &mouseX, &mouseY)) {
      if (closetopen) {
        set(closedoor);
      } else {
        set(opendoor);
      }
      goto(doneclick);
    }
    
    [ broom
    reset(clickbroom);
    if (broommoved) {
      [ broom is in corner
      if (mouseX > 40 && mouseX < 48 && mouseY > 130 && mouseY < 155) {
        if (obj.in.box(ego, 47, 149, 58, 155)) {
          print("You don't need the broom so you leave it in the corner.");
        } else {
          print("You can't reach that from here.");
        }
        goto(doneclick);
      }
    } else {
      [ broom is on floor
      if (mouseX > 57 && mouseX < 65 && mouseY > 146 && mouseY < 154) {
        set(clickbroom);
      }
      if (mouseX > 62 && mouseX < 67 && mouseY > 143 && mouseY < 149) {
        set(clickbroom);
      }
      if (mouseX > 65 && mouseX < 70 && mouseY > 141 && mouseY < 147) {
        set(clickbroom);
      }
      if (mouseX > 68 && mouseX < 73 && mouseY > 139 && mouseY < 146) {
        set(clickbroom);
      }
    }
    
    if (clickbroom) {
      if ((obj.in.box(ego, 47, 146, 77, 156) ||
           obj.in.box(ego, 57, 140, 85, 147))) {
        if (!wallbreak) {
          print("As you bend over to pick up the broom, you trip and fall!");
          fallSeq = STARTFALL;
        } else {
          print("You decide to move the broom out of the way so you won't "
                "trip on it anymore.");
          erase(broom);
          move.obj(ego, 50, 153, 1, fDone);
          movebroomstatus = MOVE2CORNER;
        }
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ ladder (55, 93, 76, 105)
    if (mouseX > 54 && mouseX < 77 && mouseY > 92 && mouseY < 106) {
      print("You have not taken ladder safety training recently, so you "
            "decide to leave it on the wall.");
      goto(doneclick);
    }
    
    [ faucet (62, 117, 66, 125)
    if (mouseX > 61 && mouseX < 67 && mouseY > 116 && mouseY < 126) {
      if (posn(ego, 57, 140, 70, 146)) {
        if (faucetfixed) {
          print("You already tightened the faucet handle.");
        }else {
          print("After tightening the faucet handle, the drip stops.");
          set(faucetfixed);
          work1 = 3;
          call(lgc.ScoreHandler);
        }
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ sink (59, 123, 68, 136)
    if (mouseX > 58 && mouseX < 69 && mouseY > 122 && mouseY < 137) {
      if (posn(ego, 57, 140, 70, 150)) {
        if (!handsWashed) {
          set(disableGameFunctions);
          prevent.input();
          if (egoY < 144) {
            washSeq = MOVESINK1;
            move.obj(ego, 68, 144, 1, fDoneWash);
          } else {
            washSeq = MOVESINK2;
            move.obj(ego, 60, 144, 1, fDoneWash);
          }
        } else {
          print("You already washed your hands.");
        }
      } else {
        print("You can't reach it from here.");
      }
      goto(doneclick);
    }
    
    [ mop/bucket/items on wall (41, 106, 48, 148 || 49, 117, 52, 142 || 53, 126, 58, 142)
    reset(clickmops);
    if (mouseX > 40 && mouseX < 49 && mouseY > 105 && mouseY < 149) {
      set(clickmops);
    }
    if (mouseX > 48 && mouseX < 53 && mouseY > 116 && mouseY < 143) {
      set(clickmops);
    }
    if (mouseX > 52 && mouseX < 59 && mouseY > 125 && mouseY < 143) {
      set(clickmops);
    }
    if (clickmops) {
      if ((posn(ego, 52, 138, 61, 149) || posn(ego, 41, 148, 51, 155))) {
        print("As much as you've always wanted to become a janitor, perhaps you "
              "should just leave the mops and brooms alone.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ plunger (80, 129, 84, 142)
    if (mouseX > 79 && mouseX < 85 && mouseY > 128 && mouseY < 143) {
      if (obj.in.box(ego, 72, 138, 92, 147)) {
        print("Maybe touching a dirty smelly plunger wasn't such a good idea.");
        [ lose 2 points
        work1 = -2;
        call(lgc.ScoreHandler);
        set(touchPlunger);
        reset(handsWashed);
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    [ boxes on top shelf (86, 92, 102, 102)
    if (mouseX > 85 && mouseX < 103 && mouseY > 91 && mouseY < 103) {
      if (obj.in.box(ego, 83, 140, 108, 146)) {
        print("The boxes are up too high for you to reach.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ boxes on ground (90, 139, 112, 154)
    if (mouseX > 89 && mouseX < 113 && mouseY > 138 && mouseY < 155) {
      if (obj.in.box(ego, 83, 144, 108, 156)) {
        print("The boxes are sealed up. You shouldn't open them.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ shelf1 supplies (89, 107, 102, 113)
    if (mouseX > 88 && mouseX < 103 && mouseY > 106 && mouseY < 114) {
      if (obj.in.box(ego, 85, 140, 108, 148)) {
        print("You have no need for cleaning supplies.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ shelf2 supplies (89, 118, 102, 126)
    if (mouseX > 88 && mouseX < 103 && mouseY > 117 && mouseY < 127) {
      if (obj.in.box(ego, 85, 140, 108, 148)) {
        print("You have no need for cleaning supplies.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ shelf (84, 103, 105, 141)
    if (mouseX > 83 && mouseX < 106 && mouseY > 102 && mouseY < 142) {
      if (obj.in.box(ego, 74, 140, 108, 148)) {
        print("There is nothing you can do with the shelf.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ floor drain (74, 142, 81, 158)
    if (mouseX > 74 && mouseX < 81 && mouseY > 142 && mouseY < 154) {
      if (obj.in.box(ego, 66, 142, 89, 156)) {
        egodir = STOPPED;
        
        print("The drain is firmly attached to the floor. You can't pull it up.");
      } else {
        print("You can't reach that from here.");
      }
      goto(doneclick);
    }
    
    [ steam pipe (44, 77, 98, 83)
    if (mouseX > 43 && mouseX < 99 && mouseY > 76 && mouseY < 86) {
        egodir = STOPPED;
      print("You can't reach the pipe. Not even if you jumped.");
    }
  }
  
  :doneclick
  set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
}

[ trip and fall into wall
if (egoHitSpecial && !wallbreak && fallSeq == NOFALL) {
  [ fall, and break wall
  print("Look out for that broom!");
  fallSeq = STARTFALL;
}
[ startfall is a separate state so that it can be reached
[ by trying to pick up the broom, or by moving over it
if (fallSeq == STARTFALL) {
  fallSeq = MOVE2WALL1;
  set.view(ego, vw.Tripping);
  set.loop(ego, 0);
  fix.loop(ego);
  move.obj(ego, 72, 145, 1, fDone);
  prevent.input();
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ disable click2move if in walk mode
  if (cursormode == C_WALK) {
    reset(click2move);
  }
    
}
if (fDone && fallSeq == MOVE2WALL1) {
  fallSeq = MOVE2WALL2;
  move.obj(ego, 72, 146, 1, fDone);
}
if (fDone && fallSeq == MOVE2WALL2) {
  fallSeq = FALLDOWN;
  [ turn off auto-cycle to allow cycle-at-rest
  work1 = 0;
  set.upper.left(&ego, &work1);
  set.loop(ego, 2);
  set.cel(ego, 0);
  end.of.loop(ego, fDone);
}

if (fDone && fallSeq == FALLDOWN) {
  fallSeq = BREAKWALL;
  [ drip could be a problem
  if (playSound) {
    stop.sound();
    reset(soundDone);
    reset(playSound);
  }
  [ no walkman to worry about when wall breaks 
  work1 = 0;
  work2 = 100;  [ instrument 100
  set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT);
  set(playSound);
  sound(s.Crumble1, soundDone);
  start.update(wall);
  end.of.loop(wall, fDone);
}
if (fallSeq == BREAKWALL) {
  current.cel(wall, work1);
  if (work1 == 10) {
    sound(s.Crumble2, soundDone);
  }
  if (fDone) {
    reset(fDone);
    fallSeq = SITUP;
    print("As you fall against the wall, it caves in, revealing a large hole.");
    set(wallbreak);
    stop.update(wall);
    ignore.objs(wall);
    set.loop(ego, 3);
    set.cel(ego, 0);
    end.of.loop(ego, fDONE);
  }
}
if (fDone && fallSeq == SITUP) {
  reset(fDone);
  player.control();
  accept.input();
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  fallSeq = FALLDONE;
  work1 = 5;
  call(lgc.ScoreHandler);
}
if (egoDir != STOPPED && fallSeq == FALLDONE) {
  [ get up first
  egoDir = STOPPED;
  fallSeq = STANDUP;
  set.loop(ego, 4);
  end.of.loop(ego, fDone);
}
if (fDone && fallSeq == STANDUP) {
  reset(fDone);
  fallSeq = NOFALL;
  set.view(ego, vw.Ego);
  set.loop(ego, 2);
  start.cycling(ego);
  release.loop(ego);
  [ reenable click2move if in walk mode
  if (cursormode == C_WALK) {
    set(click2move);
  }
  [ restore auto-cycle
  work1 = 1;
  set.upper.left(&ego, &work1);
  triptimer = TRIPDELAY;
}
[ trip over broom even after first fall, unless it's been moved
if (egoHitSpecial && !broommoved && cantrip && egodir != STOPPED && movebroomstatus == NOMOVE) {
  print("You trip over the broom again!");
  work1 = -1;
  call(lgc.ScoreHandler);
  reset(cantrip);
  triptimer = TRIPDELAY;
  triptimer = 90;
  set.view(ego, vw.Tripping);
  current.loop(ego, work1);
  if (work1 > 1) {
    work1 -= 2;
  }
  set.loop.v(ego, work1);
  fix.loop(ego);
  wander(ego);
  prevent.input();
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  prevent.input();
}
[ check for end of stumbling sequence
if (triptimer > TRIPDELAY) {
  --triptimer;
  if (triptimer == TRIPDELAY) {
    [ restore ego movement
    set.view(ego, vw.Ego);
    release.loop(ego);
    egoDir = STOPPED;
    player.control();
    accept.input();
    cursoricon = cursormode;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  }
} else {
  [ not stumbling - if not on the broom, run the countdown
  if (!egoHitSpecial && triptimer > 0 && !broommoved) {
    --triptimer;
    if (triptimer == 0) {
      set(cantrip);
    }
  }
}

[ move broom to corner
if (fDone && movebroomstatus == MOVE2CORNER) {
  erase(broom);
  set.cel(broom, 1);
  position(broom, 44, 153);
  draw(broom);
  stop.update(broom);
  movebroomstatus = NOMOVE;
  block(43, 148, 50, 154);
  set(broommoved);
  work1 = 2;
  call(lgc.ScoreHandler);
}

if (opendoor) {
  reset(opendoor);
  if (closetopen) {
    print("The closet door is already open.");
  } else {
    if (posn(ego, 97, 142, 106, 147)) {
      [ only play sound if walkman is not in use
      if ((currentTrack == 0 || currentTrack > 128)) {
        [ drip could be a problem
        if (playSound) {
          stop.sound();
          reset(soundDone);
          reset(playSound);
        }
        call(lgc.SetSound);
        sound(s.DoorOpen, soundDone);
        set(playSound);
      }
      end.of.loop(door, fDoorDone);
    } else {
      print("You are not close enough.");
    }
  }
}
if (closedoor) {
  reset(closedoor);
  if (closetopen) {
    if (posn(ego, 97, 142, 106, 156)) {
      reverse.loop(door, fDoorDone);
      [ only play sound if walkman is not in use
      if ((currentTrack == 0 || currentTrack > 128)) {
        [ drip could be a problem
        if (playSound) {
          stop.sound();
          reset(soundDone);
          reset(playSound);
        }
        call(lgc.SetSound);
        sound(s.DoorClose, soundDone);
        set(playSound);
      }
    } else {
      print("You are not close enough.");
    }
  } else {
    print("The closet door is already closed.");
  }
}

if (fDoorDone) {
  reset(fDoorDone);
  if (closetopen) {
    reset(closetopen);
  } else {
    set(closetopen);
  }
}

[ washing hands
if (fDoneWash) {
  if (washSeq == MOVESINK1) {
    washSeq = MOVESINK2;
    move.obj(ego, 60, 144, 1, fDoneWash);
    return();
  }
  if (washSeq == MOVESINK2) {
    set(noScript);
    load.view(vw.EgoWashingHands);
    set.view(ego, vw.EgoWashingHands);
    set.loop(ego, 0);
    set.cel(ego, 0);
    work1 = 0;
    set.upper.left(&ego, &work1);  [ autocycle(oSOBJ, vMODE)
    end.of.loop(ego, fDoneWash);
    washSeq = WASHING;
    return();
  }
  if (washSeq == WASHING) {
    washSeq = 0;
    set.view(ego, vw.Ego);
    set.loop(ego, 3);
    discard.view(vw.EgoWashingHands);
    start.cycling(ego);
    work1 = 1;
    set.upper.left(&ego, &work1);  [ autocycle(oSOBJ, vMODE)
    reset(noScript);
    reset(disableGameFunctions);
    accept.input();
    set(handsWashed);
    if (touchTrees && touchPlunger) {
      print("Washing the sap and plunger-stink from your hands was a good "
            "idea. They are now clean and germ-free!");
      work1 = 3;
    } else {
      if (touchTrees) {
        print("You wash the sap from your hands. Now they aren't so sticky.");
        work1 = 1;
      } else {
        if (touchPlunger) {
          print("Touching that plunger was not a great idea. Washing your "
                "hands is.");
          work1 = 2;
        } else {
          print("Might as well wash your hands. Cleanliness is next to "
                "godliness!");
          work1 = 0;
        }
      }
    }
    if (!firstWash) {
      set(firstWash);
      ++work1;
    }
    call(lgc.ScoreHandler);
    reset(touchTrees);
    reset(touchPlunger);
  }
}

[ **************************************
[ CHECK FOR AN EXIT CONDITION
[ **************************************
:exit

if (wallbreak && !crouch && posn(ego, 70, 139, 80, 139)) {
  egoDir = STOPPED;
  set(crouch);
  set.view(ego, vw.ClosetArt);
  set.loop(ego, 3);
  set.cel(ego, 0);
  program.control();
  prevent.input();
  [ turn off auto-cycle to allow cycle-at-rest
  work1 = 0;
  set.upper.left(&ego, &work1);  [ autocycle(oSOBJ, vMODE)
  end.of.loop(ego, fDone);
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
}
if (crouch && fDone) {
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  new.room(rm.SecretRoom);  [ ##LE002##
}

if ((posn(ego, 100, 143, 100, 143) || posn(ego, 101, 144, 101, 144) ||
     posn(ego, 101, 145, 101, 145) || posn(ego, 102, 146, 102, 146) ||
     posn(ego, 102, 147, 102, 147))) {
  if (closetopen) { 
    [ show wait cursor
    cursoricon = C_WAIT;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
    [ fade out, then change room
    block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
    new.room(rm.Hallway);  [ ##LE001##
  } else {
    if (egoDir > 0 && egoDir < 5) {
      egoDir = STOPPED;
    }
  }
}
return();


[ **************************************
[ DECLARED MESSAGES
[ **************************************
#message 1 "%g20"
#message 2 "%g21"
#message 3 "%g22"
#message 4 "%g23"
#message 5 "%g24"
#message 6 "%g25"
#message 7 "%g26"
#message 8 "%g27"
