[ ********************************************************************
[
[ rm.Hallway
[
[ 
[
[ ********************************************************************

#define oClosetDoor    o1

#define opendoor     f220
#define closedoor    f221
#define fDoorDone    f222
#define clicktransom   f223


if (newRoom)
  {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ do basic room initialization
  call(lgc.RoomInit);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(0);
  
  [ add additional room initialization here
  load.view(vw.HallwayArt);

  [ add the three doors that don't open
  add.to.pic(vw.HallwayArt, 1, 0, 46, 139, 0, 4);
  add.to.pic(vw.HallwayArt, 1, 0, 86, 139, 0, 4);
  add.to.pic(vw.HallwayArt, 1, 0, 126, 139, 0, 4);
    
  [ add toolbar buttons
  load.view(vw.Toolbar);
  call(lgc.InitToolbar);
  
  [ load ego view, animate and draw ego
  [ this is sometimes handled in logic 0
  [ instead of in each individual logic
  if (previousRoom == rm.Closet) {
    position(ego, 22, 149);
    egodir = STOPPED;
    set.loop(ego, 0);
  } else {
    position(ego, 151, 147);
    egodir = STOPPED;
    set.loop(ego, 1);
  }
  
  [ add ego to the room
  draw(ego);
  
  [ closet door
  animate.obj(oClosetDoor);
  set.view(oClosetDoor, vw.HallwayArt);
  set.loop(oClosetDoor, 0);
  set.cel(oClosetDoor, 0);
  stop.cycling(oClosetDoor);
  ignore.objs(oClosetDoor);
  set.priority(oClosetDoor, 12);
  if (closetopen) {
    set.cel(oClosetDoor, 4);
  }
  position(oClosetDoor, 18, 150);
  draw(oClosetDoor);
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, FADECOUNT, BLACK); [ fade.in(BYVAR, DELAY, COLOR)
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  return();
  }

[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ place said tests here
  if (said("look")) {
    print("There are several doors in this hallway.");
  }

  if ((said("open", "closet", "door") || said("open", "door"))) {
    set(opendoor);
  }
  if ((said("close", "closet", "door") || said("close", "door"))) {
    set(closedoor);
  }
}
  
[ if NOT clicking on the actual toolbar, check for
[ clicks on ingame items 13, 62, 159, 155
if (mouseX > 12 && mouseX < 160 && mouseY > 69 && mouseY < 164 && 
    controller(cLeftClick)) {
  [ look
  if (cursormode == C_LOOK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You see you.");
      goto(doneclick);
    }
    
    [ vent duct - 68, 82, 78, 90
    if (mouseX > 67 && mouseX < 79 && mouseY > 89 && mouseY < 99) {
      print("This vent duct provides a nice stream of cool air to "
            "help keep the building comfortable.");
      goto(doneclick);
    }
    
    [ doors
    if (mouseY > 114 && mouseY < 147) {
      [ door 1 - 47, 107, 59, 138
      if (mouseX > 46 && mouseX < 60) {
        print("door 1");
        goto(doneclick);
      }
      [ door 2 - 87, 107, 99, 138
      if (mouseX > 86 && mouseX < 100) {
        print("door 2");
        goto(doneclick);
      }
      [ door 3 - 127, 107, 139, 138
      if (mouseX > 126 && mouseX < 140) {
        print("door 3");
       goto(doneclick);
      }
    }
    
    [ transom windows - 47/87/127, 95, 59/99/139, 106
    reset(clicktransom);
    if (mouseY > 102 && mouseY < 114) {
      if (mouseX > 46 && mouseX < 60) {
        set(clicktransom);
      }
      if (mouseX > 86 && mouseX < 100) {
        set(clicktransom);
      }
      if (mouseX > 126 && mouseX < 140) {
        set(clicktransom);
      }
      if (clicktransom) {
        print("transom");
        goto(doneclick);
      }
    }
    
    [ nothing specific - check general areas
    
    [ walls
    
    [ floor
    
    [ ceiling
    
    print("There are several doors in this hallway.");
    goto(doneclick);
  }
  
  [ talk
  if (cursormode == C_TALK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You have nothing to say.");
    } else {
      print("There is no one here to talk to.");
    }
    goto(doneclick);
  }
  
  [ use
  if (cursormode == C_USE) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("Please don't touch yourself. Someone might see you.");
      goto(doneclick);
    }
    
  
    [ closet door
    if (posn(oClosetDoor, ON.OBJ, 1, &mouseX, &mouseY)) {
      if (closetopen) {
        set(closedoor);
      } else {
        set(opendoor);
      }
    } else {
      print("There is nothing to pick up or use here.");
      set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
    }
  }
  
  :doneclick
  set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
}

[*****
:noInput           [  all non-input related code goes here 
[*****

[ add other room specific statements here
if (opendoor) {
  reset(opendoor);
  if (closetopen) {
    print("The closet door is already open.");
  } else {
    if (posn(ego, 12, 137, 29, 156)) {
      end.of.loop(oClosetDoor, fDoorDone);
    } else {
      print("You are not close enough.");
    }
  }
}
if (closedoor) {
  reset(closedoor);
  if (closetopen) {
    if (posn(ego, 12, 137, 29, 156)) {
      reverse.loop(oClosetDoor, fDoorDone);
    } else {
      print("You are not close enough.");
    }
  } else {
    print("The closet door is already closed.");
  }
}

if (fDoorDone) {
  reset(fDoorDone);
  if (closetopen) {
    reset(closetopen);
  } else {
    set(closetopen);
  }
}
    

[*****
:exit              [  test for leaving the room
[*****
if (egoHitSpecial) {
  if (closetopen) {
    [ show wait cursor
    cursoricon = C_WAIT;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
    [ fade out, then change room
    block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
    new.room(rm.Closet);  [ ##LE002##
  } else {
    [ don't let ego go through the door
    if (egoDir > 5 && egoDir < 8) {
      egoDir = STOPPED;
    }
  }
}
if (edgeEgoHit == RIGHT_EDGE) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  new.room(rm.Lobby);  [ ##LE001##
}
return();

[*****
[ messages         [  declared messages go here
[*****
#message 1 "%g40"
#message 2 "%g41"
#message 3 "%g42"
#message 4 "%g43"
#message 5 "%g44"
#message 6 "%g45"
#message 7 "There are several doors in this hallway." 