[ ********************************************************************
[
[ rm.Hallway
[
[ 
[
[ ********************************************************************
[ TODO: more said statements for hallway

[ **************************************
[ LOCAL DEFINES
[ **************************************
#define oClosetDoor    o1
#define oExamDoor      o2
#define oReceptionist  o3
#define oNurse         o4
#define oSecurity      o3 [ receptionist and security guard never on screen at same time

#define opendoor       f220
#define closedoor      f221
#define fDoorDone      f222
#define clickOn        f223
#define openExamDoor   f224
#define fExamDoorDone  f225

#define examDoorSeq    v220
#define    SHUT         0
#define    OPENED       1
#define    NURSETURN    2
#define    NURSETALK    3
#define    OPEN4        4
#define gExamCount     v221
#define arrestSeq      v222
#define    NOARREST     0
#define    COPARRIVES   1
#define    GOTOEGO      2
#define    TURNTOEGO    3
#define    CUFFEGO      4
#define    EGOWALKS     5
#define    ESCORT1      6
#define    SHUTDOOR     7
#define    ESCORT2      8
#define    ARRESTED     9
#define arrestCounter  v223


if (newRoom)
  {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(0);
  
  [ art work used in room
  load.view(vw.HallwayArt);
  load.view(vw.Nurse);
  
  [ sound effects and music
  load.sound(s.DoorOpen);
  load.sound(s.DoorClose);
  load.sound(s.CallCops);
  load.sound(s.PerpWalk);
  
  [ add the two doors that don't open
  add.to.pic(vw.HallwayArt, 1, 0, 86, 132, 12,  4);
  add.to.pic(vw.HallwayArt, 1, 0, 126, 132, 12, 4);
    
  [ closet door
  animate.obj(oClosetDoor);
  set.view(oClosetDoor, vw.HallwayArt);
  set.loop(oClosetDoor, 0);
  set.cel(oClosetDoor, 0);
  stop.cycling(oClosetDoor);
  ignore.objs(oClosetDoor);
  set.priority(oClosetDoor, 12);
  if (closetopen) {
    set.cel(oClosetDoor, 4);
  }
  position(oClosetDoor, 18, 142);
  draw(oClosetDoor);
  
  [ exam room door
  animate.obj(oExamDoor);
  set.view(oExamDoor, vw.HallwayArt);
  set.loop(oExamDoor, 1);
  set.cel(oExamDoor, 0);
  stop.cycling(oExamDoor);
  ignore.objs(oExamDoor);
  ignore.blocks(oExamDoor);
  set.priority(oExamDoor, 12);
  position(oExamDoor, 47, 132);
  draw(oExamDoor);
  
  [ nurse
  animate.obj(oNurse);
  set.view(oNurse, vw.Nurse);
  random(0, 1, work1);
  set.loop.v(oNurse, work1);
  set.cel(oNurse, 8);
  stop.cycling(oNurse);
  ignore.objs(oNurse);
  ignore.blocks(oNurse);
  set.priority(oNurse, 11);
  position(oNurse, 49, 131);
  [don't draw yet
  
  [ add toolbar buttons
  load.view(vw.ButtonsDown);
  call(lgc.InitToolbar);
  
  [ ego  
  if (previousRoom == rm.Closet) {
    position(ego, 22, 141);
    egodir = STOPPED;
    set.loop(ego, 0);
  } else {
    [ if ego is in hall without permission
    if (!allowedPastLobby) {
      [ add receptionist
      load.view(vw.ReceptionistWalk);
      animate.obj(oReceptionist);
      set.view(oReceptionist, vw.ReceptionistWalk);
      set.loop(oReceptionist, 0);
      stop.cycling(oReceptionist);
      position(oReceptionist, 115, 143);
      draw(oReceptionist);
    }
    position(ego, 151, 139);
    egodir = STOPPED;
    set.loop(ego, 1);
  }
  
  [ add ego to the room
  draw(ego);
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, FADECOUNT, BLACK); [ fade.in(BYVAR, DELAY, COLOR)
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
[  return();
}

[ if ego is not allowed
if (!allowedPastLobby) {
  print("Excuse me sir! Only patients are allowed past the lobby. Please go back.");
  goto(exit);
}

[ if ego is being escorted from the premises
if (arrestSeq > NOARREST) {
  if (arrestSeq == COPARRIVES) {
    [ cop appears
    animate.obj(oSecurity);
    load.view(vw.Security);
    set.view(oSecurity, vw.Security);
    position(oSecurity, 153, 140);
    [ set autocycle
    work1 = 1;
    set.upper.left(&oSecurity, &work1);
    draw(oSecurity);
    get.posn(ego, work1, work2);
    work2 += 2;
    work3 = 1;
    move.obj.v(oSecurity, work1, work2, work3, fExamDoorDone);
    arrestSeq = GOTOEGO;
  }
  if (arrestSeq == GOTOEGO && fExamDoorDone) {
    [ turn to ego
    get.posn(ego, work1, work2);
    ++work2;
    work3 = 1;
    move.obj.v(oSecurity, work1, work2, work3, fExamDoorDone);
    work1 = egoX;
    arrestSeq = TURNTOEGO;
  }
  if (arrestSeq == TURNTOEGO && fExamDoorDone) {
    [ 'handcuff' ego
    arrestSeq = CUFFEGO;
    arrestCounter = 15;
  }
  if (arrestSeq == CUFFEGO) {
    if (arrestCounter > 0) {
      --arrestCounter;
    } else {
      arrestSeq = EGOWALKS;
    }
  }
  if (arrestSeq == EGOWALKS) {
    [ start perp-walk music
    [ (sound settings are already correct)
    sound(s.PerpWalk, soundDone);
    set(playSound);
    [ ego starts walking
    load.view(vw.EgoArrest);
    set.view(ego, vw.EgoArrest);
    work1 = egoX;
    work2 = egoY;
    work1 += 7;
    work3 = 1;
    move.obj.v(ego, work1, work2, work3, fExamDoorDone);
    set.loop(oSecurity, 0);
    arrestSeq = ESCORT1;
  }
  if (arrestSeq == ESCORT1 && fExamDoorDone) {
    [ cop escorts ego off screen
    move.obj(ego, 159, 138, 1, fExamDoorDone);
    move.obj(oSecurity, 159, 140, 1, fExamDoorDone);
    arrestSeq = SHUTDOOR;
  }
  if (arrestSeq == SHUTDOOR && posn(ego, 63, 134, 63, 148)) {
    [ nurse shuts door
    reverse.loop(oExamDoor, fExamDoorDone);
    normal.cycle(oNurse);
    arrestSeq = ESCORT2;
  }
  
  
[**** BEGIN DEBUG CODE
if ((arrestSeq == SHUTDOOR || arrestSeq == ESCORT2)) {
  display(24, 0, "ego pos: (%v38, %v39)  egoedge: %v2   ");
}
[**** END DEBUG CODE

  if (arrestSeq == ESCORT2 && edgeEgoHit == RIGHT_EDGE) {
    [ done
    erase(ego);
    erase(oSecurity);
    deathType = 4;
    arrestSeq = ARRESTED;
    stop.sound();
    [ restore cursor
    cursoricon = cursormode;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  }
  return();
}

if (soundDone && arrestSeq >= ESCORT1 && arrestSeq != ARRESTED) {
  [ continue perp-walk sound
  sound(s.PerpWalk, soundDone);
  set(playSound);
}

[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ place said tests here
  if (said("look")) {
    print("There are several doors in this hallway.");
  }

  if ((said("open", "closet", "door") || said("open", "door"))) {
    set(opendoor);
  }
  if ((said("close", "closet", "door") || said("close", "door"))) {
    set(closedoor);
  }
}
  
[ if cursor is look/talk/use and left-click and NOT 
[ clicking on the actual toolbar, check for clicks
[ on in-game items
if (cursoricon > C_WALK && cursoricon < C_WAIT && 
    controller(cLeftClick) &&
    (mouseX < 14 || mouseX > 145 || mouseY > 17)) {
  [ ignore borders
  if ((mouseX < 13 || mouseX > 151 || mouseY < 55 || mouseY > 148)) {
    goto(doneclick);
  }

  [ look
  if (cursormode == C_LOOK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You see you.");
      goto(doneclick);
    }
    
    [ vent duct - 68, 75, 78, 83
    if (mouseX > 67 && mouseX < 79 && mouseY > 74 && mouseY < 84) {
      print("This vent duct provides a nice stream of cool air to "
            "help keep the building comfortable.");
      goto(doneclick);
    }
    
    [ doors
    if (mouseX > 17 && mouseX < 25 && mouseY > 100 && mouseY < 141) {
      print("This is a closet door. It has a sign on it that says \"Please keep this door closed.\"");
      goto(doneclick);
    }
    if (mouseY > 99 && mouseY < 133) {
      [ door 1 - 47, 107, 59, 138
      if (mouseX > 46 && mouseX < 60) {
        print("This door leads to one of the exam rooms.");
        goto(doneclick);
      }
      [ door 2 - 87, 107, 99, 138
      if (mouseX > 86 && mouseX < 100) {
        print("This is the door to the restroom.");
        goto(doneclick);
      }
      [ door 3 - 127, 107, 139, 138
      if (mouseX > 126 && mouseX < 140) {
        print("It looks like a typical office door.");
       goto(doneclick);
      }
    }
    
    [ transom windows - 47/87/127, 95, 59/99/139, 106
    reset(clickOn);
    if (mouseY > 87 && mouseY < 100) {
      if (mouseX > 46 && mouseX < 60) {
        set(clickOn);
      }
      if (mouseX > 86 && mouseX < 100) {
        set(clickOn);
      }
      if (mouseX > 126 && mouseX < 140) {
        set(clickOn);
      }
      if (clickOn) {
        print("A transom at the top of the door allows light to filter into the hallway from the room.");
        if (mouseX > 86 && mouseX < 100) {
          print("And also a slightly unpleasant odor.");
        }
        goto(doneclick);
      }
    }
    
    [ door signs
    [62, 102, 68, 106
    if (mouseX > 61 && mouseX < 69 && mouseY > 101 && mouseY < 107) {
      print("The door sign says \"Exam Room 1\"");
      goto(doneclick);
    }
    [102, 102, 110, 109
    if (mouseX > 101 && mouseX < 111 && mouseY > 101 && mouseY < 110) {
      print("The door sign indicates this is a unisex bathroom.");
      goto(doneclick);
    }
    [142, 102, 148, 106
    if (mouseX > 141 && mouseX < 149 && mouseY > 101 && mouseY < 107) {
      print("The name plate for this office says \"Dr. Brane, MD\"");
      goto(doneclick);
    }
    
    [ nothing specific
    print("The floors and walls of this hallway are clean and bright, as you would expect in a medical facility.");
    print("There several doors leading to other rooms.");
    goto(doneclick);
  }
  
  [ talk
  if (cursormode == C_TALK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You have nothing to say.");
    } else {
      print("There is no one here to talk to.");
    }
    goto(doneclick);
  }
  
  [ use
  if (cursormode == C_USE) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("Please don't touch yourself. Someone might see you.");
      goto(doneclick);
    }
    
    [ closet door
    if (posn(oClosetDoor, ON.OBJ, 1, &mouseX, &mouseY)) {
      if (posn(ego, 12, 129, 29, 148)) {
        if (closetopen) {
          set(closedoor);
        } else {
          set(opendoor);
        }
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
      
    [ exam room door
    if (posn(oExamDoor, ON.OBJ, 1, &mouseX, &mouseY)) {
      if (posn(ego, 40, 132, 60, 138)) {
        set(openExamDoor);
      } else {
        print("Move closer.");
      }
    }

    [ other doors
    if (mouseY > 99 && mouseY < 133) {
      [ bathroom door
      if (mouseX > 86 && mouseX < 100) {
        if (posn(ego, 80, 132, 100, 138)) {
          print("KNOCK KNOCK");
          block(MSGBOX.COLOR, LT_MAGENTA, MAGENTA, WHITE); [ msgbox.color(BORDER, FG, BG)
          print.at("\"Occupied! I'll be out in a minute.\"",11, 46, 35);
          block(MSGBOX.COLOR.V, &msgBoxBorder, &msgBoxFG, &msgBoxBG); [ msgbox.color.v(vBORDER, vFG, vBG)
          print("<sniff> Hmm, something tells you it might be more than a minute...");
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      [ office door
      if (mouseX > 126 && mouseX < 140) {
        if (posn(ego, 120, 132, 140, 138)) {
          print("KNOCK KNOCK");
          print("No answer. And the door is locked. Dr. Brane must be at his castle today.");
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
    }

    [ transom windows - 47/87/127, 95, 59/99/139, 106
    reset(clickOn);
    if (mouseY > 86 && mouseY < 100) {
      if (mouseX > 46 && mouseX < 60) {
        set(clickOn);
      }
      if (mouseX > 86 && mouseX < 100) {
        set(clickOn);
      }
      if (mouseX > 126 && mouseX < 140) {
        set(clickOn);
      }
      if (clickOn) {
        print("You can't reach the transoms. They are too high.");
        goto(doneclick);
      }
    }

    [ vent duct - 68, 75, 78, 83
    if (mouseX > 67 && mouseX < 79 && mouseY > 75 && mouseY < 84) {
      print("The vent duct is too high for you to reach.");
      goto(doneclick);
    }
  }
  
  :doneclick
  set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
}

[*****
:noInput           [  all non-input related code goes here 
[*****

[**************************************************************
[ CLOSET DOOR
[**************************************************************
if (opendoor) {
  reset(opendoor);
  if (closetopen) {
    print("The closet door is already open.");
  } else {
    if (posn(ego, 12, 129, 29, 148)) {
      [ only play sound effects if walkman not in use
      if ((currentTrack == 0 || currentTrack > 128)) {
        [ play door-open sound, using current sound settings
        [ TODO: set parameters based on current sound settings
          set(4channelsound);
          set.key(0, 0, SET.SNDMODE); [ set.sndmode(MODE)
          [ use correct instrument
          work1 = 0;
          work2 = 128;
          set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
        sound(s.DoorOpen, soundDone);
      }
      end.of.loop(oClosetDoor, fDoorDone);
    } else {
      print("You are not close enough.");
    }
  }
}
if (closedoor) {
  reset(closedoor);
  if (closetopen) {
    if (posn(ego, 12, 129, 29, 148)) {
      [ only play sound effects if walkman not in use
      if ((currentTrack == 0 || currentTrack > 128)) {
        [ play door-open sound, using current sound settings
        [ TODO: set parameters based on current sound settings
          set(4channelsound);
          set.key(0, 0, SET.SNDMODE); [ set.sndmode(MODE)
          [ use correct instrument
          work1 = 0;
          work2 = 128;
          set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
        sound(s.DoorClose, soundDone);
      }
      reverse.loop(oClosetDoor, fDoorDone);
      if (!scoreClosetDoor) {
        set(scoreClosetDoor);
        work1 = 1;
        call(lgc.ScoreHandler);
      }
    } else {
      print("You are not close enough.");
    }
  } else {
    print("The closet door is already closed.");
  }
}
if (fDoorDone) {
  reset(fDoorDone);
  if (closetopen) {
    reset(closetopen);
  } else {
    set(closetopen);
  }
}
    
[**************************************************************
[ EXAM ROOM DOOR
[**************************************************************
if (openExamDoor) {
  [ only play sound effects if walkman not in use
  if ((currentTrack == 0 || currentTrack > 128)) {
    [ play open-door sound, using current sound settings
    [ TODO: set parameters based on current sound settings
      set(4channelsound);
      set.key(0, 0, SET.SNDMODE); [ set.sndmode(MODE)
      [ use correct instrument
      work1 = 0;
      work2 = 128;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
    sound(s.DoorOpen, soundDone);
  }
  reset(openExamDoor);
  examDoorSeq = OPENED;
  end.of.loop(oExamDoor, fExamDoorDone);
  set.cel(oNurse, 8);
  stop.cycling(oNurse);
  draw(oNurse);
  prevent.input();
  program.control();
}

if (fExamDoorDone) {
  reset(fExamDoorDone);
  if (examDoorSeq == OPENED) {
    [ nurse turns around
    examDoorSeq = NURSETURN;
    reverse.loop(oNurse, fExamDoorDone);
    goto(doneEDoorSeq);
  }
  if (examDoorSeq == NURSETURN) {
    if (gExamCount < 4) {
      examDoorSeq = NURSETALK;
      if (gExamCount == 0) {
        print("I'm sorry, I'm conducting a patient exam right now, you can't come in.");
      }
      if (gExamCount == 1) {
        print("Hey! Close that door, I'm in the middle of a patient exam!");
      }
      if (gExamCount == 2) {
        print("I said close that door! You can't come in here!");
      }
      if (gExamCount == 3) {
        print("Please stop interrupting me, or I will call security!");
      }
      ++gExamCount;
      [ nurse closes the door
      reverse.loop(oExamDoor, fExamDoorDone);
      normal.cycle(oNurse);
      [ only play sound effects if walkman not in use
      if ((currentTrack == 0 || currentTrack > 128)) {
        [ play door-close sound, using current sound settings
        [ TODO: set parameters based on current sound settings 
          set(4channelsound);
          set.key(0, 0, SET.SNDMODE); [ set.sndmode(MODE)
          [ use correct instrument
          work1 = 0;
          work2 = 128;
          set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
        sound(s.DoorClose, soundDone);
        set(playSound);
      }
    } else {
      print("I asked you to stop interrupting!! Security!!!");
      prevent.input();
      program.control();
      [ switch to wait cursor
      [ show wait cursor
      cursoricon = C_WAIT;
      set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
      set(disableGameFunctions);
      arrestSeq = COPARRIVES;
      [ if walkman in use, stop it
      if (currentTrack >0 && currentTrack < 128) {
        currentTrack *= -1;
        stop.sound();
        reset(playSound);
      }
      [ play call-cops music, using current sound settings
      [ TODO: set parameters based on current sound settings
        set(4channelsound);
        set.key(0, 0, SET.SNDMODE); [ set.sndmode(MODE)
        [ use correct instrument
        work1 = 0;
        work2 = 0;
        set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT);
      sound(s.CallCops, soundDone);
      set(playSound);
    }
    goto(doneEDoorSeq);
  }
  if (examDoorSeq == NURSETALK) {
    accept.input();
    player.control();
    erase(oNurse);
    examDoorSeq = SHUT;
  }
}
:doneEDoorSeq

[*****
:exit              [  test for leaving the room
[*****
if (egoHitSpecial) {
  if (closetopen) {
    [ show wait cursor
    cursoricon = C_WAIT;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
    [ fade out, then change room
    block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
    new.room(rm.Closet);  [ ##LE002##
  } else {
    [ don't let ego go through the door
    if (egoDir > 5 && egoDir < 8) {
      egoDir = STOPPED;
    }
  }
}
if ((edgeEgoHit == RIGHT_EDGE || !allowedPastLobby)) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  new.room(rm.Lobby);  [ ##LE001##
}
return();

[ **************************************
[ DECLARED MESSAGES
[ **************************************
#message 1 "%g20"
#message 2 "%g21"
#message 3 "%g22"
#message 4 "%g23"
#message 5 "%g24"
#message 6 "%g25"
#message 7 "%g26"
#message 8 "%g27"
