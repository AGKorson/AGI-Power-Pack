[ ********************************************************************
[
[ rm.Office
[
[ 
[
[ ********************************************************************

#define oSafe          o1
#define oPicture       o2
#define oDeskDrawer    o3
#define oFileDrawer    o4
#define oFolder        o5
#define oRug           o6

#define fMoveRug     f220
#define tripped      f221


if (newRoom) {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(1);
  
  [ add additional room initialization here
  
  [ add toolbar buttons
  load.view(vw.Toolbar);
  call(lgc.InitToolbar);
  
  [ load view with this room's art
  load.view(vw.OfficeArt);
  
  [ printer page
  if (has("memo")) {
    [ memo is not on printer
    add.to.pic(vw.OfficeArt, 5, 0, 80, 107, 4, 15);
  }
  
  [ folder under rug
  if (!has("folder")) {
    animate.obj(oFolder);
    set.view(oFolder, vw.OfficeArt);
    set.loop(oFolder, 5);
    set.cel(oFolder, 1);
    stop.cycling(oFolder);
    ignore.objs(oFolder);
    position(oFolder, 92, 149);
    set.priority(oFolder, 4);
    if (rugStatus == 1) {
      draw(oFolder);
    }
  }
  [ rug
  animate.obj(oRug);
  set.view(oRug, vw.OfficeArt);
  if (rugStatus == 0) { [ 0=down, 1=up, 2=flat
    set.loop(oRug, 0);
    set.cel(oRug, 0);
  } else {
    if (rugStatus == 1) {
      set.loop(oRug, 0);
      set.cel(oRug, 5);
    } else {
      set.loop(oRug, 5);
      set.cel(oRug, 2);
    }
  }
  stop.cycling(oRug);
  ignore.objs(oRug);
  position(oRug, 87, 149);
  set.priority(oRug, 4);
  draw(oRug);
  
  [ safe
  animate.obj(oSafe);
  set.view(oSafe, vw.OfficeArt);
  set.loop(oSafe, 1);
  set.cel(oSafe, 0);
  stop.cycling(oSafe);
  position(oSafe, 45, 119);
  [ draw(oSafe);
  
  [ picture
  animate.obj(oPicture);
  set.view(oPicture, vw.OfficeArt);
  set.loop(oPicture, 4);
  set.cel(oPicture, 0);
  stop.cycling(oPicture);
  position(oPicture, 46, 119);
  draw(oPicture);
  
  [ add ego to the room
  position(ego, 107, 138);
  draw(ego);
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, 25, 0); [ fade.in(BYVAR, DELAY, COLOR)
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  
  return();
}

[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ place said tests here
  if (said("look")) {
    print("You see nothing of interest.");
  }
}

[*****
:noInput           [  all non-input related code goes here 
[*****
[ if NOT clicking on the actual toolbar, check for
[ clicks on ingame items
if (controller(cLeftClick)) {
  if (mouseX > 33 && mouseX < 128 && mouseY > 71 && mouseY < 163) {
    [ look
    if (cursormode == C_LOOK) {
      [ ego
      if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
        print("You see you.");
        goto(doneclick);
      }
      
      [ anything else is the generic room look
      print("You are in an old office. Dust everywhere.");
      goto(doneclick);
    }
    
    [ talk
    if (cursormode == C_TALK) {
      [ ego
      if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
        print("You have nothing to say.");
      } else {
        print("There is no one here to talk to.");
      }
      goto(doneclick);
    }
    
    [ use
    if (cursormode == C_USE) {
      [ ego
      if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
        print("Please don't touch yourself. Not even when you're alone.");
        goto(doneclick);
      }
      
      [ folder
      if (posn(oFolder, ON.OBJ, 0, &mouseX, &mouseY)) {
        [ pick it up
        get("folder");
        erase(oFolder);
      }
      
      [ lift/lower carpet 89, 141, 98, 149
      if (mouseX > 88 && mouseX < 99 && mouseY > 148 && mouseY < 158) {
        if (rugStatus == 1) { [0=down; 1=up; 2=flat; 3=raising; 4=lowering
          [ lower the rug
          rugStatus = 4;
          reverse.loop(oRug, fMoveRug);
        } else {
          if ((rugStatus == 0 || rugStatus == 2)) {
            [ raise it
            rugStatus = 3;
            set.loop(oRug, 0);
            set.cel(oRug, 0);
            end.of.loop(oRug, fMoveRug);
            if (!has("folder")) {
              draw(oFolder);
            }
          }
        }
        goto(doneclick);
      }
    }
    
    :doneclick
    set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
  }
}

[ rug movement
if (fMoveRug) {
  reset(fMoveRug);
  if (rugStatus == 3) {
    rugStatus = 1;
  } else {
    if (has("folder")) {
      rugStatus = 2;
      set.loop(oRug, 5);
      set.cel(oRug, 2);
    } else {
      rugStatus = 0;
      erase(oFolder);
    }
  }
}

[ trip on rug
if (!tripped && rugStatus == 1 && egoHitSpecial && egoY < 115) {
  [trip! or block?
  print("trip!");
  set(tripped);
}


[ if tripped, AND not on rug, reset tripped
if (tripped) {
  if (!egoHitSpecial) {
    reset(tripped);
  }
}

[*****
:exit              [  test for leaving the room
[*****

[ add exit checks here (or use layout editor)

if (egoX > 115 && egoHitSpecial) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  new.room(rm.ControlRoom);  [ ##LE001##
}
return();

[*****
[ messages         [  declared messages go here
[*****
#message 1 "%l90|40"
#message 2 "%l90|41"
#message 3 "%l90|42"
#message 4 "%l90|43"
#message 5 "%l90|44"
#message 6 "%l90|45"