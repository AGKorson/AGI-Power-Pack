[*********************************************************************
[
[ rm.Office
[
[ 
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
#define oSafe          o1
#define oPainting      o2
#define oDeskDrawer    o3
#define oFileDrawer    o4
#define oFolder        o5
#define oRug           o6

#define tmpPalette     s12 [ s13, s14
#define tmpStr         s15

#define tripCounter   v220
#define tmpLoop       v221

#define fMoveRug      f220
#define fOnTripLine   f221
#define fDeskDrawer   f222
#define fFileDrawer   f223
#define fPainting     f224
#define fSafe         f225
#define fBackup       f226
#define fTripping     f227
#define fWarnTrip     f228
#define tempflag      f229

[ rug status
#define RUG_DOWN     0
#define RUG_UP       1
#define RUG_FLAT     2
#define RUG_LIFTING  3
#define RUG_LOWERING 4

[***************************************
[ FIRST CYCLE ONLY
[***************************************
if (newRoom) {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(1);
  
  [ add toolbar buttons
  load.view(vw.ButtonsDown);
  call(lgc.InitToolbar);
  
  [ use a custom palette so the area behind painting can be shown 
  [ to be a bit darker than rest of wall
  work1 = 66;
  block(STR.LEFT, &palette, &tmpPalette, &work1); [ str.left(sINPUT, sRESULT, vLEN)
  tmpStr = "001E1E";
  block(STR.CONCAT, &tmpPalette, &tmpStr, 0); [ str.concat(sINPUT, sADD)
  work1 = 24;
  block(STR.RIGHT, &palette, &tmpStr, &work1); [ strRight(sINPUT, sRESULT, vLEN)
  block(STR.CONCAT, &tmpPalette, &tmpStr, 0); [ str.concat(sINPUT, sADD)
  block(SET.MSG, BYNUM, &m9, &tmpPalette); [ set.msg(FN_MODE, MSGOLD, STRNEW)
  block(SET.PALETTE, BYNUM, &m9, 1); [ set.palette(FN_MODE, PALETTE, LOADONLY)
  
  [ load view with this room's art
  load.view(vw.OfficeArt);
  
  [ printer page
  if (has("memo")) {
    [ memo is not on printer
    add.to.pic(vw.OfficeArt, 5, 0, 80, 107, 4, 15);
  }
  
  [ folder under rug
  if (!has("folder")) {
    animate.obj(oFolder);
    set.view(oFolder, vw.OfficeArt);
    set.loop(oFolder, 5);
    set.cel(oFolder, 1);
    stop.cycling(oFolder);
    ignore.objs(oFolder);
    position(oFolder, 92, 149);
    set.priority(oFolder, 5);
    if (rugStatus == 1) {
      draw(oFolder);
    }
  }
  [ rug
  animate.obj(oRug);
  set.view(oRug, vw.OfficeArt);
  if (rugStatus == RUG_DOWN) {
    set.loop(oRug, 0);
      set.cel(oRug, 0);
  } else {
    if (rugStatus == RUG_UP) {
      set.loop(oRug, 0);
      set.cel(oRug, 5);
    } else {
      [ show rug flat
      set.loop(oRug, 5);
      set.cel(oRug, 2);
    }
  }
  stop.cycling(oRug);
  ignore.objs(oRug);
  position(oRug, 87, 149);
  set.priority(oRug, 5);
  draw(oRug);
  
  [ safe
  animate.obj(oSafe);
  set.view(oSafe, vw.OfficeArt);
  position(oSafe, 45, 119);
  stop.cycling(oSafe);
  ignore.objs(oSafe);
  if (safeStatus == OPEN) {
    if (has("BaDASS manual")) {
      set.loop(oSafe, 1);
      set.cel(oSafe, 7);
    } else {
      set.loop(oSafe, 5);
      set.cel(oSafe, 7);
    }
  } else {
    set.loop(oSafe, 1);
    set.cel(oSafe, 0);
  }
  
  [ painting
  animate.obj(oPainting);
  set.view(oPainting, vw.OfficeArt);
  set.loop(oPainting, 4);
  if (paintingStatus == OPEN) {
    set.cel(oPainting, 7);
    draw(oSafe);
  } else {
    set.cel(oPainting, 0);
  }
  stop.cycling(oPainting);
  position(oPainting, 46, 119);
  draw(oPainting);
  
  [ desk drawer
  animate.obj(oDeskDrawer);
  set.view(oDeskDrawer, vw.OfficeArt);
  ignore.objs(oDeskDrawer);
  ignore.blocks(oDeskDrawer);
  stop.cycling(oDeskDrawer);
  if (officeDskDrStatus != CLOSED) {
    if (officeDskDrStatus == OPEN1) {
      position(oDeskDrawer, 84, 126);
      if (has("Walkman")) {
        set.loop(oDeskDrawer, 2);
        set.cel(oDeskDrawer, 2);
      } else {
        set.loop(oDeskDrawer, 5);
        set.cel(oDeskDrawer, 4);
      }
    } else {
      position(oDeskDrawer, 84, 130);
      if (has("post-it note")) {
        set.loop(oDeskDrawer, 2);
        set.cel(oDeskDrawer, 2);
      } else {
        set.loop(oDeskDrawer, 5);
        set.cel(oDeskDrawer, 3);
      }
    }
    draw(oDeskDrawer);
  }
  
  [ file cabinet drawer
  animate.obj(oFileDrawer);
  set.view(oFileDrawer, vw.OfficeArt);
  set.priority(oFileDrawer, 10);
  ignore.objs(oFileDrawer);
  stop.cycling(oFileDrawer);
  if (officeFileDrStatus != CLOSED) {
    if (officeFileDrStatus == OPEN1) {
      position(oFileDrawer, 93, 112);
      set.loop(oFileDrawer, 3);
      set.cel(oFileDrawer, 2);
    }
    if (officeFileDrStatus == OPEN2) { 
      position(oFileDrawer, 93, 121);
      if (has("notebook")) {
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 2);
      } else {
        set.loop(oFileDrawer, 5);
        set.cel(oFileDrawer, 5);
      }
    }
    if (officeFileDrStatus == OPEN3) { 
      position(oFileDrawer, 93, 130);
      if (has("floppy disk")) {
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 2);
      } else {
        set.loop(oFileDrawer, 5);
        set.cel(oFileDrawer, 6);
      }
    }
    draw(oFileDrawer);
  }
  
  [ add ego to the room
  if (previousRoom != rm.SafeDoor) {
    position(ego, 107, 138);
  }
  draw(ego);
  egoDir = STOPPED;
  
  load.sound(s.GetItem);
  load.sound(s.RugTrip);
  load.sound(s.CloseSafe);
  load.sound(s.OpenDrawer);
  load.sound(s.CloseDrawer);
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, BYNUM, FADECOUNT, 0); [ fade.in(FN_MODE, DELAY, COLOR)
  [ restore cursor
  cursoricon = cursormode;
  set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM);
  return();
}

[***************************************
[ NON-INPUT CODE 
[***************************************

[*******************
[ PAINTING
[*******************
[ ego backing up from painting
if (fBackup) {
  reset(fBackup);
  release.loop(ego);
}
[ painting movement
if (fPainting) {
  reset(fPainting);
  if (paintingStatus == OPENING) {
    paintingStatus = OPEN;
    if (!scorePainting) {
      print("Look at that! There's a hidden safe behind the painting!");
      set(scorePainting);
      work1 = 2;
      call(lgc.ScoreHandler);
    }
    draw(oSafe);
  } else {
    paintingStatus = CLOSED;
  }
}

[*******************
[ SAFE
[*******************
[ safe door movement
if (fSafe) {
  reset(fSafe);
  if (safeStatus == OPENING) {
    safeStatus = OPEN;
    if (!has("BaDASS manual")) {
      set.loop(oSafe, 5);
      set.cel(oSafe, 7);
      force.update(oSafe);
    }
  } else {
    safeStatus = CLOSED;
  }
}
[ open the safe
if (fOpenSafe) {
  reset(fOpenSafe);
  [ if too close
  if (obj.in.box(ego, 38, 126, 65, 129)) {
    work1 = 130;
    work2 = 1;
    set.loop(ego, 3);
    fix.loop(ego);
    move.obj.v(ego, egoX, work1, work2, fBackup);
  }
  end.of.loop(oSafe, fSafe);
  safeStatus = OPENING;
}

[*******************
[ RUG
[*******************
[ rug movement
if (fMoveRug) {
  reset(fMoveRug);
  if (rugStatus == RUG_LIFTING) {
    rugStatus = RUG_UP;
    if (!findFolder && !has("folder")) {
      set(findFolder);
      print("You see a folder tucked under the corner of the rug!");
      work1 = 2;
      call(lgc.ScoreHandler);
    }
  } else {
    if (has("folder")) {
      rugStatus = RUG_FLAT;
      set.loop(oRug, 5);
      set.cel(oRug, 2);
    } else {
      rugStatus = RUG_DOWN;
      erase(oFolder);
    }
  }
}
[ trip on rug
if (!fOnTripLine && rugStatus == RUG_UP && egoHitSpecial && egoX < 100) {
  set(fOnTripLine);
  set(fTripping);
  load.view(vw.Tripping);
  current.loop(ego, work1);
  tmpLoop = work1;
  if (work1 > 1) {
    work1 -=2;
  }
  work1 *= -1; [ because trip loops are backwards!
  work1 += 1;
  set.view(ego, vw.Tripping);
  set.loop.v(ego, work1);
  fix.loop(ego);
  program.control();
  tripCounter = 9;
  [ play sound if walkman not active
  if ((currentTrack == 0 || currentTrack > 128)) {
    call(lgc.SetSound);
    sound(s.RugTrip, soundDone);
  }
}
[ check trip counter
if (tripCounter) {
  --tripCounter;
  if (tripCounter == 0) {
    set.view(ego, vw.Ego);
    set.loop.v(ego, tmpLoop);
    release.loop(ego);
    player.control();
    if (!fWarnTrip) {
      set(fWarnTrip);
      print("You tripped on the carpet! Good thing you didn't fall "
            "and break your neck.");
    }
    work1 = -1;
    call(lgc.ScoreHandler);
  }
}
[ if fOnTripLine set, AND not on rug, reset fOnTripLine
if (fOnTripLine) {
  if (!egoHitSpecial) {
    reset(fOnTripLine);
  }
}

[*******************
[ DRAWERS
[*******************
[ desk drawer
if (fDeskDrawer) {
  reset(fDeskDrawer);
  if (officeDskDrStatus == CLOSING) {
    erase(oDeskDrawer);
    officeDskDrStatus = CLOSED;
        } else {
    get.posn(oDeskDrawer, work1, work2);
    if (work2 == 126) {
      officeDskDrStatus = OPEN1;
      if (!has("Walkman")) {
        set.loop(oDeskDrawer, 5);
        set.cel(oDeskDrawer, 4);
        force.update(oDeskDrawer);
      }
    } else {
      officeDskDrStatus = OPEN2;
      if (!has("post-it note")) {
        set.loop(oDeskDrawer, 5);
        set.cel(oDeskDrawer, 3);
        force.update(oDeskDrawer);
      }
    }
  }
}
[ file cabinet drawer
if (fFileDrawer) {
  reset(fFileDrawer);
  if (officeFileDrStatus == CLOSING) {
    erase(oFileDrawer);
    officeFileDrStatus = CLOSED;
  } else {
    get.posn(oFileDrawer, work1, work2);
    if (work2 == 112) {
      officeFileDrStatus = OPEN1;
    } else {
      if (work2 == 121) {
        officeFileDrStatus = OPEN2;
        if (!has("notebook")) {
          set.loop(oFileDrawer, 5);
          set.cel(oFileDrawer, 5);
          force.update(oFileDrawer);
        }
      } else {
        officeFileDrStatus = OPEN3;
        if (!has("floppy disk")) {
          set.loop(oFileDrawer, 5);
          set.cel(oFileDrawer, 6);
          force.update(oFileDrawer);
        }
      }
    }
  }
}

[***************************************
[ PROCESS PLAYER INPUT
[***************************************
if (haveInput && !haveMatch && unknownWordNum == 0) {
  if (said("look")) {
    print("This office hasn't been used in a long time. There is a "
          "desk and file cabinet, but no chair. It was probably used "
          "by the control room operator.");
  }
  if (said("look", "floor")) {
    print("There is a thin layer of dust on the plain floor. You can "
          "see your footprints in the dust. It looks like you've been "
          "the only one in here in a very long time. There is a small "
          "rug in the center of the room.");
  }
  if (said("look", "ceiling")) {
    print("The ceiling tiles in the drop-down ceiling are plain and "
          "dull. The fluorescent lights provide adequate lighting for "
          "typical office work.");
  }
  if (said("look", "walls")) {
    print("The walls are mostly bare. There is a large painting on "
          "the wall next to the desk.");
    [ if painting is open, say a little more
    if (paintingStatus == OPEN) {
      print("The painting is hinged on the right side. Behind "
            "it you see a wall safe.");
    }
  }
  if (said("look", "desk")) {
    print("The plain metal desk is old and worn. It has two drawers.");
  }
  if (said("look", "in", "desk")) {
    print("The desk has two drawers that you can look at more closely if you want.");
  }
  if (said("look", "under", "desk")) {
    [ is ego close enough?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      print("There is nothing under the desk.");
    } else {
      print("Move closer.");
    }
  }
  if (said("climb", "desk")) {
    [ is ego close enough?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      print("There is not enough room on the desk for you to climb it.");
    } else {
      print("Move closer.");
    }
  }
  if (said("move", "desk")) {
    print("The desk is too heavy for you to move.");
  }
  
  if (said("look", "file cabinet")) {
    print("A large 3-drawer filing cabinet is in the corner.");
  }
  if (said("look", "in", "file cabinet")) {
    print("The file cabinet has three drawers that you can look at more closely if you want.");
  }
  if (said("move", "file cabinet")) {
    print("The file cabinet is too heavy for you to move.");
  }

  if ((said("look", "drawer") || 
       said("look", "in", "drawer"))) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if ((officeDskDrStatus == OPEN1 || officeDskDrStatus == OPEN2)) {
        goto(lookdeskdrawer);
      } else {
        print("Neither desk drawer is open.");
      }
    } else {
      [ close to file cabinet?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        if ((officeFileDrStatus == OPEN1 || officeFileDrStatus == OPEN2 || 
             officeFileDrStatus == OPEN3)) {
          goto(lookfilecabdrawer);
        } else {
          print("There are no open drawers in the file cabinet.");
        }
      } else {
        print("Move closer.");
      }
    }      
  }
  if ((said("look", "top", "drawer") || 
       said("look", "in", "top", "drawer"))) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == OPEN1) {
        goto(lookdeskdrawer);
      } else {
        print("The top desk drawer is not open.");
      }
    } else {
      [ close to file cabinet?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        if (officeFileDrStatus == OPEN1) {
          goto(lookfilecabdrawer);
        } else {
          print("The top file cabinet drawer is not open.");
        }
      } else {
        print("Move closer.");
      }
    }      
  }
  if ((said("look", "middle", "drawer") || 
       said("look", "in", "middle", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == OPEN2) {
        goto(lookfilecabdrawer);
      } else {
        print("The middle file cabinet drawer is not open.");
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("look", "bottom", "drawer") || 
       said("look", "in", "bottom", "drawer"))) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == OPEN2) {
        goto(lookdeskdrawer);
      } else {
        print("The bottom desk drawer is not open.");
      }
    } else {
      [ close to file cabinet?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        if (officeFileDrStatus == OPEN3) {
          goto(lookfilecabdrawer);
        } else {
          print("The bottom file cabinet drawer is not open.");
        }
      } else {
        print("Move closer.");
      }
    }      
  }
  if ((said("look", "desk", "drawer") || 
       said("look", "in", "desk", "drawer"))) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if ((officeDskDrStatus == OPEN1 || officeDskDrStatus == OPEN2)) {
        goto(lookdeskdrawer);
      } else {
        print("Neither desk drawer is open.");
      }
    } else {
      print("Move closer.");
    }      
  }
  if ((said("look", "top", "desk", "drawer") || 
       said("look", "desk", "top", "drawer") || 
       said("look", "in", "top", "desk", "drawer") || 
       said("look", "in", "desk", "top", "drawer"))) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == OPEN1) {
        goto(lookdeskdrawer);
      } else {
        print("The top desk drawer is not open.");
      }
    } else {
      print("Move closer.");
    }      
  }
  if ((said("look", "bottom", "desk", "drawer") || 
       said("look", "desk", "bottom", "drawer") || 
       said("look", "in", "bottom", "desk", "drawer") || 
       said("look", "in", "desk", "bottom", "drawer"))) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == OPEN2) {
        goto(lookdeskdrawer);
      } else {
        print("The bottom desk drawer is not open.");
      }
    } else {
      print("Move closer.");
    }      
  }
  if ((said("look", "file cabinet", "drawer") ||
       said("look", "in", "file cabinet", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if ((officeFileDrStatus == OPEN1 || officeFileDrStatus == OPEN2 || 
           officeFileDrStatus == OPEN3)) {
        goto(lookfilecabdrawer);
      } else {
        print("There are no open drawers in the file cabinet.");
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("look", "top", "file cabinet", "drawer") || 
       said("look", "file cabinet", "top", "drawer") || 
       said("look", "in", "top", "file cabinet", "drawer") || 
       said("look", "in", "file cabinet", "top", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == OPEN1) {
        goto(lookfilecabdrawer);
      } else {
        print("The top file cabinet drawer is not open.");
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("look", "bottom", "file cabinet", "drawer") || 
       said("look", "file cabinet", "bottom", "drawer") || 
       said("look", "in", "bottom", "file cabinet", "drawer") || 
       said("look", "in", "file cabinet", "bottom", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == OPEN3) {
        goto(lookfilecabdrawer);
      } else {
        print("The bottom file cabinet drawer is not open.");
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("look", "middle", "file cabinet", "drawer") || 
       said("look", "file cabinet", "middle", "drawer") || 
       said("look", "in", "middle", "file cabinet", "drawer") || 
       said("look", "in", "file cabinet", "middle", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == OPEN2) {
        goto(lookfilecabdrawer);
      } else {
        print("The middle file cabinet drawer is not open.");
      }
    } else {
      print("Move closer.");
    }
  }
       
  if (said("open", "drawer")) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      print("Which drawer?");
    } else {
      [ close to file cabinet?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        print("Wich drawer?");
      } else {
        print("Move closer.");
      }
    }
  }
  if (said("open", "top", "drawer")) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == CLOSED) {
        goto(opendesktop);
      } else {
        print("It's not safe to open more than one drawer at a time.");
      }
    } else {
      [ close to file cabinet?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        if (officeFileDrStatus == CLOSED) {
          goto(openfiletop);
        } else {
          print("It's not safe to open more than one drawer at a time.");
        }
      } else {
        print("Move closer.");
      }
    }    
  }
  if (said("open", "middle", "drawer")) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == CLOSED) {
        goto(openfiletop);
      } else {
        print("It's not safe to open more than one drawer at a time.");
      }
    } else {
      print("Move closer.");
    }
  }
  if (said("open", "bottom", "drawer")) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == CLOSED) {
        goto(opendeskbtm);
      } else {
        print("It's not safe to open more than one drawer at a time.");
      }
    } else {
      [ close to file cabinet?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        if (officeFileDrStatus == CLOSED) {
          goto(openfilebtm);
        } else {
          print("It's not safe to open more than one drawer at a time.");
        }
      } else {
        print("Move closer.");
      }    
    }
  }
  if (said("open", "desk", "drawer")) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == CLOSED) {
        print("Which drawer?");
      } else {
        print("It's not safe to open more than one drawer at a time.");
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("open", "top", "desk", "drawer") || 
       said("open", "desk", "top", "drawer"))) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == CLOSED) {
        goto(opendesktop);
      } else {
        if (officeDskDrStatus == OPEN1) {
          print("It's already open.");
        } else {
          print("It's not safe to open more than one drawer at a time.");
        }
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("open", "bottom", "desk", "drawer") || 
       said("open", "desk", "bottom", "drawer"))) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == CLOSED) {
        goto(opendesktop);
      } else {
        if (officeDskDrStatus == OPEN2) {
          print("It's already open.");
        } else {
          print("It's not safe to open more than one drawer at a time.");
        }
      }
    } else {
      print("Move closer.");
    }
  }
       
  if (said("open", "file cabinet", "drawer")) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == CLOSED) {
        print("Which drawer?");
      } else {
        print("It's not safe to open more than one drawer at a time.");
      }
    }
  }
  if ((said("open", "top", "file cabinet", "drawer") || 
       said("open", "file cabinet", "top", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == CLOSED) {
        goto(openfiletop);
      } else {
        if (officeFileDrStatus == OPEN1) {
          print("It's already open.");
        } else {
          print("It's not safe to open more than one drawer at a time.");
        }
      }
    }
  }
  if ((said("open", "bottom", "file cabinet", "drawer") || 
       said("open", "file cabinet", "bottom", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == CLOSED) {
        goto(openfilebtm);
      } else {
        if (officeFileDrStatus == OPEN3) {
          print("It's already open.");
        } else {
          print("It's not safe to open more than one drawer at a time.");
        }
      }
    }
  }
  if ((said("open", "middle", "file cabinet", "drawer") || 
       said("open", "file cabinet", "middle", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == CLOSED) {
        goto(openfilemid);
      } else {
        if (officeFileDrStatus == OPEN2) {
          print("It's already open.");
        } else {
          print("It's not safe to open more than one drawer at a time.");
        }
      }
    }
  }
       
  if (said("close", "drawer")) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if ((officeDskDrStatus == OPEN1 || officeDskDrStatus == OPEN2)) {
        goto(closedeskdrawer);
      } else {
        print("Neither desk drawer is open.");
      }
    } else {
      [ close to file cabinet?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        if ((officeFileDrStatus == OPEN1 || officeFileDrStatus == OPEN2 || 
             officeFileDrStatus == OPEN3)) {
          goto(closefiledrawer);
        } else {
          print("There are no open drawers in the file cabinet.");
        }
      } else {
        print("Move closer.");
      }
    }
  }
  if (said("close", "top", "drawer")) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == OPEN1) {
        goto(closedeskdrawer);
      } else {
        print("The top desk drawer is not open.");
      }
    } else {
      [ close to file cabinet?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        if (officeFileDrStatus == OPEN1) {
          goto(closefiledrawer);
        } else {
          print("The top file cabinet drawer is not open.");
        }
      } else {
        print("Move closer.");
      }
    }
  }
  if (said("close", "middle", "drawer")) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == OPEN2) {
        goto(closefiledrawer);
      } else {
        print("The middle file cabinet drawer is not open.");
      }
    } else {
      print("Move closer.");
    }
  }
  if (said("close", "bottom", "drawer")) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == OPEN2) {
        goto(closedeskdrawer);
      } else {
        print("The bottom desk drawer is not open.");
      }
    } else {
      [ close to file cabinet?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        if (officeFileDrStatus == OPEN3) {
          goto(closefiledrawer);
        } else {
          print("The bottom file cabinet drawer is not open.");
        }
      } else {
        print("Move closer.");
      }
    }
  }
  if (said("close", "desk", "drawer")) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if ((officeDskDrStatus == OPEN1 || officeDskDrStatus == OPEN2)) {
        goto(closedeskdrawer);
      } else {
        print("Neither desk drawer is open.");
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("close", "top", "desk", "drawer") || 
       said("close", "desk", "top", "drawer"))) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == OPEN1) {
        goto(closedeskdrawer);
      } else {
        print("The top desk drawer is not open.");
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("close", "bottom", "desk", "drawer") || 
       said("close", "desk", "bottom", "drawer"))) {
    [ close to desk?
    if (obj.in.box(ego, 74, 131, 101, 136)) {
      if (officeDskDrStatus == OPEN2) {
        goto(closedeskdrawer);
      } else {
        print("The bottom desk drawer is not open.");
      }
    } else {
      print("Move closer.");
    }
  }

  if (said("close", "file cabinet", "drawer")) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if ((officeFileDrStatus == OPEN1 || officeFileDrStatus == OPEN2 || 
           officeFileDrStatus == OPEN3)) {
        goto(closefiledrawer);
      } else {
        print("There are no open drawers in the file cabinet.");
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("close", "top", "file cabinet", "drawer") || 
       said("close", "file cabinet", "top", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == OPEN1) {
        goto(closefiledrawer);
      } else {
        print("The top file cabinet drawer is not open.");
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("close", "middle", "file cabinet", "drawer") || 
       said("close", "file cabinet", "middle", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == OPEN2) {
        goto(closefiledrawer);
      } else {
        print("The middle file cabinet drawer is not open.");
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("close", "bottom", "file cabinet", "drawer") || 
       said("close", "file cabinet", "bottom", "drawer"))) {
    [ close to file cabinet?
    if (obj.in.box(ego, 82, 129, 105, 133)) {
      if (officeFileDrStatus == OPEN3) {
        goto(closefiledrawer);
      } else {
        print("The bottom file cabinet drawer is not open.");
      }
    } else {
      print("Move closer.");
    }
  }
  
  if (said("look", "walkman")) {
    if (has("Walkman")) {
      [ let game functions logic handle it
      reset(haveMatch);
    } else {
      if (obj.in.box(ego, 74, 131, 101, 136) && officeDskDrStatus == OPEN1) {
          print("There is an old Sony Walkman cassette player in "
                "the top desk drawer.");
      } else {
        print("You don't see that here.");
      }
    }
  }
  if (said("get", "walkman")) {
    if (has("Walkman")) {
      print("You already have it.");
    } else {
      if (obj.in.box(ego, 74, 131, 101, 136) && officeDskDrStatus == OPEN1) {
        goto(getwalkman);
      } else {
        print("You don't see that here.");
      }
    }
  }
  
  if (said("look", "post it")) {
    if (has("Walkman")) {
      [ let game functions logic handle it
      reset(haveMatch);
    } else {
      if (obj.in.box(ego, 74, 131, 101, 136) && officeDskDrStatus == OPEN1) {
        print("There is an old Sony Walkman cassette player in "
              "the top desk drawer.");
      } else {
        print("You don't see that here.");
      }
    }
  }
  if (said("get", "post it")) {
    if (has("post-it note")) {
      print("You already have it.");
    } else {
      if (obj.in.box(ego, 74, 131, 101, 136) && officeDskDrStatus == OPEN2) {
        goto(getnote);
      } else {
        print("You don't see that here.");
      }
    }
  }
  
  if (said("look", "notebook")) {
    if (has("notebook")) {
      [ let game functions logic handle it
      reset(haveMatch);
    } else {
      if (obj.in.box(ego, 82, 129, 105, 133) && officeFileDrStatus == OPEN2) {
        print("There is a notebook in the middle file cabinet drawer.");
      } else {
        print("You don't see that here.");
      }
    }
  }
  if (said("get", "notebook")) {
    if (has("notebook")) {
      print("You already have it.");
    } else {
      if (obj.in.box(ego, 82, 129, 105, 133) && officeFileDrStatus == OPEN2) {
        goto(getnotebook);
      } else {
        print("You don't see that here.");
      }
    }
  }
  if (said("look", "disk")) {
    if (has("floppy disk")) {
      [ let game functions logic handle it
      reset(haveMatch);
    } else {
      if (obj.in.box(ego, 82, 129, 105, 133) && officeFileDrStatus == OPEN3) {
        print("There is a box of floppy disks in the bottom file cabinet drawer.");
      } else {
        print("You don't see that here.");
      }
    }
  }
  if (said("get", "disk")) {
    if (has("floppy disk")) {
      print("You already have it.");
    } else {
      if (obj.in.box(ego, 82, 129, 105, 133) && officeFileDrStatus == OPEN3) {
        goto(getdisk);
      } else {
        print("You don't see that here.");
      }
    }
  }
  
  if (said("look", "computer")) {
    print("There is an old IBM PC sitting on the desk, with a "
          "keyboard. No mouse. It is powered down.");
  }
  if ((said("use", "computer") || said("turn on", "computer") || 
       said("turn", "computer", "on"))) {
    if (obj.in.box(ego, 64, 131, 86, 135)) {
      print("The computer is not working. It looks like the hard "
            "drive and power supply have been removed a long time "
            "ago.");
    } else {
      print("Move closer.");
    }
  }
  if (said("look", "monitor")) {
    print("The monitor is an old VGA display. Probably state of the art back in the 80s and 90s.");
  }
  if ((said("turn on", "monitor") || said("turn", "monitor", "on"))) {
    if (obj.in.box(ego, 64, 131, 86, 135)) {
      print("The monitor doesn't have a power cord. It won't turn on.");
    } else {
      print("Move closer.");
    }
  }
  if (said("look", "printer")) {
    print("An old dot matrix printer sits next to the computer.");
    if (!has("memo")) {
      print("The output spool still has a piece of paper in it.");
    }
  }
  if ((said("turn on", "printer") || said("turn", "printer", "on") || 
       said("power up", "printer"))) {
    if (obj.in.box(ego, 73, 131, 97, 135)) {
      print("Pressing the power button doesn't do anything. The printer must be broken.");
    } else {
      print("Move closer.");
    }
  }
  if ((said("look", "paper") || said("look", "memo"))) {
    if (has("memo")) {
      [ let game functions logic handle it
      reset(haveMatch);
    } else {
      if (obj.in.box(ego, 73, 131, 97, 135)) {
        print("The document on the printer output spool looks like an office memo.");
      } else {
        print("Move closer.");
      }
    }
  }
  if ((said("read", "paper") || said("read", "memo"))) {
    if (has("memo")) {
      [ let game functions logic handle it
      reset(haveMatch);
    } else {
      if (obj.in.box(ego, 73, 131, 97, 135)) {
        print("If you want to read it, you should take it off the printer.");
      } else {
        print("Move closer.");
      }
    }
  }
  if ((said("get", "paper") || said("get", "memo"))) {
    if (has("memo")) {
      print("You already have it.");
    } else {
      if (obj.in.box(ego, 73, 131, 97, 135)) {
        goto(getmemo);
      }
    }
  }
  if (said("look", "keyboard")) {
    print("The keyboard is on the desk next to the computer.");
  }
  if (said("get", "keyboard")) {
    if (obj.in.box(ego, 64, 131, 86, 135)) {
      print("You don't need a keyboard, especially one that isn't compatible with modern computers.");
    } else {
      print("Move closer.");
    }
  }
  
  if (said("look", "rug")) {
    goto(lookrug);
  }
  if (said("look", "under", "rug")) {
    if (rugStatus == RUG_UP) {
      if (!has("folder")) {
        if (obj.in.box(ego, 80, 138, 104, 151)) {
          print("There is a folder tucked under the corner of the rug.");
        } else {
          print("It looks like there's something under this corner "
                "of the rug.");
        }
      } else {
        print("There is nothing under the rug corner.");
      }
    } else {
      print("Without lifting the corner, you can't see under "
            "the rug.");
    }
  }
  if ((said("move", "rug") || said("lift", "rug"))) {
    [ ego has to be close to the rug corner
    if (obj.in.box(ego, 80, 138, 104, 151)) {
      [ but not too close
      if (obj.in.box(ego, 83, 141, 101, 148)) {
        print("You can't move the rug corner if you're standing on it.");
      } else {
        if (rugStatus == RUG_UP) {
          print("The rug corner is already up.");
        } else {
          [ raise it
          rugStatus = RUG_LIFTING;
          set.loop(oRug, 0);
          set.cel(oRug, 0);
          end.of.loop(oRug, fMoveRug);
          if (!has("folder")) {
            draw(oFolder);
          }
        }
      }
    } else {
      print("Move closer.");
    }
  }
  if ((said("replace", "rug") || said("restore", "rug") || 
       said("lower", "rug"))) {
    [ ego has to be close to the rug corner
    if (obj.in.box(ego, 80, 138, 104, 151)) {
      [ but not too close
      if (obj.in.box(ego, 83, 141, 101, 148)) {
        print("You can't move the rug corner if you're standing on it.");
      } else {
        if (rugStatus == RUG_UP) {
          [ lower the rug
          rugStatus = RUG_LOWERING;
          reverse.loop(oRug, fMoveRug);
        } else {
          print("The rug corner is already down.");
        }
      }
    } else {
      print("Move closer.");
    }
  }
  if (said("look", "folder")) {
    if (has("folder")) {
      [ let game functions handle it
      reset(haveMatch);
    } else {
      if (rugStatus == RUG_UP && obj.in.box(ego, 80, 138, 104, 151)) {
        print("The folder is tucked under the corner of the rug.");
      } else {
        print("You can't see it from here.");
      }
    }
  }
  if (said("get", "folder")) {
    if (has("folder")) {
        print("You already have it.");
    } else {
      if (rugStatus == RUG_UP && obj.in.box(ego, 80, 138, 104, 151)) {
        goto(getfolder);
      } else {
        print("You can't see it from here.");
      }
    }
  }
  
  if (said("look", "painting")) {
    goto(lookpainting);
  }
  if (said("get", "painting")) {
    print("The painting is much to big for you to carry around.");
  }
  if (said("look", "behind", "painting")) {
    if (paintingStatus == OPEN) {
      print("There is a wall safe behind the painting.");
    } else {
      print("How can you, with the painting up against the wall?");
    }
  }
  if ((said("move", "painting") || said("open", "painting"))) {
    if (paintingStatus == OPEN) {
      goto(openpainting);
    } else {
      print("The painting is already moved away from the wall.");
    }
  }
  if (said("close", "painting")) {
    goto(closepainting);
  }
    
  if (said("look", "safe")) {
    if (paintingStatus == OPEN) {
      if (obj.in.box(ego, 38, 126, 65, 132)) {
        goto(looksafe);
      } else {
        print("Move closer.");
      }
    } else {
      print("What safe?");
    }
  }
  if (said("look", "in", "safe")) {
    if (paintingStatus == OPEN) {
      if (safeStatus == OPEN) {
        if (obj.in.box(ego, 38, 126, 65, 132)) {
          if (!has("BaDASS manual")) {
            print("The safe contains a thin paper back binder.");
          } else {
            print("The safe is empty.");
          }
        } else {
          print("Move closer.");
        }
      } else {
        print("The safe is closed.");
      }
    } else {
      print("What safe?");
    }
  }
  if ((said("open", "safe") || said("unlock", "safe"))) {
    if (paintingStatus == OPEN) {
      if (obj.in.box(ego, 38, 126, 65, 132)) {
        if (safeStatus == OPEN) {
          print("It is already open.");
        } else {
          goto(unlocksafe);
        }
      } else {
        print("Move closer.");
      }
    } else {
      print("What safe?");
    }
  }
  if ((said("close", "safe") || said("lock", "safe"))) {
    if (paintingStatus == OPEN) {
      if (obj.in.box(ego, 38, 126, 65, 132)) {
        if (safeStatus == OPEN) {
          goto(closesafe);
        } else {
          print("It is already shut.");
        }
      } else {
        print("Move closer.");
      }
    } else {
      print("What safe?");
    }
  }
  if ((said("break", "safe") || said("break", "open", "safe"))) {
    if (paintingStatus == OPEN) {
      if (obj.in.box(ego, 38, 126, 65, 132)) {
        if (safeStatus == OPEN) {
          print("No need, it's already open.");
        } else {
          print("Are you kidding? That thing is a Masterlock Model XN-4000. "
                "Practically unbreakable.");
        }
      } else {
        print("Move closer.");
      }
    } else {
      [ let game fuctions logic handle it
      reset(haveMatch);
    }
  }
  if (said("crack", "safe")) {
    if (paintingStatus == OPEN) {
      if (obj.in.box(ego, 38, 126, 65, 132)) {
        if (safeStatus == OPEN) {
          print("No need, it's already open.");
        } else {
          print("If you had any safecracking skills, that just might work...");
          print("But you don't.");
        }
      } else {
        print("Move closer.");
      }
    } else {
      [ let game fuctions logic handle it
      reset(haveMatch);
    }
  }
  if (said("look", "manual")) {
    if (has("BaDASS manual")) {
      [ let game functions logic handle it
      reset(haveMatch);
    } else {
      if (safeStatus == OPEN) {
        if (obj.in.box(ego, 38, 126, 65, 132)) {
          print("In the safe you see what looks like an old computer manual.");
        } else {
          print("Move closer.");
        }
      } else {
        print("You don't see that here.");
      }
    }
  }
  if (said("get", "manual")) {
    if (has("BaDASS manual")) {
      print("You already have it.");
    } else {
      if (safeStatus == OPEN) {
        goto(getmanual);
      } else {
        print("You don't see that here.");
      }
    }
  }

}

[***************************************
[ MOUSE INPUT
[***************************************
[ if cursor is look/talk/use and left-click and NOT 
[ clicking on the actual toolbar, check for clicks
[ on in-game items
if (cursoricon > C_WALK && cursoricon < C_WAIT && 
    controller(cLeftClick) &&
    (mouseX < 14 || mouseX > 145 || mouseY > 17)) {
  [ ignore borders
  if ((mouseX < 32 || mouseX > 127 || mouseY < 64 || mouseY > 154)) {
    goto(doneclick);
  }
  [ look
  if (cursormode == C_LOOK) {
    [ ego
    if (posn(ego, ON.OBJ, OBJ_VISIBLE, &mouseX, &mouseY)) {
      print("This is you.");
      goto(doneclick);
    }
    
    [ painting on wall
    if (posn(oPainting, ON.OBJ, OBJ_VISIBLE, &mouseX, &mouseY)) {
      :lookpainting
      if (paintingStatus == OPEN) {
        print("The painting is hinged on the right side. "
              "Behind it you see a wall safe.");
      } else {
        print("It's a painting of a medieval tower. A very "
              "interesting piece of artwork.");
      }
      goto(doneclick);
    }   
    
    [ safe behind painting, only if painting is open
    if (posn(oSafe, ON.OBJ, OBJ_VISIBLE, &mouseX, &mouseY) && 
        paintingStatus == OPEN) {
      :looksafe
      if (safeStatus == OPEN) {
        if (!has("BaDASS manual")) {
        print("In the safe you see what looks like an old computer manual.");
        } else {
          print("The safe is empty.");
        }
      } else {
        print("A wall safe has been installed behind the painting.");
      }
      goto(doneclick);
    }
    
    [ computer/keyboard
    reset(tempflag);
    if (mouseX > 66 && mouseX < 77 && mouseY > 92 && mouseY < 116) {
      set(tempflag);
    }
    if (mouseX > 69 && mouseX < 80 && mouseY > 114 && mouseY < 120) {
      set(tempflag);
    }
    if (tempflag) {
      print("There is an old IBM PC sitting on the desk, with a "
            "keyboard. No mouse. It is powered down.");
      goto(doneclick);
    }
    
    [ printer
    if (mouseX > 77 && mouseX < 90 && mouseY > 106 && mouseY < 116) {
      print("An old dot matrix printer sits next to the computer.");
      if (!has("memo")) {
        print("The output spool still has a piece of paper in it.");
      }
      goto(doneclick);
    }
    
    [ desk drawer
    if (posn(oDeskDrawer, ON.OBJ, OBJ_VISIBLE, &mouseX, &mouseY)) {
      :lookdeskdrawer
      if (officeDskDrStatus == OPEN1) {
        if (!has("Walkman")) {
          print("There is an old Sony Walkman cassette player in "
                "this drawer.");
        } else {
          print("The desk drawer is empty.");
        }
      } else {
        if (officeDskDrStatus == OPEN2) {
          if (!has("post-it note")) {
            print("There is a large yellow post-it note in "
                  "this drawer.");
          } else {
            print("The desk drawer is empty.");
          }
        } else {
          print("The desk drawer is empty");
        }
      }   
      goto(doneclick);
    }
          
    [ file cabinet drawer
    if (posn(oFileDrawer, ON.OBJ, OBJ_VISIBLE, &mouseX, &mouseY)) {
      :lookfilecabdrawer
      if (officeFileDrStatus == OPEN2) {
        if (!has("notebook")) {
          print("There is a notebook in this file cabinet drawer.");
        } else {
          print("The file cabinet drawer is empty.");
        }
      } else {
        if (officeFileDrStatus == OPEN3) {
          if (!has("floppy disk")) {
            print("There is a box of floppy disks in this file cabinet drawer.");
          } else {
            print("The file cabinet drawer is empty.");
          }
        } else {
          [ if not 2 or 2, must be drawer 1
          print("The file cabinet drawer is empty");
        }
      }   
      goto(doneclick);
    }
    
    [ desk
    if (mouseX > 62 && mouseX < 93 && mouseY > 111 && mouseY < 132) {
      print("The plain metal desk is old and worn. It has two drawers.");
      goto(doneclick);
    }
    
    [ file cabinet
    if (mouseX > 87 && mouseX < 102 && mouseY > 95 && mouseY < 130) {
      print("A large 3-drawer filing cabinet is in the corner.");
      goto(doneclick);
    }
    
    [ folder
    if (posn(oFolder, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      print("There is a folder tucked under the corner of the rug.");
      goto(doneclick);
    }
    
    [ rug
    if (mouseX > 68 && mouseX < 99 && mouseY > 137 && mouseY < 150) {
      [ ignore if on priority-4 pixel
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      work1 /= 16;
      if (work1 != 4) {
        :lookrug
        print("A small throw rug is in the center of the room. The "
              "rug looks worn and dirty.");
        if (rugStatus == RUG_UP) {
          if (!has("folder")) {
            print("It looks like there's something under this corner "
                  "of the rug.");
          } else {
            print("One corner is folded back.");
          }
        } else {
          if (!has("folder")) {
            print("One corner looks a bit rumpled.");
          }
        }
        goto(doneclick);
      }
    }
    
    [ floor
    if (mouseX > 31 && mouseX < 128 && mouseY > 124 && mouseY < 155) {
      [ walls are cyan- ignore them
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      [ ignore priority
      block(BIT.AND, BYNUM, &work1, 15); [ bit.and(FN_MODE, VAR, MASK)
      if (work1 != CYAN) {
        print("There is a thin layer of dust on the plain floor. You can "
              "see your footprints in the dust. It looks like you've been "
              "the only one in here in a very long time. There is a small "
              "rug in the center of the room.");
        goto(doneclick);
      }
    }
    
    [ ceiling
    if (mouseX > 31 && mouseX < 128 && mouseY > 63 && mouseY < 73) {
      [ walls are cyan- ignore them
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      [ ignore priority
      block(BIT.AND, BYNUM, &work1, 15); [ bit.and(FN_MODE, VAR, MASK)
      if (work1 != CYAN) {
        print("The ceiling tiles in the drop-down ceiling are plain and "
              "dull. The fluorescent lights provide adequate lighting for "
              "typical office work.");
        goto(doneclick);
      }
    }
    
    [ walls
    if (mouseX > 31 && mouseX < 128 && mouseY > 63 && mouseY < 150) {
      [ ignore priority
      block(BIT.AND, BYNUM, &work1, 15); [ bit.and(FN_MODE, VAR, MASK)
      if (work1 == CYAN) {
        print("The walls are mostly bare. There is a large painting on "
              "the wall next to the desk.");
        [ if painting is open, say a little more
        if (paintingStatus == OPEN) {
          print("The painting is hinged on the right side. "
                "Behind it you see a wall safe.");
        }
        goto(doneclick);
      }
    }
    [ anything else is the generic room look
    print("This office hasn't been used in a long time. There is a "
          "desk and file cabinet, but no chair. It was probably used "
          "by the control room operator.");
    goto(doneclick);
  }
  
  [ talk
  if (cursormode == C_TALK) {
    [ ego
    if (posn(ego, ON.OBJ, OBJ_VISIBLE, &mouseX, &mouseY)) {
      print("You have nothing to say.");
    } else {
      print("There is no one here to talk to.");
    }
    goto(doneclick);
  }
  
  [ use
  if (cursormode == C_USE) {
    [ ego
    if (posn(ego, ON.OBJ, OBJ_VISIBLE, &mouseX, &mouseY)) {
      print("Please don't touch yourself. Not even when you're alone.");
      goto(doneclick);
    }
    
    [ folder
    if (posn(oFolder, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      [ is ego close enough?
      if (obj.in.box(ego, 80, 138, 104, 151)) {
        [ play sound if walkman not active
        if ((currentTrack == 0 || currentTrack > 128)) {
          call(lgc.SetSound);
          sound(s.GetItem, soundDone);
        }
        :getfolder
        [ pick it up
        get("folder");
        erase(oFolder);
        work1 = 3;
        call(lgc.ScoreHandler);
        print("You pick up the folder. It might be useful.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ lift/lower carpet 89, 141, 98, 149
    if (mouseX > 88 && mouseX < 99 && mouseY > 140 && mouseY < 150) {
      [ ego has to be close to the rug corner
      if (obj.in.box(ego, 80, 138, 104, 151)) {
        [ but not too close
        if (obj.in.box(ego, 83, 141, 101, 148)) {
          print("You can't move the rug corner if you're standing on it.");
        } else {
          if (rugStatus == RUG_UP) {
            [ lower the rug
            rugStatus = RUG_LOWERING;
            reverse.loop(oRug, fMoveRug);
          } else {
            if ((rugStatus == RUG_DOWN || rugStatus == RUG_FLAT)) {
              [ raise it
              rugStatus = RUG_LIFTING;
              set.loop(oRug, 0);
              set.cel(oRug, 0);
              end.of.loop(oRug, fMoveRug);
              if (!has("folder")) {
                draw(oFolder);
              }
            }
          }
        }
      } else {
        print("You are not close enough to the rug corner to move it.");
      }
      goto(doneclick);
    }
    
    [ printer output
    if (!has("memo")) {
      if (mouseX > 77 && mouseX < 88 && mouseY > 103 && mouseY < 107) {
        [ is ego close enough?
        if (obj.in.box(ego, 73, 131, 97, 135)) {
          :getmemo
          [ play sound if walkman not active
          if ((currentTrack == 0 || currentTrack > 128)) {
            call(lgc.SetSound);
            sound(s.GetItem, soundDone);
          }
          [ get the memo
          get("memo");
          add.to.pic(vw.OfficeArt, 5, 0, 80, 107, 4, 15);
          print("You tear the memo off the printer output spool.");
          work1 = 3;
          call(lgc.ScoreHandler);
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
    }
    
    [ on desk drawer, and open
    if (posn(oDeskDrawer, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY) && 
        officeDskDrStatus != CLOSING) {
      [ is ego close enough?
      if (obj.in.box(ego, 74, 131, 101, 136)) {
        if (officeDskDrStatus == OPEN1 && mouseY < 124) {
          [ clicking on top drawer item
          if (!has("Walkman")) {
            :getwalkman
            [ play sound (walkman not active if not picked up)
            call(lgc.SetSound);
            sound(s.GetItem, soundDone);
            get("Walkman");
            work1 = 2;
            call(lgc.ScoreHandler);
            print("You take the Walkman out of the drawer.");
            set.loop(oDeskDrawer, 2);
            set.cel(oDeskDrawer, 2);
            force.update(oDeskDrawer);      
          }
        } else {
          if (officeDskDrStatus == OPEN2 && mouseY < 128) {
            [ clicking on bottom drawer item
            if (!has("post-it note")) {
              :getnote
              [ play sound if walkman not active
              if ((currentTrack == 0 || currentTrack > 128)) {
                call(lgc.SetSound);
                sound(s.GetItem, soundDone);
              }
              get("post-it note");
              work1 = 3;
              call(lgc.ScoreHandler);
              print("The post-it note may be of use so you grab it from "
                    "the drawer.");
              set.loop(oDeskDrawer, 2);
              set.cel(oDeskDrawer, 2);
              force.update(oDeskDrawer);      
            }
          } else {
            :closedeskdrawer
            [ clicking on drawer
            set.loop(oDeskDrawer, 2);
            set.cel(oDeskDrawer, 2);
            reverse.loop(oDeskDrawer, fDeskDrawer);
            officeDskDrStatus = CLOSING;
            [ play sound if walkman not active
            if ((currentTrack == 0 || currentTrack > 128)) {
              call(lgc.SetSound);
              sound(s.CloseDrawer, soundDone);
            }
          }
        }
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ top desk drawer, closed
    if (mouseX > 81 && mouseX < 93 && mouseY > 120 && mouseY < 125 && 
        officeDskDrStatus == CLOSED) {
      [ is ego close enough?
      if (obj.in.box(ego, 74, 131, 101, 136)) {
        :opendesktop
        position(oDeskDrawer, 84, 126);
        set.loop(oDeskDrawer, 2);
        set.cel(oDeskDrawer, 0);
        draw(oDeskDrawer);
        end.of.loop(oDeskDrawer, fDeskDrawer);
        officeDskDrStatus = OPENING;
        [ play sound if walkman not active
        if ((currentTrack == 0 || currentTrack > 128)) {
          call(lgc.SetSound);
          sound(s.OpenDrawer, soundDone);
        }
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ bottom desk drawer, closed
    if (mouseX > 81 && mouseX < 93 && mouseY > 124 && mouseY < 129 && 
        officeDskDrStatus == CLOSED) {
      [ is ego close enough
      if (obj.in.box(ego, 74, 131, 101, 136)) {
        :opendeskbtm
        position(oDeskDrawer, 84, 130);
        set.loop(oDeskDrawer, 2);
        set.cel(oDeskDrawer, 0);
        draw(oDeskDrawer);
        end.of.loop(oDeskDrawer, fDeskDrawer);
        officeDskDrStatus = OPENING;
        [ play sound if walkman not active
        if ((currentTrack == 0 || currentTrack > 128)) {
          call(lgc.SetSound);
          sound(s.OpenDrawer, soundDone);
        }
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ on file cabinet drawer, and open
    if (posn(oFileDrawer, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY) && 
        officeFileDrStatus != CLOSING) {
      [ is ego close enough?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        if (officeFileDrStatus == OPEN1 && mouseY < 105) {
          goto(doneclick);
        }
        if (officeFileDrStatus == OPEN2 && mouseY < 114) {
          if (!has("notebook")) {
            :getnotebook
            [ play sound if walkman not active
            if ((currentTrack == 0 || currentTrack > 128)) {
              call(lgc.SetSound);
              sound(s.GetItem, soundDone);
            }
            get("notebook");
            work1 = 3;
            call(lgc.ScoreHandler);
            print("This notebook might come in handy.");
            set.loop(oFileDrawer, 3);
            set.cel(oFileDrawer, 2);
            force.update(oFileDrawer);      
          }
          goto(doneclick);
        }
        if (officeFileDrStatus == OPEN3 && mouseY < 123) {
          if (!has("floppy disk")) {
            :getdisk
            [ play sound if walkman not active
            if ((currentTrack == 0 || currentTrack > 128)) {
              call(lgc.SetSound);
              sound(s.GetItem, soundDone);
            }
            get("floppy disk");
            work1 = 4;
            call(lgc.ScoreHandler);
            print("You decide to take one of the disks.");
            set.loop(oFileDrawer, 3);
            set.cel(oFileDrawer, 2);
            force.update(oFileDrawer);      
          }
          goto(doneclick);
        }
        :closefiledrawer
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 2);
        reverse.loop(oFileDrawer, fFileDrawer);
        officeFileDrStatus = CLOSING;
        [ play sound if walkman not active
        if ((currentTrack == 0 || currentTrack > 128)) {
          call(lgc.SetSound);
          sound(s.CloseDrawer, soundDone);
        }
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    [ top file cabinet drawer, closed
    if (mouseX > 91 && mouseX < 101 && mouseY > 99 && mouseY < 109 &&
        officeFileDrStatus == CLOSED) {
      [ is ego close enough?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        :openfiletop
        position(oFileDrawer, 93, 112);
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 0);
        draw(oFileDrawer);
        end.of.loop(oFileDrawer, fFileDrawer);
        officeFileDrStatus = OPENING;
        [ play sound if walkman not active
        if ((currentTrack == 0 || currentTrack > 128)) {
          call(lgc.SetSound);
          sound(s.OpenDrawer, soundDone);
        }
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
      
    [ middle file cabinet drawer, closed
    if (mouseX > 91 && mouseX < 101 && mouseY > 108 && mouseY < 118 &&
        officeFileDrStatus == CLOSED) {
      [ is ego close enough?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        :openfilemid
        position(oFileDrawer, 93, 121);
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 0);
        draw(oFileDrawer);
        end.of.loop(oFileDrawer, fFileDrawer);
        officeFileDrStatus = OPENING;
        [ play sound if walkman not active
        if ((currentTrack == 0 || currentTrack > 128)) {
          call(lgc.SetSound);
          sound(s.OpenDrawer, soundDone);
        }
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
      
    [ bottom file cabinet drawer, closed
    if (mouseX > 92 && mouseX < 101 && mouseY > 117 && mouseY < 127 &&
        officeFileDrStatus == CLOSED) {
      [ is ego close enough?
      if (obj.in.box(ego, 82, 129, 105, 133)) {
        :openfilebtm
        position(oFileDrawer, 93, 130);
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 0);
        draw(oFileDrawer);
        end.of.loop(oFileDrawer, fFileDrawer);
        officeFileDrStatus = OPENING;
        [ play sound if walkman not active
        if ((currentTrack == 0 || currentTrack > 128)) {
          call(lgc.SetSound);
          sound(s.OpenDrawer, soundDone);
        }
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ painting, open
    if (mouseX > 59 && mouseX < 66 && mouseY > 87 && mouseY < 118 && 
        paintingStatus == OPEN) {
      :closepainting
      [ can't if safe is still open
      if (safeStatus != CLOSED) {
        print("The painting won't close with the safe open.");
        goto(doneclick);
      }
      [ is ego close enough? 
      if (obj.in.box(ego, 38, 126, 65, 132)) {
        [ if too close
        if (egoY < 130) {
          work1 = 130;
          work2 = 1; 
          set.loop(ego, 3);
          fix.loop(ego);
          move.obj.v(ego, egoX, work1, work2, fBackup);
        }
        reverse.loop(oPainting, fPainting);
        paintingStatus = CLOSING;
        erase(oSafe);
        [ play sound if walkman not active
        if ((currentTrack == 0 || currentTrack > 128)) {
          call(lgc.SetSound);
          sound(s.CloseDrawer, soundDone);
        }
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ painting, closed
    if (mouseX > 45 && mouseX < 61 && mouseY > 87 && mouseY < 116 && 
        paintingStatus == CLOSED) {
      :openpainting
      [ is ego close enough? 
      if (obj.in.box(ego, 38, 126, 65, 132)) {
        end.of.loop(oPainting, fPainting);
        paintingStatus = OPENING;
        [ play sound if walkman not active
        if ((currentTrack == 0 || currentTrack > 128)) {
          call(lgc.SetSound);
          sound(s.OpenDrawer, soundDone);
        }
        [ if too close
        if (egoY < 130) {
          work1 = 130;
          work2 = 1;
          set.loop(ego, 3);
          fix.loop(ego);
          move.obj.v(ego, egoX, work1, work2, fBackup);
        } 
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ safe only accessible if painting is open
    if (paintingStatus == OPEN) {      
      [ safe door, open
      if (mouseX > 44 && mouseX < 50 && mouseY > 106 && mouseY < 120 && 
          safeStatus == OPEN) {
        :closesafe
        [ is ego close enough? 
        if (obj.in.box(ego, 38, 126, 65, 132)) {
          set.loop(oSafe, 1);
          set.cel(oSafe, 7);
          reverse.loop(oSafe, fSafe);
          safeStatus = CLOSING;
          [ play sound if walkman not active
          if ((currentTrack == 0 || currentTrack > 128)) {
            call(lgc.SetSound);
            sound(s.CloseSafe, soundDone);
          }
          [ if too close
          if (egoY < 130) {
            work1 = 130;
            work2 = 1;
            set.loop(ego, 3);
            fix.loop(ego);
            move.obj.v(ego, egoX, work1, work2, fBackup);
          }
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      
      [ safe interior, open
      if (mouseX > 49 && mouseX < 56 && mouseY > 103 && mouseY < 115 && 
          safeStatus == OPEN) {
        if (!has("BaDASS manual")) {
          :getmanual
          [ is ego close enough? 
          if (obj.in.box(ego, 38, 126, 65, 132)) {
            [ play sound if walkman not active
            if ((currentTrack == 0 || currentTrack > 128)) {
              call(lgc.SetSound);
              sound(s.GetItem, soundDone);
            }
            get("BaDASS manual");
            print("You take the manual out of the safe.");
            set.loop(oSafe, 1);
            set.cel(oSafe, 7);
            force.update(oSafe);
            work1 = 3;
            call(lgc.ScoreHandler);
          } else {
            print("Move closer.");
          }
        }
        goto(doneclick);
      }
      
      [ safe door, closed
      [ player has to enter correct combo to get it open
      if (mouseX > 47 && mouseX < 60 && mouseY > 102 && mouseY < 116 && 
          safeStatus == CLOSED) {
        [ is ego close enough? 
        if (obj.in.box(ego, 38, 126, 65, 132)) {
          :unlocksafe
          [ show wait cursor
          cursoricon = C_WAIT;
          set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM);
          [ fade out, then display access panel
          block(FADE.OUT, BYNUM, 10, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
          new.room(rm.SafeDoor);  [ ##LE002##
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
    }
    
    [ computer/keyboard
    reset(tempflag);
    if (mouseX > 66 && mouseX < 77 && mouseY > 92 && mouseY < 116) {
      set(tempflag);
    }
    if (mouseX > 69 && mouseX < 80 && mouseY > 114 && mouseY < 127) {
      set(tempflag);
    }
    if (tempflag) {
      [ is ego close enough?
      if (obj.in.box(ego, 64, 131, 86, 135)) {
        print("The computer is not working. It looks like the hard "
              "drive and power supply have been removed a long time "
              "ago.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ printer
    if (mouseX > 77 && mouseX < 90 && mouseY > 106 && mouseY < 116) {
      [ is ego close enough?
      if (obj.in.box(ego, 77, 131, 98, 135)) {
        print("The printer appears to be broken.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    [ rug
    if (mouseX > 68 && mouseX < 99 && mouseY > 137 && mouseY < 150) {
      [ ignore if on priority-4 pixel
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      work1 /= 16;
      if (work1 != 4) {
        [ is ego close enough?
        if (obj.in.box(ego, 61, 133, 103, 154)) {
          print("This portion of the rug is stuck to the floor.");
        } else {
          print("Move closer.");
        }
      }
    }
  }
  
  :doneclick
  set.key(CLR.CTRL, CLR.CTRL, cLeftClick); [ clear.controller(cLeftClick);
}

[***************************************
[ CHECK FOR AN EXIT CONDITION
[***************************************
:exit

if (egoX > 100 && egoHitSpecial) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM);
  [ fade out, then change room
  block(FADE.OUT, BYNUM, FADECOUNT, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
  [ restore normal palette
  block(SET.PALETTE, BYNUM, &m10, 1); [ set.palette(FN_MODE, PALETTE, LOADONLY)
  new.room(rm.ControlRoom);  [ ##LE001##
}

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "%g20"
#message 2 "%g21"
#message 3 "%g22"
#message 4 "%g23"
#message 5 "%g24"
#message 6 "%g25"
#message 7 "%g26"
#message 8 "%g27"
[ custom palette
#message 9 "00000000002A002A00002A2A2A00002A002A2A15002A2A2A15151515153F153F15001E1E3F15153F153F3F3F153F3F3F"
[ default palette
#message 10 "%s2"
