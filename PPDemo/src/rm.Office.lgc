[ ********************************************************************
[
[ rm.Office
[
[ 
[
[ ********************************************************************

#define oSafe          o1
#define oPicture       o2
#define oDeskDrawer    o3
#define oFileDrawer    o4
#define oFolder        o5
#define oRug           o6

#define tmpPalette     s12 [ s13, s14
#define tmpStr         s15

#define fMoveRug      f220
#define fOnTripLine   f221
#define fDeskDrawer   f222
#define fFileDrawer   f223
#define fPicture      f224
#define fSafe         f225
#define fBackup       f226
#define fTripping     f227
#define fWarnTrip     f228

#define tripCounter   v220
#define tmpLoop       v221

[ rug status
#define RUG_DOWN     0
#define RUG_UP       1
#define RUG_FLAT     2
#define RUG_LIFTING  3
#define RUG_LOWERING 4

[ picture/safe/drawer status
#define CLOSED       0
#define OPEN         1
#define OPEN1        1
#define OPEN2        2
#define OPEN3        3
#define CLOSING      4
#define OPENING      5


if (newRoom) {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(1);
  
  [ use a custom palette so the area behind picture can be shown 
  [ to be a bit darker than rest of wall
  work1 = 66;
  block(STR.LEFT, &palette, &tmpPalette, &work1); [ str.left(sINPUT, sRESULT, vLEN)
  tmpStr = "001E1E";
  block(STR.CONCAT, &tmpPalette, &tmpStr, 0); [ sINPUT, sADD)
  work1 = 24;
  block(STR.RIGHT, &palette, &tmpStr, &work1); [ strRight(sINPUT, sRESULT, vLEN)
  block(STR.CONCAT, &tmpPalette, &tmpStr, 0); [ sINPUT, sADD)
  block(SET.MSG, 0, &m7, &tmpPalette); [ set.msg(BYVAR, MSGOLD, STRNEW)
  block(SET.PALETTE, 0, &m7, 1); [ set.palette(BYVAR, PALETTE, LOADONLY)
  
  [ add toolbar buttons
  load.view(vw.Toolbar);
  call(lgc.InitToolbar);
  
  [ load view with this room's art
  load.view(vw.OfficeArt);
  
  [ printer page
  if (has("memo")) {
    [ memo is not on printer
    add.to.pic(vw.OfficeArt, 5, 0, 80, 107, 4, 15);
  }
  
  [ folder under rug
  if (!has("folder")) {
    animate.obj(oFolder);
    set.view(oFolder, vw.OfficeArt);
    set.loop(oFolder, 5);
    set.cel(oFolder, 1);
    stop.cycling(oFolder);
    ignore.objs(oFolder);
    position(oFolder, 92, 149);
    set.priority(oFolder, 5);
    if (rugStatus == 1) {
      draw(oFolder);
    }
  }
  [ rug
  animate.obj(oRug);
  set.view(oRug, vw.OfficeArt);
  if (rugStatus == RUG_DOWN) {
    set.loop(oRug, 0);
    set.cel(oRug, 0);
  } else {
    if (rugStatus == RUG_UP) {
      set.loop(oRug, 0);
      set.cel(oRug, 5);
    } else {
      [ RUG_FLAT
      set.loop(oRug, 5);
      set.cel(oRug, 2);
    }
  }
  stop.cycling(oRug);
  ignore.objs(oRug);
  position(oRug, 87, 149);
  set.priority(oRug, 5);
  draw(oRug);
  
  [ safe
  animate.obj(oSafe);
  set.view(oSafe, vw.OfficeArt);
  position(oSafe, 45, 119);
  stop.cycling(oSafe);
  ignore.objs(oSafe);
  if (safeStatus == OPEN) {
    if (!has("BaDASS manual")) {
      [ TODO: stupid bug in set.loop; should I patch that too?
      set.loop(oSafe, 5);
      set.cel(oSafe, 7);
    } else {
      set.loop(oSafe, 1);
      set.cel(oSafe, 7);
    }
    draw(oSafe);
  } else {
    set.loop(oSafe, 1);
    set.cel(oSafe, 0);
  }
  
  [ picture
  animate.obj(oPicture);
  set.view(oPicture, vw.OfficeArt);
  set.loop(oPicture, 4);
  if (pictureStatus == OPEN) {
    set.cel(oPicture, 7);
  } else {
    set.cel(oPicture, 0);
  }
  stop.cycling(oPicture);
  position(oPicture, 46, 119);
  draw(oPicture);
  
  [ desk drawer
  animate.obj(oDeskDrawer);
  set.view(oDeskDrawer, vw.OfficeArt);
  ignore.objs(oDeskDrawer);
  ignore.blocks(oDeskDrawer);
  stop.cycling(oDeskDrawer);
  if (deskDrawerStatus != CLOSED) {
    if (deskDrawerStatus == OPEN1) {
      position(oDeskDrawer, 84, 126);
      if (has("Walkman")) {
        set.loop(oDeskDrawer, 2);
        set.cel(oDeskDrawer, 2);
      } else {
        set.loop(oDeskDrawer, 5);
        set.cel(oDeskDrawer, 4);
      }
    } else {
      position(oDeskDrawer, 84, 130);
      if (has("piece of paper")) {
        set.loop(oDeskDrawer, 2);
        set.cel(oDeskDrawer, 2);
      } else {
        set.loop(oDeskDrawer, 5);
        set.cel(oDeskDrawer, 3);
      }
    }
    draw(oDeskDrawer);
  }
  
  [ file drawer
  animate.obj(oFileDrawer);
  set.view(oFileDrawer, vw.OfficeArt);
  ignore.objs(oFileDrawer);
  stop.cycling(oFileDrawer);
  if (fileDrawerStatus != CLOSED) {
    if (fileDrawerStatus == OPEN1) {
      position(oFileDrawer, 93, 112);
      set.loop(oFileDrawer, 3);
      set.cel(oFileDrawer, 2);
    }
    if (fileDrawerStatus == OPEN2) { 
      position(oFileDrawer, 93, 121);
      if (has("notebook")) {
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 2);
      } else {
        set.loop(oFileDrawer, 5);
        set.cel(oFileDrawer, 5);
      }
    }
    if (fileDrawerStatus == OPEN3) { 
      position(oFileDrawer, 93, 121);
      if (has("floppy disk")) {
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 2);
      } else {
        set.loop(oFileDrawer, 5);
        set.cel(oFileDrawer, 6);
      }
    }
    draw(oFileDrawer);
  }
  
  [ add ego to the room
  position(ego, 107, 138);
  draw(ego);
  egoDir = STOPPED;
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, 25, 0); [ fade.in(BYVAR, DELAY, COLOR)
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  return();
}

[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ place said tests here
  if (said("look")) {
    print("You see nothing of interest.");
  }
}

[ if NOT clicking on the actual toolbar, check for
[ clicks on ingame items
if (controller(cLeftClick)) {
  if (mouseX > 33 && mouseX < 128 && mouseY > 71 && mouseY < 163) {
    [ look
    if (cursormode == C_LOOK) {
      [ ego
      if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
        print("You see you.");
        goto(doneclick);
      }
      
      [ anything else is the generic room look
      print("You are in an old office. Dust everywhere.");
      goto(doneclick);
    }
    
    [ talk
    if (cursormode == C_TALK) {
      [ ego
      if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
        print("You have nothing to say.");
      } else {
        print("There is no one here to talk to.");
      }
      goto(doneclick);
    }
    
    [ use
    if (cursormode == C_USE) {
      [ ego
      if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
        print("Please don't touch yourself. Not even when you're alone.");
        goto(doneclick);
      }
      
      [ folder
      if (posn(oFolder, ON.OBJ, 0, &mouseX, &mouseY)) {
        [ pick it up
        get("folder");
        erase(oFolder);
        currentScore += 3;
        print("You pick up the folder. It might be useful.");
        goto(doneclick);
      }
      
      [ lift/lower carpet 89, 141, 98, 149
      if (mouseX > 88 && mouseX < 99 && mouseY > 148 && mouseY < 158) {
        [ ego has to be close to the rug corner
        if (obj.in.box(ego, 80, 138, 104, 151)) {
          [ but not too close
          if (obj.in.box(ego, 83, 141, 101, 148)) {
            print("You can't move the rug corner if you're standing on it.");
          } else {
            if (rugStatus == RUG_UP) {
              [ lower the rug
              rugStatus = RUG_LOWERING;
              reverse.loop(oRug, fMoveRug);
            } else {
              if ((rugStatus == RUG_DOWN || rugStatus == RUG_FLAT)) {
                [ raise it
                rugStatus = RUG_LIFTING;
                set.loop(oRug, 0);
                set.cel(oRug, 0);
                end.of.loop(oRug, fMoveRug);
                if (!has("folder")) {
                  draw(oFolder);
                }
              }
            }
          }
        } else {
          print("You are not close enough to the rug corner to move it.");
        }
        goto(doneclick);
      }
      
      [ printer output 78, 104, 87, 114
      if (!has("memo")) {
        if (mouseX > 77 && mouseX < 88 && mouseY > 111 && mouseY < 123) {
          [ is ego close enough?
          if (obj.in.box(ego, 73, 131, 97, 135)) {
            [ get the memo
            get("memo");
            add.to.pic(vw.OfficeArt, 5, 0, 80, 107, 4, 15);
            print("You tear the memo off the printer output spool.");
            currentScore += 3;
          } else {
            print("Move closer.");
          }
          goto(doneclick);
        }
      }
      
      [ on desk drawer, and open
      if (posn(oDeskDrawer, ON.OBJ, 0, &mouseX, &mouseY) && 
          deskDrawerStatus != CLOSING) {
        [ is ego close enough?
        if (obj.in.box(ego, 74, 131, 101, 136)) {
          if (deskDrawerStatus == OPEN1 && mouseY < 132) {
            [ cicking on top drawer item
            if (!has("Walkman")) {
              get("Walkman");
              currentScore += 2;
              print("You decide to take the Walkman before closing "
                    "the drawer.");
              set.loop(oDeskDrawer, 2);
              set.cel(oDeskDrawer, 2);
              force.update(oDeskDrawer);      
            }
          } else {
            if (deskDrawerStatus == OPEN2 && mouseY < 136) {
              [ clicking on bottom drawer item
              if (!has("piece of paper")) {
                get("piece of paper");
                currentScore += 3;
                print("The paper may be of use so you grab it from "
                      "the drawer.");
                set.loop(oDeskDrawer, 2);
                set.cel(oDeskDrawer, 2);
                force.update(oDeskDrawer);      
              }
            } else {
              [ clicking on drawer
              set.loop(oDeskDrawer, 2);
              set.cel(oDeskDrawer, 2);
              reverse.loop(oDeskDrawer, fDeskDrawer);
              deskDrawerStatus = CLOSING;
            }
          }
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      
      [ top desk drawer, closed 82, 120, 92, 124
      if (mouseX > 81 && mouseX < 93 && mouseY > 127 && mouseY < 133 && 
          deskDrawerStatus == CLOSED) {
        [ is ego close enough?
        if (obj.in.box(ego, 74, 131, 101, 136)) {
          position(oDeskDrawer, 84, 126);
          set.loop(oDeskDrawer, 2);
          set.cel(oDeskDrawer, 0);
          draw(oDeskDrawer);
          end.of.loop(oDeskDrawer, fDeskDrawer);
          deskDrawerStatus = OPENING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      
      [ bottom desk drawer, closed 82, 125, 92, 130
      if (mouseX > 81 && mouseX < 93 && mouseY > 132 && mouseY < 139 && 
          deskDrawerStatus == CLOSED) {
        [ is ego close enough
        if (obj.in.box(ego, 74, 131, 101, 136)) {
          position(oDeskDrawer, 84, 130);
          set.loop(oDeskDrawer, 2);
          set.cel(oDeskDrawer, 0);
          draw(oDeskDrawer);
          end.of.loop(oDeskDrawer, fDeskDrawer);
          deskDrawerStatus = OPENING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      
      [ on file drawer, and open
      if (posn(oFileDrawer, ON.OBJ, 0, &mouseX, &mouseY) && 
          fileDrawerStatus != CLOSING) {
        [ is ego close enough?
        if (obj.in.box(ego, 82, 129, 105, 133)) {

          if (fileDrawerStatus == OPEN1 && mouseY < 113) {
            goto(doneclick);
          }
          if (fileDrawerStatus == OPEN2 && mouseY < 122) {
            if (!has("notebook")) {
              get("notebook");
              currentScore += 3;
              print("This notebook might come in handy.");
              set.loop(oFileDrawer, 3);
              set.cel(oFileDrawer, 2);
              force.update(oFileDrawer);      
            }
            goto(doneclick);
          }
          if (fileDrawerStatus == OPEN3 && mouseY < 131) {
            if (!has("floppy disk")) {
              get("floppy disk");
              currentScore += 2;
              print("You decide to take one of the disks.");
              set.loop(oFileDrawer, 3);
              set.cel(oFileDrawer, 2);
              force.update(oFileDrawer);      
            }
            goto(doneclick);
          }
          set.loop(oFileDrawer, 3);
          set.cel(oFileDrawer, 2);
          reverse.loop(oFileDrawer, fFileDrawer);
          fileDrawerStatus = CLOSING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      [ top file drawer, closed 92, 100, 100, 108
      if (mouseX > 91 && mouseX < 101 && mouseY > 107 && mouseY < 117 &&
          fileDrawerStatus == CLOSED) {
        [ is ego close enough?
        if (obj.in.box(ego, 82, 129, 105, 133)) {
          position(oFileDrawer, 93, 112);
          set.loop(oFileDrawer, 3);
          set.cel(oFileDrawer, 0);
          draw(oFileDrawer);
          end.of.loop(oFileDrawer, fFileDrawer);
          fileDrawerStatus = OPENING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
        
      [ middle file drawer, closed 92, 109, 100, 117
      if (mouseX > 91 && mouseX < 101 && mouseY > 116 && mouseY < 126 &&
          fileDrawerStatus == CLOSED) {
        [ is ego close enough?
        if (obj.in.box(ego, 82, 129, 105, 133)) {
          position(oFileDrawer, 93, 121);
          set.loop(oFileDrawer, 3);
          set.cel(oFileDrawer, 0);
          draw(oFileDrawer);
          end.of.loop(oFileDrawer, fFileDrawer);
          fileDrawerStatus = OPENING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
        
      [ bottom file drawer, closed 93, 119, 100, 128
      if (mouseX > 92 && mouseX < 101 && mouseY > 126 && mouseY < 137 &&
          fileDrawerStatus == CLOSED) {
        [ is ego close enough?
        if (obj.in.box(ego, 82, 129, 105, 133)) {
          position(oFileDrawer, 93, 130);
          set.loop(oFileDrawer, 3);
          set.cel(oFileDrawer, 0);
          draw(oFileDrawer);
          end.of.loop(oFileDrawer, fFileDrawer);
          fileDrawerStatus = OPENING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      
      [ picture, open 60, 88, 65, 117
      if (mouseX > 59 && mouseX < 66 && mouseY > 75 && mouseY < 126 && 
          pictureStatus == OPEN) {
        [ can't if safe is still open
        if (safeStatus != CLOSED) {
          print("The picture won't close with the safe open.");
          goto(doneclick);
        }
        [ is ego close enough? 
        if (obj.in.box(ego, 38, 130, 65, 132)) {
          reverse.loop(oPicture, fPicture);
          pictureStatus = CLOSING;
          erase(oSafe);
        } else {
          [ if too close
          if (obj.in.box(ego, 38, 126, 65, 129)) {
            work1 = 130;
            work2 = 1;
            set.loop(ego, 3);
            fix.loop(ego);
            move.obj.v(ego, egoX, work1, work2, fBackup);
            reverse.loop(oPicture, fPicture);
            pictureStatus = CLOSING;
            erase(oSafe);
          } else {
            print("Move closer.");
          }
        }
        goto(doneclick);
      }
      
      [ picture, closed 46, 88, 60, 115
      if (mouseX > 45 && mouseX < 61 && mouseY > 95 && mouseY < 124 && 
          pictureStatus == CLOSED) {
        [ is ego close enough? 38, 128, 65, 132
        if (obj.in.box(ego, 38, 130, 65, 132)) {
          end.of.loop(oPicture, fPicture);
          pictureStatus = OPENING;
        } else {
          [ if too close
          if (obj.in.box(ego, 38, 126, 65, 129)) {
            work1 = 130;
            work2 = 1;
            set.loop(ego, 3);
            fix.loop(ego);
            move.obj.v(ego, egoX, work1, work2, fBackup);
            end.of.loop(oPicture, fPicture);
            pictureStatus = OPENING;
          } else {
            print("Move closer.");
          }
        }
      }
      
      [ safe only accessible if picture is open
      if (pictureStatus == OPEN) {      
        [ safe door, open 45, 107, 49, 119
        if (mouseX > 44 && mouseX < 50 && mouseY > 114 && mouseY < 128 && 
            safeStatus == OPEN) {
          [ is ego close enough? 
          if (obj.in.box(ego, 38, 130, 65, 132)) {
            reverse.loop(oSafe, fSafe);
            safeStatus = CLOSING;
          } else {
            [ if too close
            if (obj.in.box(ego, 38, 126, 65, 129)) {
              work1 = 130;
              work2 = 1;
              set.loop(ego, 3);
              fix.loop(ego);
              move.obj.v(ego, egoX, work1, work2, fBackup);
              reverse.loop(oSafe, fSafe);
              safeStatus = CLOSING;
            } else {
              print("Move closer.");
            }
          }
        }
        
        [ safe interior, open
        if (mouseX > 49 && mouseX < 56 && mouseY > 112 && mouseY < 120 && 
            safeStatus == OPEN) {
          if (!has("BaDASS manual")) {
            get("BaDASS manual");
            print("You take the manual out of the safe.");
            set.loop(oSafe, 1);
            set.cel(oSafe, 7);
            force.update(oSafe);
          }
        }
        
        [ safe door, closed 49, 104, 56, 114
        [ TODO: add new room to show closeup of safe
        [ player has to enter correct combo to get it open
        if (mouseX > 48 && mouseX < 57 && mouseY > 111 && mouseY < 121 && 
            safeStatus == CLOSED) {
          [ is ego close enough? 38, 128, 65, 132
          if (obj.in.box(ego, 38, 130, 65, 132)) {
            end.of.loop(oSafe, fSafe);
            safeStatus = OPENING;
          } else {
            [ if too close
            if (obj.in.box(ego, 38, 126, 65, 129)) {
              work1 = 130;
              work2 = 1;
              set.loop(ego, 3);
              fix.loop(ego);
              move.obj.v(ego, egoX, work1, work2, fBackup);
              end.of.loop(oSafe, fSafe);
              safeStatus = OPENING;
            } else {
              print("Move closer.");
            }
          }
        }
      }
      
[      [ computer/keyboard
[      67, 93, 76, 115
[      70, 115, 79, 118
[      print("The computer is not working. It looks like the hard "
[            "drive and power supply have been removed a long time "
[            "ago.");
[      [ printer
[      78, 107, 89, 115
[      print("The printer appears to be broken.");
[      
[      [ rug
[      69, 138, 98, 149 (and not red pri)
[      print("This portion of the rug is stuck to the floor.");
    }
    
    :doneclick
    set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
  }
}

[*****
:noInput           [  all non-input related code goes here 
[*****

[ ego backing up from picture
if (fBackup) {
  reset(fBackup);
  release.loop(ego);
}

[ picture movement
if (fPicture) {
  reset(fPicture);
  if (pictureStatus == OPENING) {
    pictureStatus = OPEN;
    draw(oSafe);
  } else {
    pictureStatus = CLOSED;
  }
}

[ safe door movement
if (fSafe) {
  reset(fSafe);
  if (safeStatus == OPENING) {
    safeStatus = OPEN;
    if (!findManual && !has("BaDASS manual")) {
      set(findManual);
      set.loop(oSafe, 5);
      set.cel(oSafe, 7);
      force.update(oSafe);
      print("There is a binder in the safe. It looks like an old "
            "computer program manual.");
    }
  } else {
    safeStatus = CLOSED;
  }
}

[ rug movement
if (fMoveRug) {
  reset(fMoveRug);
  if (rugStatus == RUG_LIFTING) {
    rugStatus = RUG_UP;
    if (!findFolder && !has("folder")) {
      set(findFolder);
      print("You see a folder tucked under the corner of the rug!");
      currentScore += 2;
    }
  } else {
    if (has("folder")) {
      rugStatus = RUG_FLAT;
      set.loop(oRug, 5);
      set.cel(oRug, 2);
    } else {
      rugStatus = RUG_DOWN;
      erase(oFolder);
    }
  }
}

[ trip on rug
if (!fOnTripLine && rugStatus == RUG_UP && egoHitSpecial && egoX < 100) {
  set(fOnTripLine);
  set(fTripping);
  load.view(vw.Tripping);
  current.loop(ego, work1);
  tmpLoop = work1;
  if (work1 > 1) {
    work1 -=2;
  }
  work1 *= -1; [ because trip loops are backwards!
  work1 += 1; [ could 'bit.not' it...
  set.view(ego, vw.Tripping);
  set.loop.v(ego, work1);
  fix.loop(ego);
  program.control();
  tripCounter = 9;
}

[ check trip counter
if (tripCounter) {
  --tripCounter;
  if (tripCounter == 0) {
    set.view(ego, vw.Ego);
    set.loop.v(ego, tmpLoop);
    release.loop(ego);
    player.control();
    if (!fWarnTrip) {
      set(fWarnTrip);
      print("You fOnTripLine on the carpet! Good thing you didn't fall "
            "and break your neck.");
    }
    [ TODO: update score reference
    --currentScore;
  }
}

[ if fOnTripLine set, AND not on rug, reset fOnTripLine
if (fOnTripLine) {
  if (!egoHitSpecial) {
    reset(fOnTripLine);
  }
}

[ drawer movement
if (fDeskDrawer) {
  reset(fDeskDrawer);
  if (deskDrawerStatus == CLOSING) {
    erase(oDeskDrawer);
    deskDrawerStatus = CLOSED;
  } else {
    get.posn(oDeskDrawer, work1, work2);
    if (work2 == 126) {
      deskDrawerStatus = OPEN1;
      if (!has("Walkman")) {
        set.loop(oDeskDrawer, 5);
        set.cel(oDeskDrawer, 4);
        force.update(oDeskDrawer);
        if (!findWalkman) {
          set(findWalkman);
          print("There is an old Sony Walkman cassette player in "
                "this drawer!");
        }
      }
    } else {
      deskDrawerStatus = OPEN2;
      if (!has("piece of paper")) {
        set.loop(oDeskDrawer, 5);
        set.cel(oDeskDrawer, 3);
        force.update(oDeskDrawer);
        if (!findList) {
          set(findList);
          print("You see a piece of paper with handwritten notes.");
        }
      }
    }
  }
}
if (fFileDrawer) {
  reset(fFileDrawer);
  if (fileDrawerStatus == CLOSING) {
    erase(oFileDrawer);
    fileDrawerStatus = CLOSED;
  } else {
    get.posn(oFileDrawer, work1, work2);
    if (work2 == 112) {
      fileDrawerStatus = OPEN1;
      if (!findEmptyDrawer) {
        set(findEmptyDrawer);
        print("This drawer is empty.");
      }
    } else {
      if (work2 == 121) {
        fileDrawerStatus = OPEN2;
        if (!has("notebook")) {
          set.loop(oFileDrawer, 5);
          set.cel(oFileDrawer, 5);
          force.update(oFileDrawer);
          if (!findNotebook) {
            set(findNotebook);
            print("A small notebook is in this drawer.");
          }
        }
      } else {
        fileDrawerStatus = OPEN3;
        if (!has("floppy disk")) {
          set.loop(oFileDrawer, 5);
          set.cel(oFileDrawer, 6);
          force.update(oFileDrawer);
          if (!findDisk) {
            set(findDisk);
            print("There's a box of blank floppy disks here.");
          }
        }
      }
    }
  }
}

[*****
:exit              [  test for leaving the room
[*****

[ add exit checks here (or use layout editor)

if (egoX > 100 && egoHitSpecial) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  [ restore normal palette
  block(SET.PALETTE, 0, &m8, 1); [ set.palette(BYVAR, PALETTE, LOADONLY)
  new.room(rm.ControlRoom);  [ ##LE001##
}
return();

[*****
[ messages         [  declared messages go here
[*****
#message 1 "%l90|40"
#message 2 "%l90|41"
#message 3 "%l90|42"
#message 4 "%l90|43"
#message 5 "%l90|44"
#message 6 "%l90|45"
#message 7 "00000000002A002A00002A2A2A00002A002A2A15002A2A2A15151515153F153F15001E1E3F15153F153F3F3F153F3F3F"
[#message 7 "000000 00002A 002A00 002A2A 2A0000 2A002A 2A1500 2A2A2A 
[            151515 15153F 153F15 001E1E 3F1515 3F153F 3F3F15 3F3F3F"
#message 8 "%s2"