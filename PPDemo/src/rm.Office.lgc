[ ********************************************************************
[
[ rm.Office
[
[ 
[
[ ********************************************************************

#define oSafe          o1
#define oPicture       o2
#define oDeskDrawer    o3
#define oFileDrawer    o4
#define oFolder        o5
#define oRug           o6

#define tmpPalette     s12 [ s13, s14

#define fMoveRug      f220
#define tripped       f221
#define fDeskDrawer   f222
#define fFileDrawer   f223

[ rug status
#define RUG_DOWN     0
#define RUG_UP       1
#define RUG_FLAT     2
#define RUG_LIFTING  3
#define RUG_LOWERING 4

[ picture/safe/drawer status
#define CLOSED       0
#define OPEN         1
#define OPEN1        1
#define OPEN2        2
#define OPEN3        3
#define CLOSING      4
#define OPENING      5


if (newRoom) {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(1);
  
  [ use a custom palette so the area behind picture can be shown 
  [ to be a bit darker than rest of wall
  work1 = 66;
  block(STR.LEFT, &palette, &tmpPalette, &work1); [ str.left(sINPUT, sRESULT, vLEN)
  s15 = "001E1E";
  block(STR.CONCAT, &tmpPalette, &s15, 0); [ sINPUT, sADD)
  work1 = 24;
  block(STR.RIGHT, &palette, &s15, &work1); [ strRight(sINPUT, sRESULT, vLEN)
  block(STR.CONCAT, &tmpPalette, &s15, 0); [ sINPUT, sADD)
  block(SET.MSG, 0, &m7, &tmpPalette); [ set.msg(BYVAR, MSGOLD, STRNEW)
  block(SET.PALETTE, 0, &m7, 1); [ set.palette(BYVAR, PALETTE, LOADONLY)
  
  [ add toolbar buttons
  load.view(vw.Toolbar);
  call(lgc.InitToolbar);
  
  [ load view with this room's art
  load.view(vw.OfficeArt);
  
  [ printer page
  if (has("memo")) {
    [ memo is not on printer
    add.to.pic(vw.OfficeArt, 5, 0, 80, 107, 4, 15);
  }
  
  [ folder under rug
  if (!has("folder")) {
    animate.obj(oFolder);
    set.view(oFolder, vw.OfficeArt);
    set.loop(oFolder, 5);
    set.cel(oFolder, 1);
    stop.cycling(oFolder);
    ignore.objs(oFolder);
    position(oFolder, 92, 149);
    set.priority(oFolder, 4);
    if (rugStatus == 1) {
      draw(oFolder);
    }
  }
  [ rug
  animate.obj(oRug);
  set.view(oRug, vw.OfficeArt);
  if (rugStatus == RUG_DOWN) {
    set.loop(oRug, 0);
    set.cel(oRug, 0);
  } else {
    if (rugStatus == RUG_UP) {
      set.loop(oRug, 0);
      set.cel(oRug, 5);
    } else {
      [ RUG_FLAT
      set.loop(oRug, 5);
      set.cel(oRug, 2);
    }
  }
  stop.cycling(oRug);
  ignore.objs(oRug);
  position(oRug, 87, 149);
  set.priority(oRug, 4);
  draw(oRug);
  
  [ safe
  animate.obj(oSafe);
  set.view(oSafe, vw.OfficeArt);
  set.loop(oSafe, 1);
  position(oSafe, 45, 119);
  stop.cycling(oSafe);
  if (safeStatus == OPEN) {
    set.cel(oSafe, 7);
    draw(oSafe);
  } else {
    set.cel(oSafe, 0);
  }
  
  [ picture
  animate.obj(oPicture);
  set.view(oPicture, vw.OfficeArt);
  set.loop(oPicture, 4);
  if (pictureStatus == OPEN) {
    set.cel(oPicture, 7);
  } else {
    set.cel(oPicture, 0);
  }
  stop.cycling(oPicture);
  position(oPicture, 46, 119);
  draw(oPicture);
  
  [ desk drawer
  animate.obj(oDeskDrawer);
  set.view(oDeskDrawer, vw.OfficeArt);
  set.loop(oDeskDrawer, 2);
  ignore.objs(oDeskDrawer);
  ignore.blocks(oDeskDrawer);
  stop.cycling(oDeskDrawer);
  
  [ file drawer
  animate.obj(oFileDrawer);
  set.view(oFileDrawer, vw.OfficeArt);
  set.loop(oFileDrawer, 3);
  ignore.objs(oFileDrawer);
  stop.cycling(oFileDrawer);
  
  [ add ego to the room
  position(ego, 107, 138);
  draw(ego);
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, 25, 0); [ fade.in(BYVAR, DELAY, COLOR)
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  
  return();
}

[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ place said tests here
  if (said("look")) {
    print("You see nothing of interest.");
  }
}

[*****
:noInput           [  all non-input related code goes here 
[*****
[ if NOT clicking on the actual toolbar, check for
[ clicks on ingame items
if (controller(cLeftClick)) {
  if (mouseX > 33 && mouseX < 128 && mouseY > 71 && mouseY < 163) {
    [ look
    if (cursormode == C_LOOK) {
      [ ego
      if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
        print("You see you.");
        goto(doneclick);
      }
      
      [ anything else is the generic room look
      print("You are in an old office. Dust everywhere.");
      goto(doneclick);
    }
    
    [ talk
    if (cursormode == C_TALK) {
      [ ego
      if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
        print("You have nothing to say.");
      } else {
        print("There is no one here to talk to.");
      }
      goto(doneclick);
    }
    
    [ use
    if (cursormode == C_USE) {
      [ ego
      if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
        print("Please don't touch yourself. Not even when you're alone.");
        goto(doneclick);
      }
      
      [ folder
      if (posn(oFolder, ON.OBJ, 0, &mouseX, &mouseY)) {
        [ pick it up
        get("folder");
        erase(oFolder);
        currentScore += 3;
        print("You pick up the folder. It might be useful.");
        goto(doneclick);
      }
      
      [ lift/lower carpet 89, 141, 98, 149
      if (mouseX > 88 && mouseX < 99 && mouseY > 148 && mouseY < 158) {
        [ ego has to be close to the rug corner
        if (obj.in.box(ego, 80, 138, 104, 151)) {
          [ but not too close
          if (obj.in.box(ego, 83, 141, 101, 148)) {
            print("You can't move the rug corner if you're standing on it.");
          } else {
            if (rugStatus == RUG_UP) {
              [ lower the rug
              rugStatus = RUG_LOWERING;
              reverse.loop(oRug, fMoveRug);
            } else {
              if ((rugStatus == RUG_DOWN || rugStatus == RUG_FLAT)) {
                [ raise it
                rugStatus = RUG_LIFTING;
                set.loop(oRug, 0);
                set.cel(oRug, 0);
                end.of.loop(oRug, fMoveRug);
                if (!has("folder")) {
                  draw(oFolder);
                }
              }
            }
          }
        } else {
          print("You are not close enough to the rug corner to move it.");
        }
        goto(doneclick);
      }
      
      [ printer output 78, 104, 87, 114
      if (!has("memo")) {
        if (mouseX > 77 && mouseX < 88 && mouseY > 111 && mouseY < 123) {
          [ is ego close enough?
          if (obj.in.box(ego, 73, 131, 97, 135)) {
            [ get the memo
            get("memo");
            add.to.pic(vw.OfficeArt, 5, 0, 80, 107, 4, 15);
            print("You tear the memo off the printer output spool.");
            currentScore += 3;
          } else {
            print("Move closer.");
          }
          goto(doneclick);
        }
      }
      
      [ on desk drawer, and open
      if (posn(oDeskDrawer, ON.OBJ, 0, &mouseX, &mouseY) && 
          deskDrawerStatus != CLOSING) {
          [ is ego close enough?
        if (obj.in.box(ego, 74, 131, 101, 136)) {
          reverse.loop(oDeskDrawer, fDeskDrawer);
          deskDrawerStatus = CLOSING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      
      [ top desk drawer, closed 82, 120, 92, 124
      if (mouseX > 81 && mouseX < 93 && mouseY > 127 && mouseY < 133 && 
          deskDrawerStatus == CLOSED) {
        [ is ego close enough?
        if (obj.in.box(ego, 74, 131, 101, 136)) {
          position(oDeskDrawer, 84, 126);
          draw(oDeskDrawer);
          end.of.loop(oDeskDrawer, fDeskDrawer);
          deskDrawerStatus = OPENING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      
      [ bottom desk drawer, closed 82, 125, 92, 130
      if (mouseX > 81 && mouseX < 93 && mouseY > 132 && mouseY < 139 && 
          deskDrawerStatus == CLOSED) {
        [ is ego close enough
        if (obj.in.box(ego, 74, 131, 101, 136)) {
          position(oDeskDrawer, 84, 130);
          draw(oDeskDrawer);
          end.of.loop(oDeskDrawer, fDeskDrawer);
          deskDrawerStatus = OPENING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      
      [ on file drawer, and open
      if (posn(oFileDrawer, ON.OBJ, 0, &mouseX, &mouseY) && 
          fileDrawerStatus != CLOSING) {
        reverse.loop(oFileDrawer, fFileDrawer);
        fileDrawerStatus = CLOSING;
        goto(doneclick);
      }
      
      [ top file drawer, closed 92, 100, 100, 108
      if (mouseX > 91 && mouseX < 101 && mouseY > 107 && mouseY < 115 &&
          fileDrawerStatus == CLOSED) {
        [ is ego close enough?
        if (obj.in.box(ego, 82, 129, 105, 133)) {
          position(oFileDrawer, 93, 112);
          draw(oFileDrawer);
          end.of.loop(oFileDrawer, fFileDrawer);
          fileDrawerStatus = OPENING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
        
      [ middle file drawer, closed 92, 109, 100, 117
      if (mouseX > 91 && mouseX < 101 && mouseY > 116 && mouseY < 124 &&
          fileDrawerStatus == CLOSED) {
        [ is ego close enough?
        if (obj.in.box(ego, 82, 129, 105, 133)) {
          position(oFileDrawer, 93, 121);
          draw(oFileDrawer);
          end.of.loop(oFileDrawer, fFileDrawer);
          fileDrawerStatus = OPENING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
        
      [ bottom file drawer, closed 93, 119, 100, 128
      if (mouseX > 92 && mouseX < 101 && mouseY > 126 && mouseY < 137 &&
          fileDrawerStatus == CLOSED) {
        [ is ego close enough?
        if (obj.in.box(ego, 82, 129, 105, 133)) {
          position(oFileDrawer, 93, 130);
          draw(oFileDrawer);
          end.of.loop(oFileDrawer, fFileDrawer);
          fileDrawerStatus = OPENING;
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
      
      [ open picture 
      
      [ closed picture 46, 88, 60, 115
      
      [ open safe door 45, 107, 49, 119
      
      [ closed safe door/safe interior 49, 104, 56, 114
      
    }
    
    :doneclick
    set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
  }
}

[ rug movement
if (fMoveRug) {
  reset(fMoveRug);
  if (rugStatus == RUG_LIFTING) {
    rugStatus = RUG_UP;
    if (!findFolder && !has("folder")) {
      set(findFolder);
      print("You see a folder tucked under the corner of the rug!");
      currentScore += 2;
    }
  } else {
    if (has("folder")) {
      rugStatus = RUG_FLAT;
      set.loop(oRug, 5);
      set.cel(oRug, 2);
    } else {
      rugStatus = RUG_DOWN;
      erase(oFolder);
    }
  }
}

[ trip on rug
if (!tripped && rugStatus == RUG_UP && egoHitSpecial && egoX < 100) {
  [trip! or block?
  print("trip!");
  set(tripped);
}


[ if tripped, AND not on rug, reset tripped
if (tripped) {
  if (!egoHitSpecial) {
    reset(tripped);
  }
}

[ drawer movement
if (fDeskDrawer) {
  reset(fDeskDrawer);
  if (deskDrawerStatus == CLOSING) {
    erase(oDeskDrawer);
    deskDrawerStatus = CLOSED;
  } else {
    get.posn(oDeskDrawer, work1, work2);
    if (work2 == 126) {
      deskDrawerStatus = OPEN1;
    } else {
      deskDrawerStatus = OPEN2;
    }
  }
}
if (fFileDrawer) {
  reset(fFileDrawer);
  if (fileDrawerStatus == CLOSING) {
    erase(oFileDrawer);
    fileDrawerStatus = CLOSED;
  } else {
    get.posn(oFileDrawer, work1, work2);
    if (work2 == 112) {
      fileDrawerStatus = OPEN1;
    } else {
      if (work2 == 121) {
        fileDrawerStatus = OPEN2;
      } else {
        fileDrawerStatus = OPEN3;
      }
    }
  }
}

[*****
:exit              [  test for leaving the room
[*****

[ add exit checks here (or use layout editor)

if (egoX > 115 && egoHitSpecial) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  [ restore normal palette
  block(SET.PALETTE, 0, &m8, 1); [ set.palette(BYVAR, PALETTE, LOADONLY)
  new.room(rm.ControlRoom);  [ ##LE001##
}
return();

[*****
[ messages         [  declared messages go here
[*****
#message 1 "%l90|40"
#message 2 "%l90|41"
#message 3 "%l90|42"
#message 4 "%l90|43"
#message 5 "%l90|44"
#message 6 "%l90|45"
#message 7 "00000000002A002A00002A2A2A00002A002A2A15002A2A2A15151515153F153F15001E1E3F15153F153F3F3F153F3F3F"
[#message 7 "000000 00002A 002A00 002A2A 2A0000 2A002A 2A1500 2A2A2A 
[            151515 15153F 153F15 001E1E 3F1515 3F153F 3F3F15 3F3F3F"
#message 8 "%s2"