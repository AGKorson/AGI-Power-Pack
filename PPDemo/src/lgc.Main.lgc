[*********************************************************************
[
[ lgc.Main: Main logic
[
[ This logic runs in every interpreter cycle and calls
[ other logics as needed.
[
[
[ To better manage variables and flags, they are 
[ grouped into reserved, global, local, and dynamic:
[
[ reserved = reserved by the interpreter 
[     v0 - v26
[     f0 - f16, f20
[     s0, s1
[
[ global = usable and accessible by all logics
[     v27 - v219
[     f17 - f18, f21 - f219
[     s2 - s11
[
[ local = usable and accessible by single room
[     v220 - v239
[     f220 - f239
[     s12 - s19
[
[ dynamic = usable and accesible by a non-room logic
[     v240 - v255
[     f240 - f255
[     s20 - s23
[
[ Local and dynamic variables/flags are reset whenever a new room is
[ loaded.
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
#define mnu_msgUseItem              m1
#define mnu_msgSndToggle            m2
#define mnu_msgSoundPC              m3
#define mnu_msgSoundFM              m4
#define mnu_msgSoundMIDI            m5
#define mnu_msgClockToggle          m6
#define mnu_msgGameTime             m7
#define mnu_msgRealTime             m8
#define mnu_msgSpeed                m9
#define mnu_msgSpeedSlow           m10
#define mnu_msgSpeedNorm           m11
#define mnu_msgSpeedFast           m12
#define mnu_msgSpeedFastest        m13
#define mnu_msgSkipIntro           m14
#define mnu_msgDebug               m15
#define mnu_msgDbgLog              m16
#define mnu_msgDbgClick            m17
#define msgSpeed                   m18
#define msgExamine                 m19
#define msgUse                     m20
#define msgDebug                   m21

[***************************************
[ ERROR CHECK
[***************************************
[ if one of the few trappable errors in AGI is encountered, the game
[ is probably not able to continue; make an announcement to the player
[ and quit
if (errorNumber > 0) {
  block(FADE.IN, BYNUM, 0, BLACK); [ fade.in(FN_MODE, DELAY, COLOR)
  call(lgc.Error);
}
[ check for start trace
if (startTrace) {
  reset(startTrace);
  trace.on();
}

[ help screens use ONLY their custom logics
if (showHelp) {
  call(lgc.GameHelp);
  return();
}

[***************************************
[ GAME START
[***************************************
[ when game begins, or restarts, there are a number
[ of setup activities to take care of
if (currentRoom == 0) {
  [ when the game starts, the current room is zero, so all the
  [ setup/initialization code goes here
  [
  [ restarting a game also sets currentRoom to zero, and also
  [ requires most of the same setup/initialization steps

  [ check for AGI startup (not restarting)
  if (!gameRestarted) { 
    [ first thing - run the Power Pack (has to be a separate
    [ call, directly from main, with no other logics loaded
    [ otherwise the message relocation functions will cause
    [ messages to be in wrong places
    
    [ confirm this is a version that can be patched
    call(lgc.Version2Check);
    [ run the patch for the detected AGI version
    work1 = IntVersion;
    work1 += 58;
    call.v(work1);

    [ validate system parameters
    call(lgc.ValidateVersion);
  }

  [ do start/restart initialization activities
  
  [ init logic sets up the game; assigns key controllers, configures 
  [ the screen and other startup activities
  call(lgc.Init);

  [ some start/restart actions have to be done within logic0
  if (gameRestarted) {
    [ make sure speed menu is enabled
    s23 = msgSpeed; [ "Speed"
    block(SET.MSG, BYNUM, &mnu_msgSpeed, &s23); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    [ go to first room here (skip Intro)
    new.room(rm.ParkingLot);
  } else {
    [ if NOT restarting (meaning this is very beginning, need to create 
    [ the menu; it can't be done in lgc.Init, it MUST be done from within 
    [ logic 0, so do that before going to start room
    set.menu("PPDemo");
    set.menu.item("About           ", cAbout);
    set.menu.item("Version  <Alt-V>", cVersion);
    set.menu.item("Help        <F1>", cHelp);
    set.menu.item(mnu_msgSkipIntro, cSkipIntro);
    set.menu("File");
    set.menu.item("Save     <F5>", cSave);
    set.menu.item("Restore  <F7>", cRestore);
    set.menu.item("컴컴컴컴컴컴", cDummy);
    set.menu.item("Restart  <F9>", cRestart);
    set.menu.item("Quit  <Alt-Z>", cQuit);
    set.menu("Action");
    set.menu.item("Inventory    <Tab>", cStatus);
    set.menu.item(mnu_msgUseItem, cChooseItem); [ "Examine Item  <F4>"
    set.menu("Sound");
    set.menu.item(mnu_msgSndToggle, cToggleSound); [ "Sound: On   <F2>"
    set.menu.item("Quieter         ", cDecrescendo);
    set.menu.item("Louder          ", cCrescendo);
    set.menu.item("컴컴컴컴컴컴컴컴", cDummy);
    set.menu.item(mnu_msgSoundPC, cSoundPC);    [ " PC Speaker     "
    set.menu.item(mnu_msgSoundFM, cSoundFM);    [ " FM Synth       "
    set.menu.item(mnu_msgSoundMIDI, cSoundMIDI);  [ " MIDI           "
    set.menu("Time");
    set.menu.item(mnu_msgClockToggle, cClock);  [ "Clock: Off  <F6>"
    set.menu.item(mnu_msgGameTime, cGameClock); [ " Game Time      "
    set.menu.item(mnu_msgRealTime, cRealClock); [ " Real Time      "
    set.menu.item("컴컴컴컴컴컴컴컴", cDummy);
    set.menu.item("Pause      <Esc>", cPause);
    set.menu(mnu_msgSpeed);  [ "Speed"
    set.menu.item( "Slower  ", cSlower);
    set.menu.item( "Faster  ", cFaster);
    set.menu.item( "컴컴컴컴", cDummy);
    set.menu.item(mnu_msgSpeedSlow, cSlow); [ " Slow   "
    set.menu.item(mnu_msgSpeedNorm, cNormal); [ " Normal "
    set.menu.item(mnu_msgSpeedFast, cFast); [ " Fast   "
    set.menu.item(mnu_msgSpeedFastest, cFastest); [ " Fastest"
    
    [ debug menu; disable(hide) by default
    set.menu(mnu_msgDebug); [ "Debug"
    set.menu.item(mnu_msgDbgLog, cLogInput);   [ "Log Input:      Off"
    set.menu.item(mnu_msgDbgClick, cShowClicks); [ "Display Clicks: Off"
    set.menu.item("Debug Help", cDebugHelp);
    s22 = "";
    block(SET.MSG, BYNUM, &mnu_msgDebug, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)

    [ submit the menu (which makes it ready to use)
    submit.menu();
    [ disable the separator lines
    disable.item(cDummy);
    
    [ if skipping the intro
    if (skipIntro) {
      [ go to starting room (the parking lot)
      new.room(rm.ParkingLot);
    } else {
      [ start the game with the title screen
      new.room(rm.Title);
    }
  }
}

[***************************************
[ NEW ROOM
[***************************************
if (newRoom) {
  [ First interpreter cycle in a new room
  [ Note: Everything other than logic 0 is discarded from memory
  [ when new.room is executed

  [ do basic room initialization
  call(lgc.RoomInit);
  
  if (!disableGameFunctions) {
    [ Load game specific functions logic into memory
    load.logics(lgc.GameFunctions);
    [ also toolbar functions
    load.logics(lgc.ToolbarFunctions);
    load.logics(lgc.UseItem);
  }
  [ load debug logic into memory so it will be there 
  [ if it's needed
  load.logics(lgc.Debug);

  [ score handler (to allow score to go below zero)
  load.logics(lgc.ScoreHandler);
  
  [ load ego view
  if (currentRoom > 1 && currentRoom < 11) {
    [ all except parking lot use full size ego
    if (currentRoom != rm.ParkingLot) {
        [ load and set view
        load.view(vw.Ego);
        set.view(ego, vw.Ego);
    }
  }
  [ force clock value to update, if time is visible
  clearStatusSeconds = -1; 
  
  [ check for closet door being left open
  if (closetopen && !closetTimerStart && currentRoom != rm.Hallway && 
      currentRoom != rm.Closet && currentRoom != rm.SecretRoom) {
    [ start a fresh timer
    set(closetTimerStart);
    timerMin = 4;
    timerSec = 0;
    block(START.TIMER, &timerMin, &timerSec, &closetTimerDone); [ start.timer(vMIN, vSEC, fDONE)
  } else {
    if (closetTimerStart) {
      [ stop the timer
      reset(closetTimerStart);
      block(TOGGLE.TIMER, STOP_TIMER, 0, 0); [ toggle.timer(STATE)
    }
  }
}

[***************************************
[ EVERY CYCLE
[***************************************

[ CHECK FOR GAME RESTORE
if (isset(gameRestored)) {
  [ restore settings
  call(lgc.GetOptions);
  [ make sure input area is cleared
  clear.lines(23, 24, BLACK);
  [ need to 're-disable menu separators)
  disable.item(cDummy);
  [ and redraw toolbar buttons
  call(lgc.InitToolbar);
}

[ check for click-resets when coming from cutscenes
if (resetClicks) {
  reset(resetClicks);
  set.key(CLR.CTRL, CLR.CTRL, cLeftClick); [ clear.controller(cNUM)
}

[ DEBUG FUNCTIONS
if (debugging) {
  if (fShowClicks) {
    [ useful to assist in creating click statements and said statements
    if (controller(cLeftClick)  && currentRoom != 12 && currentRoom != 13) {
      get.posn(ego, work1, work2);
      display(4, 1, "left click: (%v27, %v28)  ego pos:  (%v46, %v47)        ");
    }
    [ show remaining available memory
    display(24, 0, "mem pages: %v8  ");
  }
  if (fLogInput) {
    [ this logs everything player types; and also specifically
    [ identifies unknown words
    if (haveInput) {
      if (unknownWordNum > 0) {
        log("unknown word: %v9");
      } else {
        log("");
      }
    }
  }
}

[***************************************
[ MENU AND STATUS BAR CHECKS
[***************************************
if (!disableGameFunctions) {
  [ this trick makes it easy to test for
  [ 'if (controller(cLeftClick) && y > 247) || controller(cMenu)'
  if (!controller(cMenu) && (!controller(cLeftClick) || mouseY < 248)) {
  } else {   
    [ check for click on sound bar (but no controller)
    if (mouseX > 137 && !controller(cMenu)) {
      [ only in non-PC modes
      if (musicmode != PC_SOUND) {
        [ adjust sound volume based on clicked position
        work1 = mouseX;
        work1 -= 138; [ 0-21
        if (work1 >= 18) {
          [ set to max vol
          set(soundOn);
          work1 = 0;
        } else { [ 0-17
          if (work1 <= 2) {
            [ sound at minimum volume
            work1 = 63;
          } else { [ 3-17
            set(soundOn);
            [ calculate new sound attenuations from clicked value
            work1 *= -1;
            work1 += 18;  [15-1
            work1 *= 4;
          }
        }
        attenuation = work1;
      }
      return();
    }
    [ if not on sound bar, show menu
    menu.input();
    oldSeconds = -1;
    [ reset click event
    set.key(CLR.CTRL, CLR.CTRL, cLeftClick); [ clear.controller(cNUM)
    
    [ refresh dynamic menu items
    [ inventory menu item
    if (deathType < DT_SEQ_DONE) {
      if (cursormode == C_USE) {
        [ enable 'use item'
        enable.item(cChooseItem);
        s23 = msgUse; [ "Use Item     <F4>"
        block(SET.MSG, BYNUM, &mnu_msgUseItem, &s23); [ set.msg(FN_MODE, MSGOLD, STRNEW)
      } else {
        [ enable 'examine item' for all other cursors
        enable.item(cChooseItem);
        s23 = msgExamine; [ "Examine Item  <F4>"
        block(SET.MSG, BYNUM, &mnu_msgUseItem, &s23); [ set.msg(FN_MODE, MSGOLD, STRNEW)
      }
    } else {
      [ not enabled if dead
      disable.item(cChooseItem);
    }
    
    [ sound toggle and mode
    if (soundOn) {
      s23 = "On ";
      enable.item(cSoundPC);
      enable.item(cSoundFM);
      enable.item(cSoundMIDI);
      if (4channelsound) {
        work1 = 62;
      } else {
        work1 = 14;
      }
      if (attenuation > work1) {
        disable.item(cDecrescendo);
      } else {
        enable.item(cDecrescendo);
      }
      if (attenuation == 0) {
        disable.item(cCrescendo);
      } else {
        enable.item(cCrescendo);
      }
    } else {
      s23 = "Off";
      disable.item(cSoundPC);
      disable.item(cSoundFM);
      disable.item(cSoundMIDI);
      disable.item(cCrescendo);
      disable.item(cDecrescendo);
    }
    s22 = "Sound: %s23  <F2>";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgSndToggle, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)
  
    [ check the correct sound option, uncheck the rest
    if (musicMode == PC_SOUND) {
      s23 = "\x10";
    } else {
      s23 = " ";
    }
    s22 = "%s23PC Speaker     ";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgSoundPC, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    
    if (musicMode == FM_SOUND) {
      s23 = "\x10";
    } else {
      s23 = " ";
    }
    s22 = "%s23FM Synth       ";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgSoundFM, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    
    if (musicMode == MIDI_SOUND) {
      s23 = "\x10";
    } else {
      s23 = " ";
    }
    s22 = "%s23MIDI           ";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgSoundMIDI, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)

    [ clock toggle and mode
    if (clockOn) {
      s23 = "On ";
      enable.item(cGameClock);
      enable.item(cRealClock);
    } else {
      s23 = "Off";
      disable.item(cGameClock);
      disable.item(cRealClock);
    }
    s22 = "Clock: %s23  <F6>"; 
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgClockToggle, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    if (realClock) {
      s21 = " ";
      s23 = "\x10";
    } else {
      s21 = "\x10";
      s23 = " ";
    }
    s22 = "%s21Game Time      ";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgGameTime, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    s22 = "%s23Real Time      ";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgRealTime, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)

    [ speed menu not available when dead
    s23 = mnu_msgSpeed;
    block(STR.LEN, &s23, &work1, 0); [ str.len(sINPUT, vRESULT)
    if (work1 == 0 && deathType != DT_SEQ_DONE) {
      s23 = msgSpeed; [ "Speed"
      block(SET.MSG, BYNUM, &mnu_msgSpeed, &s23); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    }
    if (work1 > 0 && deathType == DT_SEQ_DONE) {
      s23 = "";
      block(SET.MSG, BYNUM, &mnu_msgSpeed, &s23); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    }
    [ speed mode
    if (animationInterval == SLOW_SPEED) {
      disable.item(cSlower);
    } else {
      enable.item(cSlower);
    }
    if (animationInterval == FASTEST_SPEED) {
      disable.item(cFaster);
    } else {
      enable.item(cFaster);
    }
    [ slow (12) is 9 - 12
    if (animationInterval > 8) {
      s23 = "\x10";
    } else {
      s23 = " ";
    } 
    s22 = "%s23Slow   ";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgSpeedSlow, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    [ normal (6) is 5 - 8
    if (animationInterval > 4 && animationInterval < 9) {
      s23 = "\x10";
    } else {
      s23 = " ";
    }
    s22 = "%s23Normal ";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgSpeedNorm, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    [ fast (3) is 1 - 4 
    if (animationInterval > 0 && animationInterval < 5) {
      s23 = "\x10";
    } else {
      s23 = " ";
    } 
    s22 = "%s23Fast   ";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgSpeedFast, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    if (animationInterval == FASTEST_SPEED) {
      s23 = "\x10";
    } else {
      s23 = " ";
    } 
    s22 = "%s23Fastest";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgSpeedFastest, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)

    [ make sure skip intro menu matches skipIntro value
    if (skipIntro) {
      s23 = "On ";
    } else {
      s23 = "Off";
    }
    s22 = "Skip Intro:  %s23";
    block(STR.FORMAT, &s22, &s22, 0); [ str.format(sINPUT, sRESULT)
    block(SET.MSG, BYNUM, &mnu_msgSkipIntro, &s22); [ set.msg(FN_MODE, MSGOLD, STRNEW)
        
    [ debug menu only available in debug mode
    s23 = mnu_msgDebug;
    block(STR.LEN, &s23, &work1, 0); [ str.len(sINPUT, vRESULT)
    if (work1 == 0 && debugging) {
      s23 = msgDebug; [ "Debug"
      block(SET.MSG, BYNUM, &mnu_msgDebug, &s23); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    }
    if (work1 > 0 && !debugging) {
      s23 = "";
      block(SET.MSG, BYNUM, &mnu_msgDebug, &s23); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    }
  }
}

[ CLOCK DISPLAY
if (clockOn) {
  [ check current time every second
  if (clearStatusSeconds != elapsedSeconds) {
    if (realClock) {
      [ check realtime clock every second
      block(GET.TIME, &currentHour, &currentMinute, &currentSecond); [ get.time(vHOUR, vMINUTE, vSECOND)
    }
    [ update the stored seconds value
    clearStatusSeconds = elapsedSeconds;
  }
}

[***************************************
[ DYNAMIC MENU CHECKS
[***************************************
[ these checks need to be in logic 0, not in the game function logic

[ SOUND MENU ITEMS
if (controller(cToggleSound)) {
  if (playWalkman) {
    [ turning sound off pauses walkman
    reset(playWalkman);
    stop.sound();
    reset(musicDone);
  }
  [ toggling will automatically set done flag for any sound currently
  [ playing
  toggle(soundOn);
}
[ check for change in sound mode
work1 = 0;
if (controller(cSoundPC) && musicMode != PC_SOUND) {
  [ set musicmode to PC sound
  musicMode = PC_SOUND;
  work1 = 1;
}
if (controller(cSoundFM) && musicMode != FM_SOUND) {
  [ set musicmode to FM synth
  musicMode = FM_SOUND;
  work1 = 1;
}
if (controller(cSoundMIDI) && musicmode != MIDI_SOUND) {
  [ set musicmode to MIDI
  musicmode = MIDI_SOUND;
  work1 = 1;
}
if (work1) {
  [ if playing a sound or Walkman
  if ((playWalkman || playSound)) {
    [ always stop sound BEFORE changing mode
    reset(playSound);
    stop.sound();
  }
  [ restart walkman, if it was playing
  if (playWalkman) {
    call(lgc.SetSound);
    work1 = &musicDone;
    block(SND.V.FN, SOUND.V, &currentTrack, &work1); [ sound.v(vSNDNUM, vDONEFLAG)
  }
}

[ SKIP INTRO OPTION
if (controller(cSkipIntro)) {
  toggle(skipIntro);
}

[ CLOCK MENU ITEMS
if (controller(cClock)) {
  [ toggle clock mode
  toggle(clockOn);
  if (clockOn) {
    if (realClock) {
      [ status bar checks realtime minutes
      set.cursor.char(" l27, v3, v7, v23, v99, v78, f9");
      [ force realtime clock update
      block(GET.TIME, &currentHour, &currentMinute, &currentSecond); [ get.time(vHOUR, vMINUTE, vSECOND)
    } else {
      [ status bar checks gametime seconds
      set.cursor.char(" l27, v3, v7, v23, v99, v11, f9");
    }
  } else {
    [ status bar doesn't check for time
    set.cursor.char(" l27, v3, v7, v23, v99, f9");
  }
}
if (controller(cRealClock)) {
  if (!realClock) {
    set(realClock);
    [ status bar checks realtime minutes
    set.cursor.char(" l27, v3, v7, v23, v99, v78, f9");
    [ force realtime clock update
    block(GET.TIME, &currentHour, &currentMinute, &currentSecond); [ get.time(vHOUR, vMINUTE, vSECOND)
  }
}
if (controller(cGameClock)) {
  if (realClock) {
    reset(realClock);
    [ status bar checks gametime seconds
    set.cursor.char(" l27, v3, v7, v23, v99, v11, f9");
  }
}

[ SPEED MENU ITEMS
if (controller(cSlow)) {
  [ 4/20ths of a second delay between interpreter cycles
  if (animationInterval != SLOW_SPEED) {
    animationInterval = SLOW_SPEED;
  }
}
if (controller(cNormal)) {
  [ 2/20ths of a second delay between interpreter cycles
  if (animationInterval != NORMAL_SPEED) {
    animationInterval = NORMAL_SPEED;
  }
}
if (controller(cFast)) {
  [ 1/20th of a second delay between interpreter cycles
  if (animationInterval != FAST_SPEED) {
    animationInterval = FAST_SPEED;
  }
}
if (controller(cFastest)) {
  [ no delay between interpreter cycles
  if (animationInterval != FASTEST_SPEED) {
    animationInterval = FASTEST_SPEED;
  }
}

[ DEBUG MENU ITEMS
if (controller(cLogInput)) {
  [ toggle the message
  toggle(fLogInput);
  s23 = "Log Input:      %s22";
  if (fLogInput) {
    s22 = " On";    
  } else {
    s22 = "Off";
  }
  block(STR.FORMAT, &s23, &s23, 0); [ str.format(sINPUT, sRESULT)
  block(SET.MSG, BYNUM, &mnu_msgDbgLog, &s23); [ set.msg(FN_MODE, MSGOLD, STRNEW)
}
[ show click data
if (controller(cShowClicks)) {
  [ toggle the message
  toggle(fShowClicks);
  s23 = "Display Clicks: %s22";
  if (fShowClicks) {
    s22 = " On";
  } else {
    s22 = "Off";
  }
  block(STR.FORMAT, &s23, &s23, 0); [ str.format(sINPUT, sRESULT)
  block(SET.MSG, BYNUM, &mnu_msgDbgClick, &s23); [ set.msg(FN_MODE, MSGOLD, STRNEW)
}

[***************************************
[ NON-CONTROLLER CHECKS
[***************************************

[ check for end of closet timer
if (closetTimerDone) {
  [ the door gets closed, and if ego is beyond the secret room, he's gonna get stuck
  reset(closetTimerStart);
  reset(closetTimerDone);
  reset(closetopen);
  if (currentRoom > 6) {
    print("%l50|9");
    set(trapped);
  }
}

[ save clock values for comparison purposes
oldSeconds = elapsedSeconds;
[ minutes handled as part of patient clock check
oldHours = elapsedHours;
oldDays = elapsedDays;

[ patient clock determines progression of patients
[ who visit the facility
if (elapsedMinutes != oldMinutes) {
  oldMinutes = elapsedMinutes;
  
  [ update patient locations if not in lobby
  [ (lobby manages this separately)
  if (currentRoom != rm.Lobby) {
    --eventMin;
    if (eventMin == 0) {
      [ set time to next event
      if (patientEvent == P2_ARRIVE) {
        patient2Loc = LOC_LOBBY;
        eventMin = 4;
      }
      if (patientEvent == P3_ARRIVE) {
        patient3Loc = LOC_LOBBY;
        eventMin = 1;
      }
      if (patientEvent == P2_EXAM) {
        patient2Loc = LOC_EXAM;
        eventMin = 3;
        reset(askToPee);
      }
      if (patientEvent == P2_EXIT) {
        patient2Loc = LOC_GONE;
        eventMin = 2;
      }
      if (patientEvent == P4_ARRIVE) {
        patient4Loc = LOC_LOBBY;
        eventMin = 1;
      }
      if (patientEvent == P3_EXAM) {
        patient3Loc = LOC_EXAM;
        eventMin = 3;
      }
      if (patientEvent == P3_EXIT) {
        patient3Loc = LOC_GONE;
        eventMin = 1;
      }
      if (patientEvent == P4_EXAM) {
        patient4Loc = LOC_EXAM;
        eventMin = 3;
      }
      if (patientEvent == P4_EXIT) {
        patient4Loc = LOC_GONE;
        eventMin = 2;
      }
      [ move to next event
      ++patientEvent;
      if (patientEvent == 9) {
        patientEvent = 0;
      }
    }
  }
}

[ check for ego death
if (deathType) {
  [ if this is first cycle since ego died
  if (deathType < DT_SEQ_DONE) {
    [ disable most menu and keyboard shortcuts
    disable.item(cSave);
    disable.item(cPause);
    disable.item(cChooseItem);
    [ hide speed menu by setting it to a null string
    s23 = "";
    block(SET.MSG, BYNUM, &mnu_msgSpeed, &s23); [ set.msg(FN_MODE, MSGOLD, STRNEW)
    
    [ load the death handler logic
    load.logics(lgc.Death);
  }
    
  [ call the death handler, where you update the screen
  [ and display a message to player about how ego died
  call(lgc.Death);
  
  [ don't process any other commands
  return();
}

[ save current ego position
get.posn(ego, egoX, egoY);
    
[ ROOM SPECIFIC CODE
[ (call BEFORE generic input checks)
call.v(currentRoom);

[ if debugging is active                
if (debugging) {
  [ call the debugging logic
  call(lgc.Debug);
}

[ WALKMAN MUSIC CHECK
if (musicDone) {
  reset(musicDone);
  [ is Walkman playing?
  if (playWalkman) {
    [ discard current sound
    set(noScript);
    block(SND.V.FN, DISCARD.SOUND.V, &currentTrack, 0); [ discard.sound.v(vSNDNUM)
    ++currentTrack;
    [ check if end of side is reached
    [ SIDE A: 1 - 41, 42 means at end of side A
    [ SIDE B: 43 - 83, 84 means at end of side B
    if ((currentTrack == 42 || currentTrack == 84)) {
      reset(playWalkman);
    } else {
      [ load and play next track
      set(noScript);
      block(SND.V.FN, LOAD.SOUND.V, &currentTrack, 0); [ load.sound.v(vSNDNUM)
      work1 = &musicDone;
      block(SND.V.FN, SOUND.V, &currentTrack, &work1); [ sound.v(vSNDNUM, vDONEFLAG)
    }
    reset(noScript);
  }
}

[ SOUND EFFECTS CHECK
if (soundDone) {
  reset(soundDone);
  reset(playSound);
}

[***************************************
[ GENERIC CONTROLLERS & TEXT INPUT
[***************************************

[ saving a cursor from ERIDOCC
if (saveCursor) {
  reset(saveCursor);
  [ copy s16 into designated cursor msg value
  [ v227 is cursor index value in cp.ERIDOCC logic
  work1 = v227;
  work1 += 30;
  work2 = 16;
  block(SET.MSG, BYVAR, &work1, &work2); [ set.msg(FN_MODE, MSGOLD, STRNEW) 
  [ provide feedback
  print.at("Cursor saved.", 14, 19, 15);
  return();
}
[ restore a cursor from ERIDOCC
if (restoreCursor) {
  reset(restoreCursor);
  [ set s12 to default of designated cursor
  work1 = &s12;
  work2 = v227;
  work2 += 40;
  block(SET.STRING.V, &work1, &work2, 0); [ set.string.v(vSTR, vMSG)
  [ format it
  block(STR.FORMAT, &s12, &s12, 0); [ str.format(sINPUT, sRESULT)
  [ now copy it to the current cursor msgnum
  work2 -= 10;
  block(SET.MSG, BYVAR, &work2, &work1); [ set.msg(FN_MODE, MSGOLD, STRNEW)
  [ force update by resetting the cursor index
  v227 = -1;
  [ provide feedback
  print.at("Cursor restored.", 14, 19, 17);
  return();
}

[ only if not disabled!
if (!disableGameFunctions) {
  [ check common game functions, depending on current cursor
  if (cursormode < C_DISK) {
    call(lgc.GameFunctions);
  } else {
    [ if using an item, use different function call
    call(lgc.UseItem);
  }
  [ check for toolbar buttons click
  call(lgc.ToolbarFunctions);
}

return();

[***************************************
[ DECLARED MESSAGES
[***************************************

[ menu layout - dynamic items will be explicitly defined
[ "PPDemo"
[     "About           "
[     "Version  <Alt-V>"
[     "Help        <F1>"
[     "Skip Intro      "
[ "File"
[     "Save     <F5>"
[     "Restore  <F7>"
[     "컴컴컴컴컴컴"
[     "Restart  <F9>"
[     "Quit  <Alt-Z>"
[ "Action"
[     "Examine Item  <F4>" or "Use Item       <F4>"
[     "Inventory    <Tab>"
[ "Sound"
[     "Quieter         "
[     "Louder          "
[     "컴컴컴컴컴컴컴컴"
[     "Sound: On   <F2>" or "Sound: Off   <F2>"
[     " PC Speaker     "
[     " FM Synth       "
[     " MIDI           "
[ "Time"
[     "Clock: Off  <F6>" or "Clock: On   <F6>"
[     " Game Time      "
[     " Real Time      "
[     "컴컴컴컴컴컴컴컴"
[     "Pause      <Esc>"
[ "Speed"
[     "Slower  "
[     "Faster  "
[     "컴컴컴컴"
[     " Normal "
[     " Slow   "
[     " Fast   "
[     " Fastest"

[ dynamic menu items (message numbers pre-assigned)
#message  1 "Examine Item  <F4>" 
#message  2 "Sound: On   <F2>"
#message  3 " PC Speaker     "
#message  4 " FM Synth       "
#message  5 " MIDI           "
#message  6 "Clock: Off  <F6>"
#message  7 " Game Time      "
#message  8 " Real Time      "
#message  9 "Speed"
#message 10 " Normal "
#message 11 " Slow   "
#message 12 " Fast   "
#message 13 " Fastest"
#message 14 "Skip Intro:  Off"
#message 15 "Debug"
#message 16 "Log Input:      Off"
#message 17 "Display Clicks: Off"
[ constant values for dynamic menus
#message 18 "Speed"
#message 19 "Examine Item  <F4>"
#message 20 "Use Item      <F4>"
#message 21 "Debug"
[ active cursors
#message 30 "0000FF3FFF1FFF0FFF07FF03FF01FF007F003F001F00FF01FF00FF307FF87FF8FFFC"
            "00000040006000700078007C007E007F807F007C006C00460006000300030000"
[ walk
#message 31 "040FFFF1FFE07FC07FC0FFE0FFF1FFE17FC03FC07FE0FFE1FFC1FF80FF087F183FBC"
            "0000000E001D001F000E0004000C0016801B0006000C001C0036006200430000"
[ look
#message 32 "0709FFFFFFFFFFFFFFFEEFF62DB40BD0024001808001018003C00FF03FFCFFFFFFFF"
            "000000000000000000000000C003F00F7C3E3E7C7C3EF00FC003000000000000"
[ talk
#message 33 "050E1FF807E0018000000000000000000000008001E007FC0FFE3FFC7FF8FFF9FFFF"
            "0000E007F81FFE7FFE7FFE7FFE7FFE7FFE1FF803F000C0008001000200000000"
[ use 
#message 34 "0803FFFFFFFF1FFE07F801F000F000F000F00090000000000000018001C003E007F0"
            "000000000000C000D806DA06DA06DA06DA06FA6FFE7FFE7FFC3FFC1FF80F0000"
[ wait 
#message 35 "0707018003C003C007E007E00FF01FF83FFC3FFC1FF80FF007E007E003C003C00180"
            "0000F81FF81F5005A00A40058002000180004003E007700E1008081000000000"
[ disk
#message 36 "0707FFFFFFFFFFFF0000000000000000000000000000000001000300FFFFFFFFFFFF"
            "0000000000000000EE77EE770E70FE7FFE7FFE7F1E78DC790000000000000000"
[ tape
#message 37 "0707FFFFFFFFFFFFFFFF01800000000000000000000000000000FFFFFFFFFFFFFFFF"
            "00000000000000000000FE7FF66FEA57F66F1E78EE7700000000000000000000"

[ default cursor pointers
#message 40 "%l50|1"
#message 41 "%l50|2"
#message 42 "%l50|3"
#message 43 "%l50|4"
#message 44 "%l50|5"
#message 45 "%l50|6"
#message 46 "%l50|7"
#message 47 "%l50|8"
