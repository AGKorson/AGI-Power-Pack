[ ********************************************************************
[
[ lgc.Main: Main logic
[
[ This logic runs in every interpreter cycle and calls
[ other logics as needed.
[
[
[ To better manage variables and flags, they are 
[ grouped into reserved, global, local, and dynamic:
[
[ reserved = reserved by the interpreter 
[     v0 - v26
[     f0 - f16, f20
[     s0, s1
[
[ global = usable and accessible by all logics
[     v27 - v219
[     f17 - f18, f21 - fShowClicks
[     s2 - s11
[
[ local = usable and accessible by single room
[     v220 - v239
[     f220 - f239
[     s12 - s19
[
[ dynamic = usable and accesible by a non-room logic
[     v240 - v255
[     f240 - f255
[     s20 - s23
[
[ Local and dynamic variables/flags are reset whenever a new room is
[ loaded.
[

[ ********************************************************************

[ **************************************
[ LOCAL DEFINES
[ **************************************
#define rndNum                     v255

[ **************************************
[ ERROR CHECK
[ **************************************

[ if one of the few trappable errors in AGI is encountered, the game
[ is probably not able to continue; make an announcement to the player
[ and quit
if (errorNumber > 0) {
  block(FADE.IN, 0, 0, BLACK); [ fade.in(BYVAR, DELAY, COLOR)
  call(lgc.Error); 
}

[ **************************************
[ GAME START
[ **************************************
[ when game begins, or restarts, there are a number
[ of setup activities to take care of
if (currentRoom == 0) {
  [ when the game starts, the current room is zero, so all the
  [ setup/initialization code goes here
  [
  [ restarting a game also sets currentRoom to zero, and also
  [ requires most of the same setup/initialization steps

  if (!gameRestarted) { 
    [ confirm platform is PC
    if (machineType != PC) {
      load.logics(lgc.Error);
      print("%l97|33");
      quit(1);
    }
          
    [ run the powerpack mod BEFORE DOING ANYTHING ELSE
    call(lgc.Version2Check);
    if (!IntVersion) {
      load.logics(lgc.Error);
      print("%l97|34");
      quit(1);
    }
    if ((IntVersion == VERSION_2089 || IntVersion == VERSION_2272)) {
      [ these versions not supported
      load.logics(lgc.Error);
      print("%l97|35");
      quit(1);
    }
    if (IntVersion == VERSION_2411) {
      call(lgc.mod2411);
      goto(continue);
    }
    if (IntVersion == VERSION_2425) {
      call(lgc.mod2425);
      goto(continue);
    }
    if (IntVersion == VERSION_2426) {
      call(lgc.mod2426);
      goto(continue);
    }
    if (IntVersion == VERSION_2435) {
      call(lgc.mod2435);
      goto(continue);
    }
    if (IntVersion == VERSION_2439) {
      call(lgc.mod2439);
      goto(continue);
    }
    if (IntVersion == VERSION_2440) {
      call(lgc.mod2440);
      goto(continue);
    }
    if (IntVersion == VERSION_2903) {
      call(lgc.mod2903);
      goto(continue);
    }
    if (IntVersion == VERSION_2911) {
      call(lgc.mod2911);
      goto(continue);
    }
    if (IntVersion == VERSION_2912) {
      call(lgc.mod2912);
      goto(continue);
    }
    if (IntVersion == VERSION_2915) {
      call(lgc.mod2915);
      goto(continue);
    }
    if (IntVersion == VERSION_2917) {
      call(lgc.mod2917);
      goto(continue);
    }
    if (IntVersion == VERSION_2936) {
      call(lgc.mod2936);
      goto(continue);
    }
    :continue
    [ confirm video, sound and mouse settings
    if (monitorType != EGA) {
      load.logics(lgc.Error);
      print("%l97|36");
      quit(1);
    }
    if (!enableDblClick) {
      load.logics(lgc.Error);
      print("%l97|37");
      quit(1);
    }
    if (numberOfVoices != 4) {
      load.logics(lgc.Error);
      print("%l97|38");
      quit(1);
    }
  }
  
  [ do start/restart initialization activities
  call(lgc.Init);       [ this sets up the game; assigns key controllers,
                        [ configures the screen and other startup activities

  [ some start/restart actions have to be done within logic0
  if (gameRestarted) {    
    [ go to first room here (skip Intro)
    new.room(rm.ParkingLot);
  } else {
    [ if NOT restarting (meaning this is very beginning, need to create 
    [ the menu; it can't be done in lgc.Init, it MUST be done from within 
    [ logic 0, so do that before going to start room
    set.menu("PPDemo");
    set.menu.item("About           ", cAbout);
    set.menu.item("Version  <Alt-V>", cVersion);
    set.menu.item("Help        <F1>", cHelp);
    set.menu("File");
    set.menu.item("Save     <F5>", cSave);
    set.menu.item("Restore  <F7>", cRestore);
    set.menu.item("컴컴컴컴컴컴", cDummy);
    set.menu.item("Restart  <F9>", cRestart);
    set.menu.item("Quit  <Alt-Z>", cQuit);
    set.menu("Action");
     set.menu.item("Inventory      <Tab>", cStatus);
     set.menu.item(m13, cChooseItem); [ "Examine Item    <F4>"
    set.menu("Sound");
    set.menu.item(m15, cToggleSound); [ "Sound: On     <F2>"
    set.menu.item(m16, cSoundPC);     [ " PC Speaker       "
    set.menu.item(m17, cSoundFM);     [ " FM Synth         "
    set.menu.item(m18, cSoundMIDI);   [ " MIDI             "
    set.menu("Special");
    set.menu.item(m20, cClock); [ "Clock: On      <F6>"
    set.menu.item("Pause         <Esc>", cPause);
    set.menu("Speed");
    set.menu.item(m21, cNormal); [ " Normal "
    set.menu.item(m22, cSlow); [ " Slow   "
    set.menu.item(m23, cFast); [ " Fast   "
    set.menu.item(m24, cFastest); [ " Fastest"
    
[ temporary debug items
set.menu("Debug");
set.menu.item("Log Input: Off     ", cLogInput);
set.menu.item("Display Clicks: Off", cShowClicks);

    [ submit the menu (which makes it ready to use)
    submit.menu();
    [ disable the separator lines
    disable.item(cDummy);
    
    [ configure sound menus
    goto(setsndmenu);
  }
}

[ **************************************
[ EVERY CYCLE
[ **************************************

[!!!!!! BEGIN TEMPORARY DEBUG CODE
if (controller(cLogInput)) {
  [ toggle the message
  toggle(fLogInput);
  s23 = "Log Input:      %s22";
  if (fLogInput) {
    s22 = " On";    
  } else {
    s22 = "Off";
  }
  block(STR.FORMAT, &s23, &s23, 0); [ str.format(sINPUT, sRESULT)
  block(SET.MSG, 0, &m38, &s23); [ set.msg(BYVAR, MSGOLD, STRNEW)
}
[ show click data
if (controller(cShowClicks)) {
  [ toggle the message
  toggle(fShowClicks);
  s23 = "Display Clicks: %s22";
  if (fShowClicks) {
    s22 = " On";
  } else {
    s22 = "Off";
  }
  block(STR.FORMAT, &s23, &s23, 0); [ str.format(sINPUT, sRESULT)
  block(SET.MSG, 0, &m39, &s23); [ set.msg(BYVAR, MSGOLD, STRNEW)
}
if (fShowClicks) {
  [ used to assist in creating click statements and said statements
  if (controller(cLeftClick)  && currentRoom != 12 && currentRoom != 13) {
    get.posn(ego, work1, work2);
    display(4, 1, "left click: (%v27, %v28)  ego pos:  (%v46, %v47)        ");
  }
  [[ show the open closet door timer
  [ display(24, 0, "%v79|2:%v80|2");
  [ show remaining available memory
  display(24, 0, "mem pages: %v8  ");
}
if (fLogInput) {
  if (haveInput) {
    if (unknownWordNum > 0) {
      log("unknown word: %v9");
    } else {
      log("");
    }
  }
}
if (said("test", "get")) {
  get.num("add to score: ", work1);
  call(lgc.ScoreHandler);
}
if (said("test", "drop")) {
  get.num("sub from score: ", work1);
  work1 *= -1;
  call(lgc.ScoreHandler);
}
[!!!!!! END TEMPORARY DEBUG CODE

[ if a new room has just been loaded
if (newRoom) {
  [ First interpreter cycle in a new room
  [ Note: Everything other than logic 0 is discarded
  [ from memory when new.room is executed

  [ do basic room initialization
  call(lgc.RoomInit);
  
  if (!disableGameFunctions) { 
    [ Load game specific functions logic into memory
    load.logics(lgc.GameFunctions);
    [ also toolbar functions
    load.logics(lgc.ToolbarFunctions);
  }
  [ if debugging is active
  if (debugging) {
    [ load debug logic into memory
    load.logics(lgc.Debug);
  }
  [ score handler (to allow score to go below zero)
  load.logics(lgc.ScoreHandler);
  
  [ always animate ego object when starting a new room
  animate.obj(ego);
  [ load and set view
  [ (for more complex games, this will probably need 
  [ to be moved into each room's logic)
  load.view(vw.Ego);
  set.view(ego, vw.Ego);
  
  [ force clock value to update, if time is visible
  clearStatusSeconds = -1; 
  
  [ check for closet door being left open
  if (closetopen && !closetTimerStart && currentRoom != rm.Hallway && 
      currentRoom != rm.Closet && currentRoom != rm.SecretRoom) {
    [ start a fresh timer
    set(closetTimerStart);
    timerMin = 0; [4;
    timerSec = 20; [0;
    block(START.TIMER, &timerMin, &timerSec, &closetTimerDone); [ start.timer(vMINUTES, vSECONDS, fDONE)
  } else {
    [ stop the timer
    reset(closetTimerStart);
    block(TOGGLE.TIMER, 0, 0, 0); [ toggle.timer(STATE)
  }
}

[ check for end of closet timer
if (closetTimerDone) {
  [ the door gets closed, and if ego is beyond the secret room, he's gonna get stuck
  reset(closetTimerStart);
  reset(closetTimerDone);
  reset(closetopen);
  if (currentRoom > 6) {
    print("Uh oh. Sounds like you are trapped.");
    set(trapped);
  }
}

[ if this is first cycle since restore game executed
if (isset(gameRestored)) {
  [ make sure input area is cleared
  clear.lines(23, 24, 0);
  [ need to 're-disable menu separators)
  disable.item(cDummy);
  
  [ set menus and sound settings
  :setsndmenu
  if (soundOn) {
    s23 = "On ";
    enable.item(cSoundPC);
    enable.item(cSoundFM);
    enable.item(cSoundMIDI);
  } else {
    s23 = "Off";
    disable.item(cSoundPC);
    disable.item(cSoundFM);
    disable.item(cSoundMIDI);
  }
  s22 = "Sound: %s23  <F2>";
  block(STR.FORMAT, &s22, &s23, 0); [ str.format(sINPUT, sRESULT)
  block(SET.MSG, 0, &m15, &s23); [ set.msg(BYVAR, MSGOLD, STRNEW)

  [ check the correct sound option, uncheck the rest
  if (!4channelsound) {
    s23 = "\x10";
  } else {
    s23 = " ";
  }
  s22 = "%s23PC Speaker     ";
  block(STR.FORMAT, &s22, &s23, 0); [ str.format(sINPUT, sRESULT)
  block(SET.MSG, 0, &m16, &s23); [ set.msg(BYVAR, MSGOLD, STRNEW)
  
  if (4channelsound && !useMIDI) {
    set.key(0, 0, SET.SNDMODE); [ set.sndmode(MODE)
    s23 = "\x10";
  } else {
    s23 = " ";
  }
  s22 = "%s23FM Synth       ";
  block(STR.FORMAT, &s22, &s23, 0); [ str.format(sINPUT, sRESULT)
  block(SET.MSG, 0, &m17, &s23); [ set.msg(BYVAR, MSGOLD, STRNEW)
  
  if (4channelsound && useMIDI) {
    set.key(1, 0, SET.SNDMODE); [ set.sndmode(MODE)
    s23 = "\x10";
  } else {
    s23 = " ";
  }
  s22 = "%s23MIDI           ";
  block(STR.FORMAT, &s22, &s23, 0); [ str.format(sINPUT, sRESULT)
  block(SET.MSG, 0, &m18, &s23); [ set.msg(BYVAR, MSGOLD, STRNEW)
  [ update the config file
  call(lgc.SaveOptions);
  
  [ if starting, current room is zero
  if (currentRoom == 0) {
    [ start the game with the title screen
    new.room(rm.Title);
  }
  
  return();
}

[ if clock display is needed
if (clockOn) {
  [ check current time every second
  if (clearStatusSeconds != elapsedSeconds) {
    block(GET.TIME, &work1, &work2, &work3); [ get.time(vHOUR, vMINUTE, vSECOND)
    [ update the displayed clock whenever the minute value changes
    if (work2 != currentMinute) {
      currentMinute = work2;
      currentHour = work1;
    }
    if (!deathType) {
      [ update game time clock every second (if not disabled)
      set.text.attribute(-1, 0);
      set.text.attribute(YELLOW, BROWN);
      [ update the clock
      display(24, 56, "Elapsed Time: %v13:%v12|2:%v11|2 ");
      [ restore default colors
      set.text.attribute(0, -1);
    }
    
    [ update the stored seconds value
    clearStatusSeconds = elapsedSeconds;
  }
}

[ check for ego death (usually happens a lot in AGI games...)
if (deathType > 0) {
  [ if this is first cycle since ego died
  if (deathType != 255) {
    [ disable most menu and keyboard shortcuts
    disable.item(cSave);
    disable.item(cPause);
    disable.item(cChooseItem);
    disable.item(cClock);
    disable.item(cNormal);
    disable.item(cFastest);
    disable.item(cFast);
    disable.item(cSlow);
    
    [ load the death handler logic
    load.logics(lgc.Death);
  }
    
  [ call the death handler, where you update the screen
  [ and display a message to player about how ego died
  call(lgc.Death);
  
  [ don't process any other commands
  return();
}

[ ***********************
[ CHECK CONTROLLER INPUT
[ *********************** 

[ if normal game functions have not been disabled
if (!disableGameFunctions) {
  [ sound options
  if (controller(cToggleSound)) {
    toggle(soundOn);
    goto(setsndmenu);
  }
  if (controller(cSoundPC)) {
    [ if not already in PC sound mode
    if (4channelsound) {
      reset(4channelsound);
      reset(useMIDI);
      goto(setsndmenu);
    }
  }
  if (controller(cSoundFM)) {
    [ if not already in FM mode
    if ((!4channelsound || useMIDI)) {
      set(4channelsound);
      reset(useMIDI);
      goto(setsndmenu);
    }
  }
  if (controller(cSoundMIDI)) {
    [ if not already in MIDI mode
    if ((!4channelsound || !useMIDI)) {
      set(4channelsound);
      set(useMIDI);
      goto(setsndmenu);
    }
  }
}
  
[ ***********************
[ NON-CONTROLLER CHECKS
[ *********************** 

[ save current ego position
get.posn(ego, egoX, egoY);
    
[ IMPORTANT: This calls the logic for the current room - this
[ is not done automatically by the interpreter so it has to be
[ called manually by logic 0.
call.v(currentRoom);

[ if debugging is active                
if (debugging) {
  [ call the debugging logic
  call(lgc.Debug);
}

[ run game specific functions only if they are not disabled
if (!disableGameFunctions) {
  [ saving a cursor
  if (saveCursor) {
    reset(saveCursor);
    [ copy s16 into designated cursor msg value
    [ v227 is cursor index value in cp.ERIDOCC logic
    work1 = v227;
    work1 += 40;
    work2 = 16;
    block(SET.MSG, 1, &work1, &work2); [ set.msg(BYVAR, MSGOLD, STRNEW) 
    [ provide feedback
    print.at("Cursor saved.", 14, 19, 15);
    return();
  }
  
  [ restore a cursor
  if (restoreCursor) {
    reset(restoreCursor);
    [ set s12 to default of designated cursor
    work1 = &s12;
    work2 = v227;
    work2 += 46;
    block(SET.STRING.V, &work1, &work2, 0); [ set.string.v(vSTR, vMSG);
    [ now copy it to the current cursor msgnum
    work2 -= 6;
    block(SET.MSG, 1, &work2, &work1); [ set.msg(BYVAR, MSGOLD, STRNEW)
    [ force update by resetting the cursor index
    v227 = -1;
    [ provide feedback
    print.at("Cursor restored.", 14, 19, 17);
    return();
  }
  
  [ check for instrument change
  if (saveCustomInst) {
    reset(saveCustomInst);
    work1 = 22;
    block(STR.LEFT, &s12, &s12, &work1); [ str.left(sINPUT, sRESULT, vLEN);
    work1 = v221; [ selinst in cp.CAISOCC
    work1 += 52; [convert to msg offset
    work2 = &s12; [ use string 12
    block(SET.MSG, 1, &work1, &work2); [set.msg(BYVAR, MSGNUM, STRNEW);
    return();
  }
  
  call(lgc.GameFunctions); 
  call(lgc.ToolbarFunctions);
}
  
[ check player input for unknown words
if (haveInput && !haveMatch && unknownWordNum > 0) {
  reset(haveInput);
  [copy word to a string
  if (unknownWordNum == 1) {
    word.to.string(unknownWord, w1);
  }
  if (unknownWordNum == 2) {
    word.to.string(unknownWord, w2);
  }
  if (unknownWordNum == 3) {
    word.to.string(unknownWord, w3);
  }
  if (unknownWordNum == 4) {
    word.to.string(unknownWord, w4);
  }
  if (unknownWordNum == 5) {
    word.to.string(unknownWord, w5);
  }
  if (unknownWordNum == 6) {
    word.to.string(unknownWord, w6);
  }
  if (unknownWordNum == 7) {
    word.to.string(unknownWord, w7);
  }
  if (unknownWordNum == 8) {
    word.to.string(unknownWord, w8);
  }
  if (unknownWordNum == 9) {
    word.to.string(unknownWord, w9);
  }
  if (unknownWordNum == 10) {
    word.to.string(unknownWord, w10);
  }
  [choose a random unknown word response
  random(33, 35, rndNum);
  print.v(rndNum);
}
  
[ finally, if there is input that hasn't 
[ been recognized yet
if (haveInput && !haveMatch) {
  print("I don't understand your request.");
  reset(haveInput);
}

[ save clock values for comparison purposes
oldSeconds = elapsedSeconds;
oldMinutes = elapsedMinutes;
oldHours = elapsedHours;
oldDays = elapsedDays;

return();

[ **************************************
[ MESSAGES
[ **************************************
[#message xx "PPDemo"
[#message xx "About          "
[#message xx "Version <Alt-V>"
[#message xx "Help       <F1>"
[#message xx "File"
[#message xx "Save     <F5>"
[#message xx "Restore  <F7>"
[#message xx "컴컴컴컴컴컴"
[#message xx "Restart  <F9>"
[#message xx "Quit  <Alt-Z>"
[#message xx "Action"
[ TODO: change inv screen(F4) depending on cursor
#message 13 "Use Item        <F4>" 
[           "Examine Item    <F4>"
[#message xx "Inventory      <Tab>"
[#message xx "Sound"
#message 15 "Sound: On   <F2>"
#message 16 " PC Speaker     "
#message 17 " FM Synth       "
#message 18 " MIDI           "
[#message xx "Special"
[ TODO: add clock menu toggle code
#message 20 "Clock: Off     <F6>"
[#message xx "Pause         <Esc>"
[#message xx "Speed"
[ TODO: add speed menu toggle code
#message 21 " Normal "
#message 22 " Slow   "
#message 23 " Fast   "
#message 24 " Fastest"

[debug menu items
#message 38 "Log Input:      Off"
#message 39 "Display Clicks: Off"

 [ active cursors
#message 40 "0000FF3FFF1FFF0FFF07FF03FF01FF007F003F001F00FF01FF00FF307FF87FF8FFFC"
            "00000040006000700078007C007E007F807F007C006C00460006000300030000"
[ walk
#message 41 "040FFFF1FFE07FC07FC0FFE0FFF1FFE17FC03FC07FE0FFE1FFC1FF80FF087F183FBC"
            "0000000E001D001F000E0004000C0016801B0006000C001C0036006200430000"
[ look
#message 42 "0709FFFFFFFFFFFFFFFEEFF62DB40BD0024001808001018003C00FF03FFCFFFFFFFF"
            "000000000000000000000000C003F00F7C3E3E7C7C3EF00FC003000000000000"
[ talk
#message 43 "050E1FF807E0018000000000000000000000008001E007FC0FFE3FFC7FF8FFF9FFFF"
            "0000E007F81FFE7FFE7FFE7FFE7FFE7FFE1FF803F000C0008001000200000000"
[ use 
#message 44 "0803FFFFFFFF1FFE07F801F000F000F000F00090000000000000018001C003E007F0"
            "000000000000C000D806DA06DA06DA06DA06FA6FFE7FFE7FFC3FFC1FF80F0000"
[ wait 
#message 45 "0707018003C003C007E007E00FF01FF83FFC3FFC1FF80FF007E007E003C003C00180"
            "0000F81FF81F5005A00A40058002000180004003E007700E1008081000000000"
[ default cursors
#message 46 "0000FF3FFF1FFF0FFF07FF03FF01FF007F003F001F00FF01FF00FF307FF87FF8FFFC"
            "00000040006000700078007C007E007F807F007C006C00460006000300030000"
[ walk
#message 47 "040FFFF1FFE07FC07FC0FFE0FFF1FFE17FC03FC07FE0FFE1FFC1FF80FF087F183FBC"
            "0000000E001D001F000E0004000C0016801B0006000C001C0036006200430000"
[ look
#message 48 "0709FFFFFFFFFFFFFFFEEFF62DB40BD0024001808001018003C00FF03FFCFFFFFFFF"
            "000000000000000000000000C003F00F7C3E3E7C7C3EF00FC003000000000000"
[ talk
#message 49 "050E1FF807E0018000000000000000000000008001E007FC0FFE3FFC7FF8FFF9FFFF"
            "0000E007F81FFE7FFE7FFE7FFE7FFE7FFE1FF803F000C0008001000200000000"
[ use 
#message 50 "0803FFFFFFFF1FFE07F801F000F000F000F00090000000000000018001C003E007F0"
            "000000000000C000D806DA06DA06DA06DA06FA6FFE7FFE7FFC3FFC1FF80F0000"
[ wait
#message 51 "0707018003C003C007E007E00FF01FF83FFC3FFC1FF80FF007E007E003C003C00180"
            "0000F81FF81F5005A00A40058002000180004003E007700E1008081000000000"

#message 52 "%s16"
#message 53 "22215909FFFF030F020000"
#message 54 "22215909FFFF030F020000"
#message 55 "22215909FFFF030F020000"
#message 56 "22215909FFFF030F020000"
#message 57 "22215909FFFF030F020000"