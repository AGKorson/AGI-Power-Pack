[*********************************************************************
[
[ lgc.GameFunctions: Game-specific functions
[
[ You should use this logic to perform any game specific functions, 
[ such as counting down timers, etc and processing player input 
[ related to the game (such as examining/using inventory items) and 
[ any other things that are required in several rooms that you don't
[ want to duplicate in each room.
[
[ This logic is called from logic 0, on every cycle, unless 
[ disableGameFunctions is set.
[
[ Sierra did not use a separate logic for all this - they just did it
[ all from logic 0. I find it is neater this way, as you can keep your
[ game specific processing separate from other system-related things
[ (although these may require some modification for your game). Also,
[ this makes logic 0 easier to manage.
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[TODO: PP FUNCTIONS not in demo game:
[    GET.DATE - add easter egg to show a message on certain days of the week (in gameinit logic)
[    GET.DAYOFWEEK - add easter egg to show a message on certain days of the year (in gameinit logic)
[    DELETE.FILE - add option to clear all save games
[    SET.ITEM - change disk description once it's formatted
[    ITEM.COUNT - ? maybe make a comment when ego has everything? 'your pockets are full!'

[***************************************
[ LOCAL DEFINES
[***************************************
#define entryDelay   v240
#define rndNum       v241

#define tmpStr1       s20
#define tmpStr2       s21

[***************************************
[ MAIN CODE BLOCK 
[***************************************

[ check for cancel click2move
if (controller(cRightClick)) {
  if (click2move) {
    egoDir = 0;
    set.dir(ego, egoDir); 
  }
}

[ check generic new room actions
if (newRoom) {
  [ check for walkman
  if (currentTrack > 0 && currentTrack < 128 && currentRoom != rm.Lobby) {
    [ load it, and start it
    set(noScript);
    block(SND.V.FN, LOAD.SOUND.V, &currentTrack, 0); [ load.sound.v(vSNDNUM)
    work1 = &musicDone;
    block(SND.V.FN, SOUND.V, &currentTrack, &work1); [ sound.v(vSNDNUM, vDONEFLAG)
    reset(noScript);
  }
}

[ check for kicking bucket
if (kickBucket) {
  if (currentRoom == rm.ParkingLot) {
    [ in parking lot, end the sequence after the death loop
    [ finishes
    if (bucketDone) {
      reset(kickBucket);
      deathType = DT_KICK_BUCKET;
    }
    return();
  }
  [ in all other rooms, check ego loop to determine next 
  [ action in the sequence
  current.loop(ego, work1);
  if (bucketDone) {
    [ when each loop finishes, move to next sequence
    if (work1 == 0) {
      [ after kicking, switch to falling
      set.loop(ego, 1);
      set.cel(ego, 0);
      end.of.loop(ego, bucketDone);
    } else {
      [ after falling, end the sequence
      reset(kickBucket);
      deathType = DT_KICK_BUCKET;
      return();
    }
  } else {
    [ during kick cycle, check for the right time
    [ to start the bucket moving
    if (work1 == 0) {
      current.cel(ego, work1);
      if (work1 == 2) {
        work1 = UP_RIGHT;
        set.dir(oBucket, work1);
      }
    }
  }
  [ if bucket reaches right edge, erase it
  if (v4 == &oBucket) {
    erase(oBucket);
  }
}

[***************************************
[ PROCESS PLAYER INPUT
[***************************************
[ check for cussing
work1 = 0;
:loop1
get.string(tmpStr1, m1, CURRENT.INPUT, 0, 0); [ current.input(sRESULT)
work2 = 70; ['F'
[ pass starting pos via vRESULT
work3 = work1;
block(INSTR, &tmpStr1, &work2, &work3); [ instr(sINPUT, vCHAR, vRESULT)
if (work3 == 255) {
  work2 = 102; [ 'f'
  work3 = work1;
  block(INSTR, &tmpStr1, &work2, &work3); [ instr(sINPUT, vCHAR, vRESULT)
}
[ save result in work1
work1 = work3;
[ if letter 'f' is found
if (work1 < 255) {
  [ check for match against 'fuck' by getting 
  [ 'f' plus next three letters
  work2 = 4;
  block(STR.MID, &tmpStr1, &work1, &work2); [ str.mid(sINPUT, vPOS, vLEN)
  tmpStr2 = "fuck";
  [ advance position so loop can look for next 'f'
  ++work1;
  if (compare.strings(tmpStr1, tmpStr2)) {
    print("Watch your language, you son of a bitch!");
    get.string(tmpStr1, m1, -1, 0, 0);
    block(STR.LEFT, &tmpStr1, &tmpStr1, &work1); [ str.left(sINPUT, sRESULT, vLEN)
    tmpStr2 = "***";
    block(STR.CONCAT, &tmpStr1, &tmpStr2, 0); [ str.concat(sINPUT, sADD)
    set.string(SET.INPUT, "%s20"); [ set.input(mINPUT)
  } else {
    [ check rest of line
    goto(loop1);
  }
}

[ now check for any controllers that may have been activated
if (controller(cAbout)) {
  print(gameAboutMsg);
}

[ sound volume controls
if (controller(cCrescendo) && attenuation > 0 && soundOn) {
  [ to increase volume, lower sound attenuation variable
  if (attenuation > 0) {
    --attenuation;
    [ update the config file
    call(lgc.SaveOptions);
  }
}
if (controller(cDecrescendo) && attenuation < 63 && soundOn) {
  [ to decrease volume, raise sound attenuation variable
  if (4channelsound) {
    work1 = 63;
  } else {
    work1 = 15;
  }
  if (attenuation < work1) {
    ++attenuation;
    [ update the config file
    call(lgc.SaveOptions);
  }
}

if ((said("debug") || said("debug", "on"))) {
  [ if not in debug mode yet
  if (!debugging) {
    [ activate it
    set(debugging);
    [ allow tracing
    set(enableTrace);
    [ display the game-version message
    print(gameVersionMsg); 
    [ and interpreter version
    version();
    [ load debug logic into memory 
    [ (!this could be a concern since some logics
    [ do dynamic loading/unloading of resources; user 
    [ needs to be careful
[    load.logics(lgc.Debug);
  }
}
if (said("debug", "off")) {
  [ if debugging, turn it off
  if (debugging) {
    reset(debugging);
    reset(enableTrace);
    [ debug logic will unload at next new.room
  }
}

[ save game
if ((controller(cSave) || 
     said("save", "game") || 
     said("save"))) {
  [ because patient views aren't always loaded, need to erase them
  [ prior to saving; then redrawn afterward; when restoring,
  [ the views need to be loaded BEFORE the patients are redrawn
  if (currentRoom == rm.Lobby) {
    #define oPatient2        o5
    #define oPatient3        o6
    #define oPatient4        o7
    [ erase patients 2,3,4
    erase(oPatient2);
    erase(oPatient3);
    erase(oPatient4);
  } 
  save.game();
  if (currentRoom == rm.Lobby) {
    [ redraw patients, if currently in the room
    if (patient2Loc != LOC_GONE) {
      draw(oPatient2);
    }
    if (patient3Loc != LOC_GONE) {
      draw(oPatient3);
    }
    if (patient4Loc != LOC_GONE) {
      draw(oPatient4);
    }
  }
}

[ restore game
if ((controller(cRestore) || 
     said("restore", "game") || 
     said("restore"))) {
  restore.game();
}

[ restart game
if ((controller(cRestart) || 
     said("restart", "game") || 
     said("restart"))) {
  call(lgc.CacheOptions);
  restart.game();
}

[ ask for help
if ((controller(cHelp) || said("help"))) {
  [ show the main help screen
  set(showHelp);
  [ use local variable to initialize the help screen
  set(f240);
  call(lgc.GameHelp);
}

[ echo line draws previous input on input line
if (controller(cEchoLine)) {
  echo.line();
}

[ clear the input line
if (controller(cCancelLine)) {
  cancel.line();
}

[ pause game (showing menu is another 
[ way to pause the game)
if ((controller(cPause) || 
     said("pause", "game") || 
     said("pause"))) {
  pause();
}

[ check player's current inventory
if ((controller(cStatus) || 
     said("inventory"))) {
  [ disable selection of inventory items
  reset(enableItemSelect);
  status();
}

[ use or examine an item
if (controller(cChooseItem)) {
  [ enable section of inventory items
  set(enableItemSelect);
  [ show inventory screen
  status();
  
  [ selectedItem is set to 255 if ESC is pressed
  [ in the inventory screen
  if (selectedItem > 0 && selectedItem != 255) {
    [ use or examine selected item
    if (cursormode == C_USE) {
      [ magazine
      if (selectedItem == 1) {
        [ same as 'read magazine'
        goto(readmag);
      }
      [ disk
      if (selectedItem == 2) {
        [ disk can only be used in control room
        if (currentRoom == rm.ControlRoom) {
          cursormode = C_DISK;
          status.line.off();
          prevent.input();
        } else {
          print("There is nothing you can do with the disk here.");
        }
      }
      [ keyfob
      if (selectedItem == 3) {
        [ disk can only be used in parking lot
        if (currentRoom == rm.ParkingLot) {
          if (carLocked) {
            print("Using your keyfob, you unlock your car door.");
            reset(carLocked);
            work1 = -2;
            call(lgc.ScoreHandler);
          } else {
            print("Thinking it's best not to take any chances, "
                  "you hit the lock button on your keyfob to "
                  "prevent your car from being stolen.");
            set(carLocked);
            work1 = 2;
            call(lgc.ScoreHandler);
          }
        } else {
          print("You are too far fromy your vehicle for the keyfob to work.");
        }
      }
      [ cassette
      if (selectedItem == 4) {
        if (has("Walkman")) {
          [ show the walkman
          selectedItem = 5;
        } else {
          [ nothing to do with the cassette
          print("There is nothing you can do with the cassette right now.");
        }
      }
      [ walkman
      if (selectedItem == 5) {
        if (currentRoom == rm.Lobby) {
          if (warnWalkman) {
            print("The receptionist asked you to not play your music in the lobby.");
          } else {
            [ not allowed
            block(MSGBOX.COLOR, LT_MAGENTA, MAGENTA, WHITE); [ msgbox.color(BORDER, FG, BG)
            print.at("\"Excuse me, our patients prefer quiet- please don't play music in the lobby. Thanks!\"", 6, 6, 35);
            block(MSGBOX.COLOR.V, &msgBoxBorder, &msgBoxFG, &msgBoxBG); [ msgbox.color.v(vBORDER, vFG, vBG)
            print("You decide it's best to comply with the receptionist's request, and put the walkman back in your pocket.");
            set(warnWalkman);
            [ receptionistLoc = HALL            
            if (v220 == 4) {
              print("(How did she hear that from all the way out in the hallway?)");
            }
          }
        } else {
          if (currentTrack >0 && currentTrack < 128) {
            [ stop and unload current track; it will get restarted
            [ after showing the walkman
            stop.sound();
            set(noScript);
            block(SND.V.FN, DISCARD.SOUND.V, &currentTrack, 0); [ discard.sound.v(vSNDNUM)
            reset(noScript);
          }
          [ show a closeup of walkman, player can press play/stop/ff/rewind/eject
          call(lgc.Walkman);
          call(lgc.RedrawRoom);
          
          if (currentTrack >0 && currentTrack < 128) {
            [ restart current track
            set(noScript);
            block(SND.V.FN, LOAD.SOUND.V, &currentTrack, 0); [ load.sound.v(vSNDNUM)
            work1 = &musicDone;
            block(SND.V.FN, SOUND.V, &currentTrack, &work1); [ sound.v(vSNDNUM, vDONEFLAG)
            reset(noScript);
          }
        }
      }
      [ memo
      if (selectedItem == 6) {
        [ read the memo
        goto(readMemo);
      }
      [ post-it note
      if (selectedItem == 7) {
        [ read the note
        selectedItem += 100;
        call(lgc.ExamineItem);
        call(lgc.RedrawRoom);
      }
      [ BaDASS manual
      if (selectedItem == 8) {
        [ read the manual
        goto(readManual);
      }
      [ notebook
      if (selectedItem == 9) {
        [ read the notebook
        goto(readNotebook);
      }
      [ folder
      if (selectedItem == 10) {
        selectedItem += 100;
        call(lgc.ExamineItem);
        call(lgc.RedrawRoom);
      }
      [ scroll
      if (selectedItem == 11) {
        goto(readScroll);
      }
      [ music sheet
      if (selectedItem == 12) {
        selectedItem += 100;
        call(lgc.ExamineItem);
        call(lgc.RedrawRoom);
      }
      [ PQ idea list
      if (selectedItem == 13) {
        selectedItem += 100;
        call(lgc.ExamineItem);
        call(lgc.RedrawRoom);
      }
    } else {
      selectedItem += 100;
      show.obj.v(selectedItem);
      [ some items get a second, detailed look
      [  post-it note, music sheet, PQList
      if ((selectedItem == 107 || selectedItem == 112 || 
           selectedItem == 113)) {
        call(lgc.ExamineItem);
        call(lgc.RedrawRoom);
      }
      [ some items get additional information, which is a clue to 
      [ look closer
      if (selectedItem == 101) {
        [ magazine
        print("The articles are, of course, outdated, but probably "
              "very interesting as nostalgia items.");
      }
      if (selectedItem == 106) {
        [ memo
        print("It looks like a memo for AMOS users.");
      }
      if (selectedItem == 108) {
        [ BaDASS manual
        print("This would be useful to BaDASS system administrators.");
      }
      if (selectedItem == 109) {
        [ notebook
        print("Notes from AMOS Team meetings - that could be interesting.");
      }
      if (selectedItem == 110) {
        [ folder
        print("The folder is thin- is there anything left in it?");
      }
      if (selectedItem == 111) {
        [ scroll
        print("It was probably used by the original development "
              "team when KQIII was first designed.");
      }
    }
  }
}

[ quit the game
if ((controller(cQuit) || 
     said("quit", "game") || 
     said("quit"))) {
  stop.sound();
  quit(0);
}

[ if player hasn't provided input or if match already found, no need
[ to do rest of said tests (including unknown words)
if (haveInput && !haveMatch) {
  [ read magazine
  if (said("read", "magazine")) {
    if (has("magazine")) {
      :readmag
      call(lgc.ReadMagazine);
      call(lgc.RedrawRoom);
    } else {
      print("You don't have it.");
    }
  }
  [ read memo
  if (said("read", "memo")) {
    if (has("memo")) {
      :readMemo
      call(txt.Memo);
    } else {
      print("You don't have it.");
    }
  }
  [ read manual
  if (said("read", "manual")) {
    if (has("BaDASS manual")) {
      :readManual
      call(lgc.ReadManual);
      call(lgc.RedrawRoom);
    } else {
      print("You don't have it.");
    }
  }
  [ read notebook
  if (said("read", "notebook")) {
    if (has("notebook")) {
      :readNotebook
      call(lgc.ReadNotebook);
      call(lgc.RedrawRoom);
    } else {
      print("You don't have it.");
    }
  }
  [ look inside folder
  if ((said("look", "in", "folder") ||
       said("read", "folder"))) {
    if (has("folder")) {
      selectedItem = 110;
      call(lgc.ExamineItem);
      call(lgc.RedrawRoom);
    } else {
      print("You don't have it.");
    }
  }
  [ read scroll
  if (said("read", "scroll")) {
    if (has("scroll")) {
      :readScroll
      call(lgc.ReadScroll);
      call(lgc.RedrawRoom);
    } else {
      print("You don't have it.");
    }
  }
  
  [ wegup is not a word
  if (said("wegup")) {
    print("That is not a valid word.");
  }
  
  [ if player says any dirty words
  if ((said("bad word", "rol") || 
       said("anyword", "bad word", "rol") || 
       said("anyword", "anyword", "bad word", "rol") || 
       said("anyword", "anyword", "anyword", "bad word", "rol") || 
       said("anyword", "anyword", "anyword", "anyword", "bad word", "rol") || 
       said("anyword", "anyword", "anyword", "anyword", "anyword", "bad word", "rol"))) {
    ++badWordCount;
    if (badWordCount == 1) {
      [ first warning
      print("I am going to tell on you!");
    }
    if (badWordCount == 2) {
      [second warning
      print("Don't say anymore words like that!");
    }
    if (badWordCount == 3) {
      [ no more warnings
      print("Don't say I didn't warn you.");
      quit(1);
    }
  }
  
  if (said("kick", "bucket")) {
    print("OK.");
    [ stop ego, have him kick a bucket
    egoDir = STOPPED;
    work1 = CYCLE_AT_REST;
    set.upper.left(&ego, &work1);  [ auto.cycle(oSOBJ, vMODE)
    if (currentRoom == rm.ParkingLot) {
      [ in parking lot, ego is tiny - so skip 
      [ the kicking animation, just die
      load.view(vw.EgoDead);
      set.view(ego, vw.EgoDead);
      set.cel(ego, 0);
      end.of.loop(ego, bucketDone);
    } else {
      [ start by showing kicking loop
      load.view(vw.KickBucket);
      set.view(ego, vw.KickBucket);
      set.loop(ego, 0);
      set.cel(ego, 0);
      end.of.loop(ego, bucketDone);
      [ add the bucket
      animate.obj(oBucket);
      get.posn(ego, work1, work2);
      work1 += 8;
      position.v(oBucket, work1, work2);
      set.view(oBucket, vw.KickBucket);
      set.loop(oBucket, 2);
      fix.loop(oBucket);
      set.priority(oBucket, 15);
      ignore.blocks(oBucket);
      ignore.objs(oBucket);
      ignore.horizon(oBucket);
      work1 = 3;
      step.size(oBucket, work1);
      draw(oBucket);
    }
    program.control();
    prevent.input();
    set(kickBucket);
    return();
  }
  [ hit/punch/kill/shoot/kick anything
  if ((said("hit", "rol") || said("kick", "rol"))) {
    print("Unprovoked violence is never acceptable.");
  }
  
  [ look anything
  if (said("look", "anyword", "rol")) {
    print("What? Where?");
  }

  [ get anything
  if (said("get", "rol")) {
    print("You can't get that here.");
  }
  
  [ use anything
  if (said("use", "rol")) {
    print("Be more specific. What do you want me to do with it?");
  }

  if (said("move", "anyword", "rol")) {
    print("Leave things be. No need to move anything here.");
  }
  if ((said("push", "anyword", "rol") || said("pull", "anyword", "rol"))) {
    print("There is no need to push or pull anything.");
  }

  if ((said("look", "ego") || said("look", "drew"))) {
    print("Yes, Drew is a very handsome fellow. Easy on the eyes.");
  }
  if (said("look", "pocket")) {
    print("Your enormously oversized pockets have room for all kinds of "
          "amazing things. Right now you have the following items:");
    [ disable selection of inventory items
    reset(enableItemSelect);
    status();
  }
  
  [ climb anything - not allowed (in most rooms)
  if (said("climb", "rol")) {
    print("There is no need to cimb anything here.");
  }
  
  if (said("climb", "up", "anyword", "rol")) {
    print("What are you, a mountain goat?");
  }
  if ((said("climb", "in", "anyword", "rol") || said("get", "in", "anyword", "rol"))) {
    print("What are you, a hermit crab?");
  }
  if (said("what", "anyword", "rol")) {
    print("That's a good question. Maybe you should ask google.");
  }
  if (said("ask", "google", "rol")) {
    print("If only you had a cell phone, that might work.");
  }
  if (said("jump", "rol")) {
    print("There's no reason for jumping of any kind needed here.");
  }
  if (said("drop", "anyword", "rol")) {
    print("Since your pockets are so ridiculously large, and can carry "
          "more than your average pack mule, there's no reason to drop "
          "anything. It's best to keep it all.");
  }
  
  [ UNKNOWN WORD CHECK
  if (unknownWordNum > 0) {
    set(haveMatch);
    [copy word to a string
    if (unknownWordNum == 1) {
      word.to.string(unknownWord, w1);
    }
    if (unknownWordNum == 2) {
      word.to.string(unknownWord, w2);
    }
    if (unknownWordNum == 3) {
      word.to.string(unknownWord, w3);
    }
    if (unknownWordNum == 4) {
      word.to.string(unknownWord, w4);
    }
    if (unknownWordNum == 5) {
      word.to.string(unknownWord, w5);
    }
    if (unknownWordNum == 6) {
      word.to.string(unknownWord, w6);
    }
    if (unknownWordNum == 7) {
      word.to.string(unknownWord, w7);
    }
    if (unknownWordNum == 8) {
      word.to.string(unknownWord, w8);
    }
    if (unknownWordNum == 9) {
      word.to.string(unknownWord, w9);
    }
    if (unknownWordNum == 10) {
      word.to.string(unknownWord, w10);
    }
    [choose a random unknown word response
    random(&m38, &m40, rndNum);
    print.v(rndNum);
  }
  
  [ finally, if there is input that still hasn't been recognized
  if (!haveMatch) {
    print("I don't understand your request.");
    reset(haveInput);
  }
}

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
[ default cursors
#message 1 "0000FF3FFF1FFF0FFF07FF03FF01FF007F003F001F00FF01FF00FF307FF87FF8FFFC"
            "00000040006000700078007C007E007F807F007C006C00460006000300030000"
[ walk
#message 2 "040FFFF1FFE07FC07FC0FFE0FFF1FFE17FC03FC07FE0FFE1FFC1FF80FF087F183FBC"
            "0000000E001D001F000E0004000C0016801B0006000C001C0036006200430000"
[ look
#message 3 "0709FFFFFFFFFFFFFFFEEFF62DB40BD0024001808001018003C00FF03FFCFFFFFFFF"
            "000000000000000000000000C003F00F7C3E3E7C7C3EF00FC003000000000000"
[ talk
#message 4 "050E1FF807E0018000000000000000000000008001E007FC0FFE3FFC7FF8FFF9FFFF"
            "0000E007F81FFE7FFE7FFE7FFE7FFE7FFE1FF803F000C0008001000200000000"
[ use 
#message 5 "0803FFFFFFFF1FFE07F801F000F000F000F00090000000000000018001C003E007F0"
            "000000000000C000D806DA06DA06DA06DA06FA6FFE7FFE7FFC3FFC1FF80F0000"
[ wait
#message 6 "0707018003C003C007E007E00FF01FF83FFC3FFC1FF80FF007E007E003C003C00180"
            "0000F81FF81F5005A00A40058002000180004003E007700E1008081000000000"
[ disk
#message 7 "0707FFFFFFFFFFFF0000000000000000000000000000000001000300FFFFFFFFFFFF"
            "0000000000000000EE77EE770E70FE7FFE7FFE7F1E78DC790000000000000000"
[ tape
#message 8 "0707FFFFFFFFFFFFFFFF01800000000000000000000000000000FFFFFFFFFFFFFFFF"
            "00000000000000000000FE7FF66FEA57F66F1E78EE7700000000000000000000"
#message 9 "Uh oh. You suddenly get the feeling you forgot to do something important back in the closet."

#message 11 gameAboutMsg
#message 12 gameVersionMsg

[ unknown input messages
#message 38 "I don't understand \"%s1\""
#message 39 "\"%s1\" is not in my vocabulary."
#message 40 "What is \"%s1\""
