[********************************************************************
[
[ rm.Title: Opening/Intro
[
[ Show animated AGI Guy Studio logo, then animated Power Pack logo. 
[ Fade out, then fade in Demo title page:
[
[            "Sierra Quest - a Power Pack Demo game"
[
[********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
#define oLogo              o1
#define oDrew              o2
#define oCat               o3

#define sText             s12
#define sChar             s13
#define sColor1           s14
#define sColor2           s15
#define sColor3           s16
#define sColor4           s17
#define sColor5           s18
#define sColor6           s19
#define sColor7           s20
#define sColor8           s21
#define sColor9           s22
#define sColor10          s23

#define tmpSpeed          v220
#define titleSeq          v221
  #define TS_FADEIN_AG      0
  #define TS_SALUTE         1
  #define TS_SALUTE_UP      2
  #define TS_SALUTE_WAIT    3
  #define TS_SALUTE_DOWN    4
  #define TS_PAUSE1         5
  #define TS_FADEOUT_AG     6
  #define TS_FADEIN_WIN     7
  #define TS_PAUSE2         8
  #define TS_WIN_SOUND      9
  #define TS_FADEOUT_WIN   10
  #define TS_WAIT_AGI      11
  #define TS_ANIMATE_AGI   12
  #define TS_MOVE_AGI      13
  #define TS_EXPLODE       14
  #define TS_EXPLODED      15
  #define TS_FADEWHITE     16
  #define TS_PPACK         17
  #define TS_PP_SHIFT      18
  #define TS_FADEOUT_PP    19
  #define TS_FADEIN_SQ     20
  #define TS_START_CREDITS 21
  #define TS_SCROLLCREDS   22
  #define TS_FINISHED      23
#define titleTimer        v222
#define letterIndex       v223
#define delaycounter      v224
#define wordcount         v225
#define musicDelay        v226
#define creditline        v227
  #define CR_PPDESIGN      0
  #define CR_DEMODESIGN    1
  #define CR_BKGD_ART      2
  #define CR_ANIMATION     3
  #define CR_ADDL_ART      4
  #define CR_MUSIC         5
  #define CR_SOUND         6
#define creditstep        v228
#define tmpSeconds        v229

#define fLogoDone         f220
#define themeMusicDone    f221

[***************************************
[ FIRST CYCLE ONLY
[***************************************
if (newRoom) {
  [ start up sequence doesn't need to use scripts since it will 
  [ never be part of a game restore
  set(noScript);
  
  [ since this is the first room in the game, there is no
  [ previous room that faded out; so force a fade.out
  [ (with zero delay) so this room can then be faded back in
  block(FADE.OUT, BYNUM, 0, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
  
  [ disable player control
  program.control();
  
  [ no ego used in this room
  
  [ use a blank cursor so it doesn't get in the way but
  [ clicks still work
  set.key(BYNUM, &m7, mouse); [ set.cursor(FN_MODE, MSGNUM)
  
  [ save speed value, to restore it later
  tmpSpeed = animationInterval;
  animationInterval = 1;
  
  [ logo objects
  load.view(vw.Logos);
  
  [ background
  animate.obj(oLogo);
  set.view(oLogo, vw.Logos);
  set.loop(oLogo, 0);
  set.cel(oLogo, 0);
  ignore.objs(oLogo);
  stop.cycling(oLogo);
  set.priority(oLogo, 4);
  position(oLogo, 67, 97);
  
  [ Drew
  animate.obj(oDrew);
  set.view(oDrew, vw.Logos);
  set.loop(oDrew, 1);
  set.cel(oDrew, 0);
  ignore.objs(oDrew);
  stop.cycling(oDrew);
  set.priority(oDrew, 5);
  position(oDrew, 73, 77);
  work1 = 6;
  cycle.time(oDrew, work1);
  
  [ cat
  animate.obj(oCat);
  set.view(oCat, vw.Logos);
  set.loop(oCat, 2);
  set.cel(oCat, 0);
  ignore.objs(oCat);
  normal.cycle(oCat);
  set.priority(oCat, 5);
  position(oCat, 80, 77);
  work1 = 12;
  cycle.time(oCat, work1);
  
  [ show the picture on screen
  show.pic();
  [ save text colors
  set.text.attribute(PUSH.ATTRIBUTES, 0); [ push.attributes()
  sText = "AGI GUY Studios presents...";
  draw(oLogo);
  draw(oDrew);
  draw(oCat);
  
  [ begin by displaying text and fading in
  [ fade in
  block(FADE.IN, BYNUM, 20, 0); [ fade.in(FN_MODE, DELAY, COLOR)
  
  [ load logo/theme music
  load.sound(m.Fanfare);
  load.sound(s.Salute);
  load.sound(s.SafeDial);
  load.sound(m.Theme);
  load.sound(m.ExplodeAGI);
  load.sound(s.Kaboom);
  
  [ set up mode and instruments for first stage
  set(4channelsound);
  reset(useMIDI);
  set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
  work1 = 0;
  work2 = 128;
  set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
  work1 = 4;
  work2 = 1;
  set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
}

[***************************************
[ NON-INPUT CODE 
[***************************************

[ one-time events
if (titleSeq == TS_SCROLLCREDS) {
  [ credit events are based on which line, its position and state/step
  get.posn(oLogo, work1, work2);
  if (creditstep == 0) {
    [ showing only top line of credit
    if (creditline == CR_PPDESIGN) {
      if (work2 == 142) {
        [ show next line
        creditstep = 1;
        set.cel(oLogo, 1);
        work1 = 0;
        work2 = 22; [ 23; [ adjust by one to smooth out movmement
        reposition(oLogo, work1, work2);
      }
    }
    if (creditline == CR_DEMODESIGN) {
      if (work2 == 142) {
        [ show next line
        creditstep = 1;
        set.cel(oLogo, 1);
        work1 = 0;
        work2 = 21; [ 22; [ adjust by one to smooth out movmement
        reposition(oLogo, work1, work2);
      }
    }
    if (creditline == CR_BKGD_ART) {
      if (work2 == 147) {
        [ show next line
        creditstep = 1;
        set.cel(oLogo, 1);
        work1 = 0;
        work2 = 12; [ 13; [ adjust by one to smooth out movmement
        reposition(oLogo, work1, work2);
      }
    }
    if (creditline == CR_ANIMATION) {
      if (work2 == 144) {
        [ show next line
        creditstep = 1;
        set.cel(oLogo, 1);
        work1 = 0;
        work2 = 13; [ 14; [ adjust by one to smooth out movmement
        reposition(oLogo, work1, work2);
      }
    }
    if (creditline == CR_ADDL_ART) {
      if (work2 == 144) {
        [ show next line
        creditstep = 1;
        set.cel(oLogo, 1);
        work1 = 0;
        work2 = 14; [ 15; [ adjust by one to smooth out movmement
        reposition(oLogo, work1, work2);
      }
    }
    if (creditline == CR_MUSIC) {
      if (work2 == 144) {
        [ show next line
        creditstep = 1;
        set.cel(oLogo, 1);
        work1 = 0;
        work2 = 14; [ 15; [ adjust by one to smooth out movmement
        reposition(oLogo, work1, work2);
      }
    }
    if (creditline == CR_SOUND) {
      if (work2 == 144) {
        [ show next line
        creditstep = 1;
        set.cel(oLogo, 1);
        work1 = 0;
        work2 = 14; [ 15; [ adjust by one to smooth out movmement
        reposition(oLogo, work1, work2);
      }
    }
  }
  if (creditstep == 1) {
    [ showing top two lines of 3-line credit
    [ showing both lines of 2-line credit
    if (creditline == CR_PPDESIGN) {
      if (work2 == 147) {
        [ show next line
        creditstep = 2;
        set.cel(oLogo, 2);
        work1 = 0;
        work2 = 12; [ 13;  [ adjust by one to smooth out movement
        reposition(oLogo, work1, work2);
      }
    }
    if (creditline == CR_DEMODESIGN) {
      if (work2 == 146) {
        [ show next line
        creditstep = 2;
        set.cel(oLogo, 2);
        work1 = 0;
        work2 = 13; [ 14;  [ adjust by one to smooth out movement
        reposition(oLogo, work1, work2);
      }
    }
    if (creditline == CR_BKGD_ART) {
      if (work2 == 30) {
        [ remove top line
        creditstep = 2;
        set.cel(oLogo, 2);
      }
    }
    if (creditline == CR_ANIMATION) {
      if (work2 == 30) {
        [ remove top line
        creditstep = 2;
        set.cel(oLogo, 2);
      }
    }
    if (creditline == CR_ADDL_ART) {
      if (work2 == 30) {
        [ remove top line
        creditstep = 2;
        set.cel(oLogo, 2);
      }
    }
    if (creditline == CR_MUSIC) {
      if (work2 == 30) {
        [ remove top line
        creditstep = 2;
        set.cel(oLogo, 2);
      }
    }
    if (creditline == CR_SOUND) {
      if (work2 == 30) {
        [ remove top line
        creditstep = 2;
        set.cel(oLogo, 2);
      }
    }
  }
  if (creditstep == 2) {
    [ showing all lines of 3-line credit
    [ showing bottom line of 2-line credit
    if (creditline == CR_PPDESIGN) {
      if (work2 == 48) {
        [ remove top line
        creditstep = 3;
        set.cel(oLogo, 3);
      }
    }
    if (creditline == CR_DEMODESIGN) {
      if (work2 == 48) {
        [ remove top line
        creditstep = 3;
        set.cel(oLogo, 3);
      }
    }
    if (creditline == CR_BKGD_ART) {
      if (work2 == 12) {
        [ restart with next credit line, at bottom
        creditline = CR_ANIMATION;
        creditstep = 0;
        set.loop(oLogo, CR_ANIMATION);
        set.cel(oLogo, 0);
        reposition.to(oLogo, 48, 164);
        goto(getkey);
      }
    }
    if (creditline == CR_ANIMATION) {
      if (work2 == 12) {
        [ restart with next credit line, at bottom
        creditline =  CR_ADDL_ART;
        creditstep = 0;
        set.loop(oLogo, CR_ADDL_ART);
        set.cel(oLogo, 0);
        reposition.to(oLogo, 46, 164);
        goto(getkey);
      }
    }
    if (creditline == CR_ADDL_ART) {
      if (work2 == 12) {
        [ restart with next credit line, at bottom
        creditline = CR_MUSIC;
        creditstep = 0;
        set.loop(oLogo, CR_MUSIC);
        set.cel(oLogo, 0);
        reposition.to(oLogo, 48, 162);
        goto(getkey);
      }
    }
    if (creditline == CR_MUSIC) {
      if (work2 == 12) {
        [ restart with next credit line, at bottom
        set.loop(oLogo, CR_SOUND);
        set.cel(oLogo, 0);
        reposition.to(oLogo, 43, 162);
        creditline = CR_SOUND;
        creditstep = 0;
        goto(getkey);
      }
    }
    if (creditline == CR_SOUND) {
      if (work2 == 12) {
        [ all done
        goto(continue);
      }
    }
  }
  if (creditstep == 3) {
    [ showing bottom two lines of 3-credit line
    if (creditline == CR_PPDESIGN) {
      if (work2 == 30) {
        [ show only bottom line
        creditstep = 4;
        set.cel(oLogo, 4);
      }
    }
    if (creditline == CR_DEMODESIGN) {
      if (work2 == 30) {
        [ show only bottom line
        creditstep = 4;
        set.cel(oLogo, 4);
      }
    }
  }
  if (creditstep == 4) {
    [ showing bottom line of 3-credit line
    if (creditline == CR_PPDESIGN) {
      if (work2 == 12) {
        [ restart with next credit line, at bottom
        set.loop(oLogo, CR_DEMODESIGN);
        set.cel(oLogo, 0);
        reposition.to(oLogo, 32, 162);
        creditline = CR_DEMODESIGN;
        creditstep = 0;
        goto(getkey);
      }
    }
    if (creditline == CR_DEMODESIGN) {
      if (work2 == 12) {
        [ restart with next credit line, at bottom
        creditline = CR_BKGD_ART;
        creditstep = 0;
        set.loop(oLogo, CR_BKGD_ART);
        set.cel(oLogo, 0);
        reposition.to(oLogo, 46, 167);
      }
    }
  }      
}

if (titleSeq == TS_FADEIN_AG && letterindex < 27) {
  [ display letters, one at a time and fade them in at same time
  ++delaycounter;
  if (delaycounter == 3) {
    sound(s.SafeDial, soundDone);
    delaycounter = 0;
    work1 = 16;
    work2 = letterindex;
    work2 += 6;
    work3 = 1;
    block(STR.LEFT, &sText, &sChar, &work3); [ str.left(sINPUT, sRESULT, vLEN)
    work3 = &m2;
    display.v(work1, work2, work3);
    ++letterindex;
    work3 = 27;
    work3 -= letterindex;
    if (letterindex == 27) {
      titleTimer = 100;
    } else {
      [ strip off each letter as it gets added
      block(STR.RIGHT, &sText, &sText, &work3); [ str.right(sINPUT, sRESULT, vCOUNT)
    }
  }
}

if (titleSeq == TS_FADEIN_WIN && letterindex < 20) {
  [ display letters, one at a time
  [ and fade them in at same time
  ++delaycounter;
  if (delaycounter == 3) {
    delaycounter = 0;
    work1 = 16;
    work2 = letterindex;
    work2 += 11;
    work3 = 1;
    block(STR.LEFT, &sText, &sChar, &work3); [ str.left(sINPUT, sRESULT, vLEN)
    work3 = &m2;
    display.v(work1, work2, work3);
    ++letterindex;
    work3 = 20;
    work3 -= letterindex;
    if (letterindex == 20) {
      titleTimer = 10;
    } else {
      block(STR.RIGHT, &sText, &sText, &work3); [ str.right(sINPUT, sRESULT, vCOUNT)
    }
  }
}

if (titleSeq == TS_ANIMATE_AGI) {
  ++delaycounter;
  if (delaycounter == 10) {
    delaycounter = 0;
    --letterindex;
    [color1
    if (letterindex < 64) {
      block(BYTE2HEX, &sChar, &letterindex, 0); [ byte2hex(sRESULT, vINPUT)
      block(STR.COPY, &sChar, &sColor1, 0); [ str.copy(sINPUT, sRESULT)
      block(STR.CONCAT, &sColor1, &sChar, 0); [ str.concat(sINPUT, sADD)
      block(STR.CONCAT, &sColor1, &sChar, 0); [ str.concat(sINPUT, sADD)
    }
    [color2
    if (letterindex > 3 && letterindex < 68) {
      work1 = letterindex;
      work1 -= 4;
      block(BYTE2HEX, &sChar, &work1, 0); [ byte2hex(sRESULT, vINPUT)
      block(STR.COPY, &sChar, &sColor2, 0); [ str.copy(sINPUT, sRESULT)
      block(STR.CONCAT, &sColor2, &sChar, 0); [ str.concat(sINPUT, sADD)
      block(STR.CONCAT, &sColor2, &sChar, 0); [ sstr.concat(sINPUT, sADD)
    }
    [color3
    if (letterindex > 7 && letterindex < 72) {
      work1 = letterindex;
      work1 -= 8;
      block(BYTE2HEX, &sChar, &work1, 0); [ byte2hex(sRESULT, vINPUT)
      block(STR.COPY, &sChar, &sColor3, 0); [ str.copy(sINPUT, sRESULT)
      block(STR.CONCAT, &sColor3, &sChar, 0); [ str.concat(sINPUT, sADD)
      block(STR.CONCAT, &sColor3, &sChar, 0); [ str.concat(sINPUT, sADD)
    }
    [color4
    if (letterindex > 11 && letterindex < 76) {
      work1 = letterindex;
      work1 -= 12;
      block(BYTE2HEX, &sChar, &work1, 0); [ byte2hex(sRESULT, vINPUT)
      block(STR.COPY, &sChar, &sColor4, 0); [ str.copy(sINPUT, sRESULT)
      block(STR.CONCAT, &sColor4, &sChar, 0); [ str.concat(sINPUT, sADD)
      block(STR.CONCAT, &sColor4, &sChar, 0); [ str.concat(sINPUT, sADD)
    }
    [color5
    if (letterindex > 15 && letterindex < 80) {
      work1 = letterindex;
      work1 -= 16;
      block(BYTE2HEX, &sChar, &work1, 0); [ byte2hex(sRESULT, vINPUT)
      block(STR.COPY, &sChar, &sColor5, 0); [ str.copy(sINPUT, sRESULT)
      block(STR.CONCAT, &sColor5, &sChar, 0); [ str.concat(sINPUT, sADD)
      block(STR.CONCAT, &sColor5, &sChar, 0); [ str.concat(sINPUT, sADD)
    }
    [color6
    if (letterindex > 19 && letterindex < 84) {
      work1 = letterindex;
      work1 -= 20;
      block(BYTE2HEX, &sChar, &work1, 0); [ byte2hex(sRESULT, vINPUT)
      block(STR.COPY, &sChar, &sColor6, 0); [ str.copy(sINPUT, sRESULT)
      block(STR.CONCAT, &sColor6, &sChar, 0); [ str.concat(sINPUT, sADD)
      block(STR.CONCAT, &sColor6, &sChar, 0); [ str.concat(sINPUT, sADD)
    }
    [color7
    if (letterindex > 23 && letterindex < 88) {
      work1 = letterindex;
      work1 -= 24;
      block(BYTE2HEX, &sChar, &work1, 0); [ byte2hex(sRESULT, vINPUT)
      block(STR.COPY, &sChar, &sColor7, 0); [ str.copy(sINPUT, sRESULT)
      block(STR.CONCAT, &sColor7, &sChar, 0); [ str.concat(sINPUT, sADD)
      block(STR.CONCAT, &sColor7, &sChar, 0); [ str.concat(sINPUT, sADD)
    }
    [color8
    if (letterindex > 27 && letterindex < 92) {
      work1 = letterindex;
      work1 -= 28;
      block(BYTE2HEX, &sChar, &work1, 0); [ byte2hex(sRESULT, vINPUT)
      block(STR.COPY, &sChar, &sColor8, 0); [ str.copy(sINPUT, sRESULT)
      block(STR.CONCAT, &sColor8, &sChar, 0); [ str.concat(sINPUT, sADD)
      block(STR.CONCAT, &sColor8, &sChar, 0); [ str.concat(sINPUT, sADD)
    }
    [color9
    if (letterindex > 31 && letterindex < 96) {
      work1 = letterindex;
      work1 -= 32;
      block(BYTE2HEX, &sChar, &work1, 0); [ byte2hex(sRESULT, vINPUT)
      block(STR.COPY, &sChar, &sColor9, 0); [ str.copy(sINPUT, sRESULT)
      block(STR.CONCAT, &sColor9, &sChar, 0); [ str.concat(sINPUT, sADD)
      block(STR.CONCAT, &sColor9, &sChar, 0); [ str.concat(sINPUT, sADD)
    }
    block(SET.PALETTE, BYNUM, &m3, 0); [ set.palette(FN_MODE, PALETTE, LOADONLY)
    
    if (letterindex == 0) {
      clear.text.rect(11, 16, 11, 23, BLACK);
      clear.text.rect(12, 19, 12, 21, BLACK);
      clear.text.rect(13, 15, 13, 24, BLACK);
      block(SET.PALETTE, BYNUM, &m1, 0); [ set.palette(FN_MODE, PALETTE, LOADONLY)
      set.text.attribute(WHITE, BLACK);
      ++titleSeq;
      delaycounter = 0;
    }
  }
}

if (titleSeq == TS_WAIT_AGI) {
  if (elapsedSeconds == tmpSeconds) {
    ++titleSeq;
    delaycounter = 0;
  }
}

if (titleSeq == TS_MOVE_AGI) {
  ++delaycounter;
  if (delaycounter == 4) {
    delaycounter = 0;
    if (letterindex == 7) {
      titleTimer = 1;
    } else {
      if (letterindex == 0) {
        display(13, 14, " I");
      }
      if (letterindex == 1) {
        display(11, 15, " A");
        display(13, 15, " I");
      }
      if (letterindex == 2) {
        display(11, 16, " A");
        display(13, 16, " I");
      }
      if (letterindex == 3) {
        display(11, 17, " A");
        display(13, 17, " I");
      }
      if (letterindex == 4) {
        display(11, 18, " A");
        display(12, 18, " G");
        display(13, 18, " I");
      }
      if (letterindex == 5) {
        display(11, 18, " A");
        display(12, 18, " G");
        display(13, 18, " I");
      }
      if (letterindex == 6) {
        clear.text.rect(11, 19, 11, 19, BLACK);
        clear.text.rect(13, 19, 13, 19, BLACK);
        display(12, 18, "AGI");
      }
      ++letterindex;
    }
  }
}

if (titleSeq == TS_PP_SHIFT && titleTimer == 0) {
  [delay
  ++delayCounter;
  if (delayCounter == 10) {
    delayCounter = 0;
    ++letterIndex;
    if (letterIndex == 52) {
      [ when fully shifted, begin timer
      titleTimer = 5;
    } else {
      [ advance the color wave
      v240 = v239;
      v239 = v238;
      v238 = v237;
      v237 = v236;
      v236 = v235;
      v235 = v234;
      v234 = v233;
      v233 = v232;
      v232 = v231;
      v231 = v230;
      [ increase up to max, then go back to start
      if (letterIndex < 22) {
        v230 += 3;
      }
      if (letterIndex > 21 && letterIndex < 43) {
        v230 -= 3;
      }
      block(BYTE2HEX, &s13, &v230, 0); [ byte2hex(sRESULT, vINPUT)
      block(BYTE2HEX, &s14, &v231, 0); [ byte2hex(sRESULT, vINPUT)
      block(BYTE2HEX, &s15, &v232, 0); [ byte2hex(sRESULT, vINPUT)
      block(BYTE2HEX, &s16, &v233, 0); [ byte2hex(sRESULT, vINPUT)
      block(BYTE2HEX, &s17, &v234, 0); [ byte2hex(sRESULT, vINPUT)
      block(BYTE2HEX, &s18, &v235, 0); [ byte2hex(sRESULT, vINPUT)
      block(BYTE2HEX, &s19, &v236, 0); [ byte2hex(sRESULT, vINPUT)
      block(BYTE2HEX, &s20, &v237, 0); [ byte2hex(sRESULT, vINPUT)
      block(BYTE2HEX, &s21, &v238, 0); [ byte2hex(sRESULT, vINPUT)
      block(BYTE2HEX, &s22, &v239, 0); [ byte2hex(sRESULT, vINPUT)
      block(BYTE2HEX, &s23, &v240, 0); [ byte2hex(sRESULT, vINPUT)
      [ set palette
      block(SET.PALETTE, BYNUM, &m4, 0); [ set.palette(FN_MODE, PALETTE, LOADONLY)
    }
  }
}

if (themeMusicDone) {
  reset(themeMusicDone);
  if (titleSeq == TS_WIN_SOUND) {
    titleTimer = 10;
  } 
}

[ timer events
if (titleTimer > 0) {
  --titleTimer;
  if (titleTimer == 0) {
    ++titleSeq;
    
    if (titleSeq == TS_SALUTE) {
      end.of.loop(oDrew, fLogoDone);
      [ play salute
      set(4channelsound);
      set(useMIDI);
      set.key(MIDI_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      work1 = 0;
      work2 = 73;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      ++work1;
      work2 = 58;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      ++work1;
      work2 = 115;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(s.Salute, soundDone);
    }
    if (titleSeq == TS_SALUTE_WAIT) {
      reverse.loop(oDrew, fLogoDone);
    }
    if (titleSeq == TS_PAUSE1) {
      titleTimer = 5;
      return();
    }
    if (titleSeq == TS_FADEOUT_AG) {
      [ fadeout
      block(FADE.OUT, BYNUM, 25, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
      [ play WinAGI theme
      set(4channelsound);
      set.key(MIDI_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      work1 = 0;
      work2 = 63;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      ++work1;
      work2 = 50;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      ++work1;
      work2 = 55;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(m.Fanfare, themeMusicDone);
      [ change to WinAGI logo
      erase(oLogo);
      erase(oDrew);
      erase(oCat);
      set.loop(oLogo, 3);
      set.cel(oLogo, 0);
      position(oLogo, 63, 104);
      draw(oLogo);
      stop.update(oLogo);
      clear.lines(16, 16, BLACK);
      [ fade in
      block(FADE.IN, BYNUM, 40, 0); [ fade.in(FN_MODE, DELAY, COLOR)
      sText = "A WinAGI creation...";
      letterindex = 0;
      ++titleSeq;
      goto(getkey);
    }
    if (titleSeq == TS_PAUSE2) {
      ++titleSeq;
      goto(getkey);
    }
    if (titleSeq == TS_FADEOUT_WIN) {
      [ fadeout
      block(FADE.OUT, BYNUM, 25, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
      [ play explode-agi theme
      set(4channelsound);
      set(useMIDI);
      set.key(MIDI_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      work1 = 0;
      work2 = 4; [ electric piano 1
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      ++work1;
      work2 = 49; [ string ensemble 1
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      ++work1;
      work2 = 61; [ french horn
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(m.ExplodeAGI, soundDone);
      erase(oLogo);
      clear.lines(16, 16, BLACK);
      set.text.attribute(WHITE, BLACK);
      display(11, 15, "A");
      display(12, 18, "G");
      display(13, 14, "I");
      [ display other letters in colors that will be
      [ used for the fade effect
      set.text.attribute(1, 0);
      display(13, 15, "n");
      set.text.attribute(2, 0);
      display(11, 16, "d");
      display(13, 16, "t");
      set.text.attribute(3, 0);
      display(11, 17, "v");
      display(13, 17, "e");
      set.text.attribute(4, 0);
      display(11, 18, "e");
      display(13, 18, "r");
      set.text.attribute(5, 0);
      display(11, 19, "n");
      display(12, 19, "a");
      display(13, 19, "p");
      set.text.attribute(6, 0);
      display(11, 20, "t");
      display(11, 21, "u");
      display(12, 20, "m");
      display(12, 21, "e");
      display(13, 20, "r");
      display(13, 21, "e");
      set.text.attribute(7, 0);
      display(11, 22, "r");
      display(13, 22, "t");
      set.text.attribute(8, 0);
      display(11, 23, "e");
      display(13, 23, "e");
      set.text.attribute(9, 0);
      display(13, 24, "r");
      [ set palette
      sColor1 = "3f3f3f";
      sColor2 = "3f3f3f";
      sColor3 = "3f3f3f";
      sColor4 = "3f3f3f";
      sColor5 = "3f3f3f";
      sColor6 = "3f3f3f";
      sColor7 = "3f3f3f";
      sColor8 = "3f3f3f";
      sColor9 = "3f3f3f";
      sColor10 = "3f3f3f";
      block(SET.PALETTE, BYNUM, &m3, 0); [ set.palette(FN_MODE, PALETTE, LOADONLY)
      [ fade in (no delay)
      block(FADE.IN, BYNUM, 0, BLACK); [ fade.in(FN_MODE, DELAY, COLOR)
      [ begin letter fade in process
      letterindex = 97;
      ++titleSeq;
      tmpSeconds = elapsedSeconds;
      tmpSeconds += 20;
      if (tmpSeconds > 59) {
        tmpSeconds -= 60;
      }
      return();
    }
    if (titleSeq == TS_EXPLODE) {
      [ play explosion sound
      set(4channelsound);
      set(useMIDI);
      set.key(MIDI_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      work1 = 0;
      work2 = 127; [ gunshot
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      ++work1;
      work2 = 126; [ applause
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      ++work1;
      work2 = 122; [ seashore
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(s.Kaboom, soundDone);
      work1 = titleSeq;
      work1 += 26;
      :drawpic
        load.pic(work1);
        draw.pic(work1);
        discard.pic(work1);
        show.pic();
        ++work1; 
      if (work1 <= pic.Explode7) {
        goto(drawpic);
      }
      titleSeq = TS_EXPLODED;
      titleTimer = 1;
    }
    if (titleSeq == TS_FADEWHITE) {
      [ fade out white
      block(FADE.OUT, BYNUM, 1, WHITE); [ fade.out(FN_MODE, DELAY, COLOR)
      ++titleSeq;
    }
    if (titleSeq == TS_PPACK) {
    
      [ set starting palette
      block(SET.PALETTE, BYNUM, &m5, 1); [ set.palette(FN_MODE, PALETTE, LOADONLY)
      work1 = pic.PowerPackLogo;
      load.pic(work1);
      draw.pic(work1);
      discard.pic(work1);
      show.pic();
      [ fade in
      block(FADE.IN, BYNUM, 10, WHITE); [ fade.in(FN_MODE, DELAY, COLOR)
      [ setup to do the color shift
      titleSeq = TS_PP_SHIFT;
      letterIndex = 0;
      v230 = 0;
      v231 = 0;
      v232 = 0;
      v233 = 0;
      v234 = 0;
      v235 = 0;
      v236 = 0;
      v237 = 0;
      v238 = 0;
      v239 = 0;
      v240 = 0;
    }
    if (titleSeq == TS_FADEOUT_PP) {
      block(FADE.OUT, BYNUM, 8, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
      [ restore palette
      block(SET.PALETTE, BYNUM, &m6, 1); [ set.palette(FN_MODE, PALETTE, LOADONLY)
      work1 = pic.Title;
      load.pic(work1);
      draw.pic(work1);
      discard.pic(work1);
      show.pic();
      titleTimer = 1;
    }
    if (titleSeq == TS_FADEIN_SQ) {
      [ fade in
      block(FADE.IN, BYNUM, 20, BLACK); [ fade.in(FN_MODE, DELAY, COLOR)
      titleTimer = 45;
      set(4channelsound);
      set.key(MIDI_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      work1 = 0;
      work2 = 25;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      ++work1;
      work2 = 31;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      ++work1;
      work2 = 39;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
     sound(m.Theme, themeMusicDone);
    }
    if (titleSeq == TS_START_CREDITS) {
      [ start cycling credits
      load.view(vw.Credits);
      set.view(oLogo, vw.Credits);
      set.loop(oLogo, 0);
      set.cel(oLogo, 0);
      position(oLogo, 52, 162);
      ignore.horizon(oLogo);
      draw(oLogo);
      work1 = 3;
      step.time(oLogo, work1);
      work1 = UP;
      set.dir(oLogo, work1);
      creditline = CR_PPDESIGN;
      creditstep = 0;
      ++titleSeq;
    }
  }
}

[ cycling events
if (fLogoDone) {
  reset(fLogoDone);
  ++titleSeq;
  if (titleSeq == TS_SALUTE_UP) {
    titleTimer = 30;
  }
  if (titleSeq == TS_SALUTE_DOWN) {
    titleTimer = 30;
  }
}

[***************************************
[ PROCESS PLAYER INPUT
[***************************************
[ no input in this room

[***************************************
[ CHECK FOR AN EXIT CONDITION
[***************************************
:getkey
[ check for a keypress or mouse button to move to start room
if (!initialRun && (have.key() || controller(cLeftClick) || controller(cMenu))) {
  :continue
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
  [ fade out to black
  block(FADE.OUT, BYNUM, FADECOUNT, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
  [ restore palette
  block(SET.PALETTE, BYNUM, &m1, 1); [ set.palette(FN_MODE, PALETTE, LOADONLY)
  [ enable game functions for rest of game
  reset(disableGameFunctions);
  [ restore default speed
  animationInterval = tmpSpeed;
  [ allow scripting
  reset(noScript);
  [ restore colors
  set.text.attribute(0, POP.ATTRIBUTES); [ pop.attributes()
  [ begin game in starting room
  new.room(rm.ParkingLot); [ ##LE001##
}

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "%s2"
#message 2 "%s13"
#message 3 "000000%s14%s15%s16%s17%s18%s19%s20%s21%s22%%003F15153F3F3F15153F153F3F3F153F3F3F"

[           |BLK  | BLU | GRN | CYN | RED | MGT | BRN | LGRY| DGRY| LBLU| LGRN| LCYN| LRED| LMGT| YLW | WHT |
[palettes:  | 0   | 1   | 2   | 3   | 4   | 5   | 6   | 7   | 8   | 9   | 10  | 11  | 12  | 13  | 14  | 15  |
#message 4 "000000%s13%%0000%s14%%0000%s15%%0000%s16%%0000%s17%%0000%s18%%0000%s19%%0000%s20%%0000%s21%%0000%s22%%0000%s23%%00003F15153F153F3F3F153F3F3F"
#message 5 "0000000000000000000000000000000000000000000000000000000000000000000000003F15153F153F3F3F153F3F3F"

[ default palette
#message 6 "00000000002A002A00002A2A2A00002A002A2A15002A2A2A15151515153F003F15153F3F3F15153F153F3F3F153F3F3F"

[ blank mouse cursor
#message 7 "0000FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF"
           "0000000000000000000000000000000000000000000000000000000000000000"
