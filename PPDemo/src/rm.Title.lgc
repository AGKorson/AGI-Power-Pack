[********************************************************************
[
[ rm.Title: Opening/intro
[
[ This is an example of an intro screen, with scrolling credits and
[ a theme song.
[
[********************************************************************

[ !!! need to replace this with a more jazzy title screen that
[ shows off some of the features; 80 col text, custom/changing palette
[ likely to have biggest effects

[ **************************************
[ LOCAL DEFINES
[ **************************************
#define creditLine1                  o1
#define creditLine2                  o2

#define linePtr                    v220
#define xPos                       v221
#define yPos                       v222
#define objDir                     v223
#define speed                      v224
#define tmpVal                     v225
#define musicDelay                 v226

#define LP_DESIGNED                   0
#define LP_PROGRAMMED                 1
#define LP_GRAPHICS                   2
#define LP_BY                         3
#define LP_AND                        4
#define LP_NAME                       5
#define LP_GAP                        6

[ **************************************
[ FIRST CYCLE ONLY
[ **************************************
if (newRoom) {
  [ run initialization code
  call(lgc.RoomInit);
  
  [ load, draw, discard the picture for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ set horizon to just above scroll window
  set.horizon(55);
  
  [ disable player control
  program.control();
  
  [ initialize the credit line objects
  load.view(vw.OpeningCredits);
  animate.obj(creditLine1);
  animate.obj(creditLine2);
  speed = 2; 
  cycle.time(creditLine1, speed);
  cycle.time(creditLine2, speed);
  
  [ ignore other objects; otherwise
  [ reposition.to.v command will 'shuffle'
  [ the objects to avoid collisions
  ignore.objs(creditLine1);
  ignore.objs(creditLine2);
  
  [ first object starts with 'designed'
  set.view(creditLine1, vw.OpeningCredits);
  set.loop(creditLine1, LP_DESIGNED);
  
  [ second object starts with 'by'
  set.view(creditLine2, vw.OpeningCredits);
  set.loop(creditLine2, LP_BY);
  
  [ position first line just below scroll window
  position(creditLine1, 56, 127);
  [ position second line below first by half of scroll window height
  position(creditLine2, 72, 159);
  
  [ start moving the credit lines up through the scroll window
  objDir = UP;
  set.dir(creditLine1, objDir);
  draw(creditLine1);
  set.dir(creditLine2, objDir);
  draw(creditLine2);
  
  [ ensure pointer is reset
  linePtr = 0;
  
  [ no ego used in this room
  
  [ since this is the first room in the game, there is no
  [ previous room that faded out; so we force a fade.out
  [ (with zero delay) so this room can then be faded back in
  block(FADE.OUT, 0, 0, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  [ show the picture on screen
  show.pic(); 
  [ now fade in to display the screen
  block(FADE.IN, 0, 25, 0); [ fade.in(BYVAR, DELAY, COLOR)
  
  [ display game title above scroll window
  [ you can use a view for this instead of text, if desired
  display(3, 10, "Intro/Opening screen");
  
  [ display game version below scroll window
  display(21, 9, gameVersionMsg);
  
  [ load the theme song
  load.sound(m.theme);
  [ use MIDI, and set desired instruments
  set(4channelsound);
  set.key(1, 0, SET.SNDMODE); [ set.sndmode(MODE);
  set(useMIDI);
  work1 = 0;
  work2 = 56;
  set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT);
  ++work1;
  work2 = 48;
  set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT);
  ++work1;
  work2 = 114;
  set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT);
  [ and begin playing
  sound(m.theme, musicDone);
}

[ check for a keypress or joystick button
if ((have.key() || controller(cLeftClick))) {
  [ allow menu use
  set(enableMenu);
  [ game functions are no longer disabled
  reset(disableGameFunctions);

  [ fade out to black
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  [ begin game in starting room
  new.room(rm.ParkingLot); [ ##LE001##
[ new.room(rm.Terminal);
}

[ when first creditLine object reaches top
if (objHitEdge == 1) {
  [ increment line counter
  ++linePtr;

  [ counter will always be ODD
  [ after first creditLine object reaches top
  if (linePtr == 1) {
    [ 'name'
    set.loop(creditLine1, LP_NAME);
    xPos = 66;
  }
  if (linePtr == 3) {
    [ 'programmed'
    set.loop(creditLine1, LP_PROGRAMMED);
    xPos = 47;
  }
  if (linePtr == 5) {
    [ 'name'
    set.loop(creditLine1, LP_NAME);
    xPos = 66;
  }
  if (linePtr == 7) {
    [ start over with 'designed'
    set.loop(creditLine1, LP_DESIGNED);
    xPos = 56;
  }
  [ move creditLine back to bottom
  yPos = 127;
  reposition.to.v(creditLine1, xPos, yPos);

  [ syncronize cel with other object
  current.cel(creditLine2, tmpVal);
  set.cel.v(creditLine1, tmpVal);
}

[ when second creditLine object reaches top
if (objHitEdge == 2) {
  [ increment line counter
  ++linePtr;
  
  [ counter will always be EVEN after 
  [ second creditLine object reaches top
  if (linePtr == 2) {
    [ 'gap'
    set.loop(creditLine2, LP_GAP);
    xPos = 70;
  }
  if (linePtr == 4) {
    [ 'by'
    set.loop(creditLine2, LP_BY);
    xPos = 72;
  }
  if (linePtr == 6) {
    [ 'gap'
    set.loop(creditLine2, LP_GAP);
    xPos = 70;
  }
  if (linePtr == 8) {
    [ start over with 'by'
    set.loop(creditLine2, LP_BY);
    xPos = 72;
    [ reset pointer
    linePtr = 0;
  }
  [ move credit line back to bottom
  yPos = 127;
  reposition.to.v(creditLine2, xPos, yPos);
  [ syncronize cel with other object
  current.cel(creditLine1, tmpVal);
  set.cel.v(creditLine2, tmpVal);
}
  
[ restart theme music if the intro screen is still showing
[ and end was reached
if (musicDone) {
  [ reset the flag
  reset(musicDone);
  
  [ start a timer to set a short delay before restarting music
  musicDelay = 1;
}
  
[ if music interlude is active
if (musicDelay  > 0) {
  [ increment interlude delay counter
  musicDelay += 1;
  
  [ after 36 cycles
  if (musicDelay == 36) {
    [ reset timer
    musicDelay = 0;
    
    [ play it again sam
    sound(m.theme, musicDone);
  }
}

return();

[ **************************************
[ MESSAGES
[ **************************************
#message 1 "Intro/Opening screen"
#message 2 gameVersionMsg