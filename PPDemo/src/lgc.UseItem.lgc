[*********************************************************************
[ 
[ lgc.UseItem
[
[ Contains functions to interact with items in player's inventory when
[ the player clicks on an item in inventory in the 'use-item' mode.
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
[ none

[***************************************
[ MAIN CODE BLOCK 
[***************************************

[ check for mouse clicks
if (controller(cLeftClick)) {
  [ floppy disk
  if (selectedItem == &floppydisk) {
    [ only valid in control room (which is checked before this
    [ logic is called)
    if (mouseX > 82 && mouseX < 104 && mouseY > 82 && mouseY < 105) {
      if (obj.in.box(ego, 64, 133, 104, 138)) {
        print("You insert the disk and flip the latch to secure it.");
        set(floppyInserted);
        work1 = cp.Terminal;
        put(floppydisk, work1);
        [ first time awards bonus points
        if (scoreFloppy) {
          [ temporarily lose four points for dropping the floppy
          work1 = -4;
        } else {
          [ only drop two points (for a net of +2 when it's retrieved)
          work1 = -2;
          set(scoreFloppy);
        }
        call(lgc.ScoreHandler);
      } else {
        print("Move closer.");
        return();
      }
    } else {
      print("The disk can't be used there.");
      return();
    }
  }
  accept.input();
  status.line.on();
  cursormode = C_USE;
}

if (controller(cRightClick)) {
  [ cancel - do nothing
  accept.input();
  status.line.on();
  cursormode = C_USE;
}
  
return();

[***************************************
[ DECLARED MESSAGES
[***************************************
