[ ********************************************************************
[
[ lgc.Init: Initialization
[
[
[ This logic should only be called once, when the game first starts
[ see the 'Keyboard Controller' topic in the Help file for details on
[ key assignments.
[
[ ********************************************************************

#define tmp0    v220
#define tmp1    v221
#define tmp2    v222
#define tmp3    v223
#define tmp4    v224
#define tmp5    v225
#define vRnd    v232
#define vStyle  v233

if (gameRestarted) {
  [ for a restart, there are a few things that need to be reset
  disable.item(cDummy);
  set(enableMenu);
  reset(disableGameFunctions);
  [ when restarting, s23 is set to hold sound settings
  block(STR2BYTE, &s23, &tmp0, 0); [ str2byte(sINPUT, vRESULT)
  if (tmp0 > 99) {
    set(soundOn);
    tmp0 -= 100;
    enable.item(cSoundPC);
    enable.item(cSoundFM);
    enable.item(cSoundMIDI);
  } else {
    reset(soundOn);
    disable.item(cSoundPC);
    disable.item(cSoundFM);
    disable.item(cSoundMIDI);
  }
  if (tmp0 > 9) {
    set(4channelsound);
    set.key(0, 0, SET.SNDMODE); [ set.sndmode(MODE)
    tmp0 -= 10;
  }
  if (tmp0) {
    set(useMIDI);
    set.key(1, 0, SET.SNDMODE); [ set.sndmode(MODE)
  }
  [ s22 holds clock 
  block(STR2BYTE, &s22, &tmp0, 0); [ str2byte(sINPUT, vRESULT)
  if (tmp0 > 9) {
    [ enable clock
    set(clockOn);
    tmp0 -= 10;
    enable.item(cRealClock);
    enable.item(cGameClock);
  } else {
    disable.item(cRealClock);
    disable.item(cGameClock);
  }
  if (tmp0) {
    [ enable real time
    set(realClock);
  }
  if (clockOn) {
    if (realClock) {
      [ status bar checks realtime minutes
      set.cursor.char(" l29, v3, v7, v78, f9");
    } else {
      [ status bar checks gametime seconds
      set.cursor.char(" l29, v3, v7, v11, f9");
    }
  } else {
    [ status bar doesn't check for time
    set.cursor.char(" l29, v3, v7, f9");
  }

  [ s21 holds speed
  block(STR2BYTE, &s21, &animationInterval, 0); [ str2byte(sINPUT, vRESULT)
  
  [ menus don't change during restart, so don't need to update them
} else {
  [ some things only happen during initial start - skip if restarting
  
  trace.info(lgc.traceinfo, 3, 10);  [ sets up the command tracer; you should
                                     [ delete this before releasing your game
                                     
  set.key(0, 59, cHelp);             [ F1 - Help
  set.key(0, 60, cToggleSound);      [ F2 - Sound on/off
  set.key(0, 61, cEchoLine);         [ F3 - Echo line
  set.key(0, 62, cChooseItem);          [ F4 - See object
  set.key(0, 63, cSave);             [ F5 - Save game
  set.key(0, 64, cClock);            [ F6 - Clock on/off
  set.key(0, 65, cRestore);          [ F7 - Restore game
  set.key(0, 67, cRestart);          [ F9 - Restart game
  set.key(0, 44, cQuit);             [ ALT-Z - quit game
  set.key(3, 0, cCancelLine);        [ CTRL-C - Clear input line
  set.key(5, 0, cEchoLine);          [ CTRL-E - Echo line
  set.key(9, 0, cStatus);            [ CTRL-I/TAB - inventory
  set.key(19, 0, cToggleSound);      [ CTRL-S - Sound on/off
  set.key(45, 0, cDecrescendo);      [ - - decrease volume
  set.key(43, 0, cCrescendo);        [ + - increase volume
  set.key(27, 0, cMenu);             [ ESC - activate menu
  
  set.key(1, 1, cLeftClick);         [ mouse left button click
  set.key(1, 2, cRightClick);        [ mouse right button click
  set.key(1, 3, cLeftDblClick);      [ mouse left button dbl-click
  set.key(1, 4, cRightDblClick);     [ mouse right button dbl-click
  
  set.game.id("PPD"); 
                         
  [ set up screen configuration (agi's default of 0, 23, 21 is not desired)
  configure.screen(1, 22, 0);
  
  [ assign input prompt and cursor character
  set.string(inputPrompt, ">");
  set.cursor.char(cursor);
  
  [ check for configuration file
  s23 = "ppdemo.cfg";
  tmp0 = 0;
  tmp1 = &s22;
  block(READ.STRING, &s23, &tmp0, &tmp1); [ read.string(sFILENAME, vINDEX, vSTRIN)
  [ check v18 to see if file was found
  if (errorParameter == 0) {
    [ file found - extract settings from the string
    block(STR2BYTE, &s22, &tmp0, 0); [ str2byte(sINPUT, vRESULT)
    [ options are 3-digit number: ones = useMIDI
    [                             tens = useFMSynth
    [                             hundreds = soundOn
    if (tmp0 > 99) {
      set(soundOn);
      tmp0 -= 100;
    } else {
      reset(soundOn);
    }
    if (tmp0 > 9) {
      set(4channelsound);
      set.key(0, 0, SET.SNDMODE); [ set.sndmode(MODE)
      tmp0 -= 10;
    }
    if (tmp0) {
      set(useMIDI);
      set.key(1, 0, SET.SNDMODE); [ set.sndmode(MODE)
    }
  } else {
    [ if no file found, reset v18
    errorParameter = 0;
    
    [ use MIDI, and set desired instruments
    set(4channelsound);
    set.key(1, 0, SET.SNDMODE); [ set.sndmode(MODE);
    set(useMIDI);
    work1 = 0;
    work2 = 56;
    set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT);
    ++work1;
    work2 = 48;
    set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT);
    ++work1;
    work2 = 114;
    set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT);
  
  [ TODO: save custom FM instruments to config file; save speed/clock settings to file
    [ save a default config file
    s23 = "ppdemo.cfg";
    s22 = "111";
    v220 = 22;
    block(NEW.FILE, &s23, &v220, &v220); [ new.file(sFILENAME, vSTART, vEND)
  }
  
  [ set speed to normal (delay=2/20ths of a second)
  animationInterval = NORMAL_SPEED;
  
  [ enable custom status bar (logic 13 [lgc.Statusbar])
  [ check for score (v3), max score (v7) and sound status (f9)
  set.cursor.char(" l29,  v3, v7, f9");

}

[ enable debugging and tracing by default (remove when ready to release game)
set(debugging);
set(enableTrace);

[ set mouse parameters
set(enableDblClick);
dblClickDelay = 3;

[ default cursor
cursormode = C_DEFAULT;
cursoricon = C_DEFAULT;

[ save default msgbox colors
msgBoxBG = WHITE;
msgBoxFG = BLACK;
msgBoxBorder = RED;

[ default instruments for 4-channel sound
FMInst0 = 128;
FMInst1 = 128;
FMInst2 = 128;
FMInst3 = 129;
FMInst4 = 130;


[ default ego parameters
egoStepSize = 1;
egoStepTime = 1;
egoCycleTime = 1;

[set the max score value for the game
maxScore = MAX_SCORE_VALUE;

[ set palette to AGI default
palette = m3;

[ select ego and parked car models

[ randomize the car body styles
work1 = &tmp0;
work2 = 0;
[ populate style variables with numbers 0 through 11
:loop1
*work1 = work2;
++work1;
++work2;
if (work1 < 232) {
  goto(loop1);
}
[ now 'scramble' them a few times to mix them up
work1 = 0;
:loop2
  [ start with first loop variable
  vStyle = &tmp0;
  :loop3
    [ choose a random loop
    random(0, 11, vRnd);
    [ swap the style value for current variable with
    [ the variable of the random style
    vRnd += 220;
    work2 = *vStyle;
    work3 = *vRnd;
    *vStyle = work3;
    *vRnd = work2;
    [ move to next style variable
    ++vStyle;
  if (vStyle < 232) {
    goto(loop3);
  }
  ++work1;
[ run the scramble code three times
if (work1 < 3) {
  goto(loop2);
}

[ store first six styles for ego car and the five parked cars
car0car1 = tmp0;
car0car1 *= 16;
car0car1 += tmp1;
car2car3 = tmp2;
car2car3 *= 16;
car2car3 += tmp3;
car4car5 = tmp4;
car4car5 *= 16;
car4car5 += tmp5;

[ initial patient locations
patient1Loc = LOC_LOBBY;
patient2Loc = LOC_GONE;
patient3Loc = LOC_GONE;
patient4Loc = LOC_GONE;
set(firstLobbyVisit);

return();

[ **************************************
[ MESSAGES
[ **************************************
#message 1 ">"
#message 2 cursor
#message 3 "00000000002A002A00002A2A2A00002A002A2A15002A2A2A15151515153F153F15153F3F3F15153F153F3F3F153F3F3F"