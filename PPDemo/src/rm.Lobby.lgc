[ ********************************************************************
[
[ rm.Lobby
[
[ receptionist is always here. start with patients 1 and 2
[ patient 3 arrives later, then patient 1 leaves. continue cycling
[ with 1-leave, 1-arrive, 2-leave, 2-arrive, etc. 
[
[ ********************************************************************

#define backpane   o1
#define frontpane  o2
#define oReceptionist o3
#define oPatient1     o4
#define oPatient2     o5
#define oPatient3     o6

#define frontdooropen  f220
#define fDone          f221
#define fAllowPast     f222

#define receptionistSeq v220
#define    IDLE          0
#define    MOVECENTER    1
#define    MOVEPHONE     2
#define    SITDOWN       3
#define    STANDUP       4
#define    MOVECOMP      5
#define    MOVEDESK      6
#define    MOVEHALL1     7
#define    MOVEHALL2     8

#define receptionistAction v221
#define    PHONE         1
#define    COMPUTER      2
#define    DESK          3
#define    HALL          4
#define    SITSPEAK      5
#define    STANDSPEAK    6
#define receptionistTimer  v222

#define patient1Seq    v223
#define patient1Action v224
#define patient1Timer  v225

#define patient2Seq    v226
#define patient2Action v227
#define patient2Timer  v228

#define patient3Seq    v226
#define patient3Action v227
#define patient3Timer  v228

if (newRoom) {

  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ do basic room initialization
  call(lgc.RoomInit);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(94);
  
[block(FADE.IN, 0, FADECOUNT, BLACK); [ fade.in(BYVAR, DELAY, COLOR)
[set(f10);trace.on();
  [ add additional room initialization here
  load.view(vw.LobbyArt);
  
  [ sliding glass door (two parts)
  animate.obj(backpane);
  set.view(backpane, vw.LobbyArt);
  set.loop(backpane, 0);
  ignore.blocks(backpane);
  ignore.objs(backpane);
  position(backpane, 113, 162);
  set.priority(backpane, 4);
  animate.obj(frontpane);
  set.view(frontpane, vw.LobbyArt);
  set.loop(frontpane, 1);
  ignore.blocks(frontpane);
  ignore.objs(frontpane);
  position(frontpane, 113, 162);
  set.priority(frontpane, 15);
  
  [ receptionist
  load.view(vw.Receptionist);
  [ loop 0-3 movement
  [ loop 4: sit at desk, pickup/putdown phone
  [ loop 5: sit at desk, talking on phone
  [ loop 6: sit at desk, speaking, looking right
  [ loop 7: sit at desk, speaking, looking down-right
  [ loop 8: sit at desk, speaking, looking down
  [ loop 9: sit at desk, speaking, looking down-left
  [ loop 10: sit at desk, speaking, looking left
  [ loop 11: sitdown/getup at desk
  [ loop 12: stand at desk, turn to left
  [ loop 13: stand at desk, speaking, looking right
  [ loop 14: stand at desk, speaking, looking down
  [ loop 15: stand at desk, speaking, looking left
  
  animate.obj(oReceptionist);
  set.view(oReceptionist, vw.Receptionist);
  set.loop(oReceptionist, 2);
  position(oReceptionist, 59, 112);
  [ 59, 96 for headshot views
  stop.cycling(oReceptionist);
  draw(oReceptionist);
  
  load.view(vw.Patient1); [ sits against back wall
  [ loop 0-3: movement
  [ loop 4: standing, looking right, speaking
  [ loop 5: sit down/stand up
  [ loop 6: sitting, get out cell phone
  [ loop 7: sitting, use cell phone
  
  load.view(vw.Patient2); [ sits against side wall
  [ loop 0-3: movement
  [ loop 4: standing, looking right, speaking
  [ loop 5: sit down/stand up
  [ loop 6: sitting, get out cell phone
  [ loop 7: sitting, use cell phone A
  [ loop 8: sitting, use cell phone B
  [ loop 9: standing, looking right, speaking, hands on hips
  [ loop 10: sitting, tapping toe
  
  [ patient 1
  [ TODO: need more patient views, and then randomize them when drawing lobby
  animate.obj(oPatient1);
  set.view(oPatient1, vw.Patient1);
  set.loop(oPatient1, 5);
  position(oPatient1, 102, 98);
  stop.cycling(oPatient1);
  ignore.blocks(oPatient1);
  draw(oPatient1);
  
  [ patient 2
  animate.obj(oPatient2);
  set.view(oPatient2, vw.Patient2);
  set.loop(oPatient2, 5);
  position(oPatient2, 139, 132);
  stop.cycling(oPatient2);
  ignore.blocks(oPatient2);
  draw(oPatient2);
  
  [ patient 3
  animate.obj(oPatient3);
  ignore.blocks(oPatient3);
  [ don't draw yet
  
  [ add toolbar buttons
  load.view(vw.Toolbar);
  call(lgc.InitToolbar);
  
  [ ego
  if (previousRoom == rm.ParkingLot) {
    position(ego, 115, 163);
    set.loop(ego, 3);
    egodir = STOPPED;
    set(frontdooropen);
    set.cel(backpane, 12);
    set.cel(frontpane, 12);
    ignore.objs(backpane);
  } else {
    position(ego, 87, 96);
    reset(frontdooropen);
    set.cel(backpane, 0);
    set.cel(frontpane, 0);
    observe.objs(backpane);
  }
  egoDir = STOPPED;
  
  draw(backpane);
  stop.update(backpane);
  draw(frontpane);
  stop.update(frontpane);
  
  [ add ego to the room
  draw(ego);
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, FADECOUNT, BLACK); [ fade.in(BYVAR, DELAY, COLOR)
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  return();
}

[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ place said tests here
  if (said("look")) {
    print("This is the lobby. There is a desk and some chairs in the waiting area.");
  }
  if (said("use", "bathroom")) {
    print.at("Sure! It's through that doorway. Go left and it's the second door on the right.", 6, 6, 35);
    set(fAllowPast);
  }
}

[ if NOT clicking on the actual toolbar, check for
[ clicks on ingame items
if (mouseX >= 0 && mouseX < 155 && mouseY > 30 && mouseY < 164 && 
    controller(cLeftClick)) {
  if (cursormode == C_LOOK) {
    print("This is the lobby. There is a desk and some chairs in the waiting area.");
    goto(doneclick);
  }
  
  if (cursormode == C_TALK) {
    print("There is no one here to talk to.");
    goto(doneclick);
  }
  
  if (cursormode == C_USE) {
    print("There is nothing to manipulate here.");
    goto(doneclick);
  }
  
  :doneclick
  set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
}



[*****
:noInput           [  all non-input related code goes here 
[*****

[ add other room specific statements here
if (posn(ego, 105, 156, 127, 167)) {
  if (!frontdooropen) {
    [ open it
    set(frontdooropen);
    start.update(backpane);
    end.of.loop(backpane, fDone);
    start.update(frontpane);
    end.of.loop(frontpane, fDone); [ ok to re-use fDone here!
  }
} else {
  if (frontdooropen) {
    [ close it
    reset(frontdooropen);
    start.update(backpane);
    reverse.loop(backpane, fDone);
    start.update(frontpane);
    reverse.loop(frontpane, fDone);
  }
}

if (fDone) {
  reset(fDone);
  stop.update(backpane);
  stop.update(frontpane);
  if (frontdooropen) {
    ignore.objs(backpane);
  } else {
    observe.objs(backpane);
  }
}  
    
[*****
:exit              [  test for leaving the room
[*****

[ add exit checks here (or use layout editor)

if (edgeEgoHit == TOP_EDGE) {
  [ only if allowed past
  if (fAllowPast) {
    [ show wait cursor
    cursoricon = C_WAIT;
    set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
    [ fade out, then change room
    block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
    new.room(rm.Hallway);  [ ##LE001##
  } else {
    print.at("Excuse me sir, only patients are allowed past the lobby.", 6, 6, 35);
    set.loop(ego, 2);
    work1 = 0;
    work2 = 1;
    reposition(ego, work1, work2);
    egoDir = STOPPED;
    [ cancel clicktomove action
    set.dir(ego, egoDir);
  }
}

[if (posn(ego, 113, 167, 125, 167)) {
if (edgeEgoHit == BOTTOM_EDGE) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  new.room(rm.ParkingLot);  [ ##LE002##
}
return();

[*****
[ messages         [  declared messages go here
[*****
#message 1 "%g40"
#message 2 "%g41"
#message 3 "%g42"
#message 4 "%g43"
#message 5 "%g44"
#message 6 "%g45"
#message 7 "This is the lobby. There is a desk and some chairs in the waiting area."