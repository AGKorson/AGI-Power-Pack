[ ********************************************************************
[
[ lgc.Death: Death handler
[
[ This logic is run when the player dies.
[ The death message displayed depends on the value of deathType.
[
[ 0: not dead
[ 1: run over by car
[ 2: trapped in basement
[ 3: stuck in coffin
[ 4: arrested for harassing nurse
[ 5: stunned by batmobile theft deterrent system
[ 6: slipped on wet spot in lobby
[ 7: car stolen

[ 99: player drove off
[
[ 254: death sequence complete
[ 255: winning sequence complete
[
[ ********************************************************************

[ if message announcing death hasn't been displayed yet
if (deathType < 254) { 
  [ re-allow input
  accept.input();
  [ clear the input line
  cancel.line();
  [ no more player control
  program.control();
  
  [ change save button to restart
  set(noScript);
  load.view(vw.ButtonsUp);
  add.to.pic(vw.ButtonsUp, 0, 8, 48, 16, 15, 4);
  discard.view(vw.ButtonsUp);
  reset(noScript);
[  [ clear status lines
[  clear.lines(23, 24, 0);
  
  [ start a timer to provide a delay after announcement
  [ before player is shown second message
  deathTimer = 25;

  [ WINNER!
  if (deathType == 99) {
    [ not actually dead- game is over because ego left
    print("Drew drives away, his visit to Sierra On-Line is over.");
    [ set deathType to 'win'
    deathType = 255;
  } else {
    [ hit by a car
    if (deathType == 1) {
      print("Stepping in front of a moving vehicle turned out to be a bad idea.");
      [ patients continue on their way
      #define patientCar   o3
      start.update(patientCar);
    }
  
    [ trapped in the secret room
    if (deathType == 2) {
      print("In secret rooms, no one can hear you scream. Looks like you'll be here awhile.");
    }
    
    [ stuck in the coffin
    if (deathType == 3) {
      print("Look on the bright side - at least you'll save money on your funeral costs.");
    }
    
    [ escorted from the premises 
    if (deathType == 4) {
      print("Looks like your trip to Oakhurst will include a tour of the city jail!");
    }
    
    [ stunned by bat-mobile security alarm
    if (deathType == 5) {
      print("Lesson learned - don't mess with the Dark Knight's ride.");
    }
    
    [ slip on wet floor
    if (deathType == 6) {
      print("Looks like you broke your leg! At least you did it in a medical facility!");
    }
    
    [ car stolen (left unlocked)
    if (deathType == 7) {
      print("Your car has been stolen! You should have locked your door. Now you'll need to thumb a ride back home. ");
    }
    
    [ set deathType to 254 so this logic does not
    [ display the death message again
    deathType = 254;
  }
}

[ decrement timer
if (deathTimer > 0) {
  deathTimer--;
}
  
[ after a delay, show ending message to player
if (deathTimer == 1) {
  if (deathType == 255) {
    [ check for module resets
    work1 = 0;
    if (enableCAISOCC) {
      ++work1;
    }
    if (enableRANDOCC) {
      ++work1;
    }
    if (enableICAPOCC) {
      ++work1;
    }
    if (enableESADOCC) {
      ++work1;
    }
    if (enableOFROCC) {
      ++work1;
    }
    if (enableERIDOCC) {
      ++work1;
    }
    [ if player has floppy with source code, it's a win
    if (has("floppy disk") && (floppyfiles == 2 || floppyfiles == 3)) {
      print("Your vist to Oakhurst was a big success! You managed "
            "to find a copy of the source files for the AGI Power "
            "Pack.");
      print("You also found several other really cool souvenirs from "
            "the glory days of AGI that you'll treasure forever.");
      if (currentScore == maxScore) {
        print("CONGRATULATIONS! You managed to get a perfect score!");
      } else {
        if (work1 < 6) {
          print("Although you enabled %v46 of the six power pack control "
                "panels, you'll never know how the ones you missed were "
                "used by Sierra in developing the Power Pack.");
        } else {
          print("Perhaps you can visit again and try to score more points.");
        }
      }
    } else {
      [ losing msg depends on score
      if (currentScore > 100) {
        print("You leave Oakhurst with a handful of souvenirs, but you "
              "can't help but feel like you left the best things behind.");
      } else {
        if (currentScore > 50) {
          print("Even though you found a few souvenirs, there's a lot "
                "left in the old Sierra building that you didn't see.");
        } else {
          print("Your trip to Oakhurst was very disappointing. You didn't "
                "find much in the way of memorabilia. Perhaps you should "
                "return for a longer visit another time.");
        }
      }
    }
  } else {
    print("Thank you for playing this game. Better luck next time!");
  }
}

[ limited toolbar functionality
call(lgc.ToolbarFunctions);

[ looking at help screen is allowed
if ((controller(cHelp) || said("help"))) {
  call(lgc.GameHelp);
}

[ restoring game is allowed
if ((controller(cRestore) || said("restore", "game") || said("restore"))) {
  restore.game();
}

[ restarting game is allowed
if ((controller(cRestart) || said("restart", "game") || said("restart"))) {
  call(lgc.CacheOptions);
  restart.game();
}

[ looking at inventory is allowed
if ((controller(cStatus) || said("inventory"))) {
  status();
}

[ quitting is allowed
if ((controller(cQuit) || said("quit"))) {
  quit(0);
}

[ showing 'about' message is allowed
if (controller(cAbout)) {
  print(gameAboutMsg);
}

[ toggling sound is allowed
if (controller(cToggleSound)) {
  toggle(soundOn);
}

[ run current room logic (animations only)
call.v(currentRoom);

[ if player tries to input something
if (haveInput && !haveMatch) {
  [ it's not allowed
  print("Game is over for you! You can only restore, restart, "
        "exit the game or view your inventory.");
}

return();

[ **************************************
[ DECLARED MESSAGES
[ **************************************
#message 1 "You are now dead."
#message 2 "Thank you for playing this game. Better luck next time!"
#message 3 "You are dead! You can only restore, restart, quit the game or view your inventory."
#message 4 "You are dead by a different method."
#message 5 gameAboutMsg