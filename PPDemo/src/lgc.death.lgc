[ ********************************************************************
[
[ lgc.Death: Death handler
[
[ This logic is run when the player dies.
[ The death message displayed depends on the value of deathType.
[
[ ********************************************************************

[ if message announcing death hasn't been displayed yet
if (deathType != 255) { 
  [ re-allow input
  accept.input();
  [ clear the input line
  cancel.line();
  [ no more player control
  program.control();
  
  [ change save button to restart
  set.cel(tbSave, 8);
  
[  [ clear status lines
[  clear.lines(23, 24, 0);
  
  [ start a timer to provide a delay after announcement
  [ before player is shown second message
  deathTimer = 25;

  [ hit by a car
  if (deathType == 1) {
    print("Stepping in front of a moving vehicle turned out to be a bad idea.");
  }

  [ trapped in the secret room
  if (deathType == 2) {
    print("In secret rooms, no one can hear you scream. Looks like you'll be here awhile.");
  }
  [ stuck in the coffin
  if (deathType == 3) {
    print("Well, at least you'll save a bit on your funeral costs.");
  }
  [ escorted from the premises 
  if (deathType == 4) {
    print("Looks like your trip to Oakhurst will include a tour of the city jail!");
  }
  [ stunned by bat-mobile security alarm
  if (deathType == 5) {
    print("Lesson learned - don't mess with the Dark Knight's ride.");
  }
  
  [ WINNER!
  if (deathType == 99) {
    [ not actually dead- game is over because ego left
    print("Drew drives away, his visit to Sierra On-Line is over.");
    
    [ animate end game, then roll credits
  }
  
  [ set deathType to 255 so this logic does not
  [ display the death message again
  deathType = 255;
}

[ decrement timer
if (deathTimer > 0) {
  deathTimer--;
}
  
[ after a delay, show ending message to player
if (deathTimer == 1) {

[ TODO: need to add a flag and messages for winning vs. losing
  print("Thank you for playing this game. Better luck next time!");
}

[ menu access is allowed, to restart or restore
if (controller(cMenu)) {
  menu.input();
}
[ limited toolbar functionality
call(lgc.ToolbarFunctions);

[ looking at help screen is allowed
if ((controller(cHelp) || said("help"))) {
  call(lgc.GameHelp);
}

[ restoring game is allowed
if ((controller(cRestore) || said("restore", "game") || said("restore"))) {
  restore.game();
}

[ restarting game is allowed
if ((controller(cRestart) || said("restart", "game") || said("restart"))) {
  restart.game();
}

[ looking at inventory is allowed
if ((controller(cStatus) || said("inventory"))) {
  status();
}

[ quitting is allowed
if ((controller(cQuit) || said("quit"))) {
  quit(0);
}

[ togglin monitor (if in CGA mode) is allowed
if (controller(cToggleMonitor)) {
  toggle.monitor();
}

[ showing 'about' message is allowed
if (controller(cAbout)) {
  print(gameAboutMsg);
}

[ toggling sound is allowed
if (controller(cToggleSound)) {
  toggle(soundOn);
}

[ run current room logic (animations only)
call.v(currentRoom);

[ if player tries to input something
if (haveInput && !haveMatch) {
  [ it's not allowed
  print("You are dead! You can only restore, restart, quit the game"
        " or view your inventory.");
}

return();

[ **************************************
[ MESSAGES
[ **************************************
#message 1 "You are now dead."
#message 2 "Thank you for playing this game. Better luck next time!"
#message 3 "You are dead! You can only restore, restart, quit the game or view your inventory."
#message 4 "You are dead by a different method."
#message 5 gameAboutMsg