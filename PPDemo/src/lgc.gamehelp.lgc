[*********************************************************************
[
[ lgc.GameHelp: Help Screen
[
[ This screen also demonstrates the use of extended characters 
[ in text mode.
[
[ Left panel has topics; use cursor to select topics; right panel will
[ display relevant information.
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
#define selectedTopic  v240
#define prevItem       v241
#define oldEgoDir      v242

#define initScreen     f240
#define moveSelection  f241

#define topicTitle      s20

[***************************************
[ MAIN CODE BLOCK 
[***************************************

if (initScreen) {
  reset(initScreen);
  
  [ pause game clock
  block(PAUSE.CLOCK, PAUSE_CLOCK, 0, 0); [ pause.clock(MODE)
  
  [ egodir used to maneuver through menu items
  oldEgoDir = egoDir;
  egoDir = STOPPED;
  
  [ no input allowed while help screen is displayed
  prevent.input();
  
  [ clear status line
  status.line.off();
  
  [ start with light gray background
  set.text.attribute(PUSH.ATTRIBUTES, 0); [ push.attributes()
  set.text.attribute(BLACK, LT_GRAY);
  
  [ switch to text screen to 80 col mode
  configure.screen(SCREEN.MODE, TEXTMODE, 80COL); [ screen.mode(MODE, WIDTH)
  
  [ make sure keyboard buffer is clear
  lastChar = 0;
  
  [ draw the main grid
  display(0, 0, "ษออออออออออออออออออหออออออออออออออออPower Pack Demo Helpอออออออออออออออออออออออป");
  work1 = 1;
  work3 = &m1;
  :drawedge
    work2 = 0;
    display.v(work1, work2, work3);
    work2 = 19;
    display.v(work1, work2, work3);
    work2 = 79;
    display.v(work1, work2, work3);
    ++work1;
  if (work1 < 24) {
    goto(drawedge);
  }
  display(4, 4, "Credits");
  display(6, 2, "Running PPDemo");
  display(8, 2, "Game Options");
  display(9, 4, "Sound");
  display(10, 4, "Clock");
  display(11, 4, "Speed");
  display(12, 4, "Skip Intro");
  display(13, 4, "PPDEMO.CFG");
  display(15, 2, "Interaction");
  display(16, 4, "Mouse");
  display(17, 4, "Parser");
  display(18, 4, "Shortcuts");
  display(19, 4, "Status Bar");
  display(20, 4, "Tool Bar");
  display(21, 4, "Viewing Items");
  display(22, 4, "Using Items");
  display(24, 0, "ศออออออออออออออออออสอออออออออออออออArrow Up/Down to Scroll Topicsออออออออออออออผ");
 
  [ initial topic is 'About PPDemo'
  selectedTopic = 1;
  prevItem = 0;
  set(moveSelection);
}

[ check for change in egoDir
if (egoDir) {
  if (egoDir == UP && selectedTopic > 1) {
    --selectedTopic;
    set(moveSelection);
  }
  if (egoDir == DOWN && selectedTopic < 17) {
      ++selectedTopic;
    set(moveSelection);
  }
  [ always reset dir
  egoDir = STOPPED;
}

[ check for change in selection
if (moveSelection) {
  reset(moveSelection);
  [ reset the previous selection
  set.text.attribute(BLACK, LT_GRAY);
  if (prevItem == 1) {
    display(3, 2, "About PPDemo");
    goto(resetprev);
  }
  if (prevItem == 2) {
    display(4, 4, "Credits");
    goto(resetprev);
  }
  if (prevItem == 3) {
    display(6, 2, "Running PPDemo");
    goto(resetprev);
  }
  if (prevItem == 4) {
    display(8, 2, "Game Options");
    goto(resetprev);
  }
  if (prevItem == 5) {
    display(9, 4, "Sound");
    goto(resetprev);
  }
  if (prevItem == 6) {
    display(10, 4, "Clock");
    goto(resetprev);
  }
  if (prevItem == 7) {
    display(11, 4, "Speed");
    goto(resetprev);
  }
  if (prevItem == 8) {
    display(12, 4, "Skip Intro");
    goto(resetprev);
  }
  if (prevItem == 9) {
    display(13, 4, "PPDEMO.CFG");
    goto(resetprev);
  }
  if (prevItem == 10) {
    display(15, 2, "Interaction");
    goto(resetprev);
  }
  if (prevItem == 11) {
    display(16, 4, "Mouse");
    goto(resetprev);
  }
  if (prevItem == 12) {
    display(17, 4, "Parser");
    goto(resetprev);
  }
  if (prevItem == 13) {
    display(18, 4, "Shortcuts");
    goto(resetprev);
  }
  if (prevItem == 14) {
    display(19, 4, "Status Bar");
    goto(resetprev);
  }
  if (prevItem == 15) {
    display(20, 4, "Tool Bar");
    goto(resetprev);
  }
  if (prevItem == 16) {
    display(21, 4, "Viewing Items");
    goto(resetprev);
  }
  if (prevItem == 17) {
    display(22, 4, "Using Items");
  }
  :resetprev
  prevItem = selectedTopic;
  clear.text.rect(1, 20, 23, 78, LT_GRAY);
  
  [ select new item
  set.text.attribute(WHITE, BLUE);
  [ 1 - 4 in lgc.HelpText1
  if (selectedTopic < 5) {
    call(lgc.HelpText1);
    goto(getkey);
  }
  [ 5 - 8 in lgc.HelpText2
  if (selectedTopic < 9) {
    call(lgc.HelpText2);
    goto(getkey);
  }
  [ 9 - 12 in lgc.HelpText1
  if (selectedTopic < 13) {
    call(lgc.HelpText3);
    goto(getkey);
  }
  [ 13 - 17 in lgc.HelpText4
  call(lgc.HelpText4);
}            

:getkey
if ((have.key() || controller(cMenu))) {
  [ reset helpmode
  reset(showHelp);
  
  [ restore colors
  [ (before statusline or stored values will be
  [ overwritten)
  set.text.attribute(0, POP.ATTRIBUTES); [ pop.attributes()
  
  [ restore input and status line
  accept.input();
  status.line.on();

  [ switch back to graphics display
  graphics();

  [ restart game clock
  block(PAUSE.CLOCK, START_CLOCK, 0, 0); [ pause.clock(MODE)
}
return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "บ"
