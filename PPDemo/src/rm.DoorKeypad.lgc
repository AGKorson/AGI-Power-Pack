[*********************************************************************
[
[ rm.DoorKeypad
[
[ This is a close up of the digital keypad that controls the door to
[ the control room. If player enters correct code, the door will open. 
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
#define oDigit1           o1
#define oDigit2           o2
#define oDigit3           o3
#define oDigit4           o4
#define oIndicator        o5

#define pressButton     v220
#define btnColor        v221
#define digitcount      v222
#define digit1          v223
#define digit2          v224
#define digit3          v225
#define digit4          v226
#define vMsg            v227
#define indicatorTimer  v228
#define tryCount        v229

#define unlockdoor      f220
#define correctcode     f221
#define pressAsterisk   f222
#define pressPound      f223

#define sFillCommand    s12
#define sFillColor      s13

[***************************************
[ FIRST CYCLE ONLY
[***************************************
if (newRoom) {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(1);
  
  [ digital display digits
  load.view(vw.PINPad);
  animate.obj(oDigit1);
  animate.obj(oDigit2);
  animate.obj(oDigit3);
  animate.obj(oDigit4);
  set.view(oDigit1, vw.PINPad);
  set.view(oDigit2, vw.PINPad);
  set.view(oDigit3, vw.PINPad);
  set.view(oDigit4, vw.PINPad);
  set.loop(oDigit1, 0);
  set.loop(oDigit2, 0);
  set.loop(oDigit3, 0);
  set.loop(oDigit4, 0);
  position(oDigit1, 91, 28);
  position(oDigit2, 78, 28);
  position(oDigit3, 65, 28);
  position(oDigit4, 52, 28);
  set.priority(oDigit1, 15);
  set.priority(oDigit2, 15);
  set.priority(oDigit3, 15);
  set.priority(oDigit4, 15);
  stop.cycling(oDigit1);
  stop.cycling(oDigit2);
  stop.cycling(oDigit3);
  stop.cycling(oDigit4);
  set.cel(oDigit1, 10);
  set.cel(oDigit2, 10);
  set.cel(oDigit3, 10);
  set.cel(oDigit4, 10);
  draw(oDigit1);
  draw(oDigit2);
  draw(oDigit3);
  draw(oDigit4);
  
  digit1 = 10;
  digit2 = 10;
  digit3 = 10;
  digit4 = 10;
  
  animate.obj(oIndicator);
  set.view(oIndicator, vw.PINPad);
  set.loop(oIndicator, 0);
  set.cel(oIndicator, 10);
  stop.cycling(oIndicator);
  position(oIndicator, 106, 26);
  set.priority(oIndicator, 15);
  draw(oIndicator);
  
  [ no ego, and no input (mouse only)
  set(disableGameFunctions);
  prevent.input();
  
  [ no button pressed to start
  pressButton = -1;
  
  [ use a palette with a much darker 'dark gray'
  block(SET.PALETTE, BYNUM, &m9, 1); [ set.palette(FN_MODE, mPALETTE, LOADONLY)
  
  [ button sounds
  work1 = s.Tone0;
  :loadit
    block(SND.V.FN, LOAD.SOUND.V, &work1, 0); [ load.sound.v(vSNDNUM)
    ++work1;
  if (work1 < 127) {
    goto(loadit);
  }
  load.sound(s.WrongCode);
  load.sound(s.RightCode);
  [ stop walkman
  if (playWalkman) {
    reset(playWalkman);
    stop.sound();
    reset(musicDone);
  }
  [ 4channel, use FM
  set(4channelsound);
  reset(useMIDI);
  set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
  [ set instrument for buttons
  work1 = 0;
  work2 = 128;
  set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
  ++work1;
  set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)

  [ display the picture on screen
  show.pic();
  [ use a finger cursor
  cursoricon = -1; [cursormode;
  set.key(BYNUM, &m11, mouse); [ set.cursor(FN_MODE, MSGNUM)
  [ fade in
  block(FADE.IN, BYNUM, FADECOUNT, BLACK); [ fade.in(FN_MODE, DELAY, COLOR)
  return();
}

[***************************************
[ NON-INPUT CODE 
[***************************************

[ check for indicator timer
if (indicatorTimer) {
  --indicatorTimer;
  if (indicatorTimer == 0) {
    set.cel(oIndicator, 10);
    if (correctcode) {
      set(unlockdoor);
    } else {
      [ clear all four digits
      digitcount = 0;
      digit1 = 10;
      digit2 = 10;
      digit3 = 10;
      digit4 = 10;
      set.cel.v(oDigit1, digit1);
      set.cel.v(oDigit2, digit2);
      set.cel.v(oDigit3, digit3);
      set.cel.v(oDigit4, digit4);
      ++tryCount;
      if (tryCount == 3) {
        work1 = -1;
        call(lgc.ScoreHandler);
        print("Trying to guess the code isn't working.");
        tryCount = 0;
      }
    }
  }
  goto(exit);
}

[***************************************
[ PROCESS PLAYER INPUT
[***************************************

[ check for keypress
if (have.key()) {
 [ digits
 if (lastChar > 47 && lastChar < 58) {
   pressButton = lastChar;
   pressButton -= 48;
   goto(pressBtn);
  }
  [ *
  if (lastChar == 42) {
   pressButton = 10;
   goto(pressBtn);
  }
  [ # (or ENTER)
  if ((lastChar == 35 || lastChar == 13)) {
   pressButton = 11;
   goto(pressBtn);
  }
} 

[ check for keypad button presses
if (mouseBtn == 1) { 
  if (pressButton == -1) {
    block(GET.PIXEL, &mouseX, &mouseY, &pressButton); [ get.pixel(vX, vY, vRESULT) 
    [ priority value indicates button
    pressButton /= 16;
    if (pressButton < 15) {
      :pressBtn
      [ highlight the pressed button
      if (pressButton == 0) {
        [ fill 78,133
        sFillCommand = "F0%s13F84E85FF";
      }
      if (pressButton == 1) {
        [ fill 58,43
        sFillCommand = "F0%s13F83A2BFF";
      }
      if (pressButton == 2) {
        [ fill 78,43
        sFillCommand = "F0%s13F84E2BFF";
      }
      if (pressButton == 3) {
        [ fill 98,43
        sFillCommand = "F0%s13F8622BFF";
      }
      if (pressButton == 4) {
        [ fill 60,73
        sFillCommand = "F0%s13F83C49FF";
      }
      if (pressButton == 5) {
        [ fill 78,73
        sFillCommand = "F0%s13F84E49FF";
      }
      if (pressButton == 6) {
        [ fill 98,73
        sFillCommand = "F0%s13F86249FF";
      }
      if (pressButton == 7) {
        [ fill 58,103
        sFillCommand = "F0%s13F83A67FF";
      }
      if (pressButton == 8) {
        [ fill 78,103
        sFillCommand = "F0%s13F84E67FF";
      }
      if (pressButton == 9) {
        [ fill 98,103
        sFillCommand = "F0%s13F86267FF";
      }
      if (pressButton == 10) {
        [ fill 58,133
        sFillCommand = "F0%s13F83A85FF";
      }
      if (pressButton == 11) {
        [ fill 98,133
        sFillCommand = "F0%s13F86285FF";
      }
      sFillColor = "0F";
      block(PIC.DRAW, COLOR_FILL, &sFillCommand, 0); [ pic.draw(FILLSTYLE, sCMDS)
      
      [ check for pound sign (enter)
      if (pressButton == 11) {
        set(pressPound);
      }
      
      [ check for asterisk (backspace)
      if (pressButton == 10) {
        set(pressAsterisk);
      }
      
      [ check for number
      if (pressButton < 10) {
        [ add number if not already at limit of four
        if (digitcount < 4) {
            [ advance digits
            digit4 = digit3;
            digit3 = digit2;
            digit2 = digit1;
            digit1 = pressButton;
            ++digitcount;
        }
      }
      [ update digital display
      set.cel.v(oDigit1, digit1);
      set.cel.v(oDigit2, digit2);
      set.cel.v(oDigit3, digit3);
      set.cel.v(oDigit4, digit4);
      
      [ sound for all but asterisk
      if (pressButton < 11) {
        work1 = pressButton;
        work1 += s.Tone0;
        work2 = &soundDone;
        block(SND.V.FN, SOUND.V, &work1, &work2); [ play.sound(vSNDNUM, vDONEFLAG)
      }
    } else {
      [ no button
      pressButton = -1;
    }
  }
} else {
  if (pressButton != -1) {
    [ unpress button
    sFillColor = "08";
    block(PIC.DRAW, COLOR_FILL, &sFillCommand, 0); [ pic.draw(FILLSTYLE, sCMDS)
    pressButton = -1;
    
    [ respond to pound sign/asterisk clicks
    if (pressPound) {
      reset(pressPound);
      [ if not four digits, auto-fail
      if (digitcount < 4) {
        set.cel(oIndicator, 12);
        indicatorTimer = 15;
      } else {
        [ confirm code (6502) by comparing the digits
        if (digit4 == 6 && digit3 == 5 && digit2 == 0 && digit1 == 2) {
          set.cel(oIndicator, 11);
          indicatorTimer = 6;
          set(correctcode);
          sound(s.RightCode, soundDone);
        } else {
          set.cel(oIndicator, 12);
          indicatorTimer = 6;
          sound(s.WrongCode, soundDone);
        }
      }
    }
    [ asterisk
    if (pressAsterisk) {
      reset(pressAsterisk);
      [ if at least one digit
      if (digitcount > 0) {
        [ back up the digits
        digit1 = digit2;
        digit2 = digit3;
        digit3 = digit4;
        digit4 = 10;
        --digitcount;
        [ update digital display
        set.cel.v(oDigit1, digit1);
        set.cel.v(oDigit2, digit2);
        set.cel.v(oDigit3, digit3);
        set.cel.v(oDigit4, digit4);
      }
    }
  }
}

[***************************************
[ CHECK FOR AN EXIT CONDITION
[***************************************
:exit

[ when correct code is entered, unlock the door and return
if (unlockdoor && !ctrlroomunlocked) {
  set(ctrlroomunlocked);
  work1 = 5;
  call(lgc.ScoreHandler);
  work1 = 1;
  call(lgc.ScoreHandler);
}

[ ESC or right-click to cancel and exit
if ((unlockdoor || controller(cMenu) || 
     controller(cRightClick))) {
 [ use work2 to indicate if door is open or closed
 [ because it doesn't get used during new.room calls
  if (unlockdoor) {
    work2 = 1;
  } else {
    work2 = 0;
  }
  [ reenable input
  accept.input();
  reset(disableGameFunctions);
  [ clear menu controller
[  set.key(CLR.CTRL, CLR.CTRL, cMenu); [ clear.controller(cNUM)
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
  [ fade out to black (faster than room fade)
  block(FADE.OUT, BYNUM, 10, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
  [ restore palette
  block(SET.PALETTE, BYNUM, &m10, 1); [ set.palette(FN_MODE, mPALETTE, LOADONLY)
  new.room(rm.Stairway); [ ##LE001##
}

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "%g30"
#message 2 "%g31"
#message 3 "%g32"
#message 4 "%g33"
#message 5 "%g34"
#message 6 "%g35"
#message 7 "%g36"
#message 8 "%g37"
[ custom palette
#message 9 "00000000002A002A00002A2A2A00002A002A2A15002A2A2A09090915153F153F15153F3F3F15153F153F3F3F153F3F3F"
[ default palete
#message 10 "%s2"
[ finger pointer
#message 11 "0400FFF3FFE1FFE1FFE1FFE149E000E000E000800000000000000100018001800380"
           "0000000C000C000C000C000CB60DB60DB60DFE6FFE6FFE6FFC7FFC3FFC3F0000" 