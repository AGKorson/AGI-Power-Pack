[*********************************************************************
[ 
[ lgc.ScoreHandler
[ 
[ This score handler logic is needed to allow for negative score
[ values. It uses a flag (negativeScore) to determine if the value of
[ currentScore is treated as positive or negative.
[ 
[ Maximum positive value is 250 and maximum negative value is -250.

[ When adjusting scores, maximum increase/decrease must be no more 
[ than +/-5 (otherwise the adjustment may interfere with the 
[ maximum/minimum values).
[ 
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
[ none

[***************************************
[ MAIN CODE BLOCK 
[***************************************

[ ignore if game is over
if (deathType == DT_GAME_OVER) {
  return();
}

[ check for negative score adjustment
if (negativeScore) {
  [ subtract the adjustment amount
  currentScore -= work1;
  [ if subtracting from a negative score
  [ (adjustment value is -1 to -5)
  if (work1 > 250) {
    [ lowest negative value is -250
    if (currentScore > 250) {
      currentScore = 250;
    }
  } else {
    [ if adding to a negative score check for
    [ reversal to positive value
    if ((currentScore > 251 || currentScore == 0)) {
      reset(negativeScore);
      currentScore *= -1;
    }
  }
} else {
  [ add the adjustment amount
  currentScore += work1;
  [ if adjustment was positive
  if (work1 < 6) {
    [ max is 250
    if (currentScore > 250) {
      currentScore = 250;
    }
  } else {
    [ check for reversal to negative value
    if (currentScore > 250) {
      set(negativeScore);
      currentScore *= -1;
    }
  }
}

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
