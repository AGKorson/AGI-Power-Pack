[*********************************************************************
[ 
[ lgc.ReadNotebook
[ 
[ The notebook has several pages. This logic handles displaying the
[ pages, and allowing the player to scroll through them all.
[ Hints about the ERIDOCC module password can be found on the pages
[ of the notebook.
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
#define pageNum                    v250
#define pageImg                    v251

[***************************************
[ MAIN CODE BLOCK 
[***************************************

[ pause game clock while reading
block(PAUSE.CLOCK, PAUSE_CLOCK, 0, 0); [ pause.clock(MODE)

[ no script entries for this sequence
set(noScript);
[ no regular input either
prevent.input();
status.line.off();

[ save colors
set.text.attribute(PUSH.ATTRIBUTES, 0); [ push.attributes()

[ start with cover, which is page 1
pageImg = pic.NotebookCover;
pageNum = 1;

[ fade out until done loading cover
block(FADE.OUT, BYNUM, 5, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
[ use default mouse cursor
cursoricon = C_DEFAULT;
set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)

[ add keyboard instructions
set.text.attribute(DK_GRAY, BLACK);
display(23, 35, "ESC = EXIT");
display(23, 67, "RIGHT = >");

[ hide objects while showing notebook
block(HIDE.OBJS, HIDE_OBJS, 0, 0); [ hide.objs(MODE)
load.pic(pageImg);
draw.pic(pageImg);
show.pic();
discard.pic(pageImg);
set.text.attribute(BLACK, YELLOW);
display(6, 34, "AMOS Meeting");
display(7, 37, "Notes");
display(8, 34, " 4/11/88 -");
display(9, 34, " 8/15/89");

[ fade back in
block(FADE.IN, BYNUM, 5, BLACK); [ fade.in(FN_MODE, DELAY, COLOR)

[ main input loop; wait until a key is pressed
[ (mouse-clicks are converted to return/escape 
[ by the have.key command)
:waitforit
  lastChar = 0;
if (!have.key()) {
  goto(waitforit);
}

[ ESC or right-click to exit
if (lastChar == 27) {
  goto(close);
}
[ '<' for left, '>' for right
if (lastChar == 60 && pageNum > 1) {
  goto(pageleft);
}
if (lastChar == 62) {
  goto(pageright);
}

[ left button down (can't check for controller
[ because normal AGI cycle is skipped)
if (mouseBtn == 1) {
  block(GET.PIXEL, &mouseX, &mouseY, &work2); [ get.pixel(vX, vY, vRESULT)
  work2 /= 16;
  if (work2 == 7) {
    [ done
    goto(close);
  }
  if ((work2 == 6 || work2 == 5)) {
    if (work2 == 6) {
      :pageright
      if (pageNum == 5) {
        print.at("The last few pages are blank.", 1, 27, 35);
        goto(waitforit);
      }
      block(FADE.OUT, BYNUM, 1, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
      ++pageNum;
      pageImg = pic.NotebookPages;
      if (pageNum == 2) {
        [ show left help text
        set.text.attribute(DK_GRAY, BLACK);
        display(23, 3, "< = LEFT");
      }
    } else {
      :pageleft
      block(FADE.OUT, BYNUM, 1, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
      --pageNum;
      if (pageNum == 1) {
        pageImg = pic.NotebookCover;
        [ hide left text
        set.text.attribute(WHITE, BLACK);
        display(23, 3, "        ");
      } else {
        pageImg = pic.NotebookPages;
      }
    }
    load.pic(pageImg);
    draw.pic(pageImg);
    show.pic();
    
    [ add text/images for current page
    if (pageNum == 1) {
      set.text.attribute(BLACK, YELLOW);
      display(6, 34, "AMOS Meeting");
      display(7, 37, "Notes");
      display(8, 34, " 11/7/88 -");
      display(9, 34, " 8/15/89");
    }
    
    if (pageNum == 2) {
      set.text.attribute(BLACK, WHITE);
      display( 4, 13, "4/11/88");
      display( 5, 13, "AMOS Kickoff meeting.");
      display( 6, 13, "Goal is to create a");
      display( 7, 13, "modified mainframe");
      display( 8, 13, "environment to allow");
      display( 9, 13, "rapid development of");
      display(10, 13, "new features that");
      display(11, 13, "should be incorporated");
      display(12, 13, "into AGI.");
      display(14, 13, "Initial development of");
      display(15, 13, "AMOS will be led by");
      display(16, 13, "Jeff and Chris. Held a");
      display(17, 13, "brainstorming session");
      display(18, 13, "on potential modules");
      display(19, 13, "to include:");

      display( 4, 48, "- mouse and cursor");
      display( 5, 50, "support;");
      display( 7, 48, "- soundboard/midi");
      display( 8, 50, "support;");
      display(10, 48, "- 80 column text mode");
      display(12, 48, "- improved text");
      display(13, 50, "support (extended");
      display(14, 50, "characters?);");
      display(16, 48, "- support for the new");
      display(17, 50, "MSDOS code page");
      display(18, 50, "feature (for inter-");
      display(19, 50, "national character");
      display(20, 50, "support);");
    }
    
    if (pageNum == 3) {
      set.text.attribute(BLACK, WHITE);
      display( 4, 14, "- improved color");
      display( 5, 16, "options - dynamic");
      display( 6, 16, "palettes, screen");
      display( 7, 16, "fade options, more");
      display( 8, 16, "display/message");
      display( 9, 16, "box color choices;");
      display(11, 14, "- reconfigure memory");
      display(12, 16, "model (more than");
      display(13, 16, "256K is becoming");
      display(14, 16, "standard)");
      display(16, 13, "4/25/88");
      display(17, 13, "The AMOS Team has de-");
      display(18, 13, "cided to develop six");
      display(19, 13, "AMOS modules that will");
      display(20, 13, "be used to deveop and");
      
      display(4, 47, "test new AGI features:");
      display(6, 47, "CAISOCC: Custom Acous-");
      display(7, 47, "tic Instrument Setting");
      display(8, 47, "Options Control Console");
      display(9, 47, "to create customized");
      display(10, 47, "FM sound instruments");
      display(12, 47, "ERIDOCC: Emulated Rod-");
      display(13, 47, "ent Input Device Option");
      display(14, 47, "Control Console to edit");
      display(15, 47, "mouse cursors");
      display(17, 47, "ESADOCC: Electronic");
      display(18, 47, "Synthesized Audio Data");
      display(19, 47, "Options Control Console");
      display(20, 47, "to change sound output");
    }
    
    if (pageNum == 4) {
      set.text.attribute(BLACK, WHITE);
      display(4, 13, "ICAPOCC: Indexed Chro-");
      display(5, 13, "matic Association Prop-");
      display(6, 13, "erties Options Control");
      display(7, 13, "Console to create cus-");
      display(8, 13, "tom color palettes");
      display(10, 13, "OFROCC: Overall Frame-");
      display(11, 13, "rate Options Control");
      display(12, 13, "Console to adjust game");
      display(13, 13, "speed");
      display(15, 13, "RANDOCC: Rectangular");
      display(16, 13, "Alpha-Numeric Display");
      display(17, 13, "Options Control Console");
      display(18, 13, "for changing the color");
      display(19, 13, "options in message box");
      display(20, 13, "text");
      
      display( 4, 47, "5/23/88");
      display( 5, 47, "New or updated modules");
      display( 6, 47, "get default password of");
      display( 7, 47, "first four letters of");
      display( 8, 47, "module followed by six");
      display( 9, 47, "digit date (mmddyy),");
      display(10, 47, "period (.) then letter");
      display(11, 47, "of current major ver-");
      display(12, 47, "sion (version 1 = 'a',");
      display(13, 47, " etc.)");
      display(14, 47, "The password should");
      display(15, 47, "automatically force");
      display(16, 47, "an update after the");
      display(17, 47, "initial load, but it");
      display(18, 47, "is currently not");
      display(19, 47, "working.");
    }
    
    if (pageNum == 5) {
      set.text.attribute(BLACK, WHITE);
      display( 4, 13, "7/31/89");
      display( 5, 13, "The bug that is not");
      display( 6, 13, "forcing admin reset of");
      display( 7, 13, "default module pass-");
      display( 8, 13, "word is still not");
      display( 9, 13, "fixed. Admins are");
      display(10, 13, "reminded to manually");
      display(11, 13, "reset passwords after");
      display(12, 13, "installing a new");
      display(13, 13, "module version.");
      display(15, 13, "8/7/89");
      display(16, 13, "Ken requested that he");
      display(17, 13, "and Roberta be added");
      display(18, 13, "as AMOS users so they");
      display(19, 13, "could evaluate");

      display( 4, 47, "progress before a");
      display( 5, 47, "final decision gets");
      display( 6, 47, "made on whether or not");
      display( 7, 47, "to continue with AMOS");
      display( 8, 47, "or abandon it to focus");
      display( 9, 47, "exclusively on SCI.");
      display(11, 47, "8/14/89");
      display(12, 47, "AK reports ERIDOCC");
      display(13, 47, "module version 1.04");
      display(14, 47, "is ready for testing.");
      display(16, 47, "8/15/89");
      display(17, 47, "Canceled! Looks like");
      display(18, 47, "SCI wins. :-(");
    }
    
    block(FADE.IN, BYNUM, 1, BLACK); [ fade.in(FN_MODE, DELAY, COLOR)
    discard.pic(pageImg);
  }
}
[ after page change, wait for next input
goto(waitforit);

:close
[ score
if (!scoreNotebook) {
  set(scoreNotebook);
  work1 = 2;
  call(lgc.ScoreHandler);
}
[ fade out during the switch
block(FADE.OUT, BYNUM, 5, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
[ clear instruction line
clear.lines(23, 23, BLACK);

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "%g30"
#message 2 "%g31"
#message 3 "%g32"
#message 4 "%g33"
#message 5 "%g34"
#message 6 "%g35"
#message 7 "%g36"
#message 8 "%g37"
