[ ********************************************************************
[
[ rm.Storeroom
[
[ 
[
[ ********************************************************************
[ TODO: sound effects for coffin open/close
[ TODO: more said statements for storeroom

#define oClockHour      o1
#define oClockMinute    o2
#define oPendulum       o3
#define oCoffin         o4
#define oCabinetDoor    o4 [ reuse o4 for cabinet door
#define oChest          o5
#define oChestDrawer    o6
#define oFileDrawer     o7 [ green file cabinet
#define oDeskDrawer     o8 [ under mirror

#define vCheckSecond  v220
#define vClockHour    v221
#define vClockMin     v222
#define vClockSec     v223
#define vCoffinState  v224 [ coffin and cabinet door close automatically
#define vDoorState    v225 [ so only local variables needed to track state
#define vCoffinDeathSeq  v226
#define vCoffinCount  v227

#define fCoffin       f220
#define fCheckClick   f221
#define fDoor         f222
#define fFileDrawer   f223
#define fDeskDrawer   f224
#define fChestDrawer  f225
#define fChest        f226
#define fEgoBackup    f227
#define fInCoffin     f228


if (newRoom) {
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ default horizon is ok
  
  [ add additional room initialization here
  load.view(vw.StorageRoomArt);
  
  [ clock
  animate.obj(oPendulum);
  set.view(oPendulum, vw.StorageRoomArt);
  set.loop(oPendulum, 7);
  ignore.objs(oPendulum);
  position(oPendulum, 39, 96);
  ignore.objs(oPendulum);
  set.priority(oPendulum, 11);
  work1 = 2;
  cycle.time(oPendulum, work1);
  draw(oPendulum);
  animate.obj(oClockHour);
  set.view(oClockHour, vw.StorageRoomArt);
  set.loop(oClockHour, 9);
  ignore.objs(oClockHour);
  stop.cycling(oClockHour);
  position(oClockHour, 39, 76);
  set.priority(oClockHour, 11);
  animate.obj(oClockMinute);
  set.view(oClockMinute, vw.StorageRoomArt);
  set.loop(oClockMinute, 8);
  ignore.objs(oClockMinute);
  stop.cycling(oClockMinute);
  position(oClockMinute, 39, 76);
  set.priority(oClockMinute, 11);
  [ get current time
  block(GET.TIME, &vClockHour, &vClockMin, &vClockSec); [ get.time(vHOUR, vMINUTE, vSECOND)
  if (vClockHour > 11) {
   vClockHour -= 12;
  }
  set.cel.v(oClockHour, vClockHour);
  [ round minutes to multiples of 2.5
  work1 = vClockMin; [ 0-59
  work1 *= 2; [ 0 - 118
  if (vClockSec > 29) {
    ++work1;
  }
  work1 /= 5;
  set.cel.v(oClockMinute, work1);
  draw(oClockHour);
  draw(oClockMinute);
  vCheckSecond = elapsedSeconds;
  
  [ coffin lid
  animate.obj(oCoffin);
  set.view(oCoffin, vw.StorageRoomArt);
  stop.cycling(oCoffin);
  ignore.objs(oCoffin);
  ignore.blocks(oCoffin);

  [ cabinet door (uses same object as the coffin)

  [ chest
  animate.obj(oChest);
  set.view(oChest, vw.StorageRoomArt);
  if (has("scroll")) {
    set.loop(oChest, 1);
  } else {
    set.loop(oChest, 10);
  }
  if (storeChestStatus == OPEN) {
    set.cel(oChest, 3);
  } else {
    set.cel(oChest, 0);
  }
  stop.cycling(oChest);
  ignore.objs(oChest);
  ignore.blocks(oChest);
  position(oChest, 119, 162);
  draw(oChest);

  [ chest drawer
  animate.obj(oChestDrawer);
  set.view(oChestDrawer, vw.StorageRoomArt);
  set.loop(oChestDrawer, 4);
  stop.cycling(oChestDrawer);
  ignore.objs(oChestDrawer);
  ignore.blocks(oChestDrawer);
  if (storeChestDrStatus == OPEN) {
    set.cel(oChestDrawer, 3);
    [ add conditional line
    s12 = "F1F201F6727C6D7C72887888FF";
    block(PIC.DRAW, 0, &s12, 0); [ pic.draw(FILLSTYLE, sCMDS)
    draw(oChestDrawer);
  }
  
  [ green file cabinet drawers
  animate.obj(oFileDrawer); 
  set.view(oFileDrawer, vw.StorageRoomArt);
  stop.cycling(oFileDrawer);
  ignore.objs(oFileDrawer);
  ignore.blocks(oFileDrawer);
  set.priority(oFileDrawer, 9);
  if (storeFileDrStatus != CLOSED) {
    if (storeFileDrStatus == OPEN1) {
      position(oFileDrawer, 76, 100);
      set.loop(oFileDrawer, 3);
      set.cel(oFileDrawer, 2);
    }
    if (storeFileDrStatus == OPEN2) { 
      position(oFileDrawer, 76, 109);
      set.loop(oFileDrawer, 3);
      set.cel(oFileDrawer, 2);
    }
    if (storeFileDrStatus == OPEN3) { 
      position(oFileDrawer, 76, 118);
      if (has("cassette tape")) {
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 2);
      } else {
        set.loop(oFileDrawer, 11);
        set.cel(oFileDrawer, 0);
      }
    }
    draw(oFileDrawer);
  }
  
  [ desk drawer (under mirror)
  animate.obj(oDeskDrawer);
  set.view(oDeskDrawer, vw.StorageRoomArt);
  stop.cycling(oDeskDrawer);
  ignore.objs(oDeskDrawer);
  position(oDeskDrawer, 100, 112);
  if (storeDeskDrStatus == OPEN) {
    set.loop(oDeskDrawer, 5);
    set.cel(oDeskDrawer, 4);
    draw(oDeskDrawer);
  }
  
  [ add toolbar buttons
  load.view(vw.ButtonsDown);
  call(lgc.InitToolbar);
  
  [ add ego to the room
  position(ego, 24, 139);
  draw(ego);
  egoDir = STOPPED;
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, 25, 0); [ fade.in(BYVAR, DELAY, COLOR)
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  
  return();
}


[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {

  [ place said tests here
  if (said("look")) {
    print("You see a room full of junk.");
  }
  
  if ((said("look", "pillow") || said("look", "in", "coffin"))) {
    [ only if coffin is open
    if (vCoffinState == OPEN) {
      print("You lean over the coffin to get a closer look at the pillow.");
      goto(lookpillow);
    } else {
      print("There's no pillow here.");
    }
  }
  
  if (said("open", "coffin")) {
    if ((vCoffinState == OPEN || vCoffinState == OPENING)) {
      print("It's already open.");
    } else {
      goto(openCoffin);
    }
  }
  if (said("close", "coffin")) {
    if ((vCoffinState == CLOSED || vCoffinState == CLOSING)) {
      print("It's already shut.");
    } else {
      goto(closeCoffin);
    }
  }
  if ((said("get", "in", "coffin") || said("climb", "in", "coffin"))) {
    [ is ego on wrong side? (lid will always be closed)
    if (posn(ego, 28, 141, 54, 149)) {
      print("The lid is closed.");
      goto(doneclick);
    }
    [ is ego close enough?
    if (posn(ego, 24, 156, 54, 161)) {
      load.view(vw.EgoCoffin);
      move.obj(ego, 37, 157, 1, fInCoffin);
      [ disallow menu use
      reset(enableMenu);
      [ game functions are disabled
      set(disableGameFunctions);
      [ no input allowed
      prevent.input();
    } else {
      print("You can't do that from here.");
    }
  }  
}


[*****
:noInput           [  all non-input related code goes here 
[*****

[ clock - check every second
if (vCheckSecond != elapsedSeconds) {
  vCheckSecond = elapsedSeconds;
  [ increment clock seconds
  ++vClockSec;
  if (vClockSec == 60) {
    vClockSec = 0;
    ++vClockMin;
    if (vClockMin == 60) {
      vClockMin = 0;
      ++vClockHour;
      if (vClockHour == 12) {
        vClockHour = 0;
      }
    }
  }
  if ((vClockSec == 0 || vClockSec == 30)) {
    [ refresh time every 30 seconds
    block(GET.TIME, &vClockHour, &vClockMin, &vClockSec); [ get.time(vHOUR, vMINUTE, vSECOND)
    if (vClockHour > 11) {
     vClockHour -= 12;
    }
    set.cel.v(oClockHour, vClockHour);
    [ round minutes to multiples of 2.5
    work1 = vClockMin; [ 0-59
    work1 *= 2; [ 0 - 118
    if (vClockSec > 29) {
      ++work1;
    }
    work1 /= 5;
    set.cel.v(oClockMinute, work1);
    draw(oClockHour);
    draw(oClockMinute);
  }
}

[ coffin death check
if (fInCoffin) {
  reset(fInCoffin);
  if (vCoffinDeathSeq == 6) {
    deathType = 3;
    ++vCoffinDeathSeq;
  }

  if (vCoffinDeathSeq == 5) {
    print("Looks like you're going to be here awhile.");
    vCoffinCount = 8;
    ++vCoffinDeathSeq;
  }

  if (vCoffinDeathSeq == 4) {
    print("And it's stuck! You can't push it open!");
    vCoffinCount = 4;
    ++vCoffinDeathSeq;
  }
  
  if (vCoffinDeathSeq == 3) {
    print("Uh oh! The coffin lid just slammed shut!");
    vCoffinCount = 4;
    ++vCoffinDeathSeq;
  }
  
  if (vCoffinDeathSeq == 2) {
    work1 = -2;
    work2 = -1;
    reposition(ego, work1, work2);
    set.loop(ego, 2);
    set.cel(ego, 0);
    work1 = 2;
    cycle.time(ego, work1);
    end.of.loop(ego, fInCoffin);
    ++vCoffinDeathSeq;
    erase(oCoffin);
 }
  
  if (vCoffinDeathSeq == 1) {
    work1 = -6;
    work2 = -10;
    reposition(ego, work1, work2);
    set.priority(ego, 15);
    set.loop(ego, 1);
    set.cel(ego, 0);
    end.of.loop(ego, fInCoffin);
    ++vCoffinDeathSeq;
  }
  
  if (vCoffinDeathSeq == 0) {
    work1 = -1;
    work2 = 0;
    reposition(ego, work1, work2);
    set.view(ego, vw.EgoCoffin);
    set.loop(ego, 0);
    set.cel(ego, 0);
    end.of.loop(ego, fInCoffin);
    [ turn off auto-cycle to allow cycle-at-rest
    work1 = 0;
    set.upper.left(&ego, &work1);
    ++vCoffinDeathSeq;
  }
}

if (vCoffinCount) {
  --vCoffinCount;
  if (vCoffinCount == 0) {
    set(fInCoffin);
  }
}

if (vCoffinDeathSeq) {
  [ nothing more to do
  return();
}

[ priority adjustments
[ behind green chair
if (posn(ego, 71, 143, 95, 148)) {
  set.priority(ego, 12);
} else {
  release.priority(ego);
}
[ treasure chest- on left, use 14
if (posn(ego, 112, 154, 123, 162)) {
  set.priority(oChest, 14);
} else {
  release.priority(oChest);
}

[ if cursor is look/talk/use and left-click and NOT 
[ clicking on the actual toolbar, check for clicks
[ on in-game items
if (cursoricon > C_WALK && cursoricon < C_WAIT && 
    controller(cLeftClick) &&
    (mouseX < 14 || mouseX > 145 || mouseY > 17)) {
  [ ignore borders
  if ((mouseX < 27 || mouseX > 149 || mouseY < 17 || mouseY > 159)) {
    goto(doneclick);
  }
  [ look
  if (cursormode == C_LOOK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You see you.");
      goto(doneclick);
    }
    
    [ moose 1
    if (mouseX > 11 && mouseX < 32 && mouseY > 29 && mouseY < 61) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      if (work1 != 67) { [67=cyan vis/red pri
        print("This is the moosehead from Leisure Suit Larry.");
      }
      goto(doneclick);
    }
    
    [ moose 2
    if (mouseX > 17 && mouseX < 33 && mouseY > 64 && mouseY < 80) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      if (work1 != 67) { [67=cyan vis/red pri
        print("You have seen this moosehead in King's Quest II.");
      }
      goto(doneclick);
    }
    
    [ moose 3
    if (mouseX > 40 && mouseX < 74 && mouseY > 34 && mouseY < 53) {
      set(fCheckClick);
    }
    if (mouseX > 49 && mouseX < 66 && mouseY > 52 && mouseY < 64) {
      set(fCheckClick);
    }
    if (fCheckClick) {
      reset(fCheckClick);
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      if (work1 != 67) { [67=cyan vis/red pri
        print("This massive moosehead looks just like the one in "
              "King's Quest III.");
      }
      goto(doneclick);
    }
        
    [ clock
    if (mouseX > 35 && mouseX < 48 && mouseY > 62 && mouseY < 121) {
      print("The clock in Mixed Up Mother Goose was modeled after "
            "this grandfather clock.");
      block(GET.TIME, &work1, &work2, &work3); [ get.time(vHOUR, vMINUTE, vSECOND)
      s12 = "AM";
      if (work1 > 11) {
        work1 -= 12;
        s12 = "PM";
      }
      if (work1 == 0) {
        work1 += 12;
      }
      print("The time is %v46:%v47|2:%v48|2 %s12. Isn't it odd that "
            "this clock matches your current time?");
      goto(doneclick);
    }
    [ podium
    if (mouseX > 60 && mouseX < 72 && mouseY > 98 && mouseY < 121) {
      print("It's the podium from the Police Quest I briefing room. "
            "You can imagine Sierra managers standing behind it giving "
            "briefings to project staff.");
      if (posn(ego, 58, 112, 74, 119)) {
        if (!has("PQ idea list")) {
          print("There is a sheet of paper on top of the podium. It looks "
          "like a list of potential expansions for Police Quest.");
        }
        [ show the closeup
        call(lgc.Podium);
        call(lgc.RedrawRoom);
      }
      goto(doneclick);
    }
    [ desk lamp
    if (mouseX > 68 && mouseX < 79 && mouseY > 71 && mouseY < 85) {
      print("This old desk lamp doesn't look like anything special.");
      goto(doneclick);
    }
    [ bookshelf
    if (mouseX > 49 && mouseX < 68 && mouseY > 63 && mouseY < 112) {
      print("The empty bookshelf is covered in dust. It probably once "
            "held computer hardware and software manuals used by Sierra "
            "developers.");
      goto(doneclick);
    }
    [ open file cabinet drawer
    if (posn(oFileDrawer, -1, 1, &mouseX, &mouseY)) {
      if ((storeFileDrStatus == OPEN1 || storeFileDrStatus == OPEN2)) {
        print("This drawer is empty.");
        goto(doneclick);
      } else {
        if (storeFileDrStatus == OPEN3) {
          if (has("cassette tape")) {
            print("This drawer is empty.");
          } else {
            print("Someone left an 80s mix tape in this drawer.");
          }
          goto(doneclick);
        }
      }
    }
    [ file cabinets
    if (mouseX > 68 && mouseX < 93 && mouseY > 79 && mouseY < 115) {
      print("There are several old filing cabinets pushed against "
            "the back wall.");
      goto(doneclick);
    }
    [ overhead light
    if (mouseX > 71 && mouseX < 81 && mouseY > 57 && mouseY < 64) {
      print("This overhead light bathes the entire room in a warm light.");
      goto(doneclick);
    }
    [ wall mirror
    if (mouseX > 78 && mouseX < 93 && mouseY > 45 && mouseY < 76) {
      print("It looks like the magic mirror from King's Quest II. But there's "
            "nothing magic about this mirror. All you see is your own "
            "reflection in the dull, dusty surface.");
      goto(doneclick);
    }
    
    [ open desk drawer
    if (posn(oDeskDrawer, -1, 1, &mouseX, &mouseY)) {
      if (storeDeskDrStatus == OPEN) {
        print("This drawer is empty.");
        goto(doneclick);
      }
    }
    [ desk
    if (mouseX > 93 && mouseX < 113 && mouseY > 96 && mouseY < 115) {
      print("This desk is of the same style as furniture found in King's "
            "Quest III.");
      goto(doneclick);
    }
    [ birdcage
    if (mouseX > 108 && mouseX < 116 && mouseY > 33 && mouseY < 52) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      if ((work1 == 69 || work1 == 77)) { [69/77=magenta/lt magenta vis & red pri)
        print("You recognize this as the birdcage from King's Quest II.");
      }
      goto(doneclick);
    }
    [ desk mirror
    if (mouseX > 98 && mouseX < 108 && mouseY > 64 && mouseY < 96) {
      print("The mirror is very dusty, but you can still see your handsome "
            "reflection in it.");
      goto(doneclick);
    }
    [ pitcher and basin
    if (mouseX > 117 && mouseX < 128 && mouseY > 91 && mouseY < 106) {
      print("This is an antique water pitcher and wash basin, that would "
            "have been used in the days before indoor plumbing.");
      goto(doneclick);
    }
    [ banner
    if (mouseX > 121 && mouseX < 140 && mouseY > 46 && mouseY < 115) {
      print("A copy of this banner can be found in King's Quest III.");
      goto(doneclick);
    }
    
    [ cabinet doors open
    if (posn(oCabinetDoor, -1, 1, &mouseX, &mouseY)) {
      if (vDoorState == OPEN) {
        if (has("music sheet")) {
          print("The cabinet is empty.");
        } else {
          print("A piece of sheet music is in this cabinet.");
        }
        goto(doneclick);
      }
    }
    [ cabinet
    if (mouseX > 128 && mouseX < 147 && mouseY > 115 && mouseY < 156) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      block(BIT.AND, 0, &work1, 15); [ bit.and(BYVAL, VAR, AMOUNT)
      if ((work1 == LT_RED || work1 == BROWN || work1 == YELLOW)) {
        print("This cabinet is of the same style as furniture found in "
              "King's Quest III.");
        goto(doneclick);
      }
    }
    
    [ chest drawer open
    if (posn(oChestDrawer, -1, 1, &mouseX, &mouseY)) {
      if (storeChestDrStatus == OPEN) {
        print("This drawer is empty.");
        goto(doneclick);
      }
    }
    [ bureau/chest
    if (mouseX > 113 && mouseX < 136 && mouseY > 99 && mouseY < 140) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      block(BIT.AND, 0, &work1, 15); [ bit.and(BYVAL, VAR, AMOUNT)
      if ((work1 == LT_RED || work1 == BROWN || work1 == YELLOW)) {
        print("This bureau is of the same style as furniture found in "
              "King's Quest III.");
        goto(doneclick);
      }
    }
    [ wardrobe
    if (mouseX > 95 && mouseX < 120 && mouseY > 48 && mouseY < 111) {
      print("This wardrobe is of the same style as furniture found in "
            "King's Quest III.");
      goto(doneclick);
    }
    [ robots
    if (mouseX > 135 && mouseX < 147 && mouseY > 132 && mouseY < 160) {
      print("These robot models look exactly like the Space Quest I robots.");
      goto(doneclick);
    }
    [ treasure chest
    if (posn(oChest, -1, 1, &mouseX, &mouseY)) {
      if (storeChestStatus == OPEN) {
        if (has("scroll")) {
          print("The treasure chest has nothing else in it.");
        } else {
          print("There is an old parchment scroll in the treasure chest.");
        }
      } else {
        print("In King's Quest II, a treasure chest just like this one had "
              "some awesome treasure in it. What might be in this one?");
      }  
      goto(doneclick); 
    }
    
    [ chair
    if (mouseX > 77 && mouseX < 96 && mouseY > 126 && mouseY < 154) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      work1 /= 16; [ get priority color only
      if ((work1 == LT_MAGENTA || work1 == BLACK)) {
        print("This comfy chair would be great to sit in while reading your "
        "favorite fairy tale stories.");
        goto(doneclick);
      }
    }
    
    [ arcade
    if (mouseX > 53 && mouseX < 63 && mouseY > 125 && mouseY < 150) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      block(BIT.AND, 0, &work1, 15); [ bit.and(BYVAL, VAR, AMOUNT)
      if ((work1 == BLACK || work1 == BLUE || work1 == LT_BLUE)) {
        print("This arcade must be the model for the one in Space Quest I.");
        goto(doneclick);
      }
    }
    [ coffin
    if (mouseX > 27 && mouseX < 53 && mouseY > 142 && mouseY < 156) {
      set(fCheckClick);
    }
    if (posn(oCoffin, -1, 1, &mouseX, &mouseY)) {
      set(fCheckClick);
    }
    if (fCheckClick) {
      reset(fCheckClick);
      if (vCoffinState == OPEN) {
        print("The coffin is empty except for a silk pillow and a padded "
              "lining. It actually looks quite comfortable.");
        [ if on pillow, show it
        if (mouseX > 27 && mouseX < 52 && mouseY > 144 && mouseY < 148) {
          :lookpillow
          call(lgc.Pillow);
          call(lgc.RedrawRoom);
        }
      } else {
        print("It's Count Dracula's coffin from King's Quest II. Hopefully "
              "no body is in there.");
      }
      goto(doneclick);
    }
    [ coat hanger
    if (mouseX > 14 && mouseX < 24 && mouseY > 90 && mouseY < 126) {
      print("You've seen this bizarre looking coat hanger before, in "
            "King's Quest II.");
      goto(doneclick);
    }
    [ boxes
    if (mouseX > 9 && mouseX < 30 && mouseY > 126 && mouseY < 159) {
      set(fCheckClick);
    }
    if (mouseX > 53 && mouseX < 62 && mouseY > 150 && mouseY < 160) {
      set(fCheckClick);
    }
    if (mouseX > 69 && mouseX < 81 && mouseY > 146 && mouseY < 160) {
      set(fCheckClick);
    }
    if (mouseX > 90 && mouseX < 103 && mouseY > 152 && mouseY < 160) {
      set(fCheckClick);
    }
    if (mouseX > 104 && mouseX < 116 && mouseY > 143 && mouseY < 161) {
      set(fCheckClick);
    }
    if (fCheckClick) {
      reset(fCheckClick);
      print("There are unmarked boxes of all shapes and sizes all "
            "along the near wall.");
      goto(doneclick);
    }
    
    [ anything else is the whole room
    print("This room is full of things that look like items from "
          "classic Sierra AGI games.");
    goto(doneclick);
  }
  
  [ talk
  if (cursormode == C_TALK) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("You have nothing to say.");
    } else {
      print("There is no one here to talk to.");
    }
    goto(doneclick);
  }
  
  [ use
  if (cursormode == C_USE) {
    [ ego
    if (posn(ego, ON.OBJ, 1, &mouseX, &mouseY)) {
      print("Please don't touch yourself. Not even when you're alone.");
      goto(doneclick);
    }
    
    [ moose 1 - always out of reach
    if (mouseX > 11 && mouseX < 32 && mouseY > 29 && mouseY < 61) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      if (work1 != 67) { [67=cyan vis/red pri
        print("The moose head is too high for you to reach it.");
      }
      goto(doneclick);
    }
    
    [ moose 2 - always out of reach
    if (mouseX > 17 && mouseX < 33 && mouseY > 64 && mouseY < 80) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      if (work1 != 67) { [67=cyan vis/red pri
        print("The moose head is too high for you to reach it.");
      }
      goto(doneclick);
    }
    
    [ moose 3 - always out of reach
    if (mouseX > 40 && mouseX < 74 && mouseY > 34 && mouseY < 53) {
      set(fCheckClick);
    }
    if (mouseX > 49 && mouseX < 66 && mouseY > 52 && mouseY < 64) {
      set(fCheckClick);
    }
    if (fCheckClick) {
      reset(fCheckClick);
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      if (work1 != 67) { [67=cyan vis/red pri
        print("The moose head is too high for you to reach it.");
      }
      goto(doneclick);
    }
        
    [ clock
    if (mouseX > 35 && mouseX < 48 && mouseY > 62 && mouseY < 121) {
      if (posn(ego, 32, 109, 52, 127)) {
        print("The glass door on the clock is locked. You can't open it.");
      } else {
        print("Move closer.");
      }
     goto(doneclick);
    }
    
    [ podium
    if (mouseX > 60 && mouseX < 72 && mouseY > 98 && mouseY < 121) {
      if (posn(ego, 58, 112, 74, 119)) {
        [ behind of podium
        if (!has("PQ idea list")) {
          print("You grab the paper from the top of the podium.");
          get("PQ idea list");
          work1 = 3;
          call(lgc.ScoreHandler);
        } else {
          print("There is nothing else to take from the podium.");
        }
      } else {
        if (posn(ego, 52, 117, 75, 125)) {
          [ in front of podium
          print("There is nothing you can do with front of the podium.");
        } else {
          print("Move closer.");
        }
      }
      goto(doneclick);
    }

    [ desk lamp
    if (mouseX > 68 && mouseX < 79 && mouseY > 71 && mouseY < 85) {
      if (posn(ego, 62, 102, 80, 114)) {
        print("You have no use for the lamp.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ bookshelf
    if (mouseX > 49 && mouseX < 68 && mouseY > 63 && mouseY < 112) {
      if (posn(ego, 44, 106, 70, 116)) {
        print("You run your fingers through the dust on the shelves. "
              "There is nothing of interest here.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ top file drawer, closed  76, 89, 83, 95
    if (mouseX > 75 && mouseX < 84 && mouseY > 88 && mouseY < 96 &&
        storeFileDrStatus == CLOSED) {
      [ is ego close enough?
      if (obj.in.box(ego, 73, 117, 93, 122)) {
        position(oFileDrawer, 76, 100);
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 0);
        draw(oFileDrawer);
        end.of.loop(oFileDrawer, fFileDrawer);
        storeFileDrStatus = OPENING;
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
      
    [ middle file drawer, closed 76, 98, 83, 105
    if (mouseX > 75 && mouseX < 84 && mouseY > 97 && mouseY < 106 &&
        storeFileDrStatus == CLOSED) {
      [ is ego close enough?
      if (obj.in.box(ego, 73, 117, 93, 122)) {
        position(oFileDrawer, 76, 109);
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 0);
        draw(oFileDrawer);
        end.of.loop(oFileDrawer, fFileDrawer);
        storeFileDrStatus = OPENING;
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ bottom file drawer, closed 76, 107, 83, 114
    if (mouseX > 75 && mouseX < 84 && mouseY > 106 && mouseY < 115 &&
        storeFileDrStatus == CLOSED) {
      [ is ego close enough?
      if (obj.in.box(ego, 73, 117, 93, 122)) {
        position(oFileDrawer, 76, 118);
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 0);
        draw(oFileDrawer);
        end.of.loop(oFileDrawer, fFileDrawer);
        storeFileDrStatus = OPENING;
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ file drawer, when drawer is open 
    if (posn(oFileDrawer, ON.OBJ, 0, &mouseX, &mouseY) && 
        storeFileDrStatus != CLOSING) {
      [ is ego close enough?
      if (obj.in.box(ego, 73, 117, 93, 122)) {
        [ contents check
        if (storeFileDrStatus== OPEN1 && mouseY < 93) {
          goto(doneclick);
        }
        if (storeFileDrStatus== OPEN2 && mouseY < 102) {
          goto(doneclick);
        }
        if (storeFileDrStatus== OPEN3 && mouseY < 111) {
          if (!has("cassette tape")) {
            get("cassette tape");
            work1 = 4;
            call(lgc.ScoreHandler);
            print("You take the mix tape, wondering "
                  "what might be on it.");
            set.loop(oFileDrawer, 3);
            set.cel(oFileDrawer, 2);
            force.update(oFileDrawer);      
          }
          goto(doneclick);
        }
        set.loop(oFileDrawer, 3);
        set.cel(oFileDrawer, 2);
        reverse.loop(oFileDrawer, fFileDrawer);
        storeFileDrStatus= CLOSING;
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }

    [ blocked file cabinets
    if (mouseX > 69 && mouseX < 75 && mouseY > 85 && mouseY < 99) {
      set(fCheckClick);
    }
    if (mouseX > 84 && mouseX < 93 && mouseY > 83 && mouseY < 113) {
      set(fCheckClick);
    }
    if (fCheckClick) {
      reset(fCheckClick);
      if (posn(ego, 77, 110, 96, 119)) {
        print("You can't open the drawers in these cabinets. They are "
              "blocked by the file cabinet in the front.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ overhead light - always out of reach
    if (mouseX > 71 && mouseX < 81 && mouseY > 57 && mouseY < 64) {
      print("The light is way beyond your reach.");
      goto(doneclick);
    }

    [ wall mirror - always beyond reach
    if (mouseX > 78 && mouseX < 93 && mouseY > 45 && mouseY < 76) {
      print("The wall mirror is too high to reach.");
      goto(doneclick);
    }
    
    [ desk drawer when closed 100, 102, 106, 108
    if (mouseX > 99 && mouseX < 107 && mouseY > 101 && mouseY < 109 &&
        storeDeskDrStatus == CLOSED) {
      [ is ego close enough?
      if (obj.in.box(ego, 94, 121, 112, 126)) {
        set.loop(oDeskDrawer, 5);
        set.cel(oDeskDrawer, 0);
        draw(oDeskDrawer);
        end.of.loop(oDeskDrawer, fDeskDrawer);
        storeDeskDrStatus = OPENING;
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ desk drawer when open
    if (posn(oDeskDrawer, ON.OBJ, 0, &mouseX, &mouseY) && 
        storeDeskDrStatus != CLOSING) {
      [ is ego close enough?
      if (obj.in.box(ego, 94, 121, 112, 126)) {
        [ contents check
        if (storeFileDrStatus== OPEN && mouseY < 111) {
          [ nothing in this drawer
          goto(doneclick);
        }
        set.loop(oDeskDrawer, 5);
        set.cel(oDeskDrawer, 4);
        reverse.loop(oDeskDrawer, fDeskDrawer);
        storeDeskDrStatus= CLOSING;
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ birdcage - always out of reach
    if (mouseX > 108 && mouseX < 116 && mouseY > 33 && mouseY < 52) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      if ((work1 == 69 || work1 == 77)) { [69/77=magenta/lt magenta vis & red pri)
        print("The bird cage is too high to reach.");
      }
      goto(doneclick);
    }

    [ desk mirror
    if (mouseX > 98 && mouseX < 108 && mouseY > 64 && mouseY < 96) {
      if (posn(ego, 93, 114, 113, 124)) {
        print("After touching the mirror, all you did was leave behind "
              "some smudged fingerprints.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }

    [ pitcher and basin
    if (mouseX > 117 && mouseX < 128 && mouseY > 91 && mouseY < 106) {
      if (posn(ego, 103, 115, 119, 126)) {
        print("You have no need for the pitcher and basin.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }

    [ banner - always out of reach
    if (mouseX > 121 && mouseX < 140 && mouseY > 46 && mouseY < 115) {
      if (posn(ego, 108, 120, 136, 141)) {
        print("The banner is just out of reach.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ cabinet door (when closed)
    if ((vDoorState == CLOSED || vDoorState == CLOSING)) {
      if (mouseX > 129 && mouseX < 133 && mouseY > 118 && mouseY < 143) {
        set(fCheckClick);
      }
      if (mouseX > 132 && mouseX < 137 && mouseY > 122 && mouseY < 147) {
        set(fCheckClick);
      }
      if (fCheckClick) {
        reset(fCheckClick);
        [ is ego close enough?
        if ((posn(ego, 120, 146, 132, 150) || posn(ego, 122, 151, 131, 156))) {
          [ if closed
          if (vDoorState == CLOSED) {
            [ animate it first
            if (has("music sheet")) {
              set.loop(oCabinetDoor, 6);
            } else {
              set.loop(oCabinetDoor, 12);
            }
            set.cel(oCabinetDoor, 0);
            position(oCabinetDoor, 125, 149);
            set.priority(oCabinetDoor, 13);
            draw(oCabinetDoor);
          }
          vDoorState = OPENING;
          end.of.loop(oCabinetDoor, fDoor);
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
    }

    [ cabinet door (when opened)
    if ((vDoorState == OPEN || vDoorState == OPENING)) {
      [ item check
      if (vDoorState == OPEN && mouseX > 129 && mouseX < 134 && mouseY > 120 && mouseY < 143) {
        if (!has("music sheet")) {
          get("music sheet");
          print("You take the sheet from the cabinet.");
          [ + 3 points
          work1 = 3;
          call(lgc.ScoreHandler);
          [ remove it from screen
          set.loop(oCabinetDoor, 6);
        }
        goto(doneclick);
      }
      if (mouseX > 124 && mouseX < 130 && mouseY > 118 && mouseY < 137) {
        set(fCheckClick);
      }
      if (mouseX > 133 && mouseX < 137 && mouseY > 126 && mouseY < 150) {
        set(fCheckClick);
      }
      if (fCheckClick) {
        reset(fCheckClick);
        [ if open, ego must be close enough, so just close it without further checks
        reverse.loop(oCabinetDoor, fDoor);
        goto(doneclick);
      }
    }
    
    [ chest drawer when closed 115, 110, 118, 126 and 119, 116, 122, 131
    if (storeChestDrStatus == CLOSED) {
      if (mouseX > 114 && mouseX < 119 && mouseY > 109 && mouseY < 127) {
        set(fCheckClick);
      }
      if (mouseX > 118 && mouseX < 123 && mouseY > 115 && mouseY < 132) {
        set(fCheckClick);
      }
      if (fCheckClick) {
        reset(fCheckClick);
        [ is ego close enough?
        if ((obj.in.box(ego, 107, 122, 117, 129) || 
             obj.in.box(ego, 108, 130, 120, 138))) {
          position(oChestDrawer, 109, 129);
          set.loop(oChestDrawer, 4);
          set.cel(oChestDrawer, 0);
          draw(oChestDrawer);
          end.of.loop(oChestDrawer, fChestDrawer);
          storeChestDrStatus = OPENING;
          [ if ego is too close, backup
          work1 = egoY;  [108,122 - 115,136 dx/dy=7/14=1/2
                         [101       108
          work1 -= 122; [ adjust for ego width
          work1 /= 2; 
          work1 += 101; [ x = (y - y0)*dx/dy + x0
                       
          if (egoY >= work1) {
            work1 -= 2;
            work2 = 2;
            set.loop(ego, 0);
            fix.loop(ego);
            move.obj.v(ego, work1, egoY, work2, fEgoBackup);
          }
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
    }
    
    [ chest drawer when open
    if (posn(oChestDrawer, ON.OBJ, 0, &mouseX, &mouseY) && 
        storeChestDrStatus!= CLOSING) {
      [ is ego close enough?
      if ((obj.in.box(ego, 101, 122, 114, 130) || 
           obj.in.box(ego, 104, 131, 120, 138))) {
        [ contents check
        if (storeChestDrStatus== OPEN && mouseY < 111) {
          goto(doneclick);
        }
        set.loop(oChestDrawer, 4);
        set.cel(oChestDrawer, 3);
        reverse.loop(oChestDrawer, fChestDrawer);
        storeChestDrStatus= CLOSING;
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ bureau
    if (mouseX > 113 && mouseX < 136 && mouseY > 99 && mouseY < 140) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      block(BIT.AND, 0, &work1, 15); [ bit.and(BYVAL, VAR, AMOUNT)
      if ((work1 == LT_RED || work1 == BROWN || work1 == YELLOW)) {
        if (posn(ego, 103, 115, 119, 126)) {
          print("There is nothing else of interest in the bureau.");
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
    }
    
    [ wardrobe - always out of reach 96, 49, 119, 100
    if (mouseX > 95 && mouseX < 120 && mouseY > 48 && mouseY < 101) {
      print("The wardrobe is blocked by the desk. You can't reach it.");
      goto(doneclick);
    }

    [ robots
    if (mouseX > 135 && mouseX < 147 && mouseY > 132 && mouseY < 159) {
      if (posn(ego, 127, 150, 141, 167)) {
        print("These robots are far too large to carry, even with your "
              "enormously oversized pockets.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }

    [ chair
    if (mouseX > 77 && mouseX < 96 && mouseY > 126 && mouseY < 154) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      work1 /= 16; [ get priority color only
      if ((work1 == LT_MAGENTA || work1 == BLACK)) {
        if (posn(ego, 71, 142, 100, 156)) {
          print("You gently caress the soft, cushiony chair.");
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
    }
    
    [ arcade
    if (mouseX > 53 && mouseX < 63 && mouseY > 125 && mouseY < 150) {
      [ check pixel color
      block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
      block(BIT.AND, 0, &work1, 15); [ bit.and(BYVAL, VAR, AMOUNT)
      if ((work1 == BLACK || work1 == BLUE || work1 == LT_BLUE)) {
        if (posn(ego, 56, 140, 66, 156)) {
          print("It's not plugged in. There's nothing you can do with "
                "the arcade.");
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
    }

    [ chest when closed
    if (posn(oChest, ON.OBJ, 1, &mouseX, &mouseY) && 
       (storeChestStatus == CLOSED || storeChestStatus == CLOSING)) {
      [ is ego close enough?
      if (obj.in.box(ego, 111, 147, 138, 166)) {
        if (egoX > 125) {
          print("The chest opens from the other side.");
          goto(doneclick);
        }
        [ open it
        set.cel(oChest, 0);
        end.of.loop(oChest, fChest);
        storeChestStatus= OPENING;
        if (!scoreChest) {
          set(scoreChest);
          work1 = 1;
          call(lgc.ScoreHandler);
        }
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ chest when open
    if (posn(oChest, ON.OBJ, 1, &mouseX, &mouseY) && 
       (storeChestStatus == OPEN || storeChestStatus == OPENING)) {
      [ is ego close enough?
      if (obj.in.box(ego, 111, 147, 138, 166)) {
        [ contents check 121,147 128,152
        if (storeChestStatus == OPEN && mouseX > 120 && mouseX < 129 && mouseY > 146 && mouseY < 166) {
          if (!has("scroll")) {
            get("scroll");
            work1 = 3;
            call(lgc.ScoreHandler);
            print("You carefully lift the scroll out of the chest.");
            set.loop(oChest, 1);
            force.update(oFileDrawer);      
          }
          goto(doneclick);
        }
        [ close it
        set.cel(oChest, 3);
        reverse.loop(oChest, fChest);
        storeChestStatus= CLOSING;
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ coffin (when closed)
    if ((vCoffinState == CLOSED || vCoffinState == CLOSING)) {
      if (mouseX > 27 && mouseX < 54 && mouseY > 142 && mouseY < 157) {
        :openCoffin
        [ is ego on wrong side?
        if (posn(ego, 28, 141, 54, 149)) {
          print("The coffin opens from the other side.");
          goto(doneclick);
        }
        [ is ego close enough?
        if (posn(ego, 24, 156, 54, 161)) {
          [ if closed
          if (vCoffinState == CLOSED) {
            [ draw it first
            set.loop(oCoffin, 2);
            set.cel(oCoffin, 0);
            set.priority(oCoffin, 14);
            position(oCoffin, 28, 155);
            draw(oCoffin);
          }
          vCoffinState = OPENING;
          end.of.loop(oCoffin, fCoffin);
        } else {
          print("Move closer.");
        }
        goto(doneclick);
      }
    }
    [ coffin and lid (when open)
    if ((vCoffinState == OPEN || vCoffinState == OPENING)) {
      if (mouseX > 27 && mouseX < 54 && mouseY > 136 && mouseY < 147) {
        [ if open, ego must be close enough, so just close it without further checks
        :closeCoffin
        reverse.loop(oCoffin, fCoffin);
        goto(doneclick);
      }
    }
    
    [ coat hanger
    if (mouseX > 14 && mouseX < 24 && mouseY > 90 && mouseY < 126) {
      if (posn(ego, 19, 135, 28, 147)) {
        print("The coat rack is just outside of your reach.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
    
    [ boxes
    if (mouseX > 9 && mouseX < 30 && mouseY > 126 && mouseY < 159) {
      set(fCheckClick);
    }
    if (mouseX > 53 && mouseX < 62 && mouseY > 150 && mouseY < 160) {
      set(fCheckClick);
    }
    if (mouseX > 69 && mouseX < 81 && mouseY > 146 && mouseY < 160) {
      set(fCheckClick);
    }
    if (mouseX > 90 && mouseX < 103 && mouseY > 152 && mouseY < 160) {
      set(fCheckClick);
    }
    if (mouseX > 104 && mouseX < 116 && mouseY > 143 && mouseY < 161) {
      set(fCheckClick);
    }
    if (fCheckClick) {
      reset(fCheckClick);
      if (posn(ego, 10, 154, 119, 167)) {
        print("The boxes are all taped securely shut. You have no "
              "way to open them.");
      } else {
        print("Move closer.");
      }
      goto(doneclick);
    }
  }

  :doneclick
  set.key(-1, -1, cLeftClick); [ clear.controller(cLeftClick);
}

[ check coffin cycling
if (fCoffin) {
  reset(fCoffin);
  if (vCoffinState == OPENING) {
    vCoffinState = OPEN;
  } else {
    vCoffinState = CLOSED;
    erase(oCoffin);
  }
}

[ close coffin if ego moves away
if ((vCoffinState == OPEN || vCoffinState == OPENING)) {
  if (!posn(ego, 24, 156, 54, 161)) {
    vCoffinState = CLOSING;
    reverse.loop(oCoffin, fCoffin);
  }
}

[ check cabinet door cycling
if (fDoor) {
  reset(fDoor);
  if (vDoorState == OPENING) {
    vDoorState = OPEN;
  } else {
    vDoorState = CLOSED;
    erase(oCabinetDoor);
  }
}

[ close cabinet door if ego moves away
if ((vDoorState == OPEN || vDoorState == OPENING)) {
  if (!posn(ego, 120, 146, 132, 156)) {
    vDoorState = CLOSING;
    reverse.loop(oCabinetDoor, fDoor);
  }
}

[ file drawer
if (fFileDrawer) {
  reset(fFileDrawer);
  if (storeFileDrStatus == CLOSING) {
    erase(oFileDrawer);
    storeFileDrStatus = CLOSED;
  } else {
    get.posn(oFileDrawer, work1, work2);
    if (work2 == 100) {
      storeFileDrStatus = OPEN1;
    } else {
      if (work2 == 109) {
        storeFileDrStatus = OPEN2;
      } else {
        storeFileDrStatus = OPEN3;
        if (!has("cassette tape")) {
          set.loop(oFileDrawer, 11);
          set.cel(oFileDrawer, 0);
          force.update(oFileDrawer);
        }
      }
    }
  }
}

[ desk drawer
if (fDeskDrawer) {
  reset(fDeskDrawer);
  if (storeDeskDrStatus == CLOSING) {
    erase(oDeskDrawer);
    storeDeskDrStatus = CLOSED;
  } else {
    storeDeskDrStatus = OPEN;
  }
}

[ chest drawer
if (fChestDrawer) {
  reset(fChestDrawer);
  if (storeChestDrStatus == CLOSING) {
    erase(oChestDrawer);
    storeChestDrStatus = CLOSED;
    [ remove conditional line
    s12 = "F1F204F6727C6D7C72887888FF";
    block(PIC.DRAW, 0, &s12, 0); [ pic.draw(FILLSTYLE, sCMDS)
  } else {
    storeChestDrStatus = OPEN;
    [ add conditional line
    s12 = "F1F201F6727C6D7C72887888FF";
    block(PIC.DRAW, 0, &s12, 0); [ pic.draw(FILLSTYLE, sCMDS)
  }
}

[ chest
if (fChest) {
  reset(fChest);
  if (storeChestStatus == CLOSING) {
    storeChestStatus = CLOSED;
  } else {
    storeChestStatus = OPEN;
  }
}

[ ego backup
if (fEgoBackup) {
  reset(fEgoBackup);
  release.loop(ego);
}

[*****
:exit              [  test for leaving the room
[*****

[ add exit checks here (or use layout editor)

if (egoHitSpecial && egoX < 30) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  new.room(rm.ControlRoom);  [ ##LE001##
}
return();

[ **************************************
[ DECLARED MESSAGES
[ **************************************
#message 1 "%g20"
#message 2 "%g21"
#message 3 "%g22"
#message 4 "%g23"
#message 5 "%g24"
#message 6 "%g25"
#message 7 "%g26"
#message 8 "%g27"
