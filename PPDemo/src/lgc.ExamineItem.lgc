[*********************************************************************
[ 
[ lgc.ExamineItem
[ 
[ Handles functions to display images of items in player's inventory.
[ Certain items need an entire picture resource to display the item
[ for examination. This logic handles the setup needed to display the
[ item's picture without interfering with current game state.
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
[ none

[***************************************
[ MAIN CODE BLOCK 
[***************************************

[ stop the game clock while examining items
block(PAUSE.CLOCK, PAUSE_CLOCK, 0, 0); [ pause.clock(MODE)

[ fade out current picture
block(FADE.OUT, BYNUM, FADECOUNT, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)

[ no script entries for this sequence
set(noScript);
[ no regular input either
prevent.input();
status.line.off();

[ save current colors
set.text.attribute(PUSH.ATTRIBUTES, 0); [ push.attributes()

[ use default mouse cursor
cursoricon = C_DEFAULT;
set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)

[ draw the picture for selected item
if (work1 == 107) {
  work2 = pic.ComboList;
}
if (work1 == 110) {
  work2 = pic.FolderInterior;
}
if (work1 == 112) {
  work2 = pic.MusicSheet;
}
if (work1 == 113) {
  work2 = pic.PQList;
}
load.pic(work2);
draw.pic(work2);
discard.pic(work2);

[ hide objects while showing items
block(HIDE.OBJS, HIDE_OBJS, 0, 0); [ hide.objs(MODE)
show.pic();

[ some items need text added
if (work1 == 110) {
  set.text.attribute(PUSH.ATTRIBUTES, 0); [ push.attributes()
  set.text.attribute(RED, WHITE);
  display(6, 47, "Drew, don't");
  display(7, 47, "forget to");
  display(8, 47, "change the");
  display(9, 47, "reset code to");
  display(10, 47, "Snd4chan$");
  set.text.attribute(0, POP.ATTRIBUTES); [ pop.attributes()
}
if (work1 == 113) {
  [ add text after drawing the picture
  [ 4     5         6   X      
  [ 4567890123456789012345
  [      Police Quest 
  [ X     Sequel Ideas
  [ X
  [ X   Hollywood Heists
  [ XX
  [ XX   The Vengance 
  [ XX      
  [ XX   The Ofrocc
  [ XXX     Infiltration
  [ XXX  PqII:10-36
  [ XXX
  [ XXXX  LA Burning
  [ 4     5         6       X      
  [ 4567890123456789012345
  [PW: PqII:10-36
  
  set.text.attribute(BLACK, WHITE);
  display(5, 33, "Police Quest");
  display(6, 34, "Sequel Ideas");
  [
  display(8, 32, "Hollywood Heists");
  [
  display(10, 33, "The Vengance");
  [
  display(12, 33, "The Ofrocc");
  display(13, 36, "Infiltration");
  display(14, 34, "PqII:10-36");
  [
  display(16, 34, "LA Burning");
}

[ fade in
block(FADE.IN, BYNUM, FADECOUNT, 0); [ fade.in(FN_MODE, DELAY, COLOR)
lastChar = 0;
:waitforit
if (!have.key()) {
  goto(waitforit);
}

[ fade out
block(FADE.OUT, BYNUM, FADECOUNT, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "%g30"
#message 2 "%g31"
#message 3 "%g32"
#message 4 "%g33"
#message 5 "%g34"
#message 6 "%g35"
#message 7 "%g36"
#message 8 "%g37"
