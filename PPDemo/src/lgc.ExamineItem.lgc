[ ********************************************************************
[ 
[ lgc.ExamineItem
[ 
[ displays a custom close up picture for certain items
[ selectedItem variable has index (offset by 100) of 
[ desired item
[
[ ********************************************************************

[ stop the game clock while examining items
block(PAUSE.CLOCK, 1, 0, 0); [ pause.clock(MODE)

[ fade out current picture
block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)

[ no script entries for this sequence
set(noScript);
[ no regular input either
prevent.input();
status.line.off();

[ save current colors
set.text.attribute(-1, 0);

[ use default mouse cursor
cursoricon = C_DEFAULT;
set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);

[ draw the picture for selected item
if (selectedItem == 107) {
  work1 = pic.ComboList;
}
if (selectedItem == 110) {
  work1 = pic.FolderInterior;
}
if (selectedItem == 112) {
  work1 = pic.MusicSheet;
}
if (selectedItem == 113) {
  work1 = pic.PQList;
}
load.pic(work1);
draw.pic(work1);
discard.pic(work1);

[ hide objects while showing items
block(HIDE.OBJS, 1, 0, 0); [ hide.objs(MODE)
show.pic();

[ some items need text added
if (selectedItem == 110) {
  set.text.attribute(-1, 0);
  set.text.attribute(RED, WHITE);
  display(6, 47, "Drew, don't");
  display(7, 47, "forget to");
  display(8, 47, "change the");
  display(9, 47, "reset code to");
  display(10, 47, "Snd4chan$");
  set.text.attribute(0, -1);
}
if (selectedItem == 113) {
  [ add text after drawing the picture
  [ 4     5         6   X      
  [ 4567890123456789012345
  [      Police Quest 
  [ X     Sequel Ideas
  [ X
  [ X   Hollywood Heists
  [ XX
  [ XX   The Vengance 
  [ XX      
  [ XX   The Ofrocc
  [ XXX     Infiltration
  [ XXX  PqII:10-36
  [ XXX
  [ XXXX  LA Burning
  [ 4     5         6       X      
  [ 4567890123456789012345
  [PW: PqII:10-36
  
  set.text.attribute(BLACK, WHITE);
  display(5, 33, "Police Quest");
  display(6, 34, "Sequel Ideas");
  [
  display(8, 32, "Hollywood Heists");
  [
  display(10, 33, "The Vengance");
  [
  display(12, 33, "The Ofrocc");
  display(13, 36, "Infiltration");
  display(14, 34, "PqII:10-36");
  [
  display(16, 34, "LA Burning");
}

[ fade in
block(FADE.IN, 0, FADECOUNT, 0); [ fade.in(BYVAR, DELAY, COLOR)
lastChar = 0;
:waitforit
if (!have.key()) {
  goto(waitforit);
}

[ fade out
block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)

return();

[ **************************************
[ DECLARED MESSAGES
[ **************************************
#message 1 "%g20"
#message 2 "%g21"
#message 3 "%g22"
#message 4 "%g23"
#message 5 "%g24"
#message 6 "%g25"
#message 7 "%g26"
#message 8 "%g27"
