[*********************************************************************
[ 
[ lgc.PowerPack
[ 
[ The main Power Pack logic. Needs to be called from main logic as the
[ first thing when the game starts. It handles identifying and loading
[ the appropriate power pack patch.
[
[ If this instance of AGI does not support the Power Pack, user is
[ informed of the problem and the game immediately quits.
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
[ none

[***************************************
[ MAIN CODE BLOCK 
[***************************************

[ confirm platform is PC
if (machineType != PC) {
  load.logics(lgc.Error);
  print("The AGI Power Pack Demo will only run on a PC (or in "
        "DosBOX emulating a PC). No other platforms are "
        "supported.\nAborting...");
  quit(1);
}
      
[ confirm this is a version that can be patched
call(lgc.Version2Check);
if (!IntVersion) {
  load.logics(lgc.Error);
  print("Invalid AGI version. Power Pack Demo requires a valid MSDOS "
        "interpreter running on a PC, or in DOSBox. ScummVM and NAGI "
        "are not supported.\n\nAborting...");
  quit(1);
}
if ((IntVersion == VERSION_2089 || IntVersion == VERSION_2272)) {
  [ these versions not supported
  load.logics(lgc.Error);
  print("The Power Pack does work with version 2.089 and 2.272, however "
        "this demo game does not support them. Please use a later "
        "version 2 interpreter to run this demo game.");
  quit(1);
}

[ run the patch for the detected AGI version
if (IntVersion == VERSION_2411) {
  call(lgc.mod2411);
  goto(continue);
}
if (IntVersion == VERSION_2425) {
  call(lgc.mod2425);
  goto(continue);
}
if (IntVersion == VERSION_2426) {
  call(lgc.mod2426);
  goto(continue);
}
if (IntVersion == VERSION_2435) {
  call(lgc.mod2435);
  goto(continue);
}
if (IntVersion == VERSION_2439) {
  call(lgc.mod2439);
  goto(continue);
}
if (IntVersion == VERSION_2440) {
  call(lgc.mod2440);
  goto(continue);
}
if (IntVersion == VERSION_2903) {
  call(lgc.mod2903);
  goto(continue);
}
if (IntVersion == VERSION_2911) {
  call(lgc.mod2911);
  goto(continue);
}
if (IntVersion == VERSION_2912) {
  call(lgc.mod2912);
  goto(continue);
}
if (IntVersion == VERSION_2915) {
  call(lgc.mod2915);
  goto(continue);
}
if (IntVersion == VERSION_2917) {
  call(lgc.mod2917);
  goto(continue);
}
if (IntVersion == VERSION_2936) {
  call(lgc.mod2936);
  goto(continue);
}
:continue

[ confirm video, mouse and sound settings
if (monitorType != EGA) {
  load.logics(lgc.Error);
  print("The AGI Power Pack Demo will only run in EGA mode on a PC "
        "(or in DosBOX).\n\nAborting...");
  quit(1);
}
if (!enableDblClick) {
  load.logics(lgc.Error);
  print("The AGI Power Pack Demo will only run on a PC (or in DosBOX) "
        "with a mouse driver installed.\n\nAborting...");
  quit(1);
}
if (numberOfVoices != 4) {
  load.logics(lgc.Error);
  print("The AGI Power Pack Demo will only run on a PC (or in DosBOX) "
        "with an FM synthesizer card (Adlib or compatible) and a MIDI "
        "device enabled.\n\nAborting...");
  quit(1);
}

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
