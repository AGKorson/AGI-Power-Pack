[*********************************************************************
[ 
[ lgc.Walkman
[ 
[ close up of the walkman; player can press start/pause,
[ rewind, fast forward and eject
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
#define helpText      v240
#define   H_BLANK       4
#define   H_EXIT        7
#define   H_FASTFWD     8
#define   H_REWIND      9
#define   H_STOP_EJ    10
#define   H_PLAY       11
#define   H_COVER      12
#define   H_INSIDE     13
#define tmpCursor     v241
#define counter1      v242
#define counter2      v243

#define coverClosed   f240
#define mouseDown     f241
#define flipCassette  f242

[***************************************
[ MAIN CODE BLOCK 
[***************************************

[ stop the game clock while examining items
block(PAUSE.CLOCK, PAUSE_CLOCK, 0, 0); [ pause.clock(MODE)

[ no script entries for this sequence
set(noScript);
[ no regular input either
prevent.input();
status.line.off();

[ fade out until done loading
block(FADE.OUT, BYNUM, 5, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)

set.text.attribute(PUSH.ATTRIBUTES, 0); [ push.attributes()
set.text.attribute(DK_GRAY, BLACK);

[ use default mouse cursor
cursoricon = C_DEFAULT;
tmpCursor = C_DEFAULT;
set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)

[ hide objects while showing manual
block(HIDE.OBJS, HIDE_OBJS, 0, 0); [ hide.objs(MODE)

[ display correct walkman picture
work1 = pic.Walkman;
load.pic(work1);
draw.pic(work1);
discard.pic(work1);
if (cassetteLoaded) {
  work1 = ovl.Walkman2;
} else {
  work1 = ovl.Walkman1;
}
load.pic(work1);
overlay.pic(work1);
discard.pic(work1);
show.pic();

[ cover always starts closed
set(coverClosed);

[ set sound parameters
call(lgc.SetSound);

[ if currently playing, reload and restart music
if ( currentTrack > 0 && currentTrack < 128) {
  block(SND.V.FN, LOAD.SOUND.V, &currentTrack, 0); [ load.sound.v(vSNDNUM)
  work1 = &musicDone;
  block(SND.V.FN, SOUND.V, &currentTrack, &work1); [ sound.v(vSNDNUM, vDONEFLAG)
}

[ fade back in
block(FADE.IN, BYNUM, 5, BLACK); [ fade.in(FN_MODE, DELAY, COLOR)

[ main input loop; wait until a key is pressed
:waitforit
  lastChar = 0;

  [ do nothing if flipping cassette
  if (flipCassette) {
    --counter2;
    if (counter2 == 0) {
      counter2 = 4;
      --counter1;
    }
    
    if (counter1 == 0) {
      reset(flipCassette);
      set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
    } else {
      goto(waitforit);
    }
  }
  
  [ get button by priority color
  if (mouseY < 168) {
    block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)
    work1 /= 16;
    if (work1 != helpText) {
      [ show button help if over a button
      helpText = work1;
      if (work1 == H_BLANK) {
        clear.lines(23, 23, BLACK);
        tmpCursor = C_DEFAULT;
      }
      if (work1 == H_EXIT) {
        display(23, 32, "return to game");
        tmpCursor = C_DEFAULT;
      }
      if (work1 == H_FASTFWD) {
        display(23, 33, "fast forward");
        tmpCursor = C_USE;
      }
      if (work1 == H_REWIND) {
        display(23, 36, "rewind");
        tmpCursor = C_USE;
      }
      if (work1 == H_STOP_EJ) {
        display(23, 34, "stop/eject");
        tmpCursor = C_USE;
      }
      if (work1 == H_PLAY) {
        display(23, 37, "play");
        tmpCursor = C_USE;
      }
      if (work1 == H_COVER) {
        clear.lines(23, 23, BLACK);
        if (coverClosed) {
          tmpCursor = C_DEFAULT;
        } else {
          tmpCursor = C_USE;
        }
      }
      if (work1 == H_INSIDE) {
        clear.lines(23, 23, BLACK);
        if (has("cassette tape")) {
          tmpCursor = C_TAPE;
          display(23, 28, "(press SPACE to flip)");
        } else {
          if (cassetteLoaded) {
            tmpCursor = C_USE;
          } else {
            tmpCursor = C_DEFAULT;
          }
        }
      }
    }
  }
  
  if (tmpCursor != cursoricon) {
    cursoricon = tmpCursor;
    set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
  }

  [ check for track end
  if (musicDone) {
    reset(musicDone);
    [ discard current track
    block(SND.V.FN, DISCARD.SOUND.V, &currentTrack, 0); [ discard.sound.v(vSNDNUM)
    [ next track
    ++currentTrack;
    if ((currentTrack == 43 || currentTrack == 85)) {
      currentTrack = 0;
    }
    [ if another track is now active, play it
    if (currentTrack > 0) {
      block(SND.V.FN, LOAD.SOUND.V, &currentTrack, 0); [ load.sound.v(vSNDNUM)
      work2 = &musicDone;
      block(SND.V.FN, SOUND.V, &currentTrack, &work2); [ sound.v(vSNDNUM, vDONEFLAG)
    }
  }
  
[[ loop until button clicked or key pressed
if ((mouseBtn != 0 || have.key())) {
} else {
  goto(waitforit);
}
[if (!have.key() && mouseBtn == 0) {
[  goto(waitforit);
[}

if (lastChar == 27) {
  goto(exit);
}

[ space to flip cassette
[if (lastChar == 32 && has("cassette tape") && !coverClosed) {
if (lastChar == 32 && cursoricon == C_TAPE) {
  toggle(cassetteSideB);
  [ adjust position
  if (currentTrack == 0) {
    [ move to beginning of new side
    if (cassetteSideB) {
      currentTrack = -43;
    } else {
      currentTrack = -1;
    }
  } else {
    [ -1=-85(0); -2=-84; ... -41=-45; -42=-44; -43=-43(0)
    [ 
    [ new=-86-old
    work1 = -86;
    if ((work1 == -43 || work1 == -85)) {
      work1 = 0;
    }
    work1 -= currentTrack;
    currentTrack = work1;
  }
  [ show a wait cursor for a bit
  set(flipCassette);
  counter1 = 255;
  counter2 = 3;
  cursoricon = C_WAIT;
  set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
  goto(waitforit);
}

[ clear mouse flag when button is up
if (mouseBtn == 0) {
  reset(mouseDown);
}

[ check for mouse click (mouse press, since events aren't 
[ triggered unless main loop is used)
if (mouseBtn == 1 && !mouseDown) {
  set(mouseDown);
  
  [ exit
  if (work1 == 7) {
    goto(exit);
  }
  
  [ fast forward
  if (work1 == 8) {
    [ if cassette is loaded and cover closed
    if (cassetteLoaded && coverClosed) {
      [ advance to next track, based on side
      [ 1 - 42 means playing side A
      [ -1 to -42 means paused/stopped side A
      [ 43 - 84 means playing side B
      [ -43 to -84 means paused side B
      [ 0 means end of tape
      if (currentTrack > 0 && currentTrack < 127) {
        [ stop current track
        stop.sound();
        block(SND.V.FN, DISCARD.SOUND.V, &currentTrack, 0); [ discard.sound.v(vSNDNUM)
        [ move to next track
        ++currentTrack;
        if ((currentTrack == 43 || currentTrack == 85)) {
          currentTrack = 0;
        }
        [ if another track is now active, play it
        if (currentTrack > 0) {
          block(SND.V.FN, LOAD.SOUND.V, &currentTrack, 0); [ load.sound.v(vSNDNUM)
          work2 = &musicDone;
          block(SND.V.FN, SOUND.V, &currentTrack, &work2); [ sound.v(vSNDNUM, vDONEFLAG)
        }
      } else {
        if (currentTrack > 128) {
          [ advance but don't play
          --currentTrack;
          if ((currentTrack == -43 || currentTrack == -85)) {
            currentTrack = 0;
          }
        }
      }
    }
  }
  
  [ rewind
  if (work1 == 9) {
    [ if cassette is loaded and cover closed
    if (cassetteLoaded && coverClosed) {
      [ go back to previous track, based on side
      [ 1 - 42 means playing side A
      [ -1 to -42 means paused/stopped side A
      [ 43 - 84 means playing side B
      [ -43 to -84 means paused side B
      [ 0 means end of tape
      if (currentTrack > 0 && currentTrack < 127) {
        [ stop current track
        stop.sound();
        block(SND.V.FN, DISCARD.SOUND.V, &currentTrack, 0); [ discard.sound.v(vSNDNUM)
        [ move to previous track
        --currentTrack;
        if (cassetteSideB) {
          if (currentTrack == 42) {
            currentTrack = 43;
          }
        } else {
          if (currentTrack == 0) {
            currentTrack = 1;
          }
        }
        [ play active track
        block(SND.V.FN, LOAD.SOUND.V, &currentTrack, 0); [ load.sound.v(vSNDNUM)
        work2 = &musicDone;
        block(SND.V.FN, SOUND.V, &currentTrack, &work2); [ sound.v(vSNDNUM, vDONEFLAG)
      } else {
        [ paused - rewind but don't play
        if (currentTrack == 0) {
          [ at end; back up
          if (cassetteSideB) {
            currentTrack = -84;
          } else {
            currentTrack = -42;
          }
        } else {
          [ rewind by moving closer to start
          ++currentTrack;
[          if (currentTrack == 0) {
[            currentTrack = -1;
[          }
          if (currentTrack == -42) {
            currentTrack = -43;
          }
        }
      }
    }
  }

  [ stop/eject
  if (work1 == 10) {
    if (currentTrack > 0 && currentTrack < 128) {
      currentTrack *= -1;
      stop.sound();
      reset(musicDone);
      block(SND.V.FN, DISCARD.SOUND.V, &currentTrack, 0); [ discard.sound.v(vSNDNUM)
    } else {
      if (coverClosed) {
        reset(coverClosed);
        if (cassetteLoaded) {
          work2 = ovl.Walkman4;
        } else {
          work2 = ovl.Walkman3;
        }
        load.pic(work2);
        overlay.pic(work2);
        discard.pic(work2);
        [ if loaded, add correct side marker
        if (cassetteLoaded && !coverClosed) {
          if (cassetteSideB) {
            [ add a 'B'
            s20 = "F000F3F75379B90A19111002FF";
          } else {
            [ add an 'A'
            s20 = "F000F3F7537CBC3AF7527802FF";
          }
          block(PIC.DRAW, AGI_FILL, &s20, 0); [ pic.draw(FILLSTYLE, sCMDS)
        }
        show.pic();
      }
    }
  }
  
  [ play
  if (work1 == 11) {
    [ cassette loaded, cover closed and currently paused/stopped
    if (cassetteLoaded && coverClosed && currentTrack > 128) {
      if (!scoreWalkman) {
        set(scoreWalkman);
        work1 = 4;
        call(lgc.ScoreHandler);
      }
      [ always turn sound on if walkman is in use
      if (!soundOn) {
        set(soundOn);
      }
      [ enable current track
      currentTrack *= -1;
      [ set parameters
      call(lgc.SetSound);
      [ now begin playing current track
      block(SND.V.FN, LOAD.SOUND.V, &currentTrack, 0); [ load.sound.v(vSNDNUM)
      work2 = &musicDone;
      block(SND.V.FN, SOUND.V, &currentTrack, &work2); [ sound.v(vSNDNUM, vDONEFLAG)
    }
  }
  
  [ cover
  if (work1 == 12) {
    if (!coverClosed) {
      set(coverClosed);
      if (cassetteLoaded) {
        work2 = ovl.Walkman2;
      } else {
        work2 = ovl.Walkman1;
      }
      load.pic(work2);
      overlay.pic(work2);
      discard.pic(work2);
      show.pic();
    }
  }
  
  [ tape/cavity - cover must be open
  if (work1 == 13) {
    if (cassetteLoaded) {
      work2 = ovl.Walkman3;
      get("cassette tape");
      reset(cassetteLoaded);
      load.pic(work2);
      overlay.pic(work2);
      discard.pic(work2);
      show.pic();
      tmpCursor = C_TAPE;
      display(23, 28, "(press SPACE to flip)");
    } else {
      if (has("cassette tape")) {
        work2 = ovl.Walkman4;
        drop("cassette tape");
        set(cassetteLoaded);
        load.pic(work2);
        overlay.pic(work2);
        discard.pic(work2);
        if (cassetteSideB) {
          [ add a 'B'
          s20 = "F000F3F75379B90A19111002FF";
        } else {
          [ add an 'A'
          s20 = "F000F3F7537CBC3AF7527802FF";
        }
        block(PIC.DRAW, AGI_FILL, &s20, 0); [ pic.draw(FILLSTYLE, sCMDS)
        show.pic();
        tmpCursor = C_USE;
        clear.lines(23, 23, BLACK);
      }
    }
  }
}

[ go back to the input loop
goto(waitforit);

:exit
[ if playing, stop before changing room
if (currentTrack > 0 && currentTrack < 128) {
  stop.sound();
  reset(musicDone);
  block(SND.V.FN, DISCARD.SOUND.V, &currentTrack, 0); [ discard.sound.v(vSNDNUM)
}

[ restore script entries
reset(noScript);

[ fade out during the switch
block(FADE.OUT, BYNUM, 5, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)

[ clear the help text
clear.lines(23, 24, BLACK);

[ restore attributes
set.text.attribute(0, POP.ATTRIBUTES); [ pop.attributes()

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "%g20"
#message 2 "%g21"
#message 3 "%g22"
#message 4 "%g23"
#message 5 "%g24"
#message 6 "%g25"
#message 7 "%g26"
#message 8 "%g27"

[ custom instrument connectors
#message 9 "%g53"
#message 10 "%g54"
#message 11 "%g55"
#message 12 "%g56"
#message 13 "%g57"
