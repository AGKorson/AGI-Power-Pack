[*********************************************************************
[
[ rm.ParkingLot
[
[ Start of game. Car drives in, ego gets out. Ego can only enter
[ building, or get in car (which ends the game.) Every few minutes,
[ another car arrives, patient gets out and enters building. Also, a
[ patient may exit building, get in car and then leave. There should
[ be 4-5 cars in the parking lot at a time. if ego crosses in front of 
[ a moving vehicle, he can get run over, ending the game in a loss. if 
[ a patient arrives in a batmobile and ego tries to touch it, it
[ shocks and kills him. Touching the trees will get sap on ego's
[ hands, which costs a point; he can wash in the sink in the closet
[ to get the point back. Ego can get back in car; this ends game
[ (winning condition is to have disk with PP source code.)
[
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
#define egoCar        o1
#define oPatient      o2
#define oPatientCar   o3
#define btnYes        o4
#define btnNo         o5

#define egoCarSeq    v220
#define   EC_IDLE      0
#define   EC_ARRIVE    1
#define   EC_PARKING   2
#define   EC_PARKED    3
#define   EC_LEAVE    10
#define   EC_STARTCAR 11
#define   EC_LEAVING  12

#define patientSeq   v221
#define   IDLE         0
[ patient 1 arriving
#define   P1_DRIVE_A 201
#define   P1_STOP    202
#define   P1_WALK_A    3
#define   P1_WALK_B    4
[ patient 1 leaving
#define   P1_WALK_C  105
#define   P1_WALK_D  106
#define   P1_WALK_E  107
#define   P1_START   208
#define   P1_DRIVE_B 209
#define   P1_DRIVE_C 210
#define   P1_DRIVE_D 211
[ patient 2 arriving
#define   P2_DRIVE_A 212
#define   P2_DRIVE_B 213
#define   P2_DRIVE_C 214
#define   P2_DRIVE_D 215
#define   P2_STOP    216
#define   P2_WALK_A   17
#define   P2_WALK_B   18
[ patient 2 leaving
#define   P2_WALK_C  119
#define   P2_WALK_D  120
#define   P2_WALK_E  121
#define   P2_START   222
#define   P2_DRIVE_E 223
#define   P2_DRIVE_F 224
#define patientTimer v222
#define carX         v223
#define carY         v224
#define deathSeq     v225
#define weekday      v226

#define fCarDone     f220
#define fPatientDone f221
#define fPatientHere f222
#define f2ndspot     f223
#define fRunOver     f224
#define fClickOn     f225
#define fSkip1       f226
#define fElectrocuted  f227
#define fCarSound      f228
#define patientVisible f229
#define egoDying       f230

#define theDay          s12
#define weekendMsg      s13

#define CAR_INSTRUMENT   41  [ 41 = viola

[***************************************
[ FIRST CYCLE ONLY
[***************************************
if (newRoom) {
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ add toolbar buttons
  load.view(vw.ButtonsDown);
  call(lgc.InitToolbar);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(117);
  
  load.view(vw.Cars);
  [ add permanently parked cars (temporarily re-use local variables)
  #define tmpView     v220
  #define tmpLoop     v221
  #define tmpCel      v222
  #define tmpX        v223
  #define tmpY        v224
  #define tmpPri      v225
  #define tmpMgn      v226
 
  [ parking spot 1
  tmpView = vw.Cars;
  tmpLoop = car0car1;
  tmpLoop /= 16;
  tmpLoop += 19;
  tmpCel = 0;
  tmpX = 106;
  tmpY = 141;
  tmpPri = 13;
  tmpMgn = 4;
  add.to.pic.v(tmpView, tmpLoop, tmpCel, tmpX, tmpY, tmpPri, tmpMgn);
  [ parking spot 2
  tmpLoop = car0car1;
  block(BIT.AND, BYNUM, &tmpLoop, 15); [ bit.and(FN_MODE, VAR, MASK)
  tmpX = 92;
  tmpY = 154;
  tmpPri = 14;
  add.to.pic.v(tmpView, tmpLoop, tmpCel, tmpX, tmpY, tmpPri, tmpMgn);
  [ parking spot 3
  tmpLoop = car2car3;
  tmpLoop /= 16;
  tmpLoop += 19;
  tmpX = 51;
  tmpY = 167;
  tmpPri = 15;
  add.to.pic.v(tmpView, tmpLoop, tmpCel, tmpX, tmpY, tmpPri, tmpMgn);
  [ parking spot 4
  tmpLoop = car2car3;
  block(BIT.AND, BYNUM, &tmpLoop, 15); [ bit.and(FN_MODE, VAR, MASK)
  tmpLoop += 19;
  tmpX = 46;
  tmpY = 155;
  tmpPri = 14;
  add.to.pic.v(tmpView, tmpLoop, tmpCel, tmpX, tmpY, tmpPri, tmpMgn);
  [ parking spot 5
  tmpLoop = car4car5;
  tmpLoop /= 16;
  tmpX = 26;
  tmpY = 156;  
  tmpPri = 0;
  add.to.pic.v(tmpView, tmpLoop, tmpCel, tmpX, tmpY, tmpPri, tmpMgn);
  
  [ ego's car
  animate.obj(egoCar);
  tmpLoop = car4car5;
  block(BIT.AND, BYNUM, &tmpLoop, 15); [ bit.and(FN_MODE, VAR, MASK)
  set.view(egoCar, vw.Cars);
  set.loop.v(egoCar, tmpLoop);
  ignore.blocks(egoCar);
  
  [ done with temp variables; reset module variables as needed
  patientSeq = IDLE;
  
  [ tiny ego view
  load.view(vw.EgoSmall);
  set.view(ego, vw.EgoSmall);
  
  [ patient car
  animate.obj(oPatientCar);
  set.view(oPatientCar, vw.Cars);
  [ don't draw until needed
  
  [ tiny patient view
  animate.obj(oPatient);
  load.view(vw.SmallPerson1);
  load.view(vw.SmallPerson2);
  load.view(vw.SmallPerson3);
  load.view(vw.SmallPerson4); 
  load.view(vw.SmallPerson5);
  load.view(vw.SmallPerson6);
  load.view(vw.SmallPerson7);
  [ don't draw until needed
  
  [ buttons for end game msgbox
  load.view(vw.ParkingLotArt);
  animate.obj(btnYes);
  animate.obj(btnNo);
  set.view(btnYes, vw.ParkingLotArt);
  set.view(btnNo, vw.ParkingLotArt);
  set.loop(btnYes, 0);
  set.loop(btnNo, 0);
  ignore.objs(btnYes);
  ignore.objs(btnNo);
  ignore.blocks(btnYes);
  ignore.blocks(btnNo);
  ignore.horizon(btnYes);
  ignore.horizon(btnNo);
  position(btnYes, 62, 96);
  set.priority(btnYes, 15);
  position(btnNo, 81, 96);
  set.priority(btnNo, 15);
  [ don't draw until needed
  
  [ set initial timer to slightly faster random amount
  random(40, 140, patientTimer);
  
  [ sound effects for this room
  load.sound(s.Car);
  load.sound(s.CarStart);
  load.sound(s.CarStop);
  load.sound(s.Shock);
  
  [ if starting (coming from intro screen) and not skipping
  if (previousRoom == rm.Title && !skipIntro) {
    [ begin opening sequence dialog
    egoCarSeq = EC_ARRIVE;
  } else {
    [ enable menu/mouse input
    set(enableMenu);
    reset(disableGameFunctions);
    [ skip opening sequence
    egoCarSeq = EC_IDLE;
    [ if car wasn't locked, there's a chance it gets stolen
    if (previousRoom > rm.Title && !carLocked) {
      random(1, 10, work1);
      if (work1 == 10) {
        [ it's gone!
        deathType = DT_STOLEN_CAR;
      }
    }
    if (!deathType) {
      [ car is in parking spot
      position(egoCar, 41, 143);
      draw(egoCar);
      stop.cycling(egoCar);
    }
    
    [ if startng/restarting 
    if ((previousRoom == 0 || previousRoom == rm.Title)) {
      [ add ego next to car
      position(ego, 50, 139);
    } else {
      [ coming from lobby
      position(ego, 54, 121);
    }
    egodir = STOPPED;
    draw(ego);
  }
  
  [ get day of the week, so opening message can be
  [ customized
  block(GET.DAYOFWEEK, &weekday, 0, 0); [ get.dayofweek(vDOW)
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, BYNUM, 25, 0); [ fade.in(FN_MODE, DELAY, COLOR)
  
  [ if not showing opening dialog
  if (egoCarSeq == EC_IDLE) {
    [ restore cursor
    cursoricon = cursormode;
    set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
  }
}

[***************************************
[ NON-INPUT CODE 
[***************************************

[*******************
[ DREW'S CAR
[*******************
if (egoCarSeq) {
  [ ego arrives
  if (egoCarSeq == EC_ARRIVE) {
    [ disallow menu use
    reset(enableMenu);
    [ game functions are disabled
    set(disableGameFunctions);
    [ no input allowed
    prevent.input();
    
    [ move car to parking spot
    ++egoCarSeq;
    position(egoCar, 0, 131);
    draw(egoCar);
    move.obj(egoCar, 40, 143, 1, fCarDone);
    [ restore cursor for message
    cursoricon = cursormode;
    set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
    print("At long last, you finally made it to Oakhurst, "
          "California to visit the original Sierra On-Line headquarters "
          "building.");
    print("It has been a long drive, but you are super excited as you "
          "pull into the parking lot.");
          
    [ show wait cursor
    cursoricon = C_WAIT;
    set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
    [ play car-running sound, force 4-channel FM
    set(4channelsound);
    set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
    [ use correct instrument
    work1 = 0;
    work2 = CAR_INSTRUMENT;
    set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
    sound(s.Car, soundDone);
    set(playSound);
  }
  if (egoCarSeq == EC_PARKING && soundDone) {
    [ continue playing engine sound until final position reached
    [ (sound parameters are already set, so just play the sound)
    sound(s.Car, soundDone);
  }
  if (egoCarSeq == EC_PARKING && fCarDone) {
    [ stop car, play engine shutdown sound
    [ (sound parameters are already set, so just play the sound)
    reset(fCarDone);
    sound(s.CarStop, soundDone);
    stop.cycling(egoCar);
    egoCarSeq = EC_PARKED;
  }
  if (egoCarSeq == EC_PARKED && soundDone) {
    [ ego gets out
    reset(soundDone);
    egoCarSeq = EC_IDLE;
    position(ego, 50, 139);
    draw(ego);
    [ restore cursor for message
    cursoricon = cursormode;
    set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
    [ check for weekend
    if ((weekday == 0 || weekday == 6)) {
      if (weekday == 0) {
        theDay = "Sunday";
      } else {
        theDay = "Saturday";
      }
      weekendMsg = "What luck! The clinic is open on %s12! ";
    } else {
      weekendMsg = "";
    }
    print("The building is now a medical clinic. %s13There aren't too many "
          "cars in the parking lot. So you decide to go inside and "
          "take a look around.");
    print("Who knows? Maybe you might find some original Sierra memorabilia "
          "or other signs of long past gaming history.");
    [ allow menu use
    set(enableMenu);
    [ game functions are no longer disabled
    reset(disableGameFunctions);
    [ allow input
    accept.input();
  }
  [ skip rest of room logic until opening sequence is finished
  if (egoCarSeq < EC_PARKED) {
    return();
  }
  
  [ ego drives away
  if (egoCarSeq == EC_LEAVE) {
    [ if walkman is in use, stop it
    if (currentTrack > 0 && currentTrack < 128) {
      currentTrack *= -1;
      stop.sound();
      reset(musicDone);
    }
    [ play car-start sound, force 4-channel FM
    set(4channelsound);
    set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
    [ use correct instrument
    work1 = 0;
    work2 = CAR_INSTRUMENT;
    set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
    sound(s.CarStart, soundDone);
    set(playSound);
    egoCarSeq = EC_STARTCAR;
  }
  if (egoCarSeq == EC_STARTCAR && soundDone) {
    [ ego drives away
    egoCarSeq = EC_LEAVING;
    start.cycling(egoCar);
    move.obj(egoCar, 143, 126, 2, fCarDone);
    [ (sound parameters are already set, so just play the sound)
    sound(s.Car, soundDone);
    set(playSound);
  }
  if (egoCarSeq == EC_LEAVING && soundDone) {
    [ keep playing car-running sound
    [ (sound parameters are already set, so just play the sound)
    sound(s.Car, soundDone);
    set(playSound);
  }
  if (egoCarSeq == EC_LEAVING && fCarDone) {
    reset(fCarDone);
    [ stop sound
    stop.sound();
    reset(playSound);
    [ ego is done; begin end-of-game sequence
    [ (using death handler)
    egoCarSeq = EC_IDLE;
    erase(egoCar);
    print("You drive away, your visit to Sierra On-Line is over.");
    [ set deathType
    deathType = DT_DRIVE_AWAY;
  }  
}

[ if first time in room and not showing opening sequence
if (newRoom && (previousRoom == 0 || previousRoom == rm.Title)) {
  if ((weekday == 0 || weekday == 6)) {
    if (weekday == 0) {
      theDay = "Sunday";
    } else {
      theDay = "Saturday";
    }
    print("What luck! The clinic is open on %s12! ");
  }
}


[ timing bug with controllers
if (fSkip1) {
  reset(fSkip1);
  set.key(CLR.CTRL, CLR.CTRL, cLeftClick); [ clear.controller(cNUM)
}

[ parked cars in rightside parking lot don't quite align with priority bands
get.posn(ego, work1, work2);
if (work1 > 102 && work1 < 121 && work2 > 141 && work2 < 145) {
  set.priority(ego, 13);
} else {
  if (work1 > 88 && work1 < 108 && work2 >154 && work2 < 157) {
    set.priority(ego, 14);
  } else {
    release.priority(ego);
  }
}

[*******************
[ PATIENTS
[*******************
[ check for random patient arriving or leaving
if (patientTimer == 0) {
  [ reset the timer
  random(80, 250, patientTimer);
  if (fPatientHere) {
    [ time for patient to leave
    position(oPatient, 55, 117);
    if (f2ndspot) {
      [ go back to parking spot 2
      move.obj(oPatient, 55, 127, 1, fPatientDone);
      patientSeq = P2_WALK_C;
    } else {
      [ go back to parking spot 1
      move.obj(oPatient, 55, 127, 1, fPatientDone);
      patientSeq = P1_WALK_C;
    }
    draw(oPatient);
    set(patientVisible);
  } else {
    [ time for patient to arrive
    random(0, 1, work1);
    if (work1 == 0) {
      reset(f2ndspot);
      patientSeq = P1_DRIVE_A; [ drive to first point
      random(0, 18, work1);
      set.loop.v(oPatientCar, work1);
      position(oPatientCar, 0, 131);
      move.obj(oPatientCar, 18, 143, 2, fPatientDone);
    } else {
      set(f2ndspot);
      patientSeq = P2_DRIVE_A; [ gdrive to first point
      random(19, 37, work1);
      set.loop.v(oPatientCar, work1);
      position(oPatientCar, 143, 128);
      move.obj(oPatientCar, 99, 128, 2, fPatientDone);
    }
    draw(oPatientCar);
    [ only play sound if walkman is not in use
    if ((currentTrack == 0 || currentTrack > 128)) {
      [ play car-running sound, force 4-channel FM
      set(4channelsound);
      set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      [ use correct instrument
      work1 = 0;
      work2 = CAR_INSTRUMENT;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(s.Car, soundDone);
      set(playSound);
    } else {
      [ skip the sound
      set(soundDone);
    }
    
    [ choose a random patient
    random(vw.SmallPerson1, vw.SmallPerson7, work1);
    set.view.v(oPatient, work1);
  }
} else {
  if (patientSeq == IDLE) {
    [ countdown
    --patientTimer;
  }
}
if (fPatientDone) {
  [ patient/path 2
  if (patientSeq == P2_DRIVE_F) {
    [ car has left 
    erase(oPatientCar);
    reset(fPatientHere);
    reset(fPatientDone);
    if (playSound) {
      stop.sound();
      reset(playSound);
    }
    patientSeq = IDLE;
  }
  if (patientSeq == P2_DRIVE_E) {
    [ move car to last point
    move.obj(oPatientCar, 0, 131, 2, fPatientDone);
    patientSeq = P2_DRIVE_F;
  }
  if (patientSeq == P2_WALK_E) {
    [ patient gets in car
    erase(oPatient);
    reset(patientVisible);
    [ start car
    reset(fPatientDone);
    patientSeq = P2_START; 
    [ only play sound if walkman is not in use
    if ((currentTrack == 0 || currentTrack > 128)) {
      [ play car-start sound, force 4-channel FM
      set(4channelsound);
      set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      [ use correct instrument
      work1 = 0;
      work2 = CAR_INSTRUMENT;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(s.CarStart, soundDone);
      set(playSound);
    } else {
      set(soundDone);
    }
  }
  if (patientSeq == P2_WALK_D) {
    [ walk to car
    move.obj(oPatient, 88, 142, 1, fPatientDone);
    patientSeq = P2_WALK_E;  
  }
  if (patientSeq == P2_WALK_C) {
    [ walk to next point
    move.obj(oPatient, 70, 142, 1, fPatientDone);
    patientSeq = P2_WALK_D;  
  }
  if (patientSeq == P2_WALK_B) {
    [ patient enters building
    erase(oPatient);
    reset(patientVisible);
    patientSeq = IDLE;
    set(fPatientHere);
    reset(fPatientDone);
  }
  if (patientSeq == P2_WALK_A) {
    [ walk to building door
    move.obj(oPatient, 55, 117, 1, fPatientDone);
    patientSeq = P2_WALK_B;  
  }
  if (patientSeq == P2_DRIVE_D) {
    [ car is at parking spot 2
    stop.cycling(oPatientCar);
    ignore.blocks(oPatientCar);
    block(78, 138, 99, 142);
    [ stop car
    reset(fPatientDone);
    patientSeq = P2_STOP; 
    [ only play sound if walkman is not in use
    if ((currentTrack == 0 || currentTrack > 128)) {
      [ play car-stop sound, force 4-channel FM
      set(4channelsound);
      set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      [ use correct instrument
      work1 = 0;
      work2 = CAR_INSTRUMENT;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(s.CarStop, soundDone);
      set(playSound);
    } else {
      set(soundDone);
    }
  }
  if (patientSeq == P2_DRIVE_C) {
    [ drive to parking spot 2
    move.obj(oPatientCar, 83, 141, 1, fPatientDone);
    patientSeq = P2_DRIVE_D;  
  }
  if (patientSeq == P2_DRIVE_B) {
    [ drive to next point
    move.obj(oPatientCar, 61, 141, 1, fPatientDone);
    patientSeq = P2_DRIVE_C;  
  }
  if (patientSeq == P2_DRIVE_A) {
    [ drive to next point
    move.obj(oPatientCar, 75, 129, 2, fPatientDone);
    patientSeq = P2_DRIVE_B;  
  }
  
  [ patient/path 1
  if (patientSeq == P1_DRIVE_D) {
    [ car has left
    erase(oPatientCar);
    reset(fPatientHere);
    reset(fPatientDone);
    if (playSound) {
      stop.sound();
      reset(playSound);
    }
    patientSeq = IDLE;
  }
  if (patientSeq == P1_DRIVE_C) {
    [ drive to last point
    move.obj(oPatientCar, 143, 128, 2, fPatientDone);
    patientSeq = P1_DRIVE_D;
  }
  if (patientSeq == P1_DRIVE_B) {
    [ drive to next point
    move.obj(oPatientCar, 20, 128, 2, fPatientDone);
    patientSeq = P1_DRIVE_C;
  }
  if (patientSeq == P1_WALK_E) {
    [ patient gets in car
    erase(oPatient);
    reset(patientVisible);
    [ start car
    reset(fPatientDone);
    patientSeq = P1_START; 
    [ only play sound if walkman is not in use
    if ((currentTrack == 0 || currentTrack > 128)) {
      [ play car-start sound, force 4-channel FM
      set(4channelsound);
      set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      [ use correct instrument
      work1 = 0;
      work2 = CAR_INSTRUMENT;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(s.CarStart, soundDone);
      set(playSound);
    } else {
      set(soundDone);
    }
  }
  if (patientSeq == P1_WALK_D) {
    [ walk to next point
    move.obj(oPatient, 25, 140, 1, fPatientDone);
    patientSeq = P1_WALK_E;
  }
  if (patientSeq == P1_WALK_C) {
    [ walk to next point
    move.obj(oPatient, 38, 127, 1, fPatientDone);
    patientSeq = P1_WALK_D;
  }
  if (patientSeq == P1_WALK_B) {
    [ patient enters building
    erase(oPatient);
    reset(patientVisible);
    patientSeq = IDLE;
    set(fPatientHere);
    reset(fPatientDone);
  }
  if (patientSeq == P1_WALK_A) {
    [ walk to building door
    move.obj(oPatient, 55, 117, 1, fPatientDone);
    patientSeq = P1_WALK_B;
  }
  if (patientSeq == P1_DRIVE_A) {
    [ car is at parking spot 1
    stop.cycling(oPatientCar);
    ignore.blocks(oPatientCar);
    block(14, 140, 34, 144);
    [ stop car
    reset(fPatientDone);
    patientSeq = P1_STOP; 
    [ only play sound if walkman is not in use
    if ((currentTrack == 0 || currentTrack > 128)) {
      [ play car-stop sound, force 4-channel FM
      set(4channelsound);
      set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      [ use correct instrument
      work1 = 0;
      work2 = CAR_INSTRUMENT;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(s.CarStop, soundDone);
      set(playSound);
    } else {
      set(soundDone);
    }
  }
}

if (soundDone) {
  reset(soundDone);
  if (patientSeq == P1_START) {
    [ drive car to first point
    start.cycling(oPatientCar);
    move.obj(oPatientCar, 5, 143, 1, fPatientDone);
    unblock();
    patientSeq = P1_DRIVE_B;
    [ only play sound effect if walkman not in use
    if ((currentTrack == 0 || currentTrack > 128)) {
      [ play car-running sound, force 4-channel FM
      set(4channelsound);
      set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      [ use correct instrument
      work1 = 0;
      work2 = CAR_INSTRUMENT;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(s.Car, soundDone);
      set(playSound);
    }
    goto(checkdeath);
  }
  if (patientSeq == P1_STOP) {
    [ patient walks to first point
    position(oPatient, 25, 140);
    draw(oPatient);
    set(patientVisible);
    move.obj(oPatient, 55, 125, 1, fPatientDone);
    patientSeq = P1_WALK_A;
    goto(checkdeath);
  }
  if (patientSeq == P2_START) {
    [ drive car to first point
    start.cycling(oPatientCar);
    move.obj(oPatientCar, 61, 131, 1, fPatientDone);
    unblock();
    patientSeq = P2_DRIVE_E;
    [ only play sound effect if walkman not in use
    if ((currentTrack == 0 || currentTrack > 128)) {
      [ play car-running sound, force 4-channel FM
      set(4channelsound);
      set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
      [ use correct instrument
      work1 = 0;
      work2 = CAR_INSTRUMENT;
      set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
      sound(s.Car, soundDone);
      set(playSound);
    }
    goto(checkdeath);
  }
  if (patientSeq == P2_STOP) {
    [ patient walks to first point
    position(oPatient, 88, 142);
    draw(oPatient);
    set(patientVisible);
    move.obj(oPatient, 72, 142, 1, fPatientDone);
    patientSeq = P2_WALK_A;
    goto(checkdeath);
  }
  
  [ if walkman not in use, continue car sound effects if car is moving
  if ((currentTrack == 0 || currentTrack > 128) && 
      patientSeq > 200) {
    [ play car-running sound, force 4-channel FM
    set(4channelsound);
    set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
    [ use correct instrument
    work1 = 0;
    work2 = CAR_INSTRUMENT;
    set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
    sound(s.Car, soundDone);
    set(playSound);
  }
}

:checkdeath
if (!egoDying && !deathType && !kickBucket) {
  [ check for ego getting run over
  get.posn(oPatientCar, carX, carY);
  work1 = carY;
  work1 -= 2;
  if (egoY >= work1 && egoY <= carY) {
    work1 = egoX;
    work1 += 3;
    work2 = carX;
    work2+= 15;
    if (work1 >= carX && egoX <= work2) {
      set(egoDying);
      [ temporarily stop driving
      stop.update(oPatientCar);
      load.view(vw.EgoDead);
      set.view(ego, vw.EgoDead);
      egoDir = STOPPED;
      program.control();
      prevent.input();
      work1 = CYCLE_AT_REST;
      set.upper.left(&ego, &work1); [ auto.cycle(oSOBJ, vMODE)
      work1 = 2;
      cycle.time(ego, work1);
      end.of.loop(ego, fRunOver);
      print("Uh oh! That car just hit you!");
    }
  }
  [ check for batmobile electrocution
  if (deathSeq > 0) {
    if (deathSeq == 1) {
      ++deathSeq;
      current.loop(ego, work1);
      set.view(ego, vw.ParkingLotArt);
      if (work1 > 0 && work1 < 3) {
        set.loop(ego, 2);
      } else {
       set.loop(ego, 1);
      }
      set.cel(ego, 0);
      work1 = CYCLE_AT_REST;
      set.upper.left(&ego, &work1); [ auto.cycle(oSOBJ, vMODE)
      end.of.loop(ego, fElectrocuted);
      [ only play sound effects if walkman not in use
      if ((currentTrack == 0 || currentTrack > 128)) {
        [ play electrify sound, force 4-channel FM
        set(4channelsound);
        set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
        [ use correct instrument
        work1 = 3;
        work2 = 129;
        set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
        sound(s.Shock, soundDone);
        set(playSound);
      }
    }
    if (fElectrocuted) {
      if (deathSeq < 5) {
        ++deathSeq;
        set.cel(ego, 0);
        end.of.loop(ego, fElectrocuted);
        [ only play sound effects if walkman not in use
        if ((currentTrack == 0 || currentTrack > 128)) {
          [ play electrify sound, force 4-channel FM
          set(4channelsound);
          set.key(FM_MODE, 0, SET.SNDMODE); [ set.sndmode(MODE)
          [ use correct instrument
          work1 = 3;
          work2 = 129;
          set.key(&work1, &work2, SET.INST); [ set.inst(vCHANNEL, vINSTRUMENT)
          sound(s.Shock, soundDone);
          set(playSound);
        }
      }
      if (deathSeq == 5) {
        ++deathSeq;
        current.loop(ego, work1);
        load.view(vw.EgoDead);
        set.view(ego, vw.EgoDead);
        --work1;
        set.loop.v(ego, work1);
        set.cel(ego, 0);
        work1 = 2;
        cycle.time(ego, work1);
        end.of.loop(ego, fElectrocuted);
        set(egoDying);
        print("Yikes! This batmobile has an anti-theft system!");
      }
    }
  }
}

[ check for end of run-over death cycle
if (fRunOver) {
  reset(fRunOver);
  deathType = DT_RUNOVER;
  work1 = 1;
  cycle.time(ego, work1);
  accept.input();
  ignore.objs(oPatient);
}

[ check for end of electrocution death cycle
if (fElectrocuted && deathSeq == 6) {
  reset(fElectrocuted);
  ++deathSeq;
  deathType = DT_STUNNED;
  accept.input();
  ignore.objs(oPatient);
}

[ if dead or dying, skip player input
if ((deathType || egoDying)) {
  [ check for stolen car
  if (deathType == DT_STOLEN_CAR) {
    print("Hey! Where is your car???");
    ignore.objs(oPatient);
  }
  return();
}

[***************************************
[ PROCESS PLAYER INPUT
[***************************************
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ look sky
  if (said("look", "sky")) {
    print("A few white clouds can be seen in the beautiful blue sky.");
  }
  [ look mountain/forest
  if ((said("look", "mountain") || said("look", "forest"))) {
    print("The forest and mountain are beautiful to look at.");
  }
  [ look trees
  if (said("look", "tree")) {
    print("Giant evergreen trees surround the parking lot.");
  }
  [ look building
  if ((said("look", "building") || said("look", "sierra"))) {
    print("The building looks almost the same as it did thirty years ago.");
  }
  [ look car
  if (said("look", "car")) {
    print("There are several cars in the parking lot, including your own.");
  }
  [ look patient
  if ((said("look", "patient") || said("look", "person") ||
       said("look", "man") || said("look", "woman"))) {
    if (patientVisible) {
      [ coming or going?
      if (patientSeq > 100) {
        print("This person is leaving.");
      } else {
        print("This person must have an appointment. They seem to be in a hurry.");
      }
    } else {
      print("There is no one here but you.");
    }
  }
  [ look bushes/lawn
  if ((said("look", "bush") || said("look", "grass"))) {
    print("The lawn and shrubbery is well maintained.");
  }
  [ look parking lot
  if (said("look", "parking lot")) {
    print("It's a parking lot. What else do you want me to say?");
  }
  [ look sidewalk
  if (said("look", "sidewalk")) {
    print("The sidewak on the side of the building leads to the back entrance, but that door is always locked.");
  }
  [ look window
  if (said("look", "window")) {
    print("Sunlight glare on the windows makes it difficult to see inside.");
  }
  [ take car, steal car/batmobile
  if ((said("steal", "car") || said("get", "car"))) {
    distance(ego, oPatientCar, work1);
    if (work1 < 14) {
      current.loop(oPatientCar, work1);
      if (work1 == 18) {
        [ bad idea, trying to steal the batmobile
        goto(stealbatmobile);
      }
      [ stealing isn't nice
      print("Not a good idea. You'll end up in jail. Besides that, it just isn't nice.");
    } else {
      print("Which car are you trying to steal?");
    }
  }

  [ get tree/bush/branch
  if ((said("get", "tree") || said("get", "bush"))) {
    print("The trees and bushes are covered in sap. You should probably wash your hands when you get a chance.");
    reset(handsWashed);
    if (!touchTrees) {
      set(touchTrees);
      [ lose 1 point
      work1 = -1;
      call(lgc.ScoreHandler);
    }
  }
  
  [ drive away, leave, go home
  if ((said("get in", "car") || said("go", "home") || 
       said("leave"))) {
    goto(entercar);
  }
  [ talk/speak to person/patient
  if ((said("talk", "patient") || said("talk", "person") || 
       said("talk", "man") || said("talk", "woman"))) {
    distance(ego, oPatient, work1);
    if (work1 == 255) {
      print("There is no one here.");
    } else {
      goto(talkpatient);
    }
  }
  
  if (said("look")) {
    print("You are in the Sierra building parking lot.");
  }
  if (said("use", "keyfob")) {
    if (carLocked) {
      print("Using your keyfob, you unlock your car door.");
      reset(carLocked);
      work1 = -2;
      call(lgc.ScoreHandler);
    } else {
      print("Thinking it's best not to take any chances, "
            "you hit the lock button on your keyfob to "
            "prevent your car from being stolen.");
      set(carLocked);
      work1 = 2;
      call(lgc.ScoreHandler);
    }
  }
  if (said("look", "door")) {
    print("There is an automatic door at the entrance to the clinic.");
  }
  if (said("open", "door")) {
    print("The door opens automatically when you approach it.");
  } 
  if (said("lock", "car")) {
    [ if already locked
    if (carLocked) {
      print("It's already locked.");
    } else {
      print("Thinking it's best not to take any chances, "
            "you hit the lock button on your keyfob to "
            "prevent your car from being stolen.");
      set(carLocked);
      work1 = 2;
      call(lgc.ScoreHandler);
    }
  }
  if (said("unlock", "car")) {
    if (carLocked) {
      print("Using your keyfob, you unlock your car door.");
      reset(carLocked);
      work1 = -2;
      call(lgc.ScoreHandler);
    } else {
      print("It's already unlocked.");
    }
  }
  if (said("get in", "car")) {
    goto(entercar);
  }
}

[***************************************
[ MOUSE INPUT
[***************************************
[ if cursor is look/talk/use and left-click and NOT 
[ clicking on the actual toolbar, check for clicks
[ on in-game items
if (cursoricon > C_WALK && cursoricon < C_WAIT && 
    controller(cLeftClick) &&
    (mouseX < 14 || mouseX > 145 || mouseY > 17)) {
  [ ignore borders, off screen
  if (mouseY > 167) {
    goto(doneclick);
  }
  if (cursormode == C_LOOK) {
    [ same as 'look'
    [ check for objects first
    
    [ ego
    if (posn(ego, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      print("Yes, Drew is a handsome fellow. Easy on the eyes.");
      goto(doneclick);
    }
    
    [ ego car
    if (posn(egoCar, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      current.loop(egoCar, work1);
      if (work1 > 18) {
        work1 -= 19;
      }
      if (work1 < 8) {
        print("This is your car. You drove it here.");
      } else {
        if (work1 < 12) {
          print("Your minivan is great to have on long road trips like this one.");
        } else {
          if (work1 < 17) {
            print("Your truck doesn't get the best mileage, but it's great for hauling stuff.");
          } else {
            if (work1 == 17) {
              print("This is your red sportscar. You love to go fast!");
            } else {
              print("Yeah, this is your batmobile. How cool is that?");
            }
          }
        }
      }
      goto(doneclick);
    }
    
    [ patient
    if (posn(oPatient, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      [ coming or going?
      if (patientSeq > 100) {
        print("This person is leaving.");
      } else {
        print("This person must have an appointment. They seem to be in a hurry.");
      }
      goto(doneclick);
    }
    
    [ patient car
    if (posn(oPatientCar, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      current.loop(oPatientCar, work1);
      if (work1 > 18) {
        work1 -= 19;
      }
      if (work1 < 8) {
        print("It's a patient's car.");
      } else {
        if (work1 < 12) {
          print("This minivan belongs to one of the patients.");
        } else {
          if (work1 < 17) {
            print("It's a truck belonging to one of the patients.");
          } else {
            if (work1 == 17) {
              print("This sportscar belongs to one of the patients.");
            } else {
              print("A batmobile? Who drives a batmobile to a doctor's appointment?");
            }
          }
        }
      }
      goto(doneclick);
    }

    [ parked cars
    reset(fClickOn);
    if (mouseX > 26 && mouseX < 41 && mouseY > 145 && mouseY < 157) {
      set(fClickOn);
    }
    if (mouseX > 46 && mouseX < 61 && mouseY > 144 && mouseY < 155) {
      set(fClickOn);
    }
    if (mouseX > 51 && mouseX < 66 && mouseY > 156 && mouseY < 167) {
      set(fClickOn);
    }
    if (mouseX > 106 && mouseX < 121 && mouseY > 130 && mouseY < 141) {
      set(fClickOn);
    }
    if (mouseX > 92 && mouseX < 107 && mouseY > 143 && mouseY < 154) {
      set(fClickOn);
    }
    if (fClickOn) {
      print("There are a several cars in the parking lot. Only one is yours. This is not the one.");
      goto(doneclick);
    }
    
    [ then check background
    [ pixel color can help with discriminating what was clicked
    block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)

    if (work1 > 239) {
      [ foreground tree
      print("Giant evergreen trees surround the parking lot.");
      goto(doneclick);
    }
    
    if (work1 > 111) { 
      [ forest/mountain/sky
      print("The forest and mountain are beautiful to look at.");
      goto(doneclick);
    }
    if (work1 > 95) {
      [ lawn/sidewalk
      print("The lawn and shrubbery is well maintained.");
      goto(doneclick);
    }
    if (work1 > 79) {
      [ building, door
      if (mouseX > 53 && mouseX < 60 && mouseY > 104 && mouseY < 139) {
        print("There is a large automatic glass door at the entrance to building.");
      } else {
        print("The building looks almost the same as it did thirty years ago.");
      }
      goto(doneclick);
    }
    [ if nothing else, must be parking lot
    print("It's a parking lot. What else do you want me to say?");
    goto(doneclick);
  }

  if (cursormode == C_TALK) {
    [ same as 'talk to'

    [ ego
    if (posn(ego, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      print("You have nothing to say to yourself.");
      goto(doneclick);
    }
    
    [ ego car
    if (posn(egoCar, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      print("Your car has nothing to say.");
      goto(doneclick);
    }
    
    [ patient
    if (posn(oPatient, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      distance(ego, oPatient, work1);
      :talkpatient
      if (work1 <= 20) {
        block(MSGBOX.COLOR, LT_GREEN, GREEN, WHITE); [ msgbox.color(BORDER, FG, BG)
        [ coming or going?
        if (patientSeq > 100) {
          random(9, 12, work1);
          print.v(work1); [ m9 - m12
        } else {
          random(13, 16, work1);
          print.v(work1); [ m13 - m16
        }
        block(MSGBOX.COLOR.V, &msgBoxBorder, &msgboxFG, &msgBoxBG); [ msgbox.color.v(vBORDER, vFG, vBG)
      } else {
        print("The person is too far away to hear you.");
      }
      
      goto(doneclick);
    }
    
    [ patient car
    if (posn(oPatientCar, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      [ if patient is driving
      if (patientSeq > 200) {
        print("The person in the car can't hear you.");
      } else {
        print("There is no one in the car to talk to.");
      }
      goto(doneclick);
    }

    [ parked cars
    reset(fClickOn);
    if (mouseX > 26 && mouseX < 41 && mouseY > 145 && mouseY < 157) {
      set(fClickOn);
    }
    if (mouseX > 46 && mouseX < 61 && mouseY > 144 && mouseY < 155) {
      set(fClickOn);
    }
    if (mouseX > 51 && mouseX < 66 && mouseY > 156 && mouseY < 167) {
      set(fClickOn);
    }
    if (mouseX > 106 && mouseX < 121 && mouseY > 130 && mouseY < 141) {
      set(fClickOn);
    }
    if (mouseX > 92 && mouseX < 107 && mouseY > 143 && mouseY < 154) {
      set(fClickOn);
    }
    if (fClickOn) {
      print("There is no one in the car to talk to.");
      goto(doneclick);
    }
    
    [ ignore anything else that's clicked on
    goto(doneclick);
  }

  if (cursormode == C_USE) {
    [ same as 'use item'
    [ ego
    if (posn(ego, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      print("Please don't touch yourself in public. There may be children watching.");
      goto(doneclick);
    }

    [ ego car
    if (posn(egoCar, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      :entercar
      if (posn(ego, 39, 132, 56, 146)) {
        if (carLocked) {
          print("The door is locked.");
          return();
        }
        [ display a msgbox with 'yes/no' buttons
        set(leaveWindow);
        print("Are you sure you want to \nleave?\n\n");
        draw(btnYes);
        draw(btnNo);
        [ need to force update so the buttons get drawn right now
        force.update(btnYes);
        [ add button text
        set.text.attribute(PUSH.ATTRIBUTES, 0); [ push.attributes()
        set.text.attribute(BLACK, WHITE);
        display(12, 33, "Yes");
        display(12, 43, "No");
        set.text.attribute(0, POP.ATTRIBUTES); [ pop.attributes()
        [ change to default cursor (it will change back 
        [ after dialog box is dismissed)
        if (cursoricon != C_DEFAULT) {
          [ change it
          cursoricon = C_DEFAULT;
          set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
          [ disable click2move
          reset(click2move);
        }
        [ clear input and controller before starting wait loop
        set.key(CLR.CTRL, CLR.CTRL, cLeftClick); [ clear.controller(cNUM)
        lastChar = 0;
        work1 = 0;
        :wait
        if (!have.key() && mouseBtn != 1) {
          goto(wait);
        }
        [ if left button was pressed, check buttons
        if (mouseBtn == 1) {
          lastChar = 0;
          if (posn(btnYes, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
            work1 = 1;
          } else {
            if (posn(btnNo, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
              work1 = 2;
            }
          }
        } else {
          [ 'ENTER', 'Y' or 'y' for YES
          if ((lastChar == 13 || lastChar == 89 || lastChar == 121)) {
            work1 = 1;
          }
          [ 'ESC', 'N' or 'n' for NO
          if ((lastChar == 27 || lastChar == 78 || lastChar == 110)) {
            work1 = 2;
          }
        }
        [ if wrong keypress, or click was not on button, continue waiting
        if (work1 == 0) {
          goto(wait);
        }
        [ hide buttons, then close print window
        erase(btnYes);
        erase(btnNo);
        close.window();
        [ if 'NO', do nothing; if 'YES', end the game
        if (work1 == 1) {
          erase(ego);
          print("You get in your car and drive away.");
          egoDir = STOPPED;
          program.control();
          prevent.input();
          egoCarSeq = EC_LEAVE;
          [ 5 pts for leaving
          work1 = 5;
          call(lgc.ScoreHandler);
          if (floppyfiles > 1) {
            [ 5 more pts for leaving with source code
            work1 = 5;
            call(lgc.ScoreHandler);
          }
        }
        [ there is a timing glitch in AGI (not sure if it's a DOSBox thing
        [ or inherent to MSDOS/AGI; but on occasion, a second 'ghost click'
        [ will set the left-click controller when the dialog is dismissed)
        [ to fix this, skip the controller on the very next cycle
        set(fSkip1);
      } else {
        print("You are too far away to get in your vehicle from here.");
      }
      goto(doneclick);
    }
    
    [ patient
    if (posn(oPatient, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      distance(ego, oPatient, work1);
      if (work1 < 6) {
        print("Get your hands off me, you big jerk!");
      } else {
        print("You are too far away to touch that person.");
      }
      goto(doneclick);
    }
    
    [ patient car
    if (posn(oPatientCar, ON.OBJ, OBJ_FRAME, &mouseX, &mouseY)) {
      [ is car moving?
      if (patientSeq > 200) {
        print("It's not a good idea to touch a moving vehicle.");
      } else {
        distance(ego, oPatientCar, work1);
        if (work1 < 14) {
          current.loop(oPatientCar, work1);
          if (work1 > 18) {
            work1 -= 19;
          }
          if (work1 < 8) {
            print("This is not your car. It's locked anyway.");
          } else {
            if (work1 < 12) {
              print("The owner of this minivan would not appreciate you stealing it.");
            } else {
              if (work1 < 17) {
                print("There's nothing worth taking from this truck.");
              } else {
                if (work1 == 17) {
                  print("This sportcar is really neat, but that's no reason to steal it.");
                } else {
                  :stealbatmobile
                  prevent.input();
                  program.control();
                  stop.motion(ego);
                  deathSeq = 1;
                }
              }
            }
          }
        } else {
          print("You are too far away to touch that vehicle.");
        }
      }
      goto(doneclick);
    }

    [ parked cars
    reset(fClickOn);
    if (mouseX > 26 && mouseX < 41 && mouseY > 145 && mouseY < 157) {
      set(fClickOn);
    }
    if (mouseX > 46 && mouseX < 61 && mouseY > 144 && mouseY < 155) {
      set(fClickOn);
    }
    if (mouseX > 51 && mouseX < 66 && mouseY > 156 && mouseY < 167) {
      set(fClickOn);
    }
    if (mouseX > 106 && mouseX < 121 && mouseY > 130 && mouseY < 141) {
      set(fClickOn);
    }
    if (mouseX > 92 && mouseX < 107 && mouseY > 143 && mouseY < 154) {
      set(fClickOn);
    }
    if (fClickOn) {
      print("There's nothing you can do with this car.");
      goto(doneclick);
    }
    
    [ then check background
    [ pixel color can help with discriminating what was clicked
    block(GET.PIXEL, &mouseX, &mouseY, &work1); [ get.pixel(vX, vY, vRESULT)

    if (work1 > 239) {
      [ foreground tree
      print("The trees are covered in sap. You should probably wash your hands when you get a chance.");
      reset(handsWashed);
      if (!touchTrees) {
        set(touchTrees);
        [ lose 1 point
        work1 = -1;
        call(lgc.ScoreHandler);
      }
      goto(doneclick);
    }
    
    if (work1 > 111) { 
      [ forest/mountain/sky
      [ nothing
      goto(doneclick);
    }
    if (work1 > 95) {
      [ lawn/sidewalk
      print("There's nothing you can do with the shrubbery or lawn. Leave it be.");
      goto(doneclick);
    }
    if (work1 > 79) {
      [ building
      if (egoX < 71 && egoY < 121) {
        [ door
        if (mouseX > 53 && mouseY < 52 && mouseY > 105) {
          print("The door will open automatically when you approach.");
        } else {
          print("You run your hands over the weathered building exterior. It's rough.");
        }
      } else {
        print("You are not close enough to touch the building.");
      }
    }
  }
  :doneClick
  set.key(CLR.CTRL, CLR.CTRL, cLeftClick); [ clear.controller(cNUM)
}

[***************************************
[ CHECK FOR AN EXIT CONDITION
[***************************************
:exit

if (egoHitSpecial) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM)
  [ fade out, then change room
  block(FADE.OUT, BYNUM, FADECOUNT, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
  [ move to lobby
  new.room(rm.Lobby);  [ ##LE001##
}

return();


[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "%g30"
#message 2 "%g31"
#message 3 "%g32"
#message 4 "%g33"
#message 5 "%g34"
#message 6 "%g35"
#message 7 "%g36"
#message 8 "%g37"
[ patient speaking parts
#message 9 "\"Sorry, gotta run!\""
#message 10 "\"I'm headed to the pharmacy. I have to pick up my prescription.\""
#message 11 "\"I can't believe the copays are so high here!\""
#message 12 "\"Leave me alone. I'm not feeling well.\""

#message 13 "\"Can't talk now, I'm late for my appointment.\""
#message 14 "\"I hope the doctor can tell me how to get rid of this fungus!\""
#message 15 "\"Can you believe this place used to be where the best computer games were developed in the 80s and 90s?\""
#message 16 "\"My mother told me not to talk to strangers.\""
