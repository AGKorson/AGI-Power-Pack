[ ********************************************************************
[
[ rm.ParkingLot
[
[ 
[
[ ********************************************************************

#define egosize     v220

if (newRoom) {
  [ run initialization code
  call(lgc.RoomInit);
  
  [ this is the first cycle through this room
  [ load, draw and discard the picture resource for the current room
  load.pic(currentRoom);
  draw.pic(currentRoom);
  discard.pic(currentRoom);
  
  [ the horizon defines the upper limit of ego's movement
  set.horizon(117);
  
  [ add permanently parked cars
  load.view(vw.ParkedCars);
  #define tmpView     v220
  #define tmpLoop     v221
  #define tmpCel      v222
  #define tmpX        v223
  #define tmpY        v224
  #define tmpPri      v225
  #define tmpMgn      v226
 
  [ parking spot 1
  tmpView = vw.ParkedCars;
  tmpLoop = 1;
  random(0, 11, tmpCel);
  tmpX = 106;
  tmpY = 141;
  tmpPri = 13;
  tmpMgn = 4;
  add.to.pic.v(tmpView, tmpLoop, tmpCel, tmpX, tmpY, tmpPri, tmpMgn);
  [ parking spot 2
  tmpLoop = 0;
  random(0, 11, tmpCel);
  tmpX = 92;
  tmpY = 154;
  tmpPri = 14;
  add.to.pic.v(tmpView, tmpLoop, tmpCel, tmpX, tmpY, tmpPri, tmpMgn);
  [ parking spot 3 ok
  tmpLoop = 1;
  random(0, 11, tmpCel);
  tmpX = 51;
  tmpY = 167;
  tmpPri = 15;
  add.to.pic.v(tmpView, tmpLoop, tmpCel, tmpX, tmpY, tmpPri, tmpMgn);
  [ parking spot 4 ok
  tmpLoop = 1;
  random(0, 11, tmpCel);
  tmpX = 46;
  tmpY = 155;
  tmpPri = 14;
  add.to.pic.v(tmpView, tmpLoop, tmpCel, tmpX, tmpY, tmpPri, tmpMgn);
  [ parking spot 5 ok
  tmpLoop = 0;
  random(0, 11, tmpCel);
  tmpX = 26;
  tmpY = 156;  
  tmpPri = 0;
  add.to.pic.v(tmpView, tmpLoop, tmpCel, tmpX, tmpY, tmpPri, tmpMgn);
  discard.view(vw.ParkedCars);
  
  [ load tiny ego view
  load.view(vw.EgoSmall);
  
  [ if starting or restarting 
  if ((previousRoom == rm.Title ||    [ just come from intro screen 
      previousRoom == 0))  {          [ or restarted game
    [ add ego to screen at starting position
    position(ego, 21, 159);
  } else {
    [ coming from lobby
    position(ego, 54, 121);
  }
  set.view(ego, vw.EgoSmall);
  egodir = STOPPED;
  
  [ add toolbar buttons
  load.view(vw.Toolbar);
  call(lgc.InitToolbar);
  
  [ add ego to the room
  draw(ego);
  
  [ display the picture on screen
  show.pic();
  [ fade in
  block(FADE.IN, 0, 25, 0); [ fade.in(BYVAR, DELAY, COLOR)
  [ restore cursor
  cursoricon = cursormode;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  return();
}

[*****
:handleInput       [ check for input from player
[*****
if (haveInput && !haveMatch && unknownWordNum == 0) {
  [ place said tests here
  if (said("look")) {
    print("You are in the Sierra building parking lot.");
  }
  
}

[ toolbar button checks
if (controller(cLeftClick)) {
  if (cursormode == C_LOOK) {
    [ same as 'look'
    if (mouseY > 25) {
      print("You are in the Sierra building parking lot.");
      set.key(-1, -1, cLeftClick); [ clear.controller(cNUM);
    }
  }

  if (cursormode == C_TALK) {
    [ same as 'talk to'
    if (mouseY > 25) {
      print("There is no one here to talk to.");
      set.key(-1, -1, cLeftClick); [ clear.controller(cNUM);
    }
  }

  if (cursormode == C_USE) {
    [ same as 'use item'
    if (mouseY > 25) {
      print("There is nothing to manipulate here.");
      set.key(-1, -1, cLeftClick); [ clear.controller(cNUM);
    }
  }
}

[*****
:noInput           [  all non-input related code goes here 
[*****

[ paeked cars in right parking lot don't quite align with priority bands
get.posn(ego, work1, work2);
if (work1 > 102 && work1 < 121 && work2 > 141 && work2 < 145) {
  set.priority(ego, 13);
} else {
  if (work1 > 88 && work1 < 108 && work2 >154 && work2 < 157) {
    set.priority(ego, 14);
  } else {
    release.priority(ego);
  }
}

[*****
:exit              [  test for leaving the room
[*****

[ add exit checks here (or use layout editor)

if (egoHitSpecial) {
  [ show wait cursor
  cursoricon = C_WAIT;
  set.key(1, &cursoricon, mouse); [ set.cursor(BYVAR, MSGNUM);
  [ fade out, then change room
  block(FADE.OUT, 0, FADECOUNT, BLACK); [ fade.out(BYVAR, DELAY, COLOR)
  [ move to lobby
  new.room(rm.Lobby);  [ ##LE001##
}

return();


[ **************************************
[ DECLARED MESSAGES
[ **************************************
#message 1 "%g40"
#message 2 "%g41"
#message 3 "%g42"
#message 4 "%g43"
#message 5 "%g44"
#message 6 "%g45"
#message 7 "You are in the Sierra building parking lot."
#message 8 "You see nothing of interest."
#message 9 "There is no one here to talk to."
#message 10 "There is nothing to manipulate here."