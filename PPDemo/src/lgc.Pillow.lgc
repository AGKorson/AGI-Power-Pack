[*********************************************************************
[ 
[ lgc.Pillow
[ 
[*********************************************************************

[ power pack support
#include "powerpack.txt"

[***************************************
[ LOCAL DEFINES
[***************************************
[ none

[***************************************
[ MAIN CODE BLOCK 
[***************************************

[ pause game clock while viewing
block(PAUSE.CLOCK, PAUSE_CLOCK, 0, 0); [ pause.clock(MODE)

[ no script entries for this sequence
set(noScript);
[ no regular input either
prevent.input();
status.line.off();

[ fade out until done loading
block(FADE.OUT, BYNUM, 5, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)
[ use default mouse cursor
cursoricon = C_DEFAULT;
set.key(BYVAR, &cursoricon, mouse); [ set.cursor(FN_MODE, MSGNUM);

[ hide objects while showing manual
block(HIDE.OBJS, HIDE_OBJS, 0, 0); [ hide.objs(MODE);

work1 = pic.Pillow;
load.pic(work1);
draw.pic(work1);
show.pic();
discard.pic(work1);

[ fade back in
block(FADE.IN, BYNUM, 5, BLACK); [ fade.in(FN_MODE, DELAY, COLOR)

[ main input loop; wait until a key is pressed
lastChar = 0;
:waitforit
if (!have.key()) {
  goto(waitforit);
}
[ any key will exit

if (!scorePillow) {
  set(scorePillow);
  work1 = 3;
  call(lgc.ScoreHandler);
}

[ fade out during the switch
block(FADE.OUT, BYNUM, 5, BLACK); [ fade.out(FN_MODE, DELAY, COLOR)

return();

[***************************************
[ DECLARED MESSAGES
[***************************************
#message 1 "%g20"
#message 2 "%g21"
#message 3 "%g22"
#message 4 "%g23"
#message 5 "%g24"
#message 6 "%g25"
#message 7 "%g26"
#message 8 "%g27"
