[ 
[ lgc.TestPart1
[

[ **************************************
[ LOCAL DEFINES
[ **************************************
#define strN              s3
#define strNo             s4
#define strAnswer         s5

#define tmpTimer1         v220
#define tmpTimer2         v221
#define msgTimer          v222
#define stepcounter       v223
#define tmpRow            v224
#define tmpCol            v225
#define tmpMsg            v226
#define volume            v227

[***********************************************************
[ run tests 1-20:
[
[ TEST1:  49 controller assignment slots/clearing controllers
[ TEST2:  'set.game.id' does not quit the game
[ TEST3:  extended characters in graphics and text modes, 
[ TEST4:  'clear.text.rect' and 'clear.lines' commands in
[ TEST5:  full color text printing/display on the graphics
[ TEST6:  'set.text.attributes' command passes colors by
[ TEST7:  'set.text.attributes' command allows "pushing" 
[ TEST8:  set message box colors (foreground, background
[ TEST9:  80 column 'graphics' and 'text.screen'  modes
[ TEST10: 'auto-cycle' feature
[ TEST11: 'get.num' command so it ignores non-numeric key
[ TEST12: 'set.string' to set the input line text
[ TEST13: 'set.string' to change the string compare function
[ TEST14: mouse support (changing cursor; 40/80; inventory)
[ TEST15: input processing ignores non-printable characters
[ TEST16: inventory screen uses graphics display
[ TEST17: custom status line
[ TEST18: 'click-to-move' support when the mouse is active, 
[         and options
[ TEST19: 'wander' command correction
[ TEST20: FMsynth and MIDI sound card support
[***********************************************************


[ TEST1:  49 controller assignment slots
++stepcounter;
if (teststep == stepcounter) {
  if (subteststep == 0) {
    if (newtest) {
      display(24, 0, "%v63: CONTROLLER ASSIGNMENT TEST");
      reset(newtest);
      [ add 20 more controllers; 49th should work,
      [ 50th should not
      set.key(0, 120, c40); [ 31 ALT+1
      set.key(24, 0, c44); [ 32 CTRL+X
      set.key(0, 120, c40); [ 33
      set.key(0, 120, c40); [ 34
      set.key(0, 120, c40); [ 35
      set.key(0, 120, c40); [ 36
      set.key(0, 120, c40); [ 37
      set.key(0, 120, c40); [ 38
      set.key(0, 120, c40); [ 39
      set.key(0, 120, c40); [ 40
      set.key(0, 120, c40); [ 41
      set.key(0, 120, c40); [ 42
      set.key(0, 120, c40); [ 43
      set.key(0, 120, c40); [ 44
      set.key(0, 120, c40); [ 45
      set.key(0, 120, c40); [ 46
      set.key(0, 120, c40); [ 47
      set.key(123, 0, c41); [ 48 {
      set.key(124, 0, c42); [ 49 |
      set.key(125, 0, c43); [ 50 }
      [ '{' and '|' should trigger controllers
      [ with no input characters
      [ '}' should NOT trigger the controller
      [ but will add to input line 
      print("Press '{', '|', and '}' to run the test.\n\n"
            "First two should show a message, third should "
            "just show up on input line.");
      print("Press CTRL+Z or enter 'continue' after testing.");
    }
    if (controller(c40)) {
      print("generic controller");
    }
    if (controller(c41)) {
      print("you pressed '{'");
    }
    if (controller(c42)) {
      print("you pressed '|'");
    }
    if (controller(c43)) {
      set.text.attribute(LT_RED, BLACK);
      display(24, 0, "TEST FAILED                   ");
      print("you pressed '}' BUT you also should never see this message");
      print("TEST FAILED. INVESTIGATE.");
      quit(1);
    }
    if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
      print("Moving on to next test.");
      ++subteststep;
      set(newtest);
    }
    return();
  }
  
  if (subteststep == 1) {
    [ check for controller cancel
    if (newtest) {
      display(24, 0, "%v63: CONTROLLER ASSIGNMENT TEST");
      reset(newtest);
      print("Try pressing '{' or '|' again to confirm they can be canceled.");
      print("Press CTRL+Z or enter 'continue' after testing.");
    }
    [ clear the controllers
    set.key(-1, -1, c41); [ clear.controller(cNUM);
    set.key(-1, -1, c42); [ clear.controller(cNUM);
    
    [ these should never happen
    if (controller(c41)) {
      set.text.attribute(LT_RED, BLACK);
      display(24, 0, "TEST FAILED                   ");
      print("you pressed '{' BUT you also should never see this message");
      print("TEST FAILED. INVESTIGATE.");
      quit(1);
    }
    if (controller(c42)) {
      print("you pressed '|'");
      set.text.attribute(LT_RED, BLACK);
      display(24, 0, "TEST FAILED                   ");
      print("you pressed '|' BUT you also should never see this message");
      print("TEST FAILED. INVESTIGATE.");
      quit(1);
    }
    
    if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
      print("Moving on to next test.");
      ++teststep;
      set(newtest);
    }
    return();
  }
}

[ TEST2:  'set.game.id' does not quit the game
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: SET.GAME.ID TEST");
    print("If this fails, game will quit without warning...");
    set.game.id("PP%v30");
    [ if successful, move to next test
    print("gameid changed to 'PP%v30'\n\n"
          "Moving on to next test.");
    ++teststep;
    set(newtest);
  }
  return();
}

[ TEST3:  extended characters in graphics and text modes, 
[         regardless of color
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    reset(newtest);
    display(24, 0, "%v63: EXTENDED CHARACTER SUPPORT TEST");
    print("Extended characters will be displayed in a print "
          "message.");
    if (IntVersion > VERSION_2272) {
      print("Also check the menus; you should see "
            "some extended characters there as well.");
      }
    print("Press CTRL+Z or enter "
          "'continue' when you are ready to move to next test.");
    print("ÄÅÇÉÑÖÜáàâäãåçéè\n\n"
          "êëíìîïñóòôöõúùûü\n\n"
          "†°¢£§•¶ß®©™´¨≠ÆØ\n\n"
          "∞±≤≥¥µ∂∑∏π∫ªºΩæø\n\n"
          "¿¡¬√ƒ≈∆«»… ÀÃÕŒœ\n\n"
          "–—“”‘’÷◊ÿŸ⁄€‹›ﬁﬂ\n\n"
          "‡·‚„‰ÂÊÁËÈÍÎÏÌÓÔ\n\n"
          "ÒÚÛÙıˆ˜¯˘˙˚¸˝˛");
  }
  
  [ check for continue
  if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
    configure.screen(-1, 0, 0);
    [ check results
    get.string(strAnswer, "Did ext chars display correctly? ", 22, 0, 3);
    goto(nexttest);
  }
  return();
}

[ TEST4:  'clear.text.rect' and 'clear.lines' commands in
[         color, and scrolling
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: CLEAR IN COLOR TEST");
    reset(newtest);
    
    print("Clearing a red area");
    clear.text.rect(13, 5, 15, 15, RED);
    print("Clearing green lines");
    clear.lines(5, 7, GREEN);
    print("scrolling the red zone up one blue line");
    set.text.attribute(BLACK, BLUE);
    clear.text.rect(13, 5, 15, 15, -1);
    print("scrolling the green zone up two blue lines");      
    clear.lines(5, 7, -2);
    [ restore colors
    set.text.attribute(WHITE, BLACK);
    [ check results
    get.string(strAnswer, "Did clear commands work? ", 22, 0, 3);
    goto(nexttest);
  }
  return();
}

[ TEST5:  full color text printing/display on the graphics
[         screen
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: FULL COLOR TEXT TEST");
    reset(newtest);
    [ show a couple color combos
    print("Displaying some text color combos...");
    set.text.attribute(LT_RED, MAGENTA);
    display(5, 5, " light red on magenta ");
    set.text.attribute(BROWN, YELLOW);
    display(7, 5, "   brown on yellow    ");
    set.text.attribute(WHITE, BLACK);
    
    [ check results
    get.string(strAnswer, "Did text display in correct color? ", 22, 0, 3);
    goto(nexttest);
  }
  return();
}

[ TEST6:  'set.text.attributes' command passes colors by
[         variable
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: SET COLORS BY VARIABLE TEST");
    reset(newtest);
    
    print("Some extended characters will be drawn with "
          "random combination of colors.");
    work3 = 11;
    :nextrow
      random(0, 15, work1);
      :getBG
        random(0, 15, work2);
      if (work2 == work1) {
        goto(getBG);
      }
      toggle.monitor();
      set.text.attribute(&work1, &work2);
      work1 = work3;
      work1 += 2;
      work2 = 12;
      display.v(work1, work2, work3);
      ++work3;
    if (work3 < 19) {
      goto(nextrow);
    }
    set.text.attribute(WHITE, BLACK);
    
    [ check results
    get.string(strAnswer, "Did text display in random colors? ", 22, 0, 3);
    goto(nexttest);
  }
  return();
}

[ TEST7:  'set.text.attributes' command allows "pushing" 
[         "and popping" the current attributes
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: PUSH/POP COLORS TEST");
    reset(newtest);
    set.text.attribute(WHITE, BLACK);
    [ push color
    set.text.attribute(-1, 0);
    set.text.attribute(BLUE, YELLOW);
    display(4, 8, "block 1: blue on yellow ");
    [ pop color
    set.text.attribute(0, -1);
    display(6, 8, "block 2: white on black ");
    
    [ check results
    get.string(strAnswer, "Did 2nd block print white on black? ", 22, 0, 3);
    goto(nexttest);
  }
  return();
}

[ TEST8:  set message box colors (foreground, background
[         and border) to any combination of colors
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: MSGBOX COLORS TEST");
    reset(newtest);
    block(MSGBOX.COLOR, BLACK, YELLOW, BROWN); [ msgbox.color(BORDER, FG, BG)
    print("Yellow text on a brown background with a black border.");
    print("Now you choose the colors.");
    get.num("border (0-15)", work1);
    get.num("foreground (0-15)", work2);
    get.num("background (0-15)", work3);
    clear.lines(22, 22, BLACK);
    block(MSGBOX.COLOR.V, &work1, &work2, &work3); [ msgbox.color.v(vBORDER, vFG, vBG)
    print("Here is a message with your custom colors.");
    
    block(MSGBOX.COLOR, RED, BLACK, WHITE); [ msgbox.color(BORDER, FG, BG)
    
    [ check results
    get.string(strAnswer, "Did msgboxes have correct colors? ", 22, 0, 3);
    goto(nexttest);
  }
  return();
}

[ TEST9:  80 column 'graphics' and 'text.screen'  modes
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: 80 COLUMN MODE TEST");
    reset(newtest);
    print("Switching to 80 column text mode");
    set.text.attribute(BLACK, LT_GRAY);
    configure.screen(-1, 1, 1);
    display(11, 14, "  This is the text screen in eighty column mode.  "); 
    display(12, 14, "Press any key to switch to forty column text mode.");
    lastChar = 0;
    :wait1
    if (!have.key()) {
      goto(wait1);
    }
    configure.screen(-1, 1, 0);
    display(11, 1, "      Text in forty column mode.      ");
    display(12, 1, "Press any key to test graphics screen.");
    lastChar = 0;
    :wait2
    if (!have.key()) {
      goto(wait2);
    }
    set.text.attribute(WHITE, BLACK);
    configure.screen(-1, 0, 1);
    display(24, 0, "%v63: 80 COLUMN MODE TEST");
    if (IntVersion > VERSION_2272) {
      s6 = "Confirm the menu displays correctly in this mode. ";
    } else {
      s6 = "";
    }
    print("This is the graphics screen in eighty column mode. "
          "%s6Press CTRL+Z or enter 'continue' "
          "to switch back to forty column mode.");
  }
  [ check for continue
  if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
    configure.screen(-1, 0, 0);
    display(24, 0, "%v63: 80 COLUMN MODE TEST");
    [ check results
    get.string(strAnswer, "Did display show correct columns? ", 22, 0, 3);
    goto(nexttest);
  }
  return();
}

[ TEST10: 'auto-cycle' feature
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: AUTOCYCLE TEST");
    reset(newtest);
    print("Enabling auto-cycle for ego. He should not cycle "
          "when not moving. A 'block' area is set up by the "
          "right exit for testing. When you are done testing, "
          "press CTRL+Z or enter 'continue'.");
    block(131, 134, 146, 164);
    work1 = 1;
    if (IntVersion == VERSION_2089) {
      init.joy();
      set.cel.v(ego, work1);
    } else {
      set.upper.left(&ego, &work1);
    }
  }
  [ check for continue
  if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
    unblock();
    [ check results
    get.string(strAnswer, "Did ego cycle correctly? ", 22, 0, 3);
    goto(nexttest);
  }
  return();
}

[ TEST11: 'get.num' command so it ignores non-numeric key
[         presses
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: GET.NUM TEST");
    reset(newtest);
    print("Try entering non-numeric characters in the "
          "following 'get.num' command. Confirm backspace "
          "works too.");
    get.num("Enter any number: ", work1);
    print("You entered '%v46'");
    [ check results
    get.string(strAnswer, "Did get.num work correctly? ", 22, 0, 3);
    goto(nexttest);
  }
  return();
}

[ TEST12: 'set.string' to set the input line text
[         programmatically
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: SET INPUT LINE TEST");
    reset(newtest);
    print("Changing the input line to 'test test test'. Confirm "
          "it can be modified, including all the way back to the "
          "start. Entering just 'test' will run the test again. "
          "Press CTRL+Z or enter 'continue' when you are done testing.");
    set.string(s255, "test test test");
  }
  
  [ check for test reset
  if (said("test")) {
    set.string(s255, "test test test");
  }
  
  [ check for continue
  if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
    unblock();
    [ check results
    get.string(strAnswer, "Did input line work correctly? ", 22, 0, 3);
    goto(nexttest);
  }
  return();
}

[ TEST13: 'set.string' to change the string compare function
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: COMPARE STRINGS TEST");
    reset(newtest);
    [ compare string test
    print("The next series of tests will compare some "
          "strings in the three different modes.");
           
    s6 = " Test String ";
    s8 = "test.string";
    s9 = "TEST STRING";
    s10 = "Test String";
    s11 = "Test String";
    [ check binary
    print("Testing binary compare mode.");
    set.string(s254, m2);
    if (compare.strings(s8, s9)) {
      goto(fail);
    }
    if (compare.strings(s9, s10)) {
      goto(fail);
    }
    if (!compare.strings(s10, s11)) {
      goto(fail);
    }
    if (compare.strings(s11, s6)) {
      goto(fail);
    }
    [ check text
    print("Binary compare mode passed. Testing text compare.");
    set.string(s254, m1);
    if (compare.strings(s8, s9)) {
      goto(fail);
    }
    if (!compare.strings(s9, s10)) {
      goto(fail);
    }
    if (!compare.strings(s10, s11)) {
      goto(fail);
    }
    if (compare.strings(s11, s6)) {
      goto(fail);
    }
    [ check AGI
    print("Text compare mode passed. Testing AGI compare.");
    set.string(s254, m0);
    [ for 2.089 only, this test must fail
    if (IntVersion == VERSION_2089) {
      if (compare.strings(s8, s9)) {
        goto(fail);
      }
    } else {
      [ for all others, this test should pass
      if (!compare.strings(s8, s9)) {
        goto(fail);
      }
    }
    if (!compare.strings(s9, s10)) {
      goto(fail);
    }
    if (!compare.strings(s10, s11)) {
      goto(fail);
    }
    if (!compare.strings(s11, s6)) {
      goto(fail);
    }
    print("AGI compare mode passed. All compare tests passed.\n\n"
          "Moving on to next test.");
    ++teststep;
    subteststep = 0;
    set(newtest);
  }
  return();
}

[ TEST14: mouse support (changing cursor; 40/80; inventory)
++stepcounter;
if (teststep == stepcounter) {
  if (newtest && subteststep == 0) {
    display(24, 0, "%v63: MOUSE SUPPORT TEST");
    reset(newtest);
    [ check for mouse
    if (!isset(enableDblClick)) {
      print("No mouse detected. Skipping mouse tests.");
      ++teststep;
      set(newtest);
      return();
    }
    set(hasMouse);
    if (IntVersion > VERSION_2272) {
      enable.item(mouse);
    } else {
      set.key(1, 0, c251);
    }
    print("Confirm basic mouse function is working. Cursor should "
          "be visible.");
    if (IntVersion > VERSION_2272) {
      print("You should be able to click status line to show the menu. "
            "You can click to highlight a menu item, and double click "
            "to select menu items.");
    }
    print("Click to dismiss print messages as well. Press CTRL+Z or "
          "enter 'continue' when you are ready to move on.");
    subteststep = 1;
  }
  [ subtest 1: mouse on
  if (subteststep == 1) {
    if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
      [ check results
      get.string(strAnswer, "Did mouse work correctly? ", 22, 0, 3);
      goto(nextsubtest);
    }
  }
  [ subtest 2: change cursor
  if (subteststep == 2) {
    if (newtest) {
      print("Next test will change cursor. Press CTRLZ+Z or enter "
            "'continue' when you are ready to move on.");
      set.key(0, &m102, mouse); [ set.key(BYVAR, mCURSOR*, c255);
      reset(newtest);
    }
    if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
      [ check results
      get.string(strAnswer, "Did cursor change to walking man? ", 22, 0, 3);
      [ use byval
      work1 = &m103;
      set.key(1, &work1, mouse); [ set.key(BYVAR, mCURSOR*, c255);
      goto(nextsubtest);
    }
  }
  
  [ subtest 3: 80 col
  if (subteststep == 3) {
    if (newtest) {
      print("Do same checks in 80 column mode.");
      configure.screen(-1, 0, 1);
      set.key(0, &m102, mouse); [ set.key(BYVAR, mCURSOR*, c255);
      display(24, 0, "%v63: MOUSE SUPPORT TEST");
      reset(newtest);
    }
    if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
      set.key(0, &m103, mouse);
      [ check results
      get.string(strAnswer, "Did mouse work correctly in 80col mode? ", 22, 0, 3);
      configure.screen(-1, 0, 0);
      display(24, 0, "%v63: MOUSE SUPPORT TEST");
      goto(nextsubtest);
    }
  }

  [ subtest 4: disable cursor
  if (subteststep == 4) {
    if (newtest) {
      if (IntVersion > VERSION_2272) {
        disable.item(mouse);
      } else {
        set.key(0, 0, c251);
      }
      [ check results
      get.string(strAnswer, "Did cursor disappear? ", 22, 0, 3);
      goto(nexttest);
    }
  }
  return();
}

[ TEST15: input processing ignores non-printable characters
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: NON-PRINTABLE CHARACTERS TEST");
    reset(newtest);
    print("Mouse will be enabled for rest of tests.");
    print("Confirm that non-printable characters (CTRL+A through Z) "
          "that are not assigned to controllers "
          "are ignored on the input line and in get.string command. "
          "Press CTRL+Z or enter 'continue' when you are done testing.");
    get.string(s7, "test get string: ", 22, 1, 20);
    clear.lines(22, 22, BLACK);
    accept.input();
    print("Now test the input line.");
  }
  [ check for continue
  if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
    [ check results
    get.string(strAnswer, "Did input line work correctly? ", 22, 0, 3);
    subteststep = 0;
    goto(nexttest);
  }
  return();
}

[ TEST16: inventory screen uses graphics display
++stepcounter;
if (teststep == stepcounter) {
  if (newtest && subteststep == 0) {
    display(24, 0, "%v63: INVENTORY SCREEN TEST");
    reset(newtest);
    get("strange colored cube");
    get(i2);
    get("another sample item");
    print("Inventory screen uses graphics mode now, to support "
          "mouse input.");
    if (IntVersion > VERSION_2272) {
      print("Use F4 to show the screen. Right-click "
            "to dismiss the screen. Single-click to highlight an "
            "item. Double-click to select an item. Enter 'continue' "
            "when done testing.");
    } else {
      print("Use the TAB key to show the screen. Left- or right-click "
            "to dismiss the screen. "
            "Enter 'continue' when done testing.");
    }
    subteststep = 1;
  }
  [ subtest 1: inventory test
  if (subteststep == 1) {
    if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
      [ check results
      get.string(strAnswer, "Did inv screen work correctly? ", 22, 0, 3);
      goto(nextsubtest);
    }
  }
  [ subtest 2: 80 col
  if (subteststep == 2) {
    if (newtest) {
      print("Do same checks in 80 column mode.");
      configure.screen(-1, 0, 1);
      display(24, 0, "%v63: INVENTORY SCREEN TEST");
      reset(newtest);
    }
    if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
      [ check results
      get.string(strAnswer, "Did inv screen work correctly? ", 22, 0, 3);
      configure.screen(-1, 0, 0);
      display(24, 0, "%v63: INVENTORY SCREEN TEST");
      subteststep = 0;
      goto(nexttest);
    }
  }
  return();
}

[ TEST17: custom status line
++stepcounter;
if (teststep == stepcounter) {
  if (newtest && subteststep == 0) {
    display(24, 0, "%v63: CUSTOM STATUS LINE TEST");
    reset(newtest);
    print("A custom status line will be shown that displays "
          "mouse coordinates, and sound status as a musical note "
          "icon.");
    print("Enter 'status on' to show the status bar, 'status off' "
          "to hide it.");
    print("Enter 'status reset' to show the default status bar "
          "and 'status test' to see the custom status bar.");
    print("After confirming it works correctly, press CTRL+Z or enter 'continue'.");
    set.cursor.char(" l6, v3, v27, v28, v29, f9");
    subteststep = 1;
  }
  [ subtest 1: custom status test
  if (subteststep == 1) {
    if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
      [ check results
      get.string(strAnswer, "Is statusline working correctly? ", 22, 0, 3);
      goto(nextsubtest);
    }
  }
  [ subtest 2: 80 col
  if (subteststep == 2) {
    if (newtest) {
      print("Do same checks in 80 column mode.");
      set(graphics80);
      configure.screen(-1, 0, 1);
      display(24, 0, "%v63: CUSTOM STATUS LINE TEST");
      reset(newtest);
    }
    if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
      [ check results
      get.string(strAnswer, "Is statusline working correctly? ", 22, 0, 3);
      reset(graphics80);
      configure.screen(-1, 0, 0);
      display(24, 0, "%v63: CUSTOM STATUS LINE TEST");
      goto(nexttest);
    }
  }
  return();
}

[ TEST18: 'click-to-move' support when the mouse is active, 
[         and options
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: CLICK-TO-MOVE TEST");
    print("With mouse enabled, left clicks can be used to "
          "move ego. To disable, enter 'clickmove off'. To "
          "enable it again, enter 'clickmove on'.");
    print("For normal AGI movement, enter 'clickmove normal'. "
          "For straight-line movement, enter 'clickmove straight'. "
          "Press CTRL+Z or enter 'continue' when done testing.");
    set(clickmove);
    set.key(0, &m102, mouse); [ set.cursor(BYVAR, MSGNUM);
    reset(newtest);
  }
  if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
    [ check results
    get.string(strAnswer, "Is clickmove working correctly? ", 22, 0, 3);
    set.key(0, &m103, mouse); [ set.cursor(BYVAR, MSGNUM);
    goto(nexttest);
  }
  return();
}

[ TEST19: 'wander' command correction
++stepcounter;
if (teststep == stepcounter) {
  if (newtest) {
    display(24, 0, "%v63: WANDER BUG TEST");
    print("Wander bugfix will be tested next. This may be hard to "
          "confirm without watching ego wander for awhile. If the "
          "patch works, you won't see ego move in long straight "
          "lines - you should see shorter lines before direction "
          "is changed. Press CTRL+Z or enter 'continue' when you "
          "are done testing.");
    wander(ego);
    reset(newtest);
  }
  if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
    [ check results
    get.string(strAnswer, "Is wander working correctly? ", 22, 0, 3);
    player.control();
    subteststep = 0;
    goto(nexttest);
  }
  return();
}

[ TEST20: FMsynth and MIDI sound card support
++stepcounter;
if (teststep == stepcounter) {
  if ((said("continue") || controller(cNext) || controller(cRightDblClick))) {
    [ force stop
    stop.sound();
  }
  work1 = 63;
  work1 -= attenuation;
  if (work1 != volume) {
    volume = work1;
    display(2, 15, "volume: %v227 ");
  }
  
  if (newtest && subteststep == 0) {
    display(24, 0, "%v63: SOUND TEST");
    set(soundOn);
    display(2, 15, "volume: %v227 ");
    reset(newtest);
    if (numberOfVoices == 1) {
      print("No sound boards detected. Skipping sound tests.");
      ++teststep;
      return();
    }
    load.sound(kq1theme);
    load.sound(s.Noise);
    if (numberOfVoices == 2) {
      s6 = "n FM synth";
      s7 = "this card";
      set(hasFM);
    }
    if (numberOfVoices == 3) {
      s6 = " MIDI";
      s7 = "this card";
      set(hasMIDI);
    }
    if (numberOfVoices == 4) {
      s6 = "n FM synth sound card and a MIDI";
      s7 = "these cards";
      set(hasFM);
      set(hasMIDI);
    }
    print("Detected a%s6 sound card. Testing sound capabilities "
          "for %s7.");
    print("For all tests, you can press CTRL+Z or enter 'continue' "
          "to end the sound early.");
    subteststep = 1;
    set(newtest);
  }
  [ subtest 1: single channel PC
  if (subteststep == 1 && newtest) {
    display(24, 0, "%v63: SOUND TEST");
    display(2, 15, "volume: %v227 ");
    print("First test is single channel PC sound. Volume should not be "
          "adjustable.");
    reset(newtest);
    reset(4channelsound);
    sound(kq1theme, soundDone);
  }
  
  [ subtest 2: 4chan fm synth default
  if (subteststep == 2) {
    if ((numberOfVoices == 2 || numberOfVoices == 4)) {
      if (newtest) {
        display(24, 0, "%v63: SOUND TEST");
        display(2, 15, "volume: %v227 ");
        print("Next test is FM synth, 4 channel with default instruments. "
        "Try adjusting volume during playback.");
        reset(newtest);
        set(4channelsound);
        sound(kq1theme, soundDone);
      }
    } else {
      ++subteststep;
    }
  }
  [ subtest 3: 4chan fm synth chg channel
  if (subteststep == 3) {
    if ((numberOfVoices == 2 || numberOfVoices == 4)) {
      if (newtest) {
        display(24, 0, "%v63: SOUND TEST");
        display(2, 15, "volume: %v227 ");
        print("Changing FM synth channels to 20-21-22.");
        reset(newtest);
        [ to change channel envelopes (instruments):
        work1 = 0;
        work2 = 20;
        set.key(&work1, &work2, c253); [ set.key(vCHANNEL*, vINSTRUMENT*, c253);
        ++work1;
        ++work2;
        set.key(&work1, &work2, c253); [ set.key(vCHANNEL*, vINSTRUMENT*, c253);
        ++work1;
        ++work2;
        set.key(&work1, &work2, c253); [ set.key(vCHANNEL*, vINSTRUMENT*, c253);
        sound(kq1theme, soundDone);
      }
    } else {
      ++subteststep;
    }
  }
  [ subtest 4: 4chan fm synth custom channel
  if (subteststep == 4) {
    if ((numberOfVoices == 2 || numberOfVoices == 4)) {
      if (newtest) {
        display(24, 0, "%v63: SOUND TEST");
        display(2, 15, "volume: %v227 ");
        print("Setting custom FM synth channels (same as 1-2-3).");
        reset(newtest);
        work1 = 0;
        work2 = 4;
        set.key(&work1, &work2, c252); [  set.key(vCHANNEL*, vENVDATAMSG*, c252);
        ++work1;
        ++work2;
        set.key(&work1, &work2, c252); [  set.key(vCHANNEL*, vENVDATAMSG*, c252);
        ++work1;
        ++work2;
        set.key(&work1, &work2, c252); [  set.key(vCHANNEL*, vENVDATAMSG*, c252);
        sound(kq1theme, soundDone);
      }
    } else {
      ++subteststep;
    }
  }
  [ subtest 5: 4chan fm synth noise default
  if (subteststep == 5) {
    if ((numberOfVoices == 2 || numberOfVoices == 4)) {
      if (newtest) {
        display(24, 0, "%v63: SOUND TEST");
        display(2, 15, "volume: %v227 ");
        print("Testing FM synth noise channels.");
        reset(newtest);
        sound(s.Noise, soundDone);
      }
    } else {
      ++subteststep;
    }
  }
  [ subtest 6: 4chan fm synth custom noise test
  if (subteststep == 6) {
    if ((numberOfVoices == 2 || numberOfVoices == 4)) {
      if (newtest) {
        display(24, 0, "%v63: SOUND TEST");
        display(2, 15, "volume: %v227 ");
        print("Setting custom FM synth noise (same as 4-5).");
        reset(newtest);
        work1 = 3;
        work2 = 4;
        set.key(&work1, &work2, c252); [  set.key(vCHANNEL*, vENVDATAMSG*, c252);
        ++work1;
        ++work2;
        set.key(&work1, &work2, c252); [  set.key(vCHANNEL*, vENVDATAMSG*, c252);
        sound(s.Noise, soundDone);
      }
    } else {
      ++subteststep;
    }
  }
  [ subtest 7: 4chan midi default
  if (subteststep == 7) {
    if((numberOfVoices == 3 || numberOfVoices == 4)) {
      if (newtest) {
        display(24, 0, "%v63: SOUND TEST");
        display(2, 15, "volume: %v227 ");
        print("Testing default MIDI.");
        reset(newtest);
        work1 = 1;
        set.key(&work1, 0, c254); [ set.key(MODE, ANY***, c254);
        sound(kq1theme, soundDone);
      }
    } else {
      ++subteststep;
    }
  }
  
  [ subtest 8: 4chan midi chg channel
  if (subteststep == 8) {
    if ((numberOfVoices == 3 || numberOfVoices == 4)) {
      if (newtest) {
        display(24, 0, "%v63: SOUND TEST");
        display(2, 15, "volume: %v227 ");
        print("Changing MIDI channels to 20-21-22.");
        reset(newtest);
        [ to change channel envelopes (instruments):
        work1 = 0;
        work2 = 20;
        set.key(&work1, &work2, c253); [ set.key(vCHANNEL*, vINSTRUMENT*, c253);
        ++work1;
        ++work2;
        set.key(&work1, &work2, c253); [ set.key(vCHANNEL*, vINSTRUMENT*, c253);
        ++work1;
        ++work2;
        set.key(&work1, &work2, c253); [ set.key(vCHANNEL*, vINSTRUMENT*, c253);
        sound(kq1theme, soundDone);
      }
    } else {
      ++subteststep;
    }
  }
  [ subtest 9: 4chan midi noise
  if (subteststep == 9) {
    if ((numberOfVoices == 3 || numberOfVoices == 4)) {
      if (newtest) {
        display(24, 0, "%v63: SOUND TEST");
        display(2, 15, "volume: %v227 ");
        print("Testing MIDI noise.");
        reset(newtest);
        sound(s.Noise, soundDone);
      }
    } else {
      ++subteststep;
    }
  }
  [ subtest 10: done
  if (subteststep == 10) {
    ++teststep;
    set(newtest);
    subteststep = 0;
    return();
  }
  
  if (soundDone) {
    reset(soundDone);
    [ check results
    get.string(strAnswer, "Did sound play correctly? ", 22, 0, 3);
    set(newtest);
    goto(nextsubtest);
  }
  return();
}
  
[ TEST21 and above are in second logic
++stepcounter;
return();

:nexttest
clear.lines(22, 22, BLACK);
accept.input();
if (!compare.strings(strAnswer, strN) && !compare.strings(strAnswer, strNo)) {
  [ if successful, move to next test
  print("Moving on to next test.");
  show.pic();
  ++teststep;
  set(newtest);
  subteststep = 0;
} else {
  :fail
  set.text.attribute(LT_RED, BLACK);
  display(24, 0, "TEST %v63 FAILED                   ");
  print("TEST %v63 FAILED. INVESTIGATE.");
  quit(1);
}
return();

:nextsubtest
clear.lines(22, 22, BLACK);
accept.input();
[display(24, 0, "%s3, %s4, %s5");
if (!compare.strings(strAnswer, strN) && !compare.strings(strAnswer, strNo)) {
  [ if successful, move to next test
  print("Moving on to next test.");
  show.pic();
  ++subteststep;
[print("new subteststep: %v64  %v63");
  set(newtest);
} else {
  set.text.attribute(LT_RED, BLACK);
  display(24, 0, "TEST %v63 FAILED                   ");
  print("TEST %v63 FAILED. INVESTIGATE.");
  quit(1);
}

return();

[ **************************************
[ MESSAGES
[ **************************************
[ Instruments
#message 1 "02023000F1F58404000000"
#message 4 "82040500F1A50C35000002"
#message 5 "42040805F1F401C4010002"
#message 6 "04420106A1F42143010100"
[#message 4 "82442000F3F30303000100"
[#message 5 "04042003A1A20102010100"
[#message 6 "02C410087162E1E2030300"
[7 08080707F7F70707030306
[8 88842007F1F10303000000

#message  9 "TEST MESSAGE"
#message 10 "Changed msg!"

#message 11 "ÄÅÇÉÑÖÜáàâäãåçéè"
#message 12 "êëíìîïñóòôöõúùûü"
#message 13 "†°¢£§•¶ß®©™´¨≠ÆØ"
#message 14 "∞±≤≥¥µ∂∑∏π∫ªºΩæø"
#message 15 "¿¡¬√ƒ≈∆«»… ÀÃÕŒœ"
#message 16 "–—“”‘’÷◊ÿŸ⁄€‹›ﬁﬂ"
#message 17 "‡·‚„‰ÂÊÁËÈÍÎÏÌÓÔ"
#message 18 "ÒÚÛÙıˆ˜¯˘˙˚¸˝˛ "

[ plus cursor
#message  101 "0707"
            "7ffe3ffc3ffc3ffc3ffc3ffc01808001"
            "800101803FFC3FFC3FFC3FFC3FFC7FFE"
            "00008001800180018001800180017E7E"
            "7E7E8001800180018001800180010000"
[ walk cursor
#message  102 "040F"
             "FFF1FFE0FFE0FFE0FFE0FFF1FFE17FC0"
             "3FC07FE0FFE1FFC1FF80FF087F183FBC"
             "0000000E000C000E000E0004000C0016"
             "801B0006000C001C0036006200430000"
             
[ default cursor
#message  103 "0000"
            "FF3FFF1FFF0FFF07FF03FF01FF007F00"
            "3F001F00FF01FF00FF307FF87FF8FFFC"
            "00000040006000700078007C007E007F"
            "807F007C006C00460006000300030000"
[ default cursor for 80col
#message  104 "0000"
            "FF3FFF0FFF03FF017F001F000F000300"
            "000000003F001F180F3C07FE07FF8FFF"
            "000000400070007C007E807FE07FF07F"
            "FC7F807F8067C043E001F00070000000"