[ ********************************************************************
[
[ lgc.Main: Main logic
[
[ This logic runs in every interpreter cycle and calls
[ other logics as needed.
[
[
[ To better manage variables and flags, they are 
[ grouped into reserved, global, local, and dynamic:
[
[ reserved = reserved by the interpreter 
[ (  v0 - v26;     f0 - f16, f20)
[
[ global = usable and accessible by all logics
[ ( v27 - v219;   clickmove - f19, f21 - f219)
[
[ local = usable and accessible by single room
[ (v220 - v239;  f220 - f239)
[
[ dynamic = usable and accesible by a non-room logic
[ (v240 - v255;  f240 - f255)
[
[ Local and dynamic variables/flags are reset whenever a new room is
[ loaded.
[
[ ********************************************************************

[ **************************************
[ LOCAL DEFINES
[ **************************************
#define rndNum                     v255

[ TODO: !!need to re-check all tests; I trashed the correct version, so now new functions may not be checked correctly
[ **************************************
[ ERROR CHECK
[ **************************************

[ if one of the few trappable errors in AGI is encountered, the game
[ is probably not able to continue; make an announcement to the player
[ and quit
if (errorNumber > 0) {
  call(lgc.Error); 
}

[ **************************************
[ GAME START
[ **************************************
if (currentRoom == 0) {
  [ when the game starts, the current room is zero, so all the
  [ setup/initialization code goes here
  [
  [ restarting a game also sets currentRoom to zero, and also
  [ requires some setup/initialization steps; begin with the
  [ steps that are common to both

  [ always load the powerpack FIRST
  if (!gameRestarted) {
    [ confirm platform is PC
    if (machineType != PC) {
      print("The AGI Power Pack Test will only run on a PC (or "
            "in DosBOX emulating a PC). No other platforms are "
            "supported.\nAborting...");
      quit(1);
    }
          
    [ run the powerpack mod BEFORE DOING ANYTHING ELSE
    call(lgc.Version2Check);
    if (!IntVersion) {
      print("Invalid AGI version. Power Pack Demo requires a valid "
            "MSDOS interpreter running on a PC, or in DOSBox. "
            "ScummVM and NAGI are not supported.\n\nAborting...");
      quit(1);
    }
    if (IntVersion == VERSION_2089) {
      call(lgc.mod2089);
      goto(continue);
    }
    if (IntVersion == VERSION_2272) {
      call(lgc.mod2272);
      goto(continue);
    }
    if (IntVersion == VERSION_2411) {
      call(lgc.mod2411);
      goto(continue);
    }
    if (IntVersion == VERSION_2425) {
      call(lgc.mod2425);
      goto(continue);
    }
    if (IntVersion == VERSION_2426) {
      call(lgc.mod2426);
      goto(continue);
    }
    if (IntVersion == VERSION_2435) {
      call(lgc.mod2435);
      goto(continue);
    }
    if (IntVersion == VERSION_2439) {
      call(lgc.mod2439);
      goto(continue);
    }
    if (IntVersion == VERSION_2440) {
      call(lgc.mod2440);
      goto(continue);
    }
    if (IntVersion == VERSION_2903) {
      call(lgc.mod2903);
      goto(continue);
    }
    if (IntVersion == VERSION_2911) {
      call(lgc.mod2911);
      goto(continue);
    }
    if (IntVersion == VERSION_2912) {
      call(lgc.mod2912);
      goto(continue);
    }
    if (IntVersion == VERSION_2915) {
      call(lgc.mod2915);
      goto(continue);
    }
    if (IntVersion == VERSION_2917) {
      call(lgc.mod2917);
      goto(continue);
    }
    if (IntVersion == VERSION_2936) {
      call(lgc.mod2936);
      goto(continue);
    }
  
    :continue
    work1 = IntVersion;
    work1 += 49;
    print.v(work1);
  } else {
    [ first step for restart is to retrieve interpreter version from errorinfo (v18)
    IntVersion = errorParameter;
  }

  [ default ego parameters
  egoStepSize = 1;
  egoStepTime = 1;
  egoCycleTime = 1;
  
  [ set the max score value for the game
  maxScore = MAX_SCORE_VALUE;
  
  [ set speed to normal (delay=2/20ths of a second)
  animationInterval = NORMAL_SPEED;

  [ if desired, enable debugging when game starts
  set(debugging); 
  [ set up trace window (remove this before releasing your game if
  [ you don't want debug features included)
  if (IntVersion > VERSION_2272) {
print("load 220 trace");
    load.logics(lgc.TraceInfo);
    trace.info(lgc.TraceInfo, 3, 15);
  } else {
print("load 160 trace");
    load.logics(lgc.TraceInfo160);
    trace.info(lgc.TraceInfo160, 3, 15);
  }
[  [ enable tracing as needed
[  set(enableTrace);
[  trace.on();


  [ now continue with specific steps depending on whether 
  [ this is a restart, or the original game beginning
  if (gameRestarted) {
  
    [ steps needed specifically for a restart
    print("Restarting - Version: %v30");
    
    [ make sure the display area below the main game
    [ picture is cleared
    clear.lines(22, 24, BLACK);
    
    [ if version has a menu (2.089/2.272 do not)
    if (IntVersion > VERSION_2272) {
      disable.item(cDummy);
      set(enableMenu);
      
      [ debug mode is disabled by default (remove these
      [ lines before distributing your game if you are
      [ not including debug mode)
      disable.item(cShowEgo);
      disable.item(cShowPriority);
      disable.item(cShowMem);
      disable.item(cObjInfo);
      disable.item(cXY);
      disable.item(cGimme);
    }
    
    [ set mouse dblclick speed
    if (enableDblClick) {
      dblClickDelay = 4;
    }
    
    [ check for restart and save tests
    s11 = "testrestore";
    if (compare.strings(s2, s11)) {
      set.string(s2, "");
      set(ppTesting);
      teststep = 41;
      subteststep = 1;
      set(newtest);
    }
    s11 = "testsave";
    if (compare.strings(s2, s11)) {
      set.string(s2, "");
      set(ppTesting);
      teststep = 42;
      subteststep = 2;
      set(newtest);
    }
    
    [ go to first room (skip Intro)
    new.room(rm.Entrance);
  } else {
    [ if room is 0, and not restarting, it means
    [ this is very beginning
    
    [ script size is 50 by default; that is usually adequate
    [ however, if you wish use a different value (larger for
    [ more complicated rooms that have lots of script entries,
    [ or smaller if you want to conserve a (tiny) amount of
    [ memory, set the script size before doing anything else)
    [ script.size(50);

    [ if version has a menu (2.089/2.272 do not)
    if (IntVersion > VERSION_2272) {
      [ next build the game menu (again, before doing anything
      [ else to keep room.0 memory block as small as possible)
      set.menu("AGI");
      set.menu.item("About€     ", cAbout);
      set.menu.item("Help   <F1>", cHelp);
      set.menu("File");
      set.menu.item("Save     <F5>", cSave);
      set.menu.item("Restore  <F7>", cRestore);
      set.menu.item("ÄÄÄÄÄÄÄÄÄÄÄÄÄ", cDummy);
      set.menu.item("Restart  <F9>", cRestart);
      set.menu.item("Quit  <Alt-Z>", cQuit);
      set.menu("Action");
      set.menu.item("See Object  <F4>", cShowObj);
      set.menu.item("Inventory  <Tab>", cStatus);
      set.menu("Special");
      set.menu.item("Sound On/Off      <F2>", cToggleSound);
      if (machineType == PC && monitorType < 2) {
        [ cga display can toggle monitor
        set.menu.item("Color/BW      <Ctrl R>", cToggleMonitor);
      }
      set.menu.item("Clock On/Off      <F6>", cClock);
      set.menu.item("Joystick      <Ctrl J>", cInitJoy);
      set.menu.item("Pause            <Esc>", cPause);
      set.menu("Speed");
      set.menu.item("Normal ", cNormal);
      set.menu.item("Slow   ", cSlow);
      set.menu.item("Fast   ", cFast);
      set.menu.item("Fastest", cFastest);
      [ set up debug menu - remove this menu and its menu
      [ items before distributing your game if you don't 
      [ want to include the debug mode
      set.menu("Debug");
      set.menu.item("Debug Help        ", cDebugHelp);
      set.menu.item("Ego Info   <Alt-E>", cShowEgo);
      set.menu.item("Pri Screen <Alt-P>", cShowPriority);
      set.menu.item("Memory     <Alt-M>", cShowMem);
      set.menu.item("Obj Info   <Alt-I>", cObjInfo);
      set.menu.item("Coords     <Alt-X>", cXY);
      set.menu.item("Get All           ", cGimme);
  
      [ submit the menu (which makes it ready to use)
      submit.menu();
      set(enableMenu);
      [ disable the separator lines
      disable.item(cDummy);
      
      [ debug mode is disabled by default (remove these
      [ lines before distributing your game if you are
      [ not including debug mode)
      disable.item(cShowEgo);
      disable.item(cShowPriority);
      disable.item(cShowMem);
      disable.item(cObjInfo);
      disable.item(cXY);
      disable.item(cGimme);
    }
    
    [ this logic sets up the game; assigns key controllers, 
    [ configures the screen and other startup activities
    call(lgc.Init);
    
    [ go to first room (skip Intro)
    new.room(rm.Entrance);
  }
}

[ **************************************
[ EVERY CYCLE
[ **************************************

[ if a new room has just been loaded
if (newRoom) {
  [ Note: Everything other than logic 0 is discarded
  [ from memory when new.room is executed

[!!!!!need to load trace logic here, not after restart!!!

  [ Load game specific functions logic into memory
  load.logics(lgc.GameFunctions);

  [ if debugging is active (remove this block when you distribute
  [ your game if you are not including the debug option)
  if (debugging) {
    [ load debug logic into memory
    load.logics(lgc.Debug);
  }

  [ always animate ego object when starting a new room
  animate.obj(ego);
  [ load and set view (for more complex games, this will probably
  [ need to be moved into each room's logic)
  load.view.v(currentEgoView);
  set.view.v(ego, currentEgoView);
  
  [ force clock value to update, if time is visible
  statusSeconds = 255; 
}

[ check for menu change requests
if (changeMenu) {
  reset(changeMenu);
  if (IntVersion <= VERSION_2272) {
    #message 70 "Logic 0 Message"
    #message 71 "CHANGE LGC0 MSG"
    print("Original message: \n  '%m70'");
    s6 = m71;
    block(SET.MSG, 0, &m70, &s6); [ set.msg(BYVAR, MSGOLD, STRNEW)
    print("Changed message: \n  '%m70'");
  } else {
    work3 = &m14;
    work2 = &s6;
    s6 = m44;
    block(SET.MSG, 1, &work3, &work2);
    work3 = &m18;
    s6 = m45;
    block(SET.MSG, 1, &work3, &work2);
    work3 = &m19;
    s6 = m46;
    block(SET.MSG, 1, &work3, &work2);
    work3 = &m20;
    s6 = m47;
    block(SET.MSG, 1, &work3, &work2);
    disable.item(cInitJoy);
  }
}
[#message 14 "Special"
[#message 18 "Joystick      <Ctrl J>"
[#message 19 "Pause            <Esc>"
[#message 20 "Speed"

#message 40 "Special"
#message 41 "Joystick      <Ctrl J>"
#message 42 "Pause            <Esc>"
#message 43 "Speed"
#message 44 "TEST"
#message 45 "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ"
#message 46 "Halt             <Esc>"
#message 47 ""

if (restoreMenu) {
  reset(restoreMenu);
  s6 = m40;
  block(SET.MSG, 0, &m14, &s6);
  s6 = m41;
  block(SET.MSG, 0, &m18, &s6);
  s6 = m42;
  block(SET.MSG, 0, &m19, &s6);
  s6 = m43;
  block(SET.MSG, 0, &m20, &s6);
  enable.item(cInitJoy);
}

[ check for ego death (usually happens a lot in AGI games...)
if (deathType > 0) {
  if (deathType != 255) {
    [ first cycle since player died. load the death logic into
    [ memory as it will handle player input from now on.
    [ load the death handler logic
    load.logics(lgc.Death);
    
    [ if version has a menu (2.089/2.272 do not)
    if (IntVersion > VERSION_2272) {
      [disable some menu items
      disable.item(cDebugHelp);
      disable.item(cSave);
      disable.item(cPause);
      disable.item(cShowObj);
      disable.item(cClock);
      disable.item(cInitJoy);
      disable.item(cNormal);
      disable.item(cFastest);
      disable.item(cFast);
      disable.item(cSlow);
      disable.item(cShowEgo);
      disable.item(cShowPriority);
      disable.item(cShowMem);
      disable.item(cObjInfo);
      disable.item(cXY);
      disable.item(cGimme);
    }
  }
    
  [ call the death handler, where you update the screen
  [ and display a message to player about how ego died
  call(lgc.Death);
  
  [ don't process any other commands after setting up the
  [ new room
  return();
}

[ ***********************
[ CHECK CONTROLLER INPUT
[ *********************** 

[ these controllers are only checked if game functions are active
if (!disableGameFunctions) {
  [ ACTIVATE DEBUG MODE
  if ((said("debug") || controller(cDebug))) 
    {
    [ if not in debug mode yet
    if (!debugging) 
      {
      [ activate it
      set(debugging);
      [ display the game-about message
      print(gameVersionMsg);
      [ and interpreter version
      version();
      [ load debug logic into memory
      load.logics(lgc.Debug);
      [ if version has a menu (2.089/2.272 do not)
      if (IntVersion > VERSION_2272) {
        [ enable debug menu items
        enable.item(cShowEgo);
        enable.item(cShowPriority);
        enable.item(cShowMem);
        enable.item(cObjInfo);
        enable.item(cXY);
        enable.item(cGimme);
      }
    }
  }

  [ SHOW DEBUG HELP SCREEN (remove these lines 
  [ before distributing your game if you are
  [ not including debug mode)
  if ((controller(cDebugHelp) ||
       said("debug", "help"))) {
    call(lgc.DebugHelp);
  }

  [ if version has a menu (2.089/2.272 do not)
  if (IntVersion > VERSION_2272) {
    [ MENU ACTIVATION
    if (controller(cMenu)) {
      menu.input();
    }
  }
  
  [ SAVE GAME
  if ((controller(cSave) || 
       said("save", "game") || 
       said("save"))) {
    save.game();
  }

  [ RESTORE GAME
  if ((controller(cRestore) || 
       said("restore", "game") || 
       said("restore"))) {
    restore.game();
  }

  [ RESTART GAME
  if ((controller(cRestart) || 
       said("restart", "game") || 
       said("restart"))) {
    restart.game();
  }

  [ SHOW HElP SCREEN
  if ((controller(cHelp) || 
       said("help"))) {
    call(lgc.GameHelp);
  }

  [ ECHO INPUT LINE (draws previous input on input line)
  if (controller(cEchoLine)) {
    echo.line();
  }

  [ CLEAR INPUT LINE
  if (controller(cCancelLine)) {
    cancel.line();
  }

  [ PAUSE GAME
  if ((controller(cPause) || 
       said("pause", "game") || 
       said("pause"))) {
    pause();
  }

  [ SHOW INVENTORY ITEMS
  if ((controller(cStatus) || 
       said("inventory"))) {
    status();
  }

  [ if version allows viewing items (2.089/2.272 do not)
  if (IntVersion > VERSION_2272) {
    [ EXAMINE INVENTORY ITEMS
    if ((controller(cShowObj) || 
         said("look", "inventory"))) {
         
      [ enable section of inventory items
      set(enableItemSelect);
      [ show inventory screen
      status();
      
      [ player has chosen an object (value of SelectedItem)
      [ note: SelectedItem is set to 255 if ESC is pressed 
      [in the inventory screen
      if (selectedItem > 0 && selectedItem != 255) {
        if (selectedItem == 1) {
          show.obj(vw.Inv.TestItem);
        }
        [ NOTE: If you have several objects, it is easier to give
        [ them consecutive view numbers. Then you can do the
        [ following (view numbers start at 220 for this
        [ example):
        [   v255 = selectedItem; v255 += 219;
        [   show.obj.v(v255);
        [ If you do this, you don't have to have separate statements for each
        [ object.
      }
        
      [ disable selection of inventory items
      reset(enableItemSelect);
    }
  }
  [ QUIT GAME
  if ((controller(cQuit) || 
       said("quit", "game") || 
       said("quit"))) {
    stop.sound();
    quit(0);
  }
}
  
[ these controllers always checked in each cycle

[ SHOW GAME ABOUT MSG
if (controller(cAbout)) {
  print(gameAboutMsg);
}

[ FASTEST SPEED
if ((controller(cFastest) || 
     said("fastest") || 
     said("fastest", "speed"))) {
  [ no delay between interpreter cycles
  animationInterval = FASTEST_SPEED;
}

[ FAST SPEED
if ((controller(cFast) || 
     said("fast") || 
     said("fast", "speed"))) {
  [ 1/20th of a second delay between interpreter cycles
  animationInterval = FAST_SPEED;
}

[ NORMAL SPEED
if ((controller(cNormal) || 
     said("normal") || 
     said("normal", "speed"))) {
  [ 2/20ths of a second delay between interpreter cycles
  animationInterval = NORMAL_SPEED;
}

[ SLOW SPEED
if ((controller(cSlow) || 
     said("slow") || 
     said("slow", "speed"))) {
  [ 4/20ths of a second delay between interpreter cycles
  animationInterval = SLOW_SPEED;
}

[ VOLUME UP
[ remember that sound controls only have an effect on non-PC systems
if (controller(cCrescendo) && attenuation > 0) {
  --attenuation;   [ to increase volume
                   [ lower sound attenuation variable
}

[ VOLUME DOWN
if (controller(cDecrescendo) && attenuation < 63) {
  ++attenuation;   [ to decrease volume
                   [ raise sound attenuation variable
}

[ SOUND ON/OFF
if (controller(cToggleSound)) {
  toggle(soundOn);
}

[ TOGGLE CLOCK DISPLAY
if ((said("clock") || controller(cClock))) {
  toggle(clockOn);
  if (!clockOn) {
    [ remove clock text from status line
    status.line.on();
  } else {
    [ make sure clock gets updated this cycle
    statusSeconds = 255;
  }
}

[ ***********************
[ NON-CONTROLLER CHECKS
[ *********************** 

[ if clock display is needed
if (clockOn && !disableGameFunctions) {
  [ update the displayed clock whenever
  [ the second value changes
  if (statusSeconds != elapsedSeconds) {
    [ need to set correct color values
    set.text.attribute(BLACK, WHITE);
    [ update the clock
    display(0, 18, " %v13:%v12|2:%v11|2 ");
    [ restore default colors
    set.text.attribute(WHITE, BLACK);
    [ update the stored seconds value
    statusSeconds = elapsedSeconds;
  }
}

[ if this is first cycle since restore game executed
if (isset(gameRestored)) {
  [ make sure input area is cleared
  clear.lines(23, 24, 0);
  [ debugging off by default
  reset(debugging);
  if (IntVersion > VERSION_2272) {
    [ disable menu separators)
    disable.item(cDummy);
    [ disable debug menu items
    disable.item(cShowEgo);
    disable.item(cShowPriority);
    disable.item(cShowMem);
    disable.item(cObjInfo);
    disable.item(cXY);
    disable.item(cGimme);
  }
}

[ IMPORTANT: This calls the logic for the current room - this
[ is not done automatically by the interpreter so it has to be
[ called manually by logic 0.
call.v(currentRoom);

[ if debugging is active                
if (debugging) {
  [ call the debugging logic
  call(lgc.Debug);
}

[ run game specific functions only if they are not disabled
if (!disableGameFunctions) {
  call(lgc.GameFunctions); 
}
  
[ check player input for unknown words
if (haveInput && !haveMatch && unknownWordNum > 0) {
  reset(haveInput);
  [ copy word to a string
  if (unknownWordNum == 1) {
    word.to.string(unknownWord, w1);
  }
  if (unknownWordNum == 2) {
    word.to.string(unknownWord, w2);
  }
  if (unknownWordNum == 3) {
    word.to.string(unknownWord, w3);
  }
  if (unknownWordNum == 4) {
    word.to.string(unknownWord, w4);
  }
  if (unknownWordNum == 5) {
    word.to.string(unknownWord, w5);
  }
  if (unknownWordNum == 6) {
    word.to.string(unknownWord, w6);
  }
  if (unknownWordNum == 7) {
    word.to.string(unknownWord, w7);
  }
  if (unknownWordNum == 8) {
    word.to.string(unknownWord, w8);
  }
  if (unknownWordNum == 9) {
    word.to.string(unknownWord, w9);
  }
  if (unknownWordNum == 10) {
    word.to.string(unknownWord, w10);
  }
  [ choose a random unknown word response
  random(33, 35, rndNum);
  print.v(rndNum);
}
  
[ finally, if there is input that hasn't 
[ been recognized yet
if (haveInput && !haveMatch) {
  [ generic message for unrecognized input
  print("I don't understand your request.");
  reset(haveInput);
}

[ save clock values for comparison purposes
oldSeconds = elapsedSeconds;
oldMinutes = elapsedMinutes;
oldHours = elapsedHours;
oldDays = elapsedDays;
[pause();
return();

[ **************************************
[ MESSAGES
[ **************************************
#message 1 "AGI"
#message 2 "About      "
#message 3 "Help   <F1>"
#message 4 "Debug Help "
#message 5 "File"
#message 6 "Save     <F5>"
#message 7 "Restore  <F7>"
#message 8 "-------------"
#message 9 "Restart  <F9>"
#message 10 "Quit  <Alt-Z>"
#message 11 "Action"
#message 12 "See Object  <F4>"
#message 13 "Inventory  <Tab>"
#message 14 "Special"
#message 15 "Sound On/Off      <F2>"
#message 16 "Color/BW      <Ctrl R>"
#message 17 "Clock On/Off      <F6>"
#message 18 "Joystick      <Ctrl J>"
#message 19 "Pause            <Esc>"
#message 20 "Speed"
#message 21 "Normal "
#message 22 "Slow   "
#message 23 "Fast   "
#message 24 "Fastest"
#message 25 "Debug"
#message 26 "Ego Info   <Alt-E>"
#message 27 "Pri Screen <Alt-P>"
#message 28 "Memory     <Alt-M>"
#message 29 "Obj Info   <Alt-I>"
#message 30 "Coords     <Alt-X>"
#message 31 "Get All           "
#message 32 " %v13:%v12|2:%v11|2 "
#message 33 "I don't understand \"%s1\""
#message 34 "\"%s1\" is not in my vocabulary."
#message 35 "What is \"%s1\""
#message 36 "I don't understand your request."
#message 37 "Debug Help        "
#message 38 gameAboutMsg
#message 39 gameVersionMsg
#message 50 "%m69 2.089"
#message 51 "%m69 2.272"
#message 52 "%m69 2.411"
#message 53 "%m69 2.425"
#message 54 "%m69 2.426"
#message 55 "%m69 2.435"
#message 56 "%m69 2.439"
#message 57 "%m69 2.440"
#message 58 "%m69 2.903"
#message 59 "%m69 2.911"
#message 60 "%m69 2.912"
#message 61 "%m69 2.915"
#message 62 "%m69 2.917"
#message 63 "%m69 2.936"
#message 64 "%m69 3.002.086"
#message 65 "%m69 3.002.098"
#message 66 "%m69 3.002.102"
#message 67 "%m69 3.002.107"
#message 68 "%m69 3.002.149"
#message 69 "Testing version"